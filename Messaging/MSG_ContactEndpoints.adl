CONTEXT "Contact Endpoints" IN ENGLISH
{- This context specifies the basics of how people may be contacted through various means (e.g. email, phone, etc.) -}

--[Contact Endpoints (CEPs)]--
RELATION cepConnectsTo[ContactEndpoint*Person] [UNI,TOT]
MEANING "A ContactEndpoint serves to establish a connection with some Person."

REPRESENT CEPMeans TYPE ALPHANUMERIC
RELATION cepMeans[ContactEndpoint*CEPMeans] [UNI,TOT]
MEANING "A ContactEndpoint uses a CEPMeans for communicating." -- e.g.: 'Phone', 'Fax'

REPRESENT CEPAddress TYPE ALPHANUMERIC
RELATION cepEndpoint[ContactEndpoint*CEPAddress] [UNI,TOT]
MEANING "A ContactEndpoint can be reached through some Address." -- e.g.: 0612345678, aap@noot.com

REPRESENT CEPNote TYPE BIGALPHANUMERIC
RELATION cepNote[ContactEndpoint*CEPNote] [UNI]
MEANING "A ContactEndpoint may be annotated."

RELATION cepRxFromAppl[ContactEndpoint*ContactEndpoint] [PROP]
MEANING "A ContactEndpoint may be used by applications for sending messages."

--[Populating some of the stuff in advance]--
POPULATION CEPMeans CONTAINS [ "Mail", "Fax", "Phone", "Linkedin", "Skype", "Twitter", "Other" ]

-----------------------------------------------------------
VIEW ContactEndpoints: ContactEndpoint(cepMeans, TXT ": ", cepEndpoint)

INTERFACE "New Contact" (cepConnectsTo, cepMeans, cepEndpoint, cepNote, cepRxFromAppl) : I[ContactEndpoint] Crud ROWS
   [ "Contact"          : cepConnectsTo
   , "Method"           : cepMeans
   , "Address or Number": cepEndpoint
   , "Note(s)"          : cepNote
   , "Appl?"            : cepRxFromAppl
   ]

INTERFACE "Update ContactEndpoint" (cepEndpoint, cepNote, cepRxFromAppl): I[ContactEndpoint] cRUD COLS
   [ "Method"           : cepMeans
   , "Address or Number": cepEndpoint
   , "Note(s)"          : cepNote
   , "Appl?"            : cepRxFromAppl
   ]

INTERFACE "Contact" (cepConnectsTo, cepMeans, cepEndpoint, cepNote, cepRxFromAppl) : I[ContactEndpoint] cRUD BOX <SHCOLS>
   [ "Contact"          : cepConnectsTo
   , "Method"           : cepMeans
   , "Address or Number": cepEndpoint
   , "Shows as"         : I <ViewContact>
   , "Note(s)"          : cepNote
   , "Appl?"            : cepRxFromAppl
   ]

VIEW ViewContact: ContactEndpoint
{ cepConnectsTo : cepConnectsTo
, cepMeans      : cepMeans
, cepEndpoint   : cepEndpoint
, cepNote       : cepNote
, cepRxFromAppl : cepRxFromAppl
} HTML TEMPLATE "ViewContact.html" ENDVIEW
-----------------------------------------------------------
ENDCONTEXT