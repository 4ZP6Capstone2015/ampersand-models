CONTEXT "MSG_Testing" IN ENGLISH
{- This is a stand-alone context used for demonstrating and testing messaging services -}

--[Definitions that are needed for stand-alone operation]--
sessionUser  :: SESSION * Person [UNI]
INTERFACE "SessionUser"  FOR NobodyInParticular : '_SESSION';V[SESSION*User] BOX [ "ignored": I]
sessionRoles :: SESSION * Role
INTERFACE "SessionRoles" FOR NobodyInParticular : sessionRoles[SESSION*Role] BOX [ "ignored": I]

--[Testable/demonstratable scripts]--
INCLUDE "../SIAM/SIAM_PersonRegistration.adl"
INCLUDE "MSG_Email.adl"
INCLUDE "MSG_Pushover.adl"
INCLUDE "MSG_Pushalot.adl"

--[Testing interface]--
sessionMsgPerson :: SESSION * Person [UNI] -- Person to send the message to
sessionMsgText :: SESSION * MsgText [UNI] -- Text to be sent

INTERFACE "Send Message" (sessionMsgText, sessionMsgPerson): '_SESSION'[SESSION]
BOX[ "Send Message" : I COLS
     [ "From": sessionUser
     , "To (person)": sessionMsgPerson
     , "Message text" : sessionMsgText
     ]
   , "Messages" : V[SESSION*MsgSendRequest] COLS -- msgsrSession~ COLS
     [ "From" : msgsrSender
     , "To" : msgsrPerson
     , "Using" : msgsrCEPMeans
     , "Address" : (msgsrPerson;cepConnectsTo~ /\ msgsrCEPMeans;cepMeans~);cepEndpoint
     , "Message text" : msgsrText
     , "Session" : msgsrSession
     , "Sent at" : msgsrTrxDATIM
     ]
   ]

--[Message sending rule]--

ROLE ExecEngine MAINTAINS "Create message send request"
RULE "Create message send request": sessionMsgPerson~;'_SESSION';sessionMsgText |- msgsrPerson~;msgsrText
VIOLATION (TXT "{EX}_;NewStruct_;MsgSendRequest"
              ,TXT "_;msgsrPerson_;MsgSendRequest_;_NEW_;Person_;", SRC I
              ,TXT "_;msgsrText_;MsgSendRequest_;_NEW_;MsgText_;", TGT I
              ,TXT "_;msgsrSession_;MsgSendRequest_;_NEW_;SESSION_;", SRC V;'_SESSION'[SESSION]
              ,TXT "_;msgsrSender_;MsgSendRequest_;_NEW_;Person_;", SRC V;'_SESSION';sessionUser
          ,TXT "{EX} DelPair;sessionMsgPerson;SESSION;", SRC V;'_SESSION'[SESSION], TXT ";Person;", SRC I
          ,TXT "{EX} DelPair;sessionMsgText;SESSION;", SRC V;'_SESSION'[SESSION], TXT ";MsgText;", TGT I
          )

--[Testing population]--

POPULATION personFirstName    CONTAINS [ ("admin", "Ad ") ]
POPULATION personLastName     CONTAINS [ ("admin", "Minister") ]

POPULATION cepRxFromAppl      CONTAINS [ ("emailadmin", "emailadmin") ]
POPULATION cepConnectsTo      CONTAINS [ ("emailadmin", "admin") ]
POPULATION cepMeans           CONTAINS [ ("emailadmin", "Email") ]
POPULATION cepEndpoint        CONTAINS [ ("emailadmin", "<your email address goes here>") ]

POPULATION cepRxFromAppl      CONTAINS [ ("pushoveradmin", "pushoveradmin") ]
POPULATION cepConnectsTo      CONTAINS [ ("pushoveradmin", "admin") ]
POPULATION cepMeans           CONTAINS [ ("pushoveradmin", "Pushover") ]
POPULATION cepEndpoint        CONTAINS [ ("pushoveradmin", "<your user-token goes here>") ]

POPULATION cepRxFromAppl      CONTAINS [ ("pushalotadmin", "pushalotadmin") ]
POPULATION cepConnectsTo      CONTAINS [ ("pushalotadmin", "admin") ]
POPULATION cepMeans           CONTAINS [ ("pushalotadmin", "Pushalot") ]
POPULATION cepEndpoint        CONTAINS [ ("pushalotadmin", "<your authorization code goes here") ]

ENDCONTEXT