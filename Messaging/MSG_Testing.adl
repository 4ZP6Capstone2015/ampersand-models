CONTEXT "MSG_Testing" IN ENGLISH
{- This is a stand-alone context used for demonstrating and testing messaging services -}

--[Definitions that are needed for stand-alone operation]--
sessionUser  :: SESSION * Person [UNI]
INTERFACE "SessionUser"  FOR NobodyInParticular : '_SESSION';V[SESSION*User] BOX [ "ignored": I]
sessionRoles :: SESSION * Role
INTERFACE "SessionRoles" FOR NobodyInParticular : sessionRoles[SESSION*Role] BOX [ "ignored": I]

--[Testable/demonstratable scripts]--
INCLUDE "../SIAM/SIAM_PersonRegistration.adl"
INCLUDE "MSG_Pushover.adl"
INCLUDE "MSG_Email.adl"

--[Testing interface]--
sessionMsgPerson :: SESSION * Person [UNI] -- Person to send the message to
sessionMsgText :: SESSION * MsgText [UNI] -- Text to be sent

INTERFACE "Send Message" (sessionMsgText, sessionMsgPerson): '_SESSION'[SESSION]
BOX[ "Send Message" : I COLS
     [ "From": sessionUser
     , "To (person)": sessionMsgPerson
     , "Message text" : sessionMsgText
     ]
   , "Messages" : V[SESSION*MsgSendRequest] COLS -- msgsrSession~ COLS
     [ "From" : msgsrSender
     , "To" : msgsrPerson
     , "Using" : msgsrCEPMeans
     , "Address" : (msgsrPerson;cepConnectsTo~ /\ msgsrCEPMeans;cepMeans~);cepEndpoint
     , "Message text" : msgsrText
     , "Session" : msgsrSession
     , "Sent at" : msgsrTrxDATIM
     ]
   ]

--[Message sending rule]--

ROLE ExecEngine MAINTAINS "Create message send request"
RULE "Create message send request": sessionMsgPerson~;'_SESSION';sessionMsgText |- msgsrPerson~;msgsrText
VIOLATION (TXT "{EX}_;NewStruct_;MsgSendRequest"
              ,TXT "_;msgsrPerson_;MsgSendRequest_;_NEW_;Person_;", SRC I
              ,TXT "_;msgsrText_;MsgSendRequest_;_NEW_;MsgText_;", TGT I
              ,TXT "_;msgsrSession_;MsgSendRequest_;_NEW_;SESSION_;", SRC V;'_SESSION'[SESSION]
              ,TXT "_;msgsrSender_;MsgSendRequest_;_NEW_;Person_;", SRC V;'_SESSION';sessionUser
          ,TXT "{EX} DelPair;sessionMsgPerson;SESSION;", SRC V;'_SESSION'[SESSION], TXT ";Person;", SRC I
          ,TXT "{EX} DelPair;sessionMsgText;SESSION;", SRC V;'_SESSION'[SESSION], TXT ";MsgText;", TGT I
          )

--[Testing population]--

POPULATION personFirstName    CONTAINS [ ("admin", "Ad ") ]
POPULATION personLastName     CONTAINS [ ("admin", "Minister") ]

POPULATION cepConnectsTo      CONTAINS [ ("pushoveradmin", "admin") ]
POPULATION cepMeans           CONTAINS [ ("pushoveradmin", "Pushover") ]
POPULATION cepEndpoint        CONTAINS [ ("pushoveradmin", "ufoaKLBxhPxdiECpxtxhdjqdSMyfDD") ]
POPULATION cepRxFromAppl      CONTAINS [ ("pushoveradmin", "pushoveradmin") ]
-- POPULATION cepEndpoint        CONTAINS [ ("pushoverRieksJ",   "ufoaKLBxhPxdiECpxtxhdjqdSMyfDD") ]
-- POPULATION cepEndpoint        CONTAINS [ ("pushoverMichielS", "uaFR8QKy4S4Bs8r3KNekMB3xmdnaox") ]

POPULATION cepConnectsTo      CONTAINS [ ("emailadmin", "admin") ]
POPULATION cepMeans           CONTAINS [ ("emailadmin", "Email") ]
--POPULATION cepEndpoint        CONTAINS [ ("emailadmin", "rieks.joosten@tno.nl") ]
POPULATION cepEndpoint        CONTAINS [ ("emailadmin", "ufoaKLBxhPxdiECpxtxhdjqdSMyfDD@api.pushover.net") ]
POPULATION cepRxFromAppl      CONTAINS [ ("emailadmin", "emailadmin") ]

--[Pushover login/logout logging]--
{-
sessionAccountRemembered :: SESSION * Account [UNI]
ROLE ExecEngine MAINTAINS "Pushover: Login"
RULE "Pushover: Login": sessionAccount |- sessionAccountRemembered 
VIOLATION(TXT "{EX} InsPair;sessionAccountRemembered;SESSION;", SRC I, TXT ";Account;", TGT I
         ,TXT "{EX} PushoverNotifications::execEnginePushNotificationOnCommit" -- Function call
              ,TXT ";", SRC V;'[not specified]'[ContactEndpoint];cepEndpoint -- <userkey>
              ,TXT ";", TGT accUserid, TXT " has logged in." -- <message>
--            ,TXT ";<title>" -- <title>
--            ,TXT ";<url>" -- <url>
         )
ROLE ExecEngine MAINTAINS "Pushover: Logout"
RULE "Pushover: Logout": sessionAccountRemembered |- sessionAccount
VIOLATION(TXT "{EX} DelPair;sessionAccountRemembered;SESSION;", SRC I, TXT ";Account;", TGT I
         ,TXT "{EX} PushoverNotifications::execEnginePushNotificationOnCommit" -- Function call
              ,TXT ";", SRC V;'[not specified]'[ContactEndpoint];cepEndpoint -- <userkey>
              ,TXT ";", TGT accUserid, TXT " has logged out." -- <message>
--            ,TXT ";<title>" -- <title>
--            ,TXT ";<url>" -- <url>
         )
-}
ENDCONTEXT