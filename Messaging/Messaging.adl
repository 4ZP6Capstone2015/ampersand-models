CONTEXT MessagingService IN ENGLISH
PURPOSE CONTEXT MessagingService
{+This service allows applications to send messages to Perons by 
1) creating a `MsgSendRequest`,
2) and populating the relations `msgsrPerson` and `msgsrText`,
3) and optionally populating the other relations.
-}

INCLUDE "MSG_ContactEndpoints.adl"

--[Message Send Requests]--
msgsrSession  :: MsgSendRequest * SESSION [UNI]  -- Session from which the message is sent
msgsrSender   :: MsgSendRequest * Person  [UNI]  -- Person that was logged into the session that sent the message.
msgsrPerson   :: MsgSendRequest * Person  [UNI,TOT]
msgsrText     :: MsgSendRequest * MsgText [UNI,TOT]   REPRESENT MsgText  TYPE ALPHANUMERIC
msgsrCEPMeans :: MsgSendRequest * CEPMeans       -- ways by which the message should be delivered.
msgsrTrxDATIM :: MsgSendRequest * DateTime            REPRESENT DateTime TYPE DATETIME

sessionUser  :: SESSION * Person [UNI]
RULE "The sender of a message may only be the user of the session within which the message was sent": 
   msgsrSender |- msgsrSession;sessionUser

--[Messaging service operation]--
RULE "Applications can only send messages to people that have registered a method for doing so":
  (I-(msgsrTrxDATIM;msgsrTrxDATIM~));msgsrPerson |- V;cepRxFromAppl;cepConnectsTo
VIOLATION (TGT I, TXT " has not specified a CEPMeans for receiving messages from this application")

ROLE ExecEngine MAINTAINS "Insert CEPMeans to message send request"
RULE "Insert CEPMeans to message send request": 
  (I-(msgsrTrxDATIM;msgsrTrxDATIM~));msgsrPerson;cepConnectsTo~;cepRxFromAppl;cepMeans |- msgsrCEPMeans
VIOLATION (TXT "{EX} InsPair;msgsrCEPMeans;MsgSendRequest;", SRC I, TXT ";CEPMeans;", TGT I)

ROLE ExecEngine MAINTAINS "Remove CEPMeans to message send request"
RULE "Remove CEPMeans to message send request": 
   msgsrCEPMeans |- msgsrPerson;cepConnectsTo~;cepRxFromAppl;cepMeans
VIOLATION (TXT "{EX} DelPair;msgsrCEPMeans;MsgSendRequest;", SRC I, TXT ";CEPMeans;", TGT I)

{- For every CEPMeans, we envisage that there is a Service that processes a MsgSendRequest as follows:
1. Send the MsgText to the Person.
2. Set the DateTime to the time that the MsgText was sent.
3. Remove the CEPMeans from the MsgSendRequest.
This allows message send requests to be deleted once their MsgText has been sent by all CEPMeans-services.
-}
ROLE ExecEngine MAINTAINS "Delete message send request"
RULE "Delete message send request": (I-(msgsrCEPMeans;msgsrCEPMeans~));msgsrTrxDATIM |- -V
VIOLATION (TXT "{EX} DelAtom;MsgSendRequest;", SRC I)

ENDCONTEXT