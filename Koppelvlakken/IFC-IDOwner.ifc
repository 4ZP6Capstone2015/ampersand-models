CONTEXT InformationDomainInterfacing IN DUTCH
-----------------------------------------------------------
INTERFACE "Overview for InfoDomainOwner" FOR IDEigenaar, InfoDomainOwner : I[ONE]
BOX[ "InformationDomain (IDs)"                    : V[ONE*InformationDomain]
   , "InterconnectionPanes (ICPs)"                : V[ONE*InterconnectionPane]
   , "Security Policy Enforcement Points (SPEPs)" : V[ONE*SPEP]
   , "Communities Of Interest (COIs)"             : V[ONE*CommunityOfInterest]
   ]
-----------------------------------------------------------
INTERFACE "Organieke Unit" (ouName,ouDescription,ouCommander) FOR COIPartij, COIParty:  I[OrganizationalUnit]
BOX[ "Naam" : ouName
   , "Beschrijving" : ouDescription
   , "Commandant" : ouCommander
   ]
-----------------------------------------------------------
PROCESS "DummyIDEigenaar"
RULE "DummyIDEigenaar": V[InformationDomain*InformationDomain]
ROLE IDEigenaar MAINTAINS "DummyIDEigenaar"
ENDPROCESS
-----------------------------------------------------------
INTERFACE "Informatie Domein" (operationalResponsible,secPolicy,classificationLevel,icpDst,icpSrc) FOR IDEigenaar, InfoDomainOwner: I[InformationDomain]
BOX[ "Informatie Domein" : I
   BOX[ "Naam"                 : I
      , "Organieke eenheid"    : operationalResponsible
      , "Policy"               : secPolicy
      , "Classification Level" : classificationLevel
      ]
--   , "COI lidmaatschap" : committedTo
   , "Inbound ICPs"     : icpDst~
   , "Outbound ICPs"    : icpSrc~
-- , "Werklijst"  : <lijst van gesignaleerd werk voor dit type Concept>
   ]
-----------------------------------------------------------
INTERFACE "Koppelvlak" (icpName,icpSrc,icpDst,icpTrustlevel,trustlevelValue,icpAllThreatsSelected,icpImplementationDecision,icpIsOperational) FOR IDEigenaar, InfoDomainOwner: I[InterconnectionPane]
BOX[ "SPEPs"  : spepICP~
   BOX[ "Type(C/P/R)" : spepType
      ]
   , "Koppelvlak (ICP)"  : I        --$Ticket#291
   , "Naam"              : icpName  --$Ticket#291
   , "Src info domain"   : icpSrc
   , "Dst info domain"   : icpDst
   , "Trustlevel"        : icpTrustlevel
   BOX[ "Waarde"           : trustlevelValue
      , "Betekenis"        : trustlevelSummary
      , "Beschrijving"     : trustlevelDescription
      ]
   , "Nodig binnen COI(s)": coiDesiredICP~
   , "Potentieel ook bruikbaar binnen": -coiDesiredICP~
                                    /\ icpSrc;operationalResponsible;coiParty~
                                    /\ icpDst;operationalResponsible;coiParty~
   , "Zijn alle dreigingen geselecteerd?" : icpAllThreatsSelected
   , "Is besloten dit koppelvlak te implementeren?" : icpImplementationDecision
   , "Is dit koppelvlak geaccrediteerd?" : icpIsAccredited
   , "Is dit koppelvlak operationeel?" : icpIsOperational
-- , "Werklijst"  : <lijst van gesignaleerd werk voor dit type Concept>
   , "Te ondernemen acties": I
   BOX[ "SPEP type 'C' aanmaken c.q. verwijderen":
        -{-sigCSPEP-}(I /\ icpSrc;classificationLevel;-I;classificationLevel~;icpDst~ = I /\ spepICP~;spepType;'C';spepType~;spepICP)
      , "SPEP type 'P' aanmaken c.q. verwijderen":
        -{-sigPSPEP-}(I /\ icpSrc;secPolicy;-I;secPolicy~;icpDst~ = I /\ spepICP~;spepType;'P';spepType~;spepICP)
      , "SPEP type 'R' aanmaken c.q. verwijderen":
        -{-sigRSPEP-}(I /\ icpSrc;operationalResponsible;-I;operationalResponsible~;icpDst~ = I /\ spepICP~;spepType;'R';spepType~;spepICP)
      , "Threats specificeren": I /\ -(icpAllThreatsSelected;'Yes';icpAllThreatsSelected~)
{-      , "SPEP type 'C'" : 
        (I /\ spepICP~;spepType;'C';spepType~;spepICP
           /\ -(icpSrc;classificationLevel;classificationLevel~;icpDst~));V;'aanmaken'[ToDo]
        \/
        (I /\ -(spepICP~;spepType;'C';spepType~;spepICP)
           /\ icpSrc;classificationLevel;classificationLevel~;icpDst~);V;'verwijderen'[ToDo]
-}
   ]  ]
-----------------------------------------------------------
PROCESS "Interconnection Panes"

RULE "COI assignment": I[InterconnectionPane] |- coiDesiredICP~;coiDesiredICP
MEANING "An ICP should only exist if there is a need for it"
MESSAGE "ICP is not needed within any COI"
ROLE InfoDomainOwner MAINTAINS "COI assignment"

RULE "Trustlevel assignment": I[InterconnectionPane] |- icpTrustlevel;icpTrustlevel~
MEANING "De eigenaar van een ICP moet daaraan een trustlevel hebben toegekend"
MESSAGE "Er moet een trustlevel worden toegekend aan"
ROLE InfoDomainOwner MAINTAINS "Trustlevel assignment"

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Threats selecteren" (icpTrustlevel,trustlevelValue,icpThreatICP,icpThreatThreat,icpThreatSignificance,icpAllThreatsSelected) FOR IDEigenaar, InfoDomainOwner: I[InterconnectionPane]
BOX[ "Koppelvlak (ICP)"  : I        --$Ticket#291
   , "Trustlevel"        : icpTrustlevel
   BOX[ "Waarde"           : trustlevelValue
      , "Betekenis"        : trustlevelSummary
      , "Beschrijving"     : trustlevelDescription
      ]
   , "Geselecteerde threat(s)": icpThreatICP~
   BOX[ "Threat" : icpThreatThreat
      , "Significantie (0-4)" : icpThreatSignificance
      ]
   , "Zijn alle threats geselecteerd?" : icpAllThreatsSelected

-- , "Werklijst"  : <lijst van gesignaleerd werk voor dit type Concept>
   , "Werklijst" : I
   BOX[ "Threat selectie completeren" : (I /\ -(icpAllThreatsSelected;'Yes';icpAllThreatsSelected~))
   ]  ]
-----------------------------------------------------------
INTERFACE "Threat mitigatie" (icpThreatSignificance,icpThreatClosed,icpThreatSelectedFSRs,icpThreatRestRisico,icpThreatClosed) FOR InfoDomainOwner: I[ICPThreat]
BOX[ "Threat"       : icpThreatThreat
   , "Category"     : icpThreatThreat;threatCategory
   , "Interconnection Pane" : icpThreatICP
   , "Significance" : icpThreatSignificance
   BOX[ "Level"   : I
      , "Meaning" : threatSignificanceDescription
      ]
   , "Geselecteerde maatregelen" : icpThreatSelectedFSRs
   , "Rest risico"  : icpThreatRestRisico
   BOX[ "Level"   : I
      , "Meaning" : threatSignificanceDescription
      ]
   , "Closed?"      : icpThreatClosed
-- , "Werklijst"  : <lijst van gesignaleerd werk voor dit type Concept>
   , "Werklijst"  : I[ICPThreat]
   BOX[ "Significance toekennen" : I /\ -(icpThreatSignificance;icpThreatSignificance~)
      , "Restrisico bepalen"     : I /\ -(icpThreatRestRisico;icpThreatRestRisico~)
      , "Threat afsluiten"       : I /\ -{-sigThreatClosed-}(I /\ icpThreatClosed;'Yes';icpThreatClosed~)
      ]
   ]
-----------------------------------------------------------
PROCESS "ICPThreats"

RULE "Threats afsluiten": I[ICPThreat] |- {-sigThreatClosed-}(I /\ icpThreatClosed;'Yes';icpThreatClosed~)
MEANING "Elke threat moet worden afgesloten"
MESSAGE "U moet de threat nog afsluiten."
ROLE InfoDomainOwner MAINTAINS "Threats afsluiten"

RULE "Restrisico bepalen" : I[ICPThreat] /\ icpThreatSelectedFSRs;icpThreatSelectedFSRs~ |- icpThreatRestRisico;icpThreatRestRisico~
MEANING "Voor elke threat waar maatregelen zijn geselecteerd, moet het restrisico zijn bepaald."
MESSAGE "Het restrisico moet worden bepaald"
ROLE InfoDomainOwner MAINTAINS "Restrisico bepalen"

RULE "Threat significance assignment": I[ICPThreat] |- icpThreatSignificance;icpThreatSignificance~
MEANING "Every ICPThreat must have a significance assigned"
MESSAGE "U moet nog een threat significance toekennen aan"
ROLE InfoDomainOwner MAINTAINS "Threat significance assignment"

RULE "Threat selectie": I[InterconnectionPane] |- icpAllThreatsSelected;'Yes'[YesNo];icpAllThreatsSelected~
MEANING "Voor alle InterconnectionPanes moeten alle threats zijn geselecteerd"
MESSAGE "U moet (mogelijk) nog threats selecteren."
ROLE InfoDomainOwner MAINTAINS "Threat selectie"

RULE "Threat selectie integriteit" : icpAllThreatsSelected;'Yes'[YesNo] |- (-icpThreatICP~!icpThreatSignificance);V
MEANING "We stellen alleen dan vast dat alle threats zijn geselecteerd, als elke geselecteerde threat is voorzien van een 'significance'"
MESSAGE "Er zijn nog threats waarvan U de significance moet vaststellen"
ROLE InfoDomainOwner MAINTAINS "Threat selectie integriteit"

RULE "Integrity of closing threats": {-sigThreatClosed-}(I /\ icpThreatClosed;'Yes';icpThreatClosed~) |- icpThreatRestRisico;icpThreatRestRisico~
MEANING "Voor elke threat die is afgesloten, moet het restrisico zijn gespecificeerd"
MESSAGE "U moet het 'restrisico' nog specificeren."

RULE "Reducing risks": icpThreatRestRisico |- icpThreatSignificance;threatSignificanceGT
MEANING "Het waarde van het restrisico moet kleiner zijn dan de waarde van de threat significance"
MESSAGE "Het risico moet worden gereduceerd"
ROLE InfoDomainOwner MAINTAINS "Reducing risks"

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Security Policy Enforcement Point (SPEP)" (spepICP,spepType) FOR IDEigenaar, InfoDomainOwner: I[SPEP]
BOX[ "SPEP"     : I
   , "Deel van" : spepICP
   BOX[ "ICP"     : I
      , "SPEP (C)" : spepICP~;(I /\ spepType;'C';spepType~)
      , "SPEP (P)" : spepICP~;(I /\ spepType;'P';spepType~)
      , "SPEP (R)" : spepICP~;(I /\ spepType;'R';spepType~)
   ]
   , "Type"     : spepType
-- , "Werklijst"  : <lijst van gesignaleerd werk voor dit type Concept>
   ]
-----------------------------------------------------------
PROCESS "SPEPs aanmaken"

RULE "C-SPEP aanmaken": {-sigCSPEP-}(I /\ icpSrc;classificationLevel;-I;classificationLevel~;icpDst~ = I /\ spepICP~;spepType;'C';spepType~;spepICP)
MEANING "Als een interconnection pane een 'C' boundary over gaat, dan moet er een SPEP van het type 'C' zijn (en anders niet)"
MESSAGE "U moet nog een SPEP van het type 'C' aanmaken"
ROLE InfoDomainOwner MAINTAINS "C-SPEP aanmaken" 

RULE "P-SPEP aanmaken": {-sigPSPEP-}(I /\ icpSrc;secPolicy;-I;secPolicy~;icpDst~ = I /\ spepICP~;spepType;'P';spepType~;spepICP)
MEANING "Als een interconnection pane een 'P' boundary over gaat, dan moet er een SPEP van het type 'P' zijn (en anders niet)"
MESSAGE "U moet nog een SPEP van het type 'P' aanmaken"
ROLE InfoDomainOwner MAINTAINS "P-SPEP aanmaken"

RULE "R-SPEP aanmaken": {-sigRSPEP-}(I /\ icpSrc;operationalResponsible;-I;operationalResponsible~;icpDst~ = I /\ spepICP~;spepType;'R';spepType~;spepICP)
MEANING "Als een interconnection pane een 'R' boundary over gaat, dan moet er een SPEP van het type 'R' zijn (en anders niet)"
MESSAGE "U moet nog een SPEP van het type 'R' aanmaken"
ROLE InfoDomainOwner MAINTAINS "R-SPEP aanmaken" 

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT
