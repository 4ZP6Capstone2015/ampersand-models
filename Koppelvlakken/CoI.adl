CONTEXT CommunityOfInterest
-----------------------------------------------------------
PATTERN "Communities of Interest"
PURPOSE PATTERN "Communities of Interest"
{+Any communication of information between Organizational Units serves a purpose that is common to these Organizational Units. Conversely, we may say that the common urge to achieve such a goal necessitates such communications. Thus, in order to justify the existence of information communication between different (information domains of) different Organizational Units, we need to be able to specify this common goal.-}

CONCEPT CommunityOfInterest "a set of Organizational Units that share a common goal"
PURPOSE CONCEPT CommunityOfInterest IN ENGLISH REF "Schotanus, H., Boonstra, D., Broenink, G., 'Decomposition of the Security Requirements for Connected Information Domains', Military Communications and Information Systems Conference, Amsterdam, The Netherlands, October 17-18, 2011."
{+In order to demarcate information flows, we specify COIs within which all relevant decisions can be made regarding which information flows are needed, what policies apply, etc. There shall only be communications between any two information domains if there is a COI that encompasses such infomration domains, and that has expressed a need for such communication to take place.-}

CONCEPT COIname "a symbol that represents a Community of Interest."
PURPOSE CONCEPT COIname IN DUTCH
{+In order to allow 'the business' to distinguish between one Community of Interest and another, we introduce the concept COIname.-}

KEY "Community of Interest Names": CommunityOfInterest(coiGoal)

coiGoal :: CommunityOfInterest -> Goal PRAGMA "" " consists of Organizational Units that all subscribe to "
coiParty :: CommunityOfInterest * OrganizationalUnit PRAGMA "The goal of " " is subscribed to by "

coiDesiredICP :: CommunityOfInterest * InterconnectionPane PRAGMA "" " has a need for "

RULE "Interconnection Pane Integrity (COI)": I[InterconnectionPane] |- coiDesiredICP~;coiDesiredICP
MEANING "An ICP should only exist if there is a need for it"
MESSAGE "No COI exists that has expressed a need for this ICP"

RULE "Integrity of desired ICP src domain": coiDesiredICP |- coiParty;operationalResponsible~;icpSrc~
MEANING "A COI may only desire an ICP if the responsibility of the source domain is born by a COI member"
MESSAGE "The party that is responsible for the SOURCE domain is not a member of the COI"

RULE "Integrity of desired ICP dst domain": coiDesiredICP |- coiParty;operationalResponsible~;icpDst~
MEANING "A COI may only desire an ICP if the responsibility of the destination domain is born by a COI member"
MESSAGE "The party that is responsible for the DESTINATION domain is not a member of the COI"

ENDPATTERN
-----------------------------------------------------------
PATTERN "Organieke Eenheid"

CONCEPT OrganizationalUnit "een georganiseerde groep van mensen die door een van hen, de commandant, wordt geleid"
KEY "OrganizationalUnits": OrganizationalUnit(ouName)

ouName :: OrganizationalUnit * OUName [UNI,TOT]
ouDescription :: OrganizationalUnit * Text [UNI]
ouCommander :: OrganizationalUnit * Person [UNI]

ENDPATTERN
-----------------------------------------------------------
ENDCONTEXT