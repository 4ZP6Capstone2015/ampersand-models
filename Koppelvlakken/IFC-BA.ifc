CONTEXT BeveiligingsAutoriteit IN DUTCH
-----------------------------------------------------------
INTERFACE "Overview (BA)" FOR BeveiligingsAutoriteit : I[ONE]
BOX["InterconnectionPanes (ICPs)"                : V[ONE*InterconnectionPane]
   ]
-----------------------------------------------------------
PROCESS BeveiligingsAutoriteit

RULE "Koppelvlak operationalisatie integriteit": icpIsOperational |- icpIsAccredited
MEANING "Een koppelvlak mag alleen operationeel zijn als het is geaccrediteerd."
MESSAGE "Het koppelvlak is nog niet c.q. niet meer geaccrediteerd."
ROLE BeveiligingsAutoriteit MAINTAINS "Koppelvlak operationalisatie integriteit"

RULE "Koppelvlak accreditatie integriteit (1)": I /\ icpIsAccredited;icpIsAccredited~ |- icpImplementationDecision;icpImplementationDecision~
MEANING "Alleen over koppelvlakken waarvan is besloten ze te implementeren kan een accreditatiebesluit worden genomen."
MESSAGE "Er is nog geen implementatiebesluit genomen"

RULE "Koppelvlak accreditatie integriteit (2)": icpIsAccredited;'Yes'[YesNo] |- icpAllThreatsSelected;'Yes'
MEANING "Een koppelvlak kan allen worden geaccrediteerd als is vastgesteld dat alle threats zijn geselecteerd"
MESSAGE "Er is niet vastgesteld dat alle threats zijn geselecteerd"

RULE "Koppelvlak accreditatie integriteit (3)": icpIsAccredited;'Yes'[YesNo] |- icpThreatICP~;icpThreatClosed;'Yes'
MEANING "Een koppelvlak kan allen worden geaccrediteerd als alle threats zijn afgesloten"
MESSAGE "Er zijn nog onafgesloten threats"

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Koppelvlak (BA)" (icpIsAccredited) FOR BeveiligingsAutoriteit: I[InterconnectionPane]
BOX[ "Koppelvlak (ICP)"  : I
   , "Trustlevel"        : icpTrustlevel
   , "Geselecteerde dreigingen": icpThreatICP~
   BOX[ "Threat" : icpThreatThreat
      , "Significantie (0-4)" : icpThreatSignificance
      , "Geselecteerde maatregelen" : icpThreatSelectedFSRs
      , "Rest risico"  : icpThreatRestRisico
      , "Is de threat afgesloten?" : icpThreatClosed
      ]
   , "Zijn ALLE dreigingen geselecteerd?" : icpAllThreatsSelected
   , "Is besloten dit koppelvlak te implementeren?" : icpImplementationDecision
   , "Is dit koppelvlak geaccrediteerd?" : icpIsAccredited
   , "Is dit koppelvlak operationeel?" : icpIsOperational
   ]

ENDCONTEXT
