CONTEXT DemoHogerberoep

INCLUDE "INDOORS.adl"
INCLUDE "DemoHogerberoep.pop"
INCLUDE "Stamtabellen.adl"

------------------------------------
-- Include relevante andere demo's --
-------------------------------------
INCLUDE "DemoBeslissingen.adl"
-- INCLUDE "DemoDossiers.adl" --Han Joosten, 24-dec-2013: Uitgecommentarieerd, vanwege ontbreken van bestand.

------------------
-- Procesregels --
------------------
PROCESS "Verwerken appelakte"

--ROLE "Administratief medewerker" MAINTAINS appelaktecompleet
ROLE Rechter MAINTAINS vonnismotivatie, vonnisbeslissing
ROLE "Juridisch medewerker" MAINTAINS vonnismotivatie 

dossiervrijgeven :: Dossier * Boolean

dossiercompleet :: Dossier * Boolean

{-RULE compleetvoorvrijgeven : dossiervrijgeven;'Ja';dossiervrijgeven~ |- dossiercompleet;'Ja';dossiercompleet~
MESSAGE "Het dossier moet compleet zijn voordat het is vrijgegeven."
-}

RULE documentkenmerk : I[Document] |- kenmerk;kenmerk~
MESSAGE "Het document heeft geen kenmerk."

RULE documenttype : I[Document] |- type;type~
MESSAGE "Het document heeft geen type."

RULE hogerberoepmogelijk : hogerberoep;hogerberoep~ |- hogerberoep;zaaksdocument~;type;'Appelakte';type~;zaaksdocument;hogerberoep~
MESSAGE "Op deze zaak is nog geen hoger beroep ingesteld. (Art. 404 Sv)"
VIOLATION (TGT hogerberoep)

RULE appelaktecompleet : zaaksdocument~;type;'Appelakte';type~ |- zaaksdocument~;datumbetekening;datumbetekening~ 
MESSAGE "De datum ontbreekt op de appelakte. (Art. 451 lid 1 Sv)"
VIOLATION (TXT "Betreffende appelakte met kenmerk: ", TGT kenmerk) 

RULE vonnisbeslissing : zaaksdocument~;type;'Appelakte';type~;zaaksdocument |- zaaksdocument~;(typebeslissing;'Beslissingstype 1';typebeslissing~ /\ beslissing;beslissing~);zaaksdocument
MESSAGE "Onderstaande vonnisen dienen met beslissingen te worden aangevuld voor het Hof. (Art. 345 lid 1 Sv)"
VIOLATION (TGT zaaksdocument~)

RULE vonnismotivatie : zaaksdocument~;type;'Appelakte';type~;zaaksdocument |- zaaksdocument~;(typebeslissing;'Beslissingstype 1';typebeslissing~ /\ motivering;motivering~);zaaksdocument
MESSAGE "Onderstaande vonnisen dienen met motivatie te worden aangevuld voor het Hof. (Art. 358 lid 4 Sv; Art. 359 lid 2 Sv)"
VIOLATION (TGT zaaksdocument~)



ENDPROCESS


---------------
-- Demo keys --
---------------

VIEW dossier : Dossier(TXT "Dossier ", zaaksdossier;zaakid)

--------------------------------------
-- Items voor in de zwarte menubalk --
--------------------------------------

INTERFACE Dagvaardingen FOR "Officier van Justitie": I[ONE]
BOX [ "Te registreren"  : V[ONE*Zaakid] --\/ V[ONE*Zaakid];zaakid;zaakid~;appointeringsvoorstel~;appointeringsvoorstel;aanhangig 
    , Dagvaardingen     : V[ONE*Zaakid];aanbrengwijze;aanbrengwijzeNaam;'Dagvaarding';aanbrengwijzeNaam~;aanbrengwijze~ --/\ -(V[ONE*Dagvaarding];aanhangig;zaakid;zaakid~;aanhangig~)
    ]
	
INTERFACE "Dagvaardingen registreren" FOR Griffie: I[ONE]
BOX [ "Dagvaardingen zonder zaaknummer"     : V[ONE*Zaakid];aanbrengwijze;aanbrengwijzeNaam;'Dagvaarding';aanbrengwijzeNaam~;aanbrengwijze~ --/\ -(V[ONE*Dagvaarding];aanhangig;zaakid;zaakid~;aanhangig~)
    ]

INTERFACE "Zaken " FOR "Administratief medewerker" : I[ONE]
BOX
[
   Zaken                               : V[ONE*Zaakid]
,  "Dossiers betreffende hoger beroep" : V[ONE*Zaakid];zaaksdocument~;type;'Appelakte';type~;zaaksdocument
]
	
INTERFACE Zaken FOR "Juridisch medewerker" : I[ONE]
BOX [ "Verdeel zaken over kamers" : V[ONE*Zaakid]
    ]

INTERFACE Dossiers FOR "Juridisch medewerker" : I[ONE]
BOX
[
   Dossiers : V[ONE*Dossier];dossiervrijgeven;dossiervrijgeven~
]

INTERFACE "Dossiers naar Gerechtshof" FOR Rechter : I[ONE];V[ONE*Zaakid];zaaksdocument~;type;'Appelakte';type~;zaaksdocument
BOX
[ 
   "Aan te vullen dossiers" : zaaksdocument~ /\ zaaksdocument~;(typebeslissing;'Beslissingstype 1';typebeslissing~ /\ (-(motivering;motivering~) \/ -(beslissing;beslissing~)))
]
	
----------------
-- Interfaces --
----------------
INTERFACE "Parketnummer toewijzen hoger beroep" (zaakid, hogerberoep) FOR "Officier van Justitie", "Advocaat Generaal"
                                    : I[Zaakid]
BOX
[
   Parketnummer                     : zaakid
,  "Betreffende zaak eerste aanleg" : hogerberoep  
]


INTERFACE "Appelakte indienen" (zaaksdocument, type, kenmerk, datumbetekening) FOR "Administratief medewerker"
                         : I[Document] /\ (-(type;type~) \/ (type;'Appelakte';type~))
BOX
[
   Parketnummer          : zaaksdocument
   BOX
   [
      ""                 : I[Zaakid]
   ,  Vonnis             : zaaksdocument~;typebeslissing;'Beslissingstype 1';typebeslissing~ /\ zaaksdocument~
      BOX
      [
         Kenmerk         : kenmerk;kenmerk~;kenmerk
      ,  Datum           : wijsdatum
      ]
	,  Betreft           : partij~;ispartij~
       BOX
       [
          Voornamen      : gbaVoornamen
       ,  Geslachtsnaam  : gbaGeslachtsnaam
       ,  Geboortedatum  : geboortedatum
       ,  Geboorteplaats : geboorteplaats
       ,  Adres          : woonadres
          BOX
          [
	         Straatnaam  : straatnaam
          ,  Huisnummer  : huisnummer
          ,  Woonplaats  : woonplaats
          ,  Postcode    : postcode
          ]
       ]
   ]	  
,  Type                  : type
,  Kenmerk               : kenmerk
,  Datum                 : datumbetekening
]


INTERFACE "Dossier vrijgeven" (zaaksdossier, dossiervrijgeven) FOR "Administratief medewerker"
                            : I[Zaakid]
BOX
[
   Parketnummer             : zaakid
,  Behandeltype             : behandeltype
,  "Status zaaksdossier"    : zaaksdossier~
   BOX
   [
      {-"Dossier compleet"    : dossiercompleet
   ,-}  "Vrijgegeven aan hof" : dossiervrijgeven
   ]
,  Zaakstukken              : zaaksdocument~
]

INTERFACE "Dossier raadplegen" FOR "Administratief medewerker", "Juridisch medewerker", Rechter
                          :I[Dossier]
BOX
[
   Parketnummer : zaaksdossier
,  Documenten   : zaaksdossier;zaaksdocument~
]

ENDCONTEXT