CONTEXT DemoRegisters IN DUTCH

INCLUDE "Processen.adl"
INCLUDE "DemoRegisters.pop"

--------------
-- Relaties --
--------------
zoeken :: Zoek * Zoek
POPULATION zoeken CONTAINS
[ ("", "Zoek")
]

zoekOpNaam :: Zoek * Inschrijving

zoekOpBAR :: Zoek * Barnummer

----------------
-- Interfaces --
----------------

INTERFACE Advocatentableau FOR Advocaat, "Juridisch medewerker", "Administratief medewerker"
                : I[ONE]
BOX [ Inschrijvingen : V[ONE*Beëdiging];beëdigd /\ V[ONE*Barnummer];barnummer
      BOX [ Advocaat  : isadvocaat
          , Barnummer : barnummer~
          , Kantoor   : houdtkantoor
          ]
    , Kantoren  : V[ONE*Kantoor]
{- In het volgende zit mogelijk een fout in de codegenerator: DIAGNOSE STELLEN!!!
    , "In behandeling" : V[ONE*Beëdiging];-beëdigd \/ V[ONE*Barnummer];-barnummer
      BOX [ Advocaat  : isadvocaat
          , "Datum beëdigd" : beëdigd~
          , Kantoor   : houdtkantoor
          ]
-}
    ]

INTERFACE Inschrijvingsverzoeken FOR "Administratief medewerker" 
                               : I[ONE]
BOX [ "Te controleren op inschrijvingseisen" : V[ONE*Inschrijvingseis];-inschrijvingseis~
    , "Te plannen beëdigingen" : V[ONE*Inschrijvingseis];inschrijvingseis~ /\ -(V[ONE*Beëdiging];beëdigd)
    ]

INTERFACE "Inschrijvingsverzoeken " FOR Secretaris 
                                             : I[ONE]
BOX [ "Te controleren op inschrijvingseisen" : V[ONE*Inschrijvingseis];-inschrijvingseis~
    ]

INTERFACE "Barnummer toekennen" (barnummer, inschrijvingsstatus) FOR Secretaris         
                                    : I[ONE]
BOX [ "Beëdigd, zonder barnummer"   : V[ONE*Inschrijving];beëdigd~;beëdigd /\ -(V[ONE*Inschrijving];barnummer~;barnummer)
    BOX [ Barnummer                 : barnummer~
        , Verzoeker                 : isadvocaat
        , Kantooradres              : houdtkantoor
        , Beëdiging                 : beëdigd~
          BOX [ Beëdigingsdatum   : datumBeëdiging
              , Beëdigingslocatie : locatieBeëdiging
            ]
        , Status                : inschrijvingsstatus
        ]
    ]


INTERFACE "Advocaat zoeken" : I[ONE]
BOX [ ""                    : V[ONE*Zoek]
    ]


INTERFACE " op BAR-nummer" (zoekOpBAR)         
                                   : I[Zoek]
BOX [ "Record voor"                : zoekOpBAR
      BOX [ ""                       : barnummer
            BOX [ "BAR-nummer"         : barnummer~
                , Naam                 : isadvocaat
                , Geboortedatum        : isadvocaat;geboortedatum
                , Beëdiging            : beëdigd~
                  BOX [ Rechtbank        : locatieBeëdiging
                      , Beëdigingsdatum  : datumBeëdiging
                      ]
                  , Kantoor              : houdtkantoor
                ]
          ]
    ]


INTERFACE " op naam" (zoekOpNaam)
                              : I[Zoek]
BOX [ "Record voor"           : zoekOpNaam
    BOX [ "BAR-nummer"        : barnummer~
        , Naam                : isadvocaat
        , Geboortedatum       : isadvocaat;geboortedatum
        , Beëdiging           : beëdigd~
        BOX [ Rechtbank       : locatieBeëdiging
            , Beëdigingsdatum : datumBeëdiging
            ]
        , Kantoor             : houdtkantoor
          ]
    ]

INTERFACE "Verzoek registreren" (isadvocaat, houdtkantoor, gerichtaan, inschrijvingseis) FOR Secretaris           
                              : I[Inschrijving] /\ -(beëdigd~;beëdigd)
BOX [ Verzoeker               : isadvocaat
    , Kantoor                 : houdtkantoor
    , "Gericht aan"           : gerichtaan
    , "Inschrijvingseisen"    : inschrijvingseis
    ]

INTERFACE "Te registreren Inschrijvingen" (beëdigd, datumBeëdiging, locatieBeëdiging) FOR "Administratief medewerker"        
                            : I[Inschrijving] /\ -(barnummer~;barnummer)
BOX [ Verzoeker             : isadvocaat
    , Kantooradres          : houdtkantoor
    , Beëdiging             : beëdigd~
      BOX [ Beëdigingsdatum   : datumBeëdiging
          , Beëdigingslocatie : locatieBeëdiging
          ]
    , Barnummer             : barnummer~
    , Status                : inschrijvingsstatus
    ]

INTERFACE Kantoorverplaatsing (houdtkantoor) FOR Secretaris           
                            : I[Inschrijving]
BOX [ Naam                  : isadvocaat
    , Kantooradres          : houdtkantoor
    , Beëdiging             : beëdigd~
      BOX [ Beëdigingsdatum   : datumBeëdiging
          , Beëdigingslocatie : locatieBeëdiging
          ]
    ]

INTERFACE Advocaatgegevens : I[Inschrijving]
BOX [ "BAR-nummer"        : barnummer~
    , Naam                : isadvocaat
    , Geboortedatum       : isadvocaat;geboortedatum
    , Beëdiging           : beëdigd~
      BOX [ Rechtbank       : locatieBeëdiging
          , Beëdigingsdatum : datumBeëdiging
          ]
    , Kantoor             : houdtkantoor
    ]

INTERFACE Kantoor (kantooradres) : I[Kantoor]
BOX[ Kantoor       : kantoornaam
   , Kantooradres  : kantooradres
   , Advocaten     : houdtkantoor~
   ]

ENDCONTEXT