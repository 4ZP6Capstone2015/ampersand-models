CONTEXT DemoRaadRechtsbijstand

INCLUDE "INDOORS.adl"
INCLUDE "DemoRaadRechtsbijstand.pop"

{- Wettelijke regelingen
Besluit aanwijzing instantie uitbetaling vergoeding werkzaamheden gerechtsdeurwaarders in rechtsbijstandzaken
Besluit draagkrachtcriteria rechtsbijstand
Besluit rechtsbijstand- en toevoegcriteria
Besluit vergoedingen rechtsbijstand 1994
Besluit vergoedingen rechtsbijstand 2000
Europese Overeenkomst inzake het verzenden van verzoeken om rechtsbijstand
Inschrijvingsvoorwaarden advocatuur 2012 krachtens de Wet op de Rechtsbijstand
Inschrijvingsvoorwaarden mediators 2012 krachtens de Wet op de Rechtsbijstand
Instellingsregeling Klachtencommissie rechtsbijstand asiel en vreemdelingenbewaring (KRAV)
Instellingsregeling Landelijke Adviescommissie Rechtsbijstand Asiel en vreemdelingenbewaring
Uitvoeringswet Verdrag inzake de toegang tot de rechter in internationale gevallen en Europese Overeenkomst inzake het doorzenden van verzoeken om rechtsbijstand
Vaststellingsbesluit normbedrag en bedrag voorschot Besluit vergoedingen rechtsbijstand 2000
Vaststellingsregeling Maatregelbeleid rechtsbijstandverlening asiel en vreemdelingenbewaring
Wet op de rechtsbijstand (WRB)
Wijzigingsbesluit Besluit vergoedingen rechtsbijstand 2000, enz. (aanpassen van de asielprocedure)
-}

CONCEPT "Heffingvrij vermogen" "het heffingvrij vermogen, bedoeld in de artikelen 5.5 en 5.6 van de Wet inkomstenbelasting 2001" "Artikel 1 lid 1 WRB"  -- Het heffingvrije vermogen bedraagt € 21 139, verhoogd met de ouderentoeslag.
CONCEPT Inkomen "Het inkomen wordt door de Raad bepaald op grond van de artikelen 34a tot en met 34d" "Artikel 1 lid 1 WRB"
CONCEPT Inkomensgegeven "Een inkomensgegeven is een gegeven over het inkomen van een persoon met bijbehorende temporele en meta-kenmerken, die door in inspecteur wordt verstrekt, zoals bedoeld in artikel 21, onderdeel e, van de Algemene wet inzake rijksbelastingen" "Artikel 1 lid 1 WRB"
CONCEPT Inspecteur "Een inspecteur is de functionaris die als zodanig bij ministeriële regeling is aangewezen op grond van artikel 2, derde lid, onder b, van de Algemene wet inzake rijksbelastingen" "Artikel 1 lid 1 WRB"
CONCEPT Mediation "Mediation is het bemiddelen in een geschil waarbij een neutrale bemiddelingsdeskundige de onderhandelingen tussen de rechtzoekende en zijn wederpartij begeleidt teneinde vanuit hun werkelijke belangen tot gezamenlijk gedragen en voor ieder van hen optimale resultaten te komen" "Artikel 1 lid 1 WRB"
CONCEPT Mediator "Een mediator is een overeenkomstig artikel 33b WRB ingeschreven persoon die mediation verricht." "Artikel 1 lid 1 WRB"
CONCEPT Peiljaar "Een peiljaar is het tweede kalenderjaar dat voorafgaat aan het kalenderjaar waarin de aanvraag om een toevoeging wordt gedaan" "Artikel 1 lid 1 WRB"
CONCEPT Rechtsbijstand "Rechtsbijstand is de rechtskundige bijstand aan een rechtzoekende ter zake van een rechtsbelang dat hem rechtstreeks en individueel aangaat, voor zover in deze wet en de daarop berustende bepalingen geregeld" "Artikel 1 lid 1 WRB"
CONCEPT Rechtsbijstandverlener "Een rechtsbijstandverlener is de advocaat, de medewerker van de voorziening voorzover belast met de verlening van rechtsbijstand en de personen, bedoeld in artikel 13, eerste lid, onder c" "Artikel 1 lid 1 WRB"
CONCEPT Rechtzoekende "Een rechtzoekende is degene die op grond van onvoldoende financiële draagkracht aanspraak kan maken op rechtsbijstand of mediation, voorzover in deze wet en de daarop berustende bepalingen geregeld, alsmede degene die zijn schade wil vorderen als slachtoffer van een misdrijf tegen de zeden of een geweldsmisdrijf" "Artikel 1 lid 1 WRB"
CONCEPT Toevoeging "Een beslissing van de Raad voor het vergoeden van rechtsbijstand als bedoeld in artikel 24, eerste lid, of mediation als bedoeld in artikel 33a" "Artikel 1 lid 1 WRB"
CONCEPT Vermogen "Een vermogen is het gemiddelde van de rendementsgrondslagen, bedoeld in artikel 5.2 van de Wet op de inkomstenbelasting 2001" "Artikel 1 lid 1 WRB"
CONCEPT Advocaat "In deze wet en de daarop berustende bepalingen worden onder advocaten mede verstaan de personen, bedoeld in artikel 16h van de Advocatenwet" "Artikel 1 lid 1 WRB"  -- Dit gaat over buitenlandse advocaten...

------------------
-- Demorelaties --
------------------

advocaatgegevens :: Document * Advocaatgegevens

heeftbarnummer :: Advocaatgegevens * Barnummer

deskundigheid :: Advocaatgegevens * Deskundigheid

clientgegevens :: Document * Persoonsgegevens

heeftBSN :: Persoonsgegevens * BurgerServiceNummer

heeftpartner :: Persoonsgegevens * Persoonsgegevens

betrekkingaanvraag :: Document * ZaakidRvR

statusaanvraag :: Document * Statusaanvraag

zaakidRvR :: ZaakidRvR * Zaakid

doorgehaald :: Advocaatgegevens * Boolean

financieelbelang :: ZaakidRvR * "Financieel belang"

soortbijstand :: ZaakidRvR * "Soort rechtsbijstand"

financielegegevens :: BurgerServiceNummer * Financielegegevens

vermogenstoets :: Financielegegevens * Vermogenstoets

inkomenstoets :: Financielegegevens * Inkomenstoets

categoriegrond :: Gronden * Categorie

----------------
-- Demoregels --
----------------
PROCESS Bijstandbeoordelen

ROLE Medewerker MAINTAINS bijstandafwijzenboete--, statusovertreding

ROLE Advocaat MAINTAINS gbaControle

--ROLE Bestuur MAINTAINS statusovertreding

RULE bijstandafwijzenboete : I[ZaakidRvR] |- zaakidRvR;zaaksdocument~;(type;'Dagvaarding';type~ /\ -(aanleiding;gronden;categoriegrond;'Eerste categorie';categoriegrond~;gronden~;aanleiding~));zaaksdocument;zaakidRvR~
MESSAGE "De aanvraag kan worden afgewezen omdat het strafzaak betreft waarin het aannemlijk is dat er een lage boete (eerste categorie) wordt opgelegd. (Art. 12 lid 2 sub c Wet op de Rechtsbijstand)"
VIOLATION (TXT "Aanvraag met kenmerk ", SRC betrekkingaanvraag~)

RULE gbaControle : clientgegevens;heeftBSN |- betrekkingaanvraag;zaakidRvR;partij~;ispartij~;gbaBSN
MESSAGE "De persoon op wie de zaak betrekking heeft, is een andere persoon dan waarvoor rechtsbijstand wordt aangevraagd."
VIOLATION(TXT "Betreffende BSN cliënt ", TGT gbaBSN~;gbaBSN)

RULE statusovertreding : statusaanvraag;'Toegekend';statusaanvraag~ |-  -(clientgegevens;heeftBSN;financielegegevens;vermogenstoets;'Voldoet niet voor rechtsbijstand';vermogenstoets~;financielegegevens~;heeftBSN~;clientgegevens~) \/ -(clientgegevens;heeftBSN;financielegegevens;inkomenstoets;'Voldoet niet voor rechtsbijstand';inkomenstoets~;financielegegevens~;heeftBSN~;clientgegevens~)
MESSAGE "De aanvraag is goedgekeurd, terwijl één of meer van de financiële gegevens vanuit BRI aangeven dat het recht op rechtsbijstand niet van toepassing is. (Art. 34 lid 1 Wet op de Rechtsbijstand)"
VIOLATION (TXT "Betreffende aanvraag met kenmerk ", SRC kenmerk)

ENDPROCESS

RULE kenmerkontbreekt : I[Document] |- kenmerk;kenmerk~
MESSAGE "Het kenmerk ontbreekt op de aanvraag."
VIOLATION (TXT "Betreffende aanvraag met kenmerk ", SRC kenmerk)

RULE advocaatgegevensontbreken : advocaatgegevens;advocaatgegevens~ \/ clientgegevens;clientgegevens~ \/ betrekkingaanvraag;betrekkingaanvraag~ |- advocaatgegevens;heeftbarnummer;heeftbarnummer~;advocaatgegevens~
MESSAGE "Niet alle gegevens betreffende de advocaat zijn ingevuld."
VIOLATION (TXT "Betreffende aanvraag met kenmerk ", SRC kenmerk)

RULE clientgegevensontbreken : advocaatgegevens;advocaatgegevens~ \/ clientgegevens;clientgegevens~ \/ betrekkingaanvraag;betrekkingaanvraag~ |- clientgegevens;heeftBSN;heeftBSN~;clientgegevens~ /\ clientgegevens;telefoonnummer;telefoonnummer~;clientgegevens~
MESSAGE "Niet alle gegevens betreffende de client zijn ingevuld."
VIOLATION (TXT "Betreffende aanvraag met kenmerk ", SRC kenmerk)

{-
RULE partnergegevensontbreken : advocaatgegevens;advocaatgegevens~ \/ clientgegevens;clientgegevens~ \/ betrekkingaanvraag;betrekkingaanvraag~ |- clientgegevens;heeftpartner;heeftBSN;heeftBSN~;heeftpartner~;clientgegevens~
MESSAGE "Niet alle gegevens betreffende de partner van de client zijn ingevuld."
-}

RULE zaakgegevensontbreken : advocaatgegevens;advocaatgegevens~ \/ clientgegevens;clientgegevens~ \/ betrekkingaanvraag;betrekkingaanvraag~ |- betrekkingaanvraag;zaakidRvR;zaakidRvR~;betrekkingaanvraag~ /\ betrekkingaanvraag;financieelbelang;financieelbelang~;betrekkingaanvraag~ /\ betrekkingaanvraag;soortbijstand;soortbijstand~;betrekkingaanvraag~
MESSAGE "Niet alle gegevens betreffende de zaak zijn ingevuld."
VIOLATION (TXT "Betreffende aanvraag met kenmerk ", SRC kenmerk)

-------------------------------------------
-- Interfaces voor in de zwarte menubalk --
-------------------------------------------
INTERFACE "Te beoordelen aanvragen" FOR Medewerker, Bestuur :I[ONE]
BOX
[ 
   "Kenmerk" : V[ONE*ZaakidRvR];betrekkingaanvraag~
]

INTERFACE "Advocaten" FOR Medewerker, Bestuur : I[ONE]
BOX
[
   "Ingeschreven advocaten" : V[ONE*Advocaatgegevens];heeftbarnummer
   BOX
   [
      "BAR-nummer"  : I[Barnummer]
   ,  Naam          : barnummer;isadvocaat
   ,  Deskundigheid : heeftbarnummer~;deskundigheid
   ]
]

INTERFACE "Verstuurde aanvragen" FOR Advocaat :I[ONE]
BOX
[
   "Kenmerk" : V[ONE*ZaakidRvR];betrekkingaanvraag~
]

----------------
-- Interfaces --
---------------- 

INTERFACE "Advocaat inschrijven" (heeftbarnummer, deskundigheid) FOR Advocaat
                 :I[Advocaatgegevens]
BOX
[
   "BAR-nummer"   : heeftbarnummer
   BOX
   [
      "BAR-nummer": barnummer;barnummer~
   ,  Naam        : barnummer;isadvocaat	  
   ,  Kantoor     : barnummer;houdtkantoor
   ]
,  Deskundigheid  : deskundigheid
]


INTERFACE Advocaatgegevens FOR Medewerker
                :I[Barnummer]
BOX
[
   "Bar-nummer"    : barnummer
   BOX
   [
      Naam            : isadvocaat	  
   ,  Telefoon        : telefoonnummer
   ,  Kantoorgegevens : houdtkantoor
      BOX
      [
         Kantoornaam  : I[Kantoornummer]
	  ,  Kantooradres : kantooradres
      ]
   ]
]


INTERFACE "Aanvraag toevoeging straf eerste aanleg" (kenmerk, advocaatgegevens, heeftbarnummer, clientgegevens, heeftBSN, heeftpartner, telefoonnummer[Persoonsgegevens*Telefoonnummer], betrekkingaanvraag, zaakidRvR, financieelbelang, soortbijstand) FOR Advocaat
                                  : I[Document]
BOX
[
   Kenmerk                        : kenmerk
,  Advocaatgegevens               : advocaatgegevens
   BOX
   [
      Registratienummer           : heeftbarnummer
	  BOX
	  [
         ""                       : I[Barnummer]   
	  ,  Naam                     : barnummer;isadvocaat
      ,  Kantoornaam              : barnummer;houdtkantoor
	  ]
   ]
,  "Persoonsgegevens cliënt"      : clientgegevens
   BOX
   [
      "BSN cliënt"                : heeftBSN
	  BOX
	  [
         ""                          : I[BurgerServiceNummer]
      ,	 Achternaam                  : gbaBSN~;gbaGeslachtsnaam
      ,  Voorletters                 : gbaBSN~;voorletters
      ,  Geboortedatum               : gbaBSN~;geboortedatum
      ,  Geslacht                    : gbaBSN~;geslacht
	  ,  Adresgegevens               : gbaBSN~;woonadres
         BOX
         [
            Straat                   : straatnaam
         ,  Huisnummer               : huisnummer
         ,  Postcode                 : postcode
         ,  Woonplaats               : woonplaats
	     ]
	  ]
      ,  Partnergegevens             : heeftpartner
	     BOX
	    [
	       "BSN partner"            : heeftBSN
		   BOX
		   [
             ""                        : I[BurgerServiceNummer]
           ,  Achternaam               : gbaBSN~;gbaGeslachtsnaam
	       ,  Voorletters              : gbaBSN~;voorletters
	       ,  Geboortedatum            : gbaBSN~;geboortedatum
	       ,  Geslacht                 : gbaBSN~;geslacht
	       ]
		]

   ,  Telefoonnummer                 : telefoonnummer
   ]
, Zaakgegevens                       : betrekkingaanvraag
  BOX
  [
     "Parketnummer"                  : zaakidRvR
	 BOX
	 [
        ""                           : I[Zaakid] 
	 ,  Behandeltype                 : behandeltype
     ,  Roldatum                     : zaaksdocument~;roldatum
     ,  Gerecht                      : behandelendgerecht
     ,  "Omschrijving rechtsprobleem": zaaksdocument~;aanleiding;omschrijvingaanleiding
	 ]
  ,  "Financieel belang"             : financieelbelang
  ,  "Soort rechtsbijstand"          : soortbijstand
  ]
 ]
 
INTERFACE "Beoordelen aanvraag" (statusaanvraag) FOR "Medewerker"
                                      : I[Document]
BOX
[
   Kenmerk                            : kenmerk
,  Advocaatgegevens                   : advocaatgegevens
   BOX
   [
      Registratienummer               : heeftbarnummer
	  BOX
	  [
         ""                           : I[Barnummer]
	  ,  Naam                         : barnummer;isadvocaat
      ,  Kantoornaam                  : barnummer;houdtkantoor
	  ]
   ]
,  "Persoonsgegevens cliënt"      : clientgegevens
   BOX
   [
      "BSN cliënt"                : heeftBSN
	  BOX
	  [
         ""                          : I[BurgerServiceNummer]
	  ,  Achternaam                  : gbaBSN~;gbaGeslachtsnaam
      ,  Voorletters                 : gbaBSN~;voorletters
      ,  Geboortedatum               : gbaBSN~;geboortedatum
      ,  Geslacht                    : gbaBSN~;geslacht
	  ,  Adresgegevens               : gbaBSN~;woonadres
         BOX
         [
            Straat                   : straatnaam
         ,  Huisnummer               : huisnummer
         ,  Postcode                 : postcode
         ,  Woonplaats               : woonplaats
	     ]
      ,  "Financiële gegevens uit BRI": financielegegevens
         BOX
         [
	        "Voldoet aan inkomenstoets"  : inkomenstoets
         ,  "Voldoet aan vermogenstoets" : vermogenstoets
	     ]
	  ]
      ,  Partnergegevens             : heeftpartner
	     BOX
	    [
	       "BSN partner"            : heeftBSN
		   BOX
		   [
	          ""                       : I[BurgerServiceNummer]
		   ,  Achternaam               : gbaBSN~;gbaGeslachtsnaam
	       ,  Voorletters              : gbaBSN~;voorletters
	       ,  Geboortedatum            : gbaBSN~;geboortedatum
	       ,  Geslacht                 : gbaBSN~;geslacht
	       ]
		]

   ,  Telefoonnummer              : telefoonnummer
  ]
,  Zaakgegevens                       : betrekkingaanvraag
   BOX
   [
      Parketnummer                    : zaakidRvR
	  BOX
	  [
	     ""                           : I[Zaakid]
	  ,  Behandeltype                 : behandeltype
      ,  Roldatum                     : zaaksdocument~;roldatum
      ,  Gerecht                      : behandelendgerecht
      ,  Kamer                        : behandelend
      ,  "Omschrijving rechtsprobleem": zaaksdocument~;aanleiding;omschrijvingaanleiding
	  ]
   ,  "Financieel belang"          : financieelbelang
   ,  "Soort rechtsbijstand"       : soortbijstand
   ]
,  "Status aanvraag"               : statusaanvraag
]


INTERFACE "Advocaat doorhalen" (doorgehaald) FOR Medewerker
                :I[Advocaatgegevens]
BOX
[  "BAR-nummer" : heeftbarnummer
,  Doorgehaald  : doorgehaald
]

ENDCONTEXT