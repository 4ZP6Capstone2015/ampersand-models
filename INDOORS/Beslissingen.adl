CONTEXT Beslissingen

--INCLUDE "Zaakgegevens.adl"
--INCLUDE "Zittinggegevens.adl"

-------------
-- Pattern --
-------------

PATTERN Beslissingen  -- hoort in INDOORS thema "Registratie"
PURPOSE PATTERN "Beslissingen"
{+
-}

-----------------
-- Invarianten --
-----------------

RULE eenzaak: beslissing~;zaak |- genomen;dient~
MEANING "De zaak waarover het vonnis uitgesproken wordt, is gelijk aan de zaak waar de beslissing over genomen is."
MESSAGE "De zaak waar het vonnis betrekking op heeft, komt niet overeen met de zaak waar de beslissing over genomen is."
PURPOSE RULE "eenzaak" IN DUTCH REF ""
{+ De beslissingen die in het vonnis zijn uitgesproken hebben betrekking op een zaak die gelijk is aan de zaak waarover de beslissingen zijn genomen.
-}

RULE geldigedagvaarding : verwijzing~ |- dagvaarding;V
MEANING "Het vonnis mag verwijzen naar geldige dagvaardingen."
MESSAGE "De dagvaarding waar het vonnis naar verwijst is ongeldig of niet-bestaand."
PURPOSE RULE "geldigedagvaarding" IN DUTCH REF "Art. 230 lid 2 Rv"
{+ Als er in een vonnis naar een dagvaarding verwezen wordt, moet deze dagvaarding geldig zijn.
-}

RULE genomendoor : genomen;dient~;rechtbankhoudt |- beslissing~;wijzen;rechter;rechtbank
MEANING "Een beslissing wordt genomen in naam van een orgaan."
PURPOSE RULE "genomendoor" IN DUTCH REF ""
{+ Beslissingen worden genomen door de rechtbank. De rechterlijke ambtenaar treedt dan op namens de rechtbank.
-}

RULE vonniswijzen : wijzen~;zaak[Vonnis*Zaakid] |- rechter;zittinghouden;dient~
MEANING "De rechter(s) die zitting houden, wijzen het vonnis."
MESSAGE "De rechters die het vonnis wijzen, zijn niet betrokken geweest bij de zitting van de zaak."
PURPOSE RULE "vonniswijzen" IN DUTCH REF ""
{+ Rechters die zitting houden aangaande een zaak, schrijven ook het vonnis over die zaak.
-}

----------------------------
-- Relaties en populaties --
----------------------------

plandatum :: Vonnis -> Datum [UNI] PRAGMA "" "wordt gedaan op" ""
MEANING "Een uitspraak staat gepland voor een datum."
PURPOSE RELATION plandatum[Vonnis*Datum] IN DUTCH REF "Art. 229 Rv"
{+ De rechter bepaalt de dag waarop hij uitspraak zal doen.
-}
POPULATION plandatum[Vonnis*Datum] CONTAINS
[ ("Vonnis 1", "30-05-2011")
; ("Vonnis 2", "14-12-2010")
; ("Vonnis 3", "26-09-2011")
]

vonnis :: Vonnis * Document PRAGMA "" "staat beschreven in" ""
MEANING "Een vonnis wordt schriftelijk vastgelegd."
PURPOSE RELATION vonnis[Vonnis*Document] IN DUTCH REF ""
{+ Het vonnis wordt schriftelijk vastgelegd tijdens de zitting.
-}
POPULATION vonnis[Vonnis*Document] CONTAINS
[ ("Vonnis 1", "Von11/123458")
; ("Vonnis 2", "Von10/569423")
; ("Vonnis 3", "Von11/489631")
]

partijnaam :: Vonnis * Partij PRAGMA "" "heeft betrekking op" ""
MEANING "Een vonnis bevat de namen van de betrokken partijen."
PURPOSE RELATION partijnaam[Vonnis*Partij] IN DUTCH REF "Art. 230 lid 1 sub a Rv"
{+ Het vonnis vermeldt de namen van de partijen.
-}
POPULATION partijnaam[Vonnis*Partij] CONTAINS
[ ("Vonnis 1", "Partij 1")
; ("Vonnis 2", "Partij 2")
; ("Vonnis 3", "Partij 3")
; ("Vonnis 1", "Partij 4")
; ("Vonnis 2", "Partij 5")
; ("Vonnis 3", "Partij 6")
]

zaak :: Vonnis * Zaakid PRAGMA "" "heeft betrekking op" ""
MEANING "Een vonnis heeft betrekking op een zaak(nummer)."
PURPOSE RELATION zaak[Vonnis*Zaakid] IN DUTCH REF "Art. 230 lid 1 sub b Rv"
{+ Een vonnis wordt geveld in een zaak.
-}
POPULATION zaak[Vonnis*Zaakid] CONTAINS
[ ("Vonnis 1", "Zaak 4")
; ("Vonnis 2", "Zaak 5")
; ("Vonnis 3", "Zaak 6")
]

beslissing :: Vonnis * Beslissing PRAGMA "" "bevat de tekst van" ""
MEANING "Het vonnis vermeldt de beslissing."
PURPOSE RELATION beslissing[Vonnis*Beslissing]  IN DUTCH REF "Art. 230 lid 1 sub f Rv"
{+
-}
POPULATION beslissing[Vonnis*Beslissing] CONTAINS
[ ("Vonnis 1", "Beslissing 1")
; ("Vonnis 2", "Beslissing 2")
; ("Vonnis 3", "Beslissing 3")
]

wijzen :: Vonnis * Rechter PRAGMA "" "is gewezen door" ""
MEANING "Een vonnis is gewezen door een of meerdere rechters."
PURPOSE RELATION wijzen[Vonnis*Rechter] IN DUTCH REF "Art. 230 lid 1 sub g Rv"
{+ Het vonnis vermeldt de naam van de rechter of, bij een meervoudige kamer, de namen van de rechters door wie het vonnis is gewezen.
-}
POPULATION wijzen[Vonnis*Rechter] CONTAINS
[ ("Vonnis 1", "Rechter 9")
; ("Vonnis 2", "Rechter 12")
; ("Vonnis 3", "Rechter 7")
; ("Vonnis 1", "Rechter 10")
]

wijsdatum :: Vonnis * Datum PRAGMA "" "is gewezen op" ""
MEANING "Het vonnis vermeldt de dag van de uitspraak."
PURPOSE RELATION wijsdatum[Vonnis*Datum] IN DUTCH REF "Art. 230 lid 1 sub h Rv"
{+
-}
POPULATION wijsdatum[Vonnis*Datum] CONTAINS
[ ("Vonnis 1", "30-05-2011")
; ("Vonnis 2", "14-12-2010")
; ("Vonnis 3", "26-09-2011")
]

verwijzing :: Vonnis * Dagvaarding PRAGMA "" "verwijst naar" ""
MEANING "Een vonnis kan verwijzen naar een dagvaarding."
PURPOSE RELATION verwijzing[Vonnis*Dagvaarding] IN DUTCH REF "Art. 230 lid 2 Rv"
{+ Indien verstek is verleend en de vorderingen van de eiser geheel of gedeeltelijk worden toegewezen, verwezen worden naar een dagvaarding waarop het vonnis wordt gesteld.
-}
POPULATION verwijzing[Vonnis*Dagvaarding] CONTAINS
[ ("Vonnis 1", "Dagvaarding 1")
; ("Vonnis 2", "Dagvaarding 2")
; ("Vonnis 3", "Dagvaarding 3")
]

afschrift :: Vonnis * Bericht PRAGMA "Een afschrfit van " "is verstuurd in" ""
MEANING "Een afschrift van het vonnis kan worden verstuurd naar betrokken partijen."
PURPOSE RELATION afschrift[Vonnis*Bericht] IN DUTCH REF "Art. 231 lid 1 en 2 Rv"
{+ Van de vonnissen verstrekt de griffier op de dag van de uitspraak een afschrift aan de eiser en aan de gedaagde die in het geding is verschenen.
-}
POPULATION afschrift[Vonnis*Bericht] CONTAINS
[ ("Vonnis 1", "Msg566")
; ("Vonnis 2", "Msg795")
; ("Vonnis 3", "Msg791")
]

tussenvonnis :: Vonnis * Document PRAGMA "" "staat beschreven in" ""
MEANING "Een vonnis kan de vorm van tussenvonnis hebben."
PURPOSE RELATION tussenvonnis[Vonnis*Document] IN DUTCH REF "Art. 232 Rv"
{+ De rechter kan, voordat hij definitief over de zaak beslist, een tussenvonnis wijzen.
-}
POPULATION tussenvonnis[Vonnis*Document] CONTAINS
[ ("Vonnis 1", "TusVon11/123458-1")
; ("Vonnis 2", "TusVon10/569423-1")
; ("Vonnis 3", "TusVon11/489631-1")
]

genomen :: Beslissing * Zitting [UNI] PRAGMA "" "is genomen tijdens" ""
MEANING "Een beslissing wordt genomen tijdens een zitting."
PURPOSE RELATION genomen[Beslissing*Zitting] IN DUTCH REF ""
{+  
-}
POPULATION genomen[Beslissing*Zitting] CONTAINS
[ ("Beslissing 1", "Zitting RbLee 591")
; ("Beslissing 2", "Zitting RbsGr 35")
; ("Beslissing 3", "Zitting RbAlm 68")
]

	
beslist :: Beslissing -> Rechter PRAGMA "" "is genomen door" ""
MEANING "De rechter beslist over al hetgeen partijen hebben gevorderd of verzocht."
PURPOSE RELATION beslist[Beslissing*Rechter] IN DUTCH REF "Art. 23 Rv; Art. 26 Rv"
{+ Beslissingen worden genomen door een of meerdere personen.
-}
POPULATION beslist[Beslissing*Rechter] CONTAINS
[ ("Beslissing 1", "Rechter 9")
; ("Beslissing 2", "Rechter 12")
; ("Beslissing 3", "Rechter 7")
; ("Vts2", "Rechter 8")
]

hogerberoep :: Document * Beslissing PRAGMA "" "is gericht tegen" ""
MEANING "Tegen een beslissing kan bezwaar worden ingediend."
PURPOSE RELATION hogerberoep[Document*Beslissing] IN DUTCH REF "Art. 130 Rv"
{+
-}

-------------------
-- Specialisatie --
-------------------

{-  
  SPEC Audiëntieblad ISA Document
  SPEC Grosse ISA Document
  SPEC Instructie ISA Document
  SPEC Minuut ISA Document
  SPEC Overeenkomst ISA Document
  SPEC "Proces Verbaal" ISA Document
  SPEC Rechtsmiddel ISA Document
  SPEC Vonnis ISA Document
-}

ENDPATTERN


ENDCONTEXT