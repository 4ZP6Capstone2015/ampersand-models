CONTEXT Berichten

META "authors" "Leon Chevalking"

--THEMES Berichten

PATTERN Berichten
PURPOSE PATTERN "Berichten"
{+
-}

--------------------
-- Specialisaties --
--------------------

{-
SPEC "Rechterlijke macht" ISA Vestiging --Was orgaan, voor de duidelijkheid binnen de rechtspraak veranderen in "Rechterlijke macht"
SPEC Vestiging ISA Instantie
SPEC Opsporingsinstantie ISA Instantie
SPEC Bericht ISA Gebeurtenis
-}

----------
-- Keys --
----------
VIEW storage: Object(identifier, TXT " ", version;versieNummer)
IDENT versie: VersieID(versieNummer)

--------------
-- Relaties --
--------------
aan :: Bericht *  NatuurlijkPersoon [TOT] PRAGMA "" "is gericht aan" ""
MEANING "Een bericht is gericht aan een persoon."
PURPOSE RELATION aan[Bericht*NatuurlijkPersoon] IN DUTCH REF ""
{+
-}

verzonden :: Bericht -> Tijdstip PRAGMA "" "is verzonden op" ""
MEANING "Een bericht is verzonden op een te bepalen tijdstip."
PURPOSE RELATION verzonden[Bericht*Tijdstip] IN DUTCH REF ""
{+
-}

ontvangen :: Bericht *  Tijdstip [UNI] PRAGMA "" "is ontvangen op" ""
MEANING "Een bericht is ontvangen op een te bepalen tijdstip."
PURPOSE RELATION ontvangen[Bericht*Tijdstip] IN DUTCH REF ""
{+
-}

antwoordOp :: Bericht * Bericht PRAGMA "" "is het antwoord op" ""
MEANING "Berichten kunnen het antwoord zijn op eerder verstuurde berichten."
PURPOSE RELATION antwoordOp[Bericht*Bericht] IN DUTCH REF ""
{+ Om volgordelijkheid aan te tonen nemen berichten een referentie naar eerder verstuurde berichten op.
-}

gegevensbron :: Bericht * Bron PRAGMA "" "is afkomstig uit" ""
MEANING "Elk bericht is afkomstig van een bepaalde bron."
PURPOSE RELATION gegevensbron [Bericht*Bron] IN DUTCH REF ""
{+ Het systeem dat het bericht heeft samengesteld.
-}

identifier :: Object -> Ref PRAGMA "" "wordt geïdentificeerd door" ""
MEANING "Een object is te onderscheiden door een identifier."
PURPOSE RELATION identifier[Object*Ref] IN DUTCH REF ""
{+ Objecten zijn aan een unieke identifier herkenbaar.
-}

refer :: Ref -> Object PRAGMA "" "refereert naar" ""
MEANING "Een identifier wordt gebruikt om naar een object te refereren. Zelfs als het object verandert gedurende de levenscyclus van het object (bijv. een document), blijft de identifier hetzelfde onder de meest recente versie te bereiken."
PURPOSE RELATION refer[Ref*Object] IN DUTCH REF ""
{+ Objecten worden via een referentie benaderd, omdat ze kunnen veranderen in de loop van de tijd.
Documenten zijn daar een voorbeeld van.
De referentie is blijvend, waardoor de meest recente versie van het object bereikbaar is.
-}

------------------------------
-- Versie- en mutatiebeheer --
------------------------------
lt :: VersieID * VersieID [ASY,TRN] PRAGMA "" "is ouder dan" ""
MEANING "Deze relatie representeert de kleiner-dan relatie over versies."
PURPOSE RELATION lt[VersieID*VersieID] IN DUTCH REF ""
{+ Deze relatie representeert de kleiner-dan relatie op versies.
-}

version :: Object -> VersieID PRAGMA "De versie van " "is" ""
MEANING "Elk object heeft een versie."
PURPOSE RELATION version[Object*VersieID] IN DUTCH REF ""
{+ Elk object heeft een versie.
-}

pre :: Gebeurtenis -> Object PRAGMA "" "bracht veranderingen aan in" ""
MEANING "Als een gebeurtenis optreedt, kan het een object veranderen. Dat object kan gezien worden als de input van de operatie die door de gebeurtenis wordt getriggerd."
PURPOSE RELATION pre[Gebeurtenis*Object] IN DUTCH REF ""
{+ Een gebeurtenis kan een object doen veranderen.
Dat object mag als input worden beschouwd van de operatie die door het event wordt getriggerd.
-}
post :: Gebeurtenis -> Object PRAGMA "" "resulteerde in veranderingen in" ""
MEANING "Als een gebeurtenis optreedt, kan het een object veranderen. Dat object kan gezien worden als de output van de operatie die door de gebeurtenis wordt getriggerd."
PURPOSE RELATION post[Gebeurtenis*Object] IN DUTCH REF ""
{+ Dat object mag worden beschouwd als output van de operatie die door het event wordt getriggerd.
-}

evolves :: Object * Object PRAGMA "" "is veranderd in" ""
MEANING "Als een gebeurtenis optreedt, kan het een object veranderen. Dat object kan gezien worden als de output van de operatie die door het object wordt getriggerd."
PURPOSE RELATION evolves[Object*Object] IN DUTCH REF ""
{+
-}

changes :: Gebeurtenis * Gebeurtenis [SYM,ASY] PRAGMA "" "heeft tot een update geleid van" ""
MEANING "De eigenschap 'changes' zegt of een event zorgt dat een object verandert."
PURPOSE RELATION changes[Gebeurtenis*Gebeurtenis] IN DUTCH REF ""
{+ 
-}

versieNummer :: VersieID * Versie PRAGMA "" "heeft versienummer" ""
MEANING "Een versie wordt gekenmerkt door een versienummer."
PURPOSE RELATION versieNummer[VersieID*Versie] IN DUTCH REF ""
{+
-}

versie :: Bericht * VersieID PRAGMA "" "heeft als versienummer" ""
MEANING "Een bericht heeft een versienummer."
PURPOSE RELATION versie[Bericht*VersieID] IN DUTCH REF ""
{+ De versie van het bedrijfsdocument.
-} 
 
-----------------
-- Invarianten --
-----------------
RULE "versienummer toekennen": version~;pre~;changes;post;version |- lt
MEANING "Het optreden van een gebeurtenis, verhoogt het versienummer van objecten."
PURPOSE RULE "versienummer toekennen" IN DUTCH
{+ Events verhogen het versienummer van objecten.
-}

RULE "recentste versie": version~;identifier;refer;version |- lt \/ I[VersieID]
MEANING "De relatie 'refer' verwijst naar de meest recente versie van een object."
PURPOSE RULE "recentste versie" IN DUTCH REF ""
{+ De relatie 'refer' wijst de meest recente versie van een object aan.
-}

RULE "verandering": changes = pre;(-I/\identifier;identifier~);post~ 
MEANING "Een gebeurtenis verandert een object als het 'pre-object' ongelijk is aan het 'post-object', maar beiden delen dezelfde identifier."
PURPOSE RULE "verandering" IN DUTCH REF ""
{+ Een verandering aan een object betekent dat het pre-object en het post-object van elkaar verschillen,
ondanks dat zij dezelfde identifier hebben.
-}

IDENT storage: identifier;identifier~ /\ version;version~ |- I
MEANING "Als de identifier en het versienummer bekend zijn, dan is het object uniek te bepalen. Merk het verschil op de relatie 'refer', dat de meest recente versie van een object identificeert, enkel door de identifier."
PURPOSE IDENT storage IN DUTCH REF ""
{+ Elk object wordt uniek bepaald door de identifier en het versienummer.
Dit verschilt van de relatie 'refer', die de meest recente versie van een object aanwijst.
-}

RULE berichtZonderAfzender: I[Bericht] |- gegevensbron;gegevensbron~
MEANING "Berichten hebben altijd een bron."
PURPOSE RULE "berichtZonderAfzender" IN DUTCH
{+ Berichten kunnen niet verstuurd worden zonder afzender.
-}
 
ENDPATTERN  



ENDCONTEXT