CONTEXT DemoBeslissingen

INCLUDE "INDOORS.adl"
INCLUDE "DemoBeslissingen.pop"

----------
-- Keys --
----------

--VIEW zaak : Zaakid(zaakid)


-----------------
-- Demo regels --
-----------------

RULE zaaksdocument~;typebeslissing;'Beslissingstype 3';typebeslissing~;zaaksdocument |- -(zaaksdocument~;type;'Verweerschrift';type~;zaaksdocument)

--------------------------------------
-- Items voor in de zwarte menubalk --
--------------------------------------

INTERFACE Vonnissen FOR "Juridisch medewerker" : I[ONE]
BOX [ "Te schrijven vonnissen" : (V[ONE*Zaakid];onderzoekgesloten;onderzoekgesloten~ \/ V[ONE*Zaakid];behandeld;behandelinggesloten;behandelinggesloten~;behandeld~ )/\ -(V[ONE*Zaakid];zaaksdocument~;wijzen;wijzen~;zaaksdocument)
    ]

INTERFACE "Vonnissen " FOR Rechter : I[ONE]
BOX [ "Eindvonnis schrijven voor zaak:"      : V[ONE*Zaakid];onderzoekgesloten;onderzoekgesloten~ /\ -(V[ONE*Zaakid];zaaksdocument~;typebeslissing;'Beslissingstype 1';typebeslissing~;zaaksdocument)
    , "Tussenvonnis schrijven voor zaak:"    : V[ONE*Zaakid];onderzoekgesloten;onderzoekgesloten~ /\ -(V[ONE*Zaakid];zaaksdocument~;typebeslissing;'Beslissingstype 1';typebeslissing~;zaaksdocument)
    , "Te ondertekenen vonnissen voor zaak:" : V[ONE*Zaakid];zaaksdocument~;typebeslissing;typebeslissing~;zaaksdocument /\ (V[ONE*Zaakid];zaaksdocument~;motivering;motivering~;zaaksdocument \/ V[ONE*Zaakid];zaaksdocument~;beslissing;beslissing~;zaaksdocument) /\ -(V[ONE*Zaakid];zaaksdocument~;wijzen;wijzen~;zaaksdocument)
    ]

INTERFACE Afschriften FOR "Administratief medewerker"
                                 : I[ONE]
BOX [ "Te versturen afschriften" : V[ONE*Zaakid];zaaksdocument~;wijzen;wijzen~;zaaksdocument
    ]

----------------------------------------------------
-- Interfaces die nagenoeg gelijk aan elkaar zijn --
----------------------------------------------------


INTERFACE "Vonnis bijwerken" (motivering, beslissing) FOR Rechter
               : I[Document] --/\ -(type;type~)
BOX
[
  Zaaknummer     : zaaksdocument
, Kenmerk        : kenmerk
, Documenttype   : typebeslissing
, Motivering     : motivering
, Beslissingen   : beslissing
, Datum          : wijsdatum
, "Gewezen door" : wijzen
]


INTERFACE "Vonnis schrijven" (zaaksdocument, motivering, typebeslissing, kenmerk, beslissing) FOR Rechter, "Juridisch medewerker"
                       : I[Zaakid] /\ -(zaaksdocument~;typebeslissing;'Beslissingstype 1';typebeslissing~;zaaksdocument)
BOX [ Zaaknummer       : zaakid;zaakid~
    , Uitspraak        : zaaksdocument~
    BOX [ Kenmerk      : kenmerk
        , Documenttype : typebeslissing
        , Motivering   : motivering
        , Beslissingen : beslissing
        ]
    ]


INTERFACE "Vonnis inzien" FOR Rechter, "Juridisch medewerker", "Administratief medewerker"
                         : I[Zaakid];zaaksdocument~;wijzen;wijzen~;zaaksdocument
BOX [ Zaak               : zaaksdocument~ /\ zaaksdocument~;typebeslissing;'Beslissingstype 1';typebeslissing~
    BOX [ Kenmerk        : kenmerk
        , Documenttype   : typebeslissing
        , Motivering     : motivering
        , Beslissingen   : beslissing
        , Datum          : wijsdatum
        , "Gewezen door" : wijzen
        ]
    ]

INTERFACE "Vonnis ondertekenen" (wijsdatum, wijzen) FOR Rechter
                         : I[Zaakid] /\ (-(zaaksdocument~;wijzen;wijzen~;zaaksdocument) \/ zaaksdocument~;beslissing;beslissing~;zaaksdocument \/ zaaksdocument~;motivering;motivering~;zaaksdocument)
BOX [ Zaak               : zaaksdocument~
    BOX [ Kenmerk        : kenmerk
        , Documenttype   : typebeslissing
        , Motivering     : motivering
        , Beslissingen   : beslissing
        , Datum          : wijsdatum
        , "Gewezen door" : wijzen
        ]
    ]

INTERFACE "Afschrift vonnis versturen" (kenmerk, afschriftvan, datumafschrift, afschriftnaar, gbaVoornamen, gbaGeslachtsnaam, woonadres, straatnaam, huisnummer, postcode, woonplaats) FOR "Administratief medewerker"
                                  : I[Zaakid];zaaksdocument~;wijzen;wijzen~;zaaksdocument
BOX [ Uitspraak : zaaksdocument~
      BOX [ Kenmerk               : kenmerk
          , Documenttype          : typebeslissing
          , Motivering            : motivering
          , Deelbeslissingen      : beslissing
          , "Datum ondertekening" : wijsdatum
          , "Gewezen door"        : wijzen
          , Afschrift             : afschriftvan
          BOX [ Kenmerk           : kenmerk
              , Datum             : datumafschrift
              , "Verstuurd naar"  : afschriftnaar
              BOX [ Voornaam      : gbaVoornamen
                  ,  Achternaam    : gbaGeslachtsnaam
                  ,  Adres        : woonadres
                     BOX
                     [
	                    Straatnaam            : straatnaam
                     ,  Huisnummer            : huisnummer
                     ,  Woonplaats            : woonplaats
                     ,  Postcode              : postcode
                     ]
				  ]
              ]
          ]
    ]





ENDCONTEXT