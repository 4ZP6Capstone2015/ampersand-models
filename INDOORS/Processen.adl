CONTEXT Processen

--INCLUDE "INDOORS.adl"
INCLUDE "Advocatentableau.adl"
--INCLUDE "Persoonsgegevens.adl"
INCLUDE "Zaakgegevens.adl"
INCLUDE "Zittinggegevens.adl"
INCLUDE "Dossiers.adl"
INCLUDE "Beslissingen.adl"
INCLUDE "Gebeurtenis.adl"

-----------------------------
-- Proces advocatentableau --
-----------------------------
PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}


ROLE "Juridisch medewerker" MAINTAINS ingeschrevenAdvocaat
ROLE Griffie MAINTAINS moetwordenbeëdigd, opleidingseisvoldaan

RULE moetwordenbeëdigd:  inschrijvingseis |- beëdigd~; V
MEANING "Een inschrijvingsverzoek waarvan gestelde opleidingseis gecontroleerd is, moet uiteindelijk leiden tot een beëdiging."
MESSAGE "Er zijn inschrijvingsverzoeken die nog niet tot een beëdiging hebben geleid, als bedoeld in Art. 3 lid 1 Advw"
PURPOSE RULE moetwordenbeëdigd IN DUTCH REF "Art. 3 lid 1 Advw"
{+ Geaccepteerde inschrijvingsverzoeken leiden tot beëdigingen.
-}


RULE ingeschrevenAdvocaat: beëdigd~ |- barnummer~;V
MEANING "Elke beëdigde advocaat moet uiteindelijk op het tableau worden ingeschreven."
MESSAGE "De beëdigde advocaat dient ingeschreven te worden op het tableau. (Art. 3 lid 1 Advw)"
PURPOSE RULE ingeschrevenAdvocaat IN DUTCH REF "Art. 3 lid 1 Advw"
{+ De griffier van de rechtbank licht de griffier van de algemene raad in over de beëdiging ingelicht, zodat de secretaris van de NOVA de advocaat op het tableau in kan schrijven.
-}


RULE opleidingseisvoldaan: inschrijvingseis |- inschrijvingseis;(I[Inschrijvingseis]='voldoet')
MEANING "Een inschrijvingsverzoek wordt geaccepteerd als de indiener aan de inschrijvingseisen voldoet."
MESSAGE "Het inschrijvingsverzoek is nog niet gecontroleerd op alle inschrijvingseisen. (Art. 2 Advw)"
PURPOSE RULE "opleidingseisvoldaan" IN DUTCH REF "Art. 2 Advw"
{+ Een inschrijvingsverzoek kan alleen worden ingeschreven als het aan de inschrijvingseisen voldoet.
-}

ENDPROCESS

----------------------------
-- Processen zaakgegevens --
----------------------------
PROCESS "Civiele zaak"

ROLE "Administratief medewerker" MAINTAINS betaling

RULE betaling : behandeld |- griffierechtbetaald;V
MESSAGE "Er zijn geagendeerde zittingen waarvan het griffierecht nog niet betaald is."
VIOLATION (TXT "Betreffende zaak ", TGT behandeld~;zaakid)

ROLE Griffie MAINTAINS "losse dagvaarding"--, procesverbaal

RULE "losse dagvaarding" : dagvaarding |- aanhangig;zaakid; V
MEANING "Dagvaardingen kunnen aanhangig gemaakt worden door er een zaaknummer aan te koppelen."
MESSAGE "Er is een dagvaarding die nog niet gekoppeld is aan een zaaknummer."
VIOLATION (TXT "Een zaaknummer moet worden toegewezen aan dagvaarding ", TGT dagvaarding~;dagvaarding)

{-
RULE procesverbaal : behandeld~ |- procesverbaal; V
MESSAGE "Er is een geplande zitting zonder procesverbaal."
VIOLATION (TXT "Van de zaak met zaaknummer ", TGT dient;dient~, TXT " is nog geen proces-verbaal van de zitting gemaakt.")
-}

ROLE Advocaat MAINTAINS dagvaardingtot
ROLE Burger MAINTAINS dagvaardingtot
ROLE "Officier van Justitie" MAINTAINS dagvaardingtot

RULE dagvaardingtot : I |- aanhangig;aanhangig~

ENDPROCESS

--------------------------------
-- Procesregels Bestuurszaken --
--------------------------------
PROCESS Bestuurszaken

ROLE Griffie MAINTAINS registreren

RULE registreren : aanhangigbestuur~ |- zaakid;zaakid~;V
MEANING "Een beroepschrift wordt geregistreerd als zaak door een zaaksnummer toe te kennen."
MESSAGE "Er is aan aanhangig beroep zonder zaaknummer."
VIOLATION (TXT "Beroep met kenmerk ", TGT beroepschrift, TXT " heeft nog geen zaaksnummer")
PURPOSE RULE "registreren" IN DUTCH REF "Art. 8:10 lid 1 Awb"
{+ Door een zaak te openen op basis van het binnengekomen beroepschrift, is de zaak administratief geregistreerd.
-}

ENDPROCESS

---------------------------------
-- Algemene procesregels zaken --
---------------------------------

PROCESS	"Procesregels zaken"

{-
RULE gewraakt: wraking;bezetting;kamer~;dient~ |- wrakingsverzoek;partij
MEANING "Elke rechter die zitting neemt in een bepaalde zaak, kan worden gewroken door een persoon die de rol van partij heeft in die zaak."
PURPOSE RULE gewraakt IN DUTCH REF "Art. 36 Rv"
{+ Het wrakingsverzoek tegen een persoon die als rechter zitting neemt in een zaak die zij behandelen, kan alleen zijn ingediend op verzoek van een partij die betrokken is bij die zaak.
-}

ENDPROCESS

-------------------------------
-- Processen zittinggegevens --
-------------------------------
PROCESS "Procesregels Zittingen"

--ROLE Griffie MAINTAINS zittingsluiten

{-
RULE "agendering": geagendeerd |- datum[Zitting*Datum]
MEANING "De griffie draagt zorg dat alle geagendeerde zittingen op den duur plaatsvinden."
PURPOSE RULE "agendering" IN DUTCH
{+ Geagendeerde zittingen vinden uiteindelijk plaats. (Dit geldt niet voor civiele zaken, checken voor bestuur en straf)
-}

ENDPROCESS


------------------------
-- Processen dossiers --
------------------------

PROCESS "Dossier handhaven"

ROLE Griffie MAINTAINS "volledig dossier"


RULE "volledig dossier" : zaakstuk;zaaksdossier = zaaksdocument
MEANING "Alle documenten die bij een zaak horen, zitten in het zaaksdossier."
MESSAGE "Er is een document dat nog niet toegekend is aan een zaak."
PURPOSE  RULE "volledig dossier"
{+Dossiers worden per zaak aangelegd.
We noemen een dossier volledig wanneer alle stukken die op een zaak betrekking hebben in dat dossier zitten.
-}


ENDPROCESS


----------------------------
-- Processen beslissingen --
----------------------------

PROCESS Vonnis

ROLE Rechter MAINTAINS vonnissen

ROLE "Juridisch medewerker" MAINTAINS vonnissen

ROLE Griffie MAINTAINS vonnissen

RULE vonnissen : plandatum |- vonnis;V
MEANING "Een vonnis dat gepland staat, moet uiteindelijk gedaan worden."
MESSAGE "Er staat een vonnis gepland, dat nog niet geregistreerd is."
PURPOSE RULE "vonnissen" IN DUTCH REF ""
{+ Als de rechter aangeeft uitspraak te doen in een zaak, moet het vonnis geregistreerd worden door de rechtspraak.
-}


ENDPROCESS


ENDCONTEXT