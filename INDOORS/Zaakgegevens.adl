CONTEXT Zaakgegevens

INCLUDE "Zittinggegevens.adl"
INCLUDE "Persoonsgegevens.adl"

--THEMES "Bestuursrecht"

--------------
-- Patterns --
--------------
PATTERN Zaakgegevens -- hoort in INDOORS thema "Registratie"
PURPOSE PATTERN "Zaakgegevens"
{+Het gegevensgebied "Zaakgegevens" bevat de gestructureerde gegevens van en over zaken.
Deze gegevens maken het zaaksgericht werken en procesbesturing mogelijk.
Onder zaken verstaan we niet alleen straf-, burgerlijke, bestuursrechterlijke zaken en hoger beroep,
maar ook registerhandelingen, toezicht en andere wettelijke taken die door rechterlijke ambtenaren als zaak worden behandeld.
De zaaksdocumenten (de ongestructureerde gegevens) bevinden zich in dossiers (gegevensgebied: Dossiers).
Op de werkplek is een zaak als één geheel te raad-plegen en te bewerken.
Tijdens het werken aan een zaak ziet een gebruiker zaaksgegevens en documenten in één scherm

In de pre-INDOORS situatie wordt het zaaksverloop in de sectorale systemen Civiel, NKP, Straf en Berber geregistreerd.
De gestructureerde gegevens zullen in INDOORS uit-eindelijk in één zaakregistratie worden bijgehouden, met behoud van sectorale verschillen.
Het zaaksnummer is/wordt landelijk geüniformeerd.
-}

----------
-- Keys --
----------

KEY dagvaarding : Dagvaarding (dagvaarding)
KEY beroepschrift : Beroepschrift (beroepschrift)
KEY zaak : Zaakid (zaakid)
KEY partij : Partij (partijnaam)
KEY kamer : Kamerid (TXT "de ", samenstelling, TXT " kamer, met als specialiteit ", specialisme)

---------------
-- Concepten --
---------------

CONCEPT Zaak ""

CONCEPT Kamer ""

CONCEPT Dagvaarding ""

CONCEPT Partij ""

CONCEPT Sector "" 

CONCEPT Specialiteit ""

----------------------------
-- Relaties en populaties --
----------------------------

zaakid :: Zaakid * Zaak [UNI] PRAGMA "" "heeft zaaksnummer" ""
MEANING "Een zaak wordt uniek gekenmerkt door een zaaksnummer."
PURPOSE RELATION zaakid[Zaakid*Zaak] IN DUTCH REF ""
{+ Een zaak heeft een uniek zaaksnummer.
-}
POPULATION zaakid[Zaakid*Zaak] CONTAINS
[ ("Zaak 4", "11/123458")
; ("Zaak 5", "10/569423")
; ("Zaak 6", "11/489631")
; ("Zaak 7", "08/476197")
; ("Zaak 10", "09/565223")
; ("Zaak 11", "12/00431")
; ("Zaak 12", "11/100197")
]

partijnaam :: Partij -> Partijnaam PRAGMA "" "heeft als naam" ""
MEANING "Een partij heeft een naam."
PURPOSE RELATION partijnaam[Partij*Partijnaam] IN DUTCH REF ""
{+
-}
POPULATION partijnaam[Partij*Partijnaam] CONTAINS
[ ("Partij 1", "B. Stolen")
; ("Partij 2", "A. Angereden")
; ("Partij 3", "O.P.G. Elicht")
; ("Partij 4", "B. Eul")
; ("Partij 5", "T.A. Sjesdief")
; ("Partij 6", "F.R. Audeur")
; ("Partij 7", "S.A. Wiltschut")
; ("Partij 8", "S. Verhoeven")
; ("Partij 9", "F. Eggink")
; ("Partij 10", "J. Honkoop")
; ("Partij 11", "R.J. Jansen")
]

deskundige :: Partij * Zaakid PRAGMA "" "is deskundige in" "" --LC: Is een deskundige een partij of een persoon?
MEANING "Een partij kan als deskundige benoemd worden bij een zaak."
PURPOSE RELATION deskundige[Partij*Zaakid] IN DUTCH REF "Art. 51i Sv."
{+ Op de wijze bij de wet bepaald wordt een deskundige benoemd met een opdracht tot het geven van informatie over of het doen van onderzoek op een terrein, waarvan hij specifieke of bijzondere kennis bezit.
-}
POPULATION deskundige[Partij*Zaakid] CONTAINS
[ ("Partij 9", "Zaak 4")
; ("Partij 10", "Zaak 5")
; ("Partij 11", "Zaak 6")
]

benadeeld :: Partij * Zaakid PRAGMA "" "is als benadeelde partij gevoegd in" ""
MEANING "Een partij kan zich als een benadeelde partij bij een zaak voegen."
PURPOSE RELATION benadeeld[Partij*Zaakid] IN DUTCH REF "Art. 51f-1,§2, Sv."
{+ In sommige soorten zaken kan een partij zich voegen als benadeelde. De benadeelde wordt in Art. 51f Sv met de term slachtoffer aangeduidt.
-}
POPULATION benadeeld[Partij*Zaakid] CONTAINS
[ ("Partij 1", "Zaak 4")
; ("Partij 2", "Zaak 5")
; ("Partij 3", "Zaak 6")
]

gevoegde :: Partij * Zaakid PRAGMA "" "heeft de rol van belanghebbende in zaak" ""
MEANING "Een partij kan als belanghebbende bij een zaak verschijnen."
PURPOSE RELATION gevoegde[Partij*Zaakid] IN DUTCH REF "Art. 8:26-1, Awb, Art. 217 Rv en Art. 271 Rv"
{+ In sommige soorten zaken kan een partij zich voegen als belanghebbende. De gevoegde partij wordt volgens Art. 217 Rv ook aangeduid als tussenkomende partij. In Art. 271 Rv wordt vermeld dat de belanghebbende opgeroepen wordt door de grifier en zich niet actief kan voegen.
-}
POPULATION gevoegde CONTAINS
[ ("Partij 4", "Zaak 4")
; ("Partij 5", "Zaak 5")
; ("Partij 6", "Zaak 6")
; ("Partij 7", "Zaak 4")
; ("Partij 8", "Zaak 5")
]


klager :: Partij * Zaakid PRAGMA "" "wordt als klager aangemerkt in" ""
MEANING "Een partij kan zich als klagende partij worden aangemerkt in een zaak."
PURPOSE RELATION klager[Partij*Zaakid] IN DUTCH REF "Art. 12-1 Sv."
{+ Een rechstreeks belanghebbende kan schriftelijk beklag doen bij het gerechtshof wanneer een strafbaar feit niet vervolgd wordt, de vervolging niet voortgezet wordt, of wanneer de vervolging plaatsvindt door het uitvaardigen van een strafbeschikking. 
-}

 
gewraakte :: Document * Rechter PRAGMA "" "heeft betrekking op" "in de rol van rechter."
MEANING "Tegen elke rechter die een zaak behandeld kan een verzoek tot wraking worden ingediend."
PURPOSE RELATION gewraakte[Document*Rechter] IN DUTCH REF "Art. 36 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan er tegen een rechter een verzoek tot wraking worden ingediend.
-}
POPULATION gewraakte[Document*Rechter] CONTAINS
[
]

wraker :: Document * Partij  PRAGMA "" "is ingediend" ""
MEANING "Op verzoek van een partij kan elk van de rechters die een zaak behandelen worden gewraakt."
PURPOSE RELATION wraker[Document*Partij] IN DUTCH REF "Art. 36 Rv"
{+ Partijen hebben het recht een wrakingsverzoek in te dienen.
-}
POPULATION wraker[Document*Partij] CONTAINS
[ 
]

wrakingsverzoek :: Document * Zaakid PRAGMA "" "heeft betrekking op zaak" ""
MEANING "Een wrakingsverzoek vindt plaats tijdens de behandeling van een zaak."
PURPOSE RELATION wrakingsverzoek[Document*Zaakid] IN DUTCH REF "Art. 37 Rv"
{+ Een wrakingsverzoek kan alleen worden ingediend tijdens een lopende zaak.
-}



verschonende :: Document * Rechter PRAGMA "" "is afkomstig van" ""
MEANING "Elk verzoek tot verschoning is ingediend door een persoon."
PURPOSE RELATION verschonende[Document*Rechter] IN DUTCH REF "Art. 40-1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan een rechter een verzoeken zich te mogen verschonen.
-}
POPULATION verschonende CONTAINS
[
]


verschoningsverzoek :: Document * Zaakid PRAGMA "" "wordt ingediend tijdens de behandeling van" ""
MEANING "Elk verzoek tot verschoning heeft betrekking op een zaak."
PURPOSE RELATION verschoningsverzoek[Document*Zaakid] IN DUTCH REF "Art. 40-1 en 40-3 Rv"
{+ Een verschoningsverzoek kan worden ingediend zodra een rechter is toegewezen aan de behandeling van de zaak
-}
POPULATION verschoningsverzoek CONTAINS
[
]

--De term "rechtsgebied is een geografische aanduiding volgens Wet Rechterlijke Indeling"  
soortrecht :: Zaakid * "Soort recht" [UNI] PRAGMA "" "is geplaatst onder" ""
MEANING "Elke zaak valt onder een rechtsgbied."
PURPOSE RELATION soortrecht[Zaakid*"Soort recht"] IN DUTCH REF "Art.2-1 Sv"
{+ Alle zaken hebben een rechtsgebied (bestuursrecht, civiel, straf, familie, enz.).
-}
POPULATION soortrecht CONTAINS
[ ("Zaak 4", "Geldvordering")
; ("Zaak 5", "Mulder beroep")
; ("Zaak 6", "Strabis verzet")
]

gedingdatum :: Zaakid * Datum
PURPOSE RELATION gedingdatum IN DUTCH REF "Art. 126 lid 1 Rv"
{+
-}
 
samenstelling :: Kamerid * Kamer PRAGMA "" "is samengesteld als" "kamer."
MEANING "Elke zaak wordt door een enkel- of meervoudige kamer behandeld."
PURPOSE RELATION samenstelling[Kamerid*Kamer] IN DUTCH REF "Art. 15-1 en 15-2 en Rv en Art. 16-1 Rv en Art. 17-1 Rv en Art. 21-1 Sv en Art. 21-5 Sv en Art. 268 Sv. en Art. 8:10 Awb."
{+ Bij de rechtbank worden zaken behandeld en beslist door een enkelvoudige kamer, en indien de zaak naar het oordeel van de enkelvoudige kamer ongeschikt is voor behandeling  en beslissing van één rechter, verwijst zij deze naar een meervoudige kamer, bestaande uit drie leden.

 Bij het gerechtshof worden zaken behandeld en beslist door een meervoudige kamer, bestaande uit drie raadsheren.

 Bij de Hoge Raad worden zaken behandeld en beslist door vijf leden van de meervoudige kamer.
-}
POPULATION samenstelling[Kamerid*Kamer] CONTAINS
[ ("Kamer 1", "Enkelvoudige")
; ("Kamer 2", "Enkelvoudige")
; ("Kamer 3", "Enkelvoudige")
; ("Kamer 4", "Enkelvoudige")
]
 
kamer :: Zaakid * Kamerid PRAGMA "" "wordt behandeld door" ""
MEANING "Een zaak wordt ter behandeling voorgelegd aan een kamer."
PURPOSE RELATION kamer[Zaakid*Kamerid] IN DUTCH REF ""
{+ Zaken worden behandeld door kamers met eventueel een bepaalde specialiteit.
-}
POPULATION kamer[Zaakid*Kamerid] CONTAINS
[ ("Zaak 4", "Kamer 1")
; ("Zaak 5", "Kamer 2")
; ("Zaak 6", "Kamer 3")
; ("Zaak 10", "Kamer 4")
]

sector :: Kamerid -> Sector PRAGMA "" "behoort tot de sector" ""
MEANING "Binnen een sector worden in de enkelvoudige en meervoudige kamers de soorten zaken behandeld en beslist die door het bestuur van de rechtbank aan die sector zijn opgedragen."
PURPOSE RELATION sector[Kamerid*Sector] IN DUTCH REF "Art. 20-1 en 20-2 RO"
{+ Het bestuur van een gerecht bepaalt in welke sector een kamer functioneert.
-}
POPULATION sector[Kamerid*Sector] CONTAINS
[ ("Kamer 1", "Civiel")
; ("Kamer 2", "Kanton")
; ("Kamer 3", "Civiel")
; ("Kamer 4", "Bestuur")
]

{-
[ ("Vierde enkelvoudige kamer van de Rechtbank Amsterdam, sector Kanton", "Kanton")
; ("Enkelvoudige kamer van de Rechtbank Roermond, sector Kanton", "Kanton")
; ("Enkelvoudige kamer van de sector Bestuursrecht Algemeen van de Rechtbank Amsterdam", "Bestuursrecht")
; ("Kamer 2 afdeling bestuursrechtspraak van de Raad van State", "Bestuursrecht")
; ("Enkelvoudige kamer van de sector Strafrecht van de Rechtbank Almelo", "Straf")
; ("Meervoudige kamer van de sector Strafrecht van de Rechtbank Leeuwarden", "Straf")
; ("Enkelvoudige kamer van de sector Civiel Recht van de Rechtbank 's-Gravenhage", "Civiel")
]
-}

specialisme :: Kamerid * Specialiteit PRAGMA "" "heeft als specialiteit" ""
MEANING "Een kamer kan een bepaald specialisme hebben."
PURPOSE RELATION specialisme[Kamerid*Specialiteit] IN DUTCH REF ""
{+ Om zaken efficiënt af te handelen, zijn bepaalde kamers ingedeeld op basis van een specialiteit.
-}
POPULATION specialisme[Kamerid*Specialiteit] CONTAINS
[ ("Kamer 1", "Echtscheiding")
; ("Kamer 2", "Erfrecht")
; ("Kamer 3", "Huurrecht")
; ("Kamer 4", "Vergunningen")
]

rechtbankhoudt :: Zaakid * Orgaan PRAGMA "" "wordt behandeld door" ""
MEANING "Bij rechterlijke organen worden zaken behandeld en beslist."
PURPOSE RELATION rechtbankhoudt[Zaakid*Orgaan] IN DUTCH REF "Art. 8:8-1 en Art. 8:8-2 Awb en Art. 15-1 en 15-2 Rv; Art. 41 RO"
{+ Zaken worden behandeld bij rechterlijke organen.
-}
POPULATION rechtbankhoudt CONTAINS
[ ("Zaak 4", "Rechtbank Leeuwarden")
; ("Zaak 5", "Rechtbank 's-Gravenhage")
; ("Zaak 6", "Rechtbank Almelo")
; ("Zaak 10", "Rechtbank Haarlem")
]

zaaksoort :: Zaakid * Zaaktype [UNI] PRAGMA "" "wordt ingedeeld onder" "" --[TOT]
MEANING "Elke zaak heeft één zaaktype."
PURPOSE RELATION zaaksoort[Zaakid*Zaaktype] IN DUTCH
{+ Alle zaken zijn in te delen naar zaaktype (eerste aanleg, beroep, hoger beroep, enz.). Alle zaaksoorten zijn in de enterprise architectuur benoemd.
-}
POPULATION zaaksoort CONTAINS
[ ("Zaak 4", "Kort geding")
; ("Zaak 5", "Eerste aanleg")
; ("Zaak 6", "Eerste aanleg")
]

griffierechtbetaald :: Zaakid * Griffierechtbetaald PRAGMA "In " "is de betaling van het griffierecht" ""
MEANING "In een zaak dient er griffierecht betaald te worden."
PURPOSE RELATION griffierechtbetaald IN DUTCH REF "Art. 3 lid 1 Wet griffierechten burgerlijke zaken, Art. 127a lid 1 Rv"
{+ De rechter houdt de zaak aan zolang de eiser het griffierecht niet heeft voldaan en de termijn genoemd in artikel 3, derde lid, van de Wet griffierechten burgerlijke zaken nog loopt.
-}
POPULATION griffierechtbetaald CONTAINS
[ ("Zaak 4", "voldaan")
; ("Zaak 5", "voldaan")
; ("Zaak 6", "voldaan")
]


-----------------
-- Invarianten --
-----------------

RULE griffierechttype : I[Griffierechtbetaald] = 'voldaan'


RULE zaaktyperingen: 
 I[Zaaktype] = 'Dagvaarding exploot' \/
               'Bodemprocedure' \/
               'Kort geding'    \/
               'Vovo'           \/
               'Eerste aanleg'  \/
               'Beroep'         \/
               'Hoger beroep'   \/
               'Cassatie'           -- o.g.v. Artikel 78 RO
		   
{-
RULE rechttyperingen : 
I["Soort recht"] = 'Familie' \/
'Geldvordering' \/
'Arbeid' \/
'Huur' \/
'Faillissement' \/
'Schuldsanering' \/
'Surceance' \/
'Mulder beroep' \/
'Strabis verzet' \/
'Handel'  -- Excel van Astrid
-}

{-
RULE bevoegdekamer : rechtbank |- kamer~;rechtbankhoudt
MEANING "De kamer die de zaak behandeld is gevestigd in bij het rechterlijk orgaan dat de zaak in behandeling heeft."
PURPOSE RULE "bevoegdekamer" IN DUTCH REF ""
{+ De rechtbank waar de zaak behandeld wordt, wijst de kamer aan die de zaak in behandeling neemt. 
-}

RULE samenstellingkamers : I[Kamer] = 'Enkelvoudige' \/ 'Meervoudige'
MEANING "Kamers zijn enkel- of meervoudig."
MESSAGE "Er zijn alleen enkelvoudige en meervoudige kamers."
PURPOSE RULE "samenstellingkamers" IN DUTCH REF ""
{+
-}

RULE partijen : partijnaam |- deskundige;V \/ benadeeld;V \/ gevoegde;V \/ klager;V --\/ eiser~;V
MEANING "Een partij is een deskundige, benadeelde, gevoegde, klager of eiser."
PURPOSE RULE "partijen" IN DUTCH REF ""
{+ Partijen in een rechtszaak, zijn gedefinieerd in het procesrecht.
-}

RULE wraker : wraker~;wrakingsverzoek |- benadeeld \/ gevoegde \/ klager --\/ eiser~
MEANING "Een wrakingsverzoek is ingediend door een partij, betrokken bij de zaak."
PURPOSE RULE "wraker" IN DUTCH REF ""
{+ Alleen betrokken partijen hebben het recht een wrakingsverzoek in te dienen.
-}

RULE wraking : gewraakte~;wrakingsverzoek |- rechter;kamer~
MEANING "Een verzoek tot wraking betreft een rechter die optreedt tijdens de behandeling van de zaak."
PURPOSE RULE "wraking" IN DUTCH REF "Art. 36 Rv"
{+ Het wrakingsverzoek kan alleen ingediend worden tegen rechters die de zaak behandelen.
-}


RULE verschoond : verschonende~;verschoningsverzoek |- rechter;kamer~
MEANING "Elk verschoningsverzoek wordt ingediend door de rechter die de zaak behandeld."
PURPOSE RULE "verschoond" IN DUTCH REF "Art. 40 lid 1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan elk van de rechters die een zaak behandelen, verzoeken zich te mogen verschonen
-}

{-
RULE kamers : I[Kamerid] |- (samenstelling;V /\ sector;V /\ specialisme;V) \/ (samenstelling;V /\ sector;V)
MEANING "Kamers hebben een enkel- of meervoudige samenstelling, behoren tot een sector en hebben optioneel een specialisme."
PURPOSE RULE "kamers" IN DUTCH REF ""
{+ Een kamer wordt gekenmerkt door de samenstelling en de sector waartoe de kamer behoort. Optioneel heeft de kamer ook een specialisme.
-}

--------------------
-- Specialisaties --
--------------------
SPEC Rechtbank ISA Orgaan
SPEC Gerecht ISA Orgaan

ENDPATTERN


--------------------
-- Pattern civiel --
--------------------

PATTERN "Burgerlijk recht"
PURPOSE PATTERN "Burgerlijk recht"
{+
-}

----------------------------
-- Relaties en populaties --
----------------------------

dagvaarding :: Dagvaarding -> Document [UNI] PRAGMA "" "heeft document kenmerk" ""
MEANING "Een dagvaarding wordt schriftelijk aangebracht bij de rechtbank."
PURPOSE RELATION dagvaarding[Dagvaarding*Document] IN DUTCH REF ""
{+ De dagvaarding maakt onderdeel uit van het zaaksdossier en is daarom te traceren op basis van een uniek document kenmerk.
-}
POPULATION dagvaarding[Dagvaarding*Document] CONTAINS
[ ("Dagvaarding 1", "Dag20120205")
; ("Dagvaarding 2", "Dag20110513")
; ("Dagvaarding 3", "Dag20110216")
]

aanhangig :: Dagvaarding -> Zaakid [UNI] PRAGMA "" "is aanhangig gemaakt als" ""
MEANING "Aan een aangebrachte dagvaarding wordt een zaaksnummer toegekend."
PURPOSE RELATION aanhangig[Dagvaarding*Zaakid] IN DUTCH REF "Art. 125 Rv"
{+ Het geding is aanhangig vanaf de dag van dagvaarding.
-}
POPULATION aanhangig[Dagvaarding*Zaakid] CONTAINS
[ ("Dagvaarding 1", "Zaak 4")
; ("Dagvaarding 2", "Zaak 5")
; ("Dagvaarding 3", "Zaak 6") 
]


eiser :: Dagvaarding * Partij [UNI] PRAGMA "" "is betekend op verzoek van" ""
MEANING "Een dagvaarding is betekend op verzoek van de eiser."
PURPOSE RELATION eiser[Dagvaarding*Partij] IN DUTCH REF "Art. 45 lid 3b Rv"
{+ Het exploot vermeldt ten minste de naam, op wiens verzoek de betekening geschiedt.
-}
POPULATION eiser[Dagvaarding*Partij] CONTAINS
[ ("Dagvaarding 1", "Partij 1")
; ("Dagvaarding 2", "Partij 2")
; ("Dagvaarding 3", "Partij 3") 
]

producties :: Dagvaarding * Aantal PRAGMA "Bij " "worden" "producties toegevoegd."
MEANING "Bij een dagvaarding kunnen een aantal producties zijn bijgevoegd."
PURPOSE RELATION producties[Dagvaarding*Aantal] IN DUTCH REF ""
{+
-}
POPULATION producties[Dagvaarding*Aantal] CONTAINS
[ ("Dagvaarding 1", "5")
; ("Dagvaarding 2", "2")
; ("Dagvaarding 3", "6")
]

deurwaarder :: Dagvaarding * Naam [TOT,UNI]
PURPOSE RELATION deurwaarder IN DUTCH REF "Art. 45 lid 3c Rv"
{+
-}
POPULATION deurwaarder[Dagvaarding*Naam] CONTAINS
[ ("Dagvaarding 1", "Peter Jansen")
; ("Dagvaarding 2", "Eman Pietersen")
; ("Dagvaarding 3", "Peter Jansen") 
]

kantoordeurwaarder :: Dagvaarding * Kantoordeurwaarder [UNI]
PURPOSE RELATION kantoordeurwaarder IN DUTCH REF "Art. 45 lid 3c Rv"
{+
-}
POPULATION kantoordeurwaarder[Dagvaarding*Kantoordeurwaarder] CONTAINS
[ ("Dagvaarding 1", "H.J. Jansen BV")
; ("Dagvaarding 2", "H.J. Jansen BV")  
; ("Dagvaarding 3", "H.J. Jansen BV")  
]

ontvanger :: Dagvaarding * Partij [UNI]
PURPOSE RELATION ontvanger[Dagvaarding*Partij] IN DUTCH REF "Art. 45 lid 3d Rv"
{+
-}
POPULATION ontvanger[Dagvaarding*Partij] CONTAINS
[ ("Dagvaarding 1", "Partij 4")
; ("Dagvaarding 2", "Partij 5")
; ("Dagvaarding 3", "Partij 6")  
]

woonplaatsontvanger :: Dagvaarding * Adres [UNI]
PURPOSE RELATION woonplaatsontvanger IN DUTCH REF "Art. 45 lid 3d Rv"
{+
-}
POPULATION woonplaatsontvanger[Dagvaarding*Adres] CONTAINS
[ ("Dagvaarding 1", "Heimerstein 91, 3328MH Dordrecht")
; ("Dagvaarding 2", "Bornsestraat 28, 7556BG Hengelo ov")  
; ("Dagvaarding 3", "Zeedijken 42, 9919BM Loppersum")
]

afschrift :: Dagvaarding * Persoon
PURPOSE RELATION afschrift[Dagvaarding*Persoon] IN DUTCH REF "Art. 45 lid 3e Rv"
{+
-}

adresafschrift :: Dagvaarding * Adres
PURPOSE RELATION adresafschrift IN DUTCH REF "Art. 45 lid 3e Rv"
{+
-}


gemachtigde :: Partij * Advocaat
PURPOSE RELATION gemachtigde[Partij*Advocaat] IN DUTCH REF "Art. 111b Rv"
{+
-}
POPULATION gemachtigde[Partij*Advocaat] CONTAINS
[ ("Partij 1", "mr. S. Levelt")
; ("Partij 2", "mr. L.L.A. Cox")
; ("Partij 3", "mr. A. Dekkers") 
]

gronden :: Dagvaarding * Eis PRAGMA "" "stelt als eis" ""
MEANING "Een dagvaarding vermeld de eis(en) van de eiser."
PURPOSE RELATION gronden[Dagvaarding*Eis] IN DUTCH REF "Art. 111d Rv"
{+ Het exploot van dagvaarding vermeldt de eis.
-}

behandelplaats :: Dagvaarding * Orgaan PRAGMA "" "wordt in behandeling genomen door" ""
MEANING "Een dagvaarding wordt in behandeling genomen door een rechterlijk orgaan."
PURPOSE RELATION behandelplaats[Dagvaarding*Orgaan] IN DUTCH REF "Art. 111e Rv"
{+ Het exploot van dagvaarding vermeldt de aanwijzing van de rechter die van de zaak kennisneemt onder de vermelding van de naam van het gerecht.
-}
POPULATION behandelplaats[Dagvaarding*Orgaan] CONTAINS
[ ("Dagvaarding 1", "Raad van State")
; ("Dagvaarding 2", "Rechtbank Leeuwarden")
; ("Dagvaarding 3", "Rechtbank Almelo") 
]

behandeladres :: Dagvaarding * Adres
PURPOSE RELATION behandeladres IN DUTCH REF "Art. 111e Rv"
{+
-}

adresvoorstukken :: Dagvaarding * Adres
PURPOSE RELATION adresvoorstukken IN DUTCH REF "Art. 111e Rv"
{+
-}

roldatum :: Dagvaarding * Termijndatum [UNI]
PURPOSE RELATION roldatum IN DUTCH REF "Art. 111f Rv"
{+
-}
POPULATION roldatum[Dagvaarding*Termijndatum] CONTAINS
[ ("Dagvaarding 1", "20-03-2012")
; ("Dagvaarding 2", "20-03-2012")
; ("Dagvaarding 3", "20-03-2012")  
]

tijdstip :: Dagvaarding * Tijdstip
PURPOSE RELATION tijdstip IN DUTCH REF "Art. 111f Rv"
{+
-}

verschijningswijze :: Dagvaarding * Persoon
PURPOSE RELATION verschijningswijze IN DUTCH REF "Art. 111g en 111h Rv"
{+
-}

conclusievanantwoord :: Zaakid * Document
PURPOSE RELATION conclusievanantwoord IN DUTCH REF "Art. 128 lid 2 Rv"
{+
-}
POPULATION conclusievanantwoord[Zaakid*Document] CONTAINS
[ ("Zaak 4", "Conclusie20120319")
; ("Zaak 5", "Conclusie20110627")
; ("Zaak 6", "Conclusie20110330")
]

ENDPATTERN

---------------------
-- Pattern Bestuur --
---------------------

PATTERN Bestuursrecht
PURPOSE PATTERN "Bestuursrecht"
{+
-}

beroepschrift :: Beroepschrift -> Document [UNI] PRAGMA "" "heeft document kenmerk" ""
MEANING "Een beroepschrift dient elektronisch en/of schriftelijk te worden ingediend."
PURPOSE RELATION beroepschrift[Beroepschrift*Document] IN DUTCH REF "Art. 6:4  Awb"
{+ Het instellen van beroep bij een bestuursrechter geschiedt door het indienen van een beroepschrift.
-}
POPULATION beroepschrift[Beroepschrift*Document] CONTAINS
[ ("Beroepschrift 1", "Beroep20120221")
; ("Beroepschrift 2", "Beroep20120115")
; ("Beroepschrift 3", "Beroep20120203")
]


indiener :: Beroepschrift * Persoon PRAGMA "" "is ingediend door of namens" ""
MEANING "Een beroepschrift is ingediend door één of meerdere personen."
PURPOSE RELATION indiener[Beroepschrift*Persoon] REF "Art. 6:5 lid 1 sub a Awb"
{+ Het beroepschrift bevat ten minste de naam  van de indiener(s).
-}
POPULATION indiener[Beroepschrift*Persoon] CONTAINS
[ ("Beroepschrift 1", "10")
; ("Beroepschrift 2", "3")
; ("Beroepschrift 3", "6")
]

gemachtigde :: Persoon * Persoon PRAGMA "" "is de vertegenwoordiger van" ""
MEANING "In het bestuursrecht kan een eiser zich door een ieder laten vertegenwoordigen."
PURPOSE RELATION gemachtigde[Persoon*Persoon] IN DUTCH REF ""
{+
-}

dagtekening :: Beroepschrift -> Datum PRAGMA "" "is betekend op" ""
MEANING "Een beroepschrift is gedagtekend."
PURPOSE RELATION dagtekening[Beroepschrift*Datum] REF "Art. 6:5 lid 1b Awb"
{+ Het beroepschrift bevat ten minste de dagtekening.
-}
POPULATION dagtekening[Beroepschrift*Datum] CONTAINS
[ ("Beroepschrift 1", "20-02-2012")
; ("Beroepschrift 2", "15-01-2012")
; ("Beroepschrift 3", "03-02-2012")
]

verweerder :: Beroepschrift * Verweerder

omschrijvingberoep :: Beroepschrift -> Tekst PRAGMA "" "heeft als omschrijving" ""
MEANING "Een beroepschrift bevat een omschrijving van het besluit of de handeling waartegen het beroep is gericht."
PURPOSE RELATION omschrijvingberoep[Beroepschrift*Tekst] IN DUTCH REF "Art. 6:5 lid 1c Awb"
{+ In een beroepschrift staat omschreven tegen welke beslissing of handeling men beroep aantekend.
-}
POPULATION omschrijvingberoep[Beroepschrift*Tekst] CONTAINS
[ ("Beroepschrift 1", "Ik ben het oneens met de beslissing genomen door de Gemeente Utrecht omtrent het verlenen van kapvergunning XXXX.")
; ("Beroepschrift 2", "Ik ben het oneens met de beslissing genomen door het UWV omtrent het uitstellen van mijn WW-uitkering.")
; ("Beroepschrift 3", "Ik ben het oneens met de beslissing tot ongegrondverklaring van het eerder ingediende bezwaarschrift met kenmerk XXXX.")
]

grondenberoep :: Beroepschrift -> Tekst PRAGMA "" "heeft als grond(en)" ""
MEANING "Een beroepschrift bevat de gronden van het beroep."
PURPOSE RELATION grondenberoep[Beroepschrift*Tekst] IN DUTCH REF "Art. 6:5 lid 1d Awb"
{+ In een beroepschrift staan de gronden vermeld waarop het beroepschrift gebaseerd is.
-}
POPULATION grondenberoep[Beroepschrift*Tekst] CONTAINS
[ ("Beroepschrift 1", "Art. XX lid Y Wm")
; ("Beroepschrift 2", "Art. XX IWfsv")
; ("Beroepschrift 3", "Art. WVW1994")
]


aanhangigbestuur :: Beroepschrift * Zaakid PRAGMA "" "is aanhangig op het moment van binnenkomst" ""
MEANING "Een door het beroepschrift ingesteld beroep is aanghangig op het moment van ontvangst."
PURPOSE RELATION aanhangigbestuur[Beroepschrift*Zaakid] IN DUTCH REF "Art. 8:10 lid 1 Awb"
{+ Het door een beroepschrift ingesteld beroep wordt aanhangig gemaakt bij ontvangst, dit gebeurt in het elektronische verkeer door er een willekeurig systeemnummer aan toe te kennen.
-}
POPULATION aanhangigbestuur[Beroepschrift*Zaakid] CONTAINS
[ ("Beroepschrift 1", "Zaak 10")
; ("Beroepschrift 2", "Zaak 11")
; ("Beroepschrift 3", "Zaak 12")
]


-----------------
-- Invarianten --
-----------------


RULE compleetberoepschrift : beroepschrift |- indiener;V /\ indiener;woonplaats;V /\ dagtekening;V /\ omschrijvingberoep;V /\ grondenberoep;V --/\ verweerder;V
MEANING "Een beroepschrift bevat ten minste de naam en adresgegevens van de indiener, de dagtekening, de omschrijving van het besluit of de handeling en de gronden van het beroep."
PURPOSE RULE "compleetberoepschrift" IN DUTCH REF "Art. 6:5 lid 1 Awb"
{+ Wettelijk is vastgesteld dat het beroepschrift ten minste moet bevatten.
-}


ENDPATTERN

-------------------
-- Pattern Straf --
-------------------

PATTERN Strafvordering
PURPOSE PATTERN "Strafvordering"
{+
-}

---------------
-- Concepten --
---------------

----------------------------
-- Relaties en populaties --
----------------------------

feit :: Dagvaarding * Feit PRAGMA "" "behelst de opgave van" ""
MEANING "Een dagvaarding vermeld het feit dat ten laste wordt gelegd."
PURPOSE RELATION feit[Dagvaarding*Feit] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding behelst een opgave van het feit dat ten laste wordt gelegd.
-}

opgavefeit :: Omschrijving * Feit PRAGMA "" "is de omschrijving van" ""
MEANING "Een feit wordt omschreven om het te duiden."
PURPOSE RELATION opgavefeit[Omschrijving*Feit] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De opgave van het feit kan worden omschreven.
-}

tijdstipdelict :: Feit * Tijdstip PRAGMA "" "is begaan om" ""
MEANING "Een feit is begaan omstreeks een bepaald tijdstip."
PURPOSE RELATION tijdstipdelict[Feit*Tijdstip] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt omstreeks welke tijd het feit begaan zou zijn.
-}

plaatsdelict :: Feit * Adres PRAGMA "" "is begaan op locatie" ""
MEANING "Een feit is begaam op een locatie."
PURPOSE RELATION plaatsdelict[Feit*Adres] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt waar ter plaatse het feit begaan zou zijn.
-}

wettelijkevoorschriften :: Dagvaarding * Tekst PRAGMA "" "vermeld dat het feit strafbaar is gesteld op basis van" ""
MEANING "Een feit is strafbaar op basis van wettelijke voorschriften."
PURPOSE RELATION wettelijkevoorschriften[Dagvaarding*Tekst] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt de wettelijke voorschriften waarbij het feit strafbaar is gesteld.
-}

omstandigheden :: Feit * Tekst PRAGMA "" "is begaan onder de volgende omstandigheden" ""
MEANING "Een feit kan zijn begaan onder bepaalde omstandigheden."
PURPOSE RELATION omstandigheden[Feit*Tekst] IN DUTCH REF "Art. 261 lid 2 Sv"
{+ De dagvaarding behelst de vermelding van de omstandigheden waaronder het feit zou zijn begaan.
-}

datumbetekening :: Dagvaarding * Datum [UNI] PRAGMA "" "is betekend op" ""
MEANING "Een dagvaarding dient betekend te zijn."
PURPOSE RELATION datumbetekening IN DUTCH REF "Art. 45 lid 3a Rv; Art. 585 lid 1 sub a Sv"
{+ Het exploot vermeldt ten minste de datum van de betekening.
-}
POPULATION datumbetekening CONTAINS
[ ("Dagvaarding 1", "07-02-2012")
; ("Dagvaarding 2", "07-02-2012")
; ("Dagvaarding 3", "07-02-2012") 
]

strafrechtsketennummer :: Strafrechtsketennummer -> Partij [UNI,TOT] PRAGMA "" "wordt toegekend aan" "in de rol van gedaagde."
MEANING "Eén strafrechtsketennummer wordt toegekend aan precies één gedaagde."
PURPOSE RELATION strafrechtsketennummer [Strafrechtsketennummer*Partij] IN DUTCH REF "Art. 27b-1 Sv."
{+ Onze Minister van Justitie kent aan de verdachte na de vaststelling van zijn identiteit een strafrechtsketennummer toe, tenzij aan hem reeds een strafrechtsketennummer is toegekend.
-}
POPULATION strafrechtsketennummer CONTAINS
[ ("004995", "Partij 5")
; ("007487", "Partij 4")
]


-----------------
-- Invarianten --
-----------------

ENDPATTERN

ENDCONTEXT