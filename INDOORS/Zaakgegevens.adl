CONTEXT Zaakgegevens

INCLUDE "Zittinggegevens.adl"
INCLUDE "Persoonsgegevens.adl"


THEMES "Zaakgegevens"
META "authors" "Leon Chevalking"

--------------
-- Patterns --
--------------
PATTERN Zaakgegevens -- hoort in INDOORS thema "Registratie"
PURPOSE PATTERN "Zaakgegevens"
{+Het gegevensgebied "Zaakgegevens" bevat de gestructureerde gegevens van en over zaken.
Deze gegevens maken het zaaksgericht werken en procesbesturing mogelijk.
Onder zaken verstaan we niet alleen straf-, burgerlijke, bestuursrechterlijke zaken en hoger beroep,
maar ook registerhandelingen, toezicht en andere wettelijke taken die door rechterlijke ambtenaren als zaak worden behandeld.
De zaaksdocumenten (de ongestructureerde gegevens) bevinden zich in dossiers (gegevensgebied: Dossiers).
Op de werkplek is een zaak als één geheel te raadplegen en te bewerken.
Tijdens het werken aan een zaak ziet een gebruiker zaaksgegevens en documenten in één scherm

In de pre-INDOORS situatie wordt het zaaksverloop in de sectorale systemen Civiel, NKP, Straf en Berber geregistreerd.
De gestructureerde gegevens zullen in INDOORS uit-eindelijk in één zaakregistratie worden bijgehouden, met behoud van sectorale verschillen.
Het zaaksnummer is/wordt landelijk geüniformeerd.
-}

----------
-- Keys --
----------

KEY zaak : Zaakid (zaakid)
KEY partij : Partij (ispartij~;gbaVoornamen, TXT " ", ispartij~;gbaGeslachtsnaam)

---------------
-- Concepten --
---------------

CONCEPT Zaak ""

CONCEPT Kamer ""

CONCEPT Dagvaarding "Een dagvaarding is een oproep om voor een rechterlijk orgaan te verschijnen."

CONCEPT Partij "Een partij is een bij een zaak betrokken natuurlijkpersoon, rechtspersoon of bij strafzaken, een vertegenwoordiger van het Openbaar Ministerie."

CONCEPT Sector "" 

CONCEPT Specialiteit ""

----------------------------
-- Relaties en populaties --
----------------------------

--=================--
-- Zaaksdocumenten --
--=================--
aanbrengwijze :: Zaakid * Document PRAGMA "" "is aangebracht als" ""
MEANING "Elke zaak wordt op een vooraf gedefinieerde wijze aangebracht."
PURPOSE RELATION aanbrengwijze[Zaakid*Document] IN DUTCH REF ""
{+ 
-}

datumbetekening :: Document * Datum [UNI] PRAGMA "" "is betekend op" ""
MEANING "Een dagvaarding dient betekend te zijn."
PURPOSE RELATION datumbetekening[Document*Datum] IN DUTCH REF "Art. 45 lid 3a Rv; Art. 585 lid 1 sub a Sv; Art. 6:5 lid 1 sub b Awb"
{+ Het exploot vermeldt ten minste de datum van de betekening.
 Het beroepschrift bevat ten minste de dagtekening.
-}


-- Eisende partij relateren aan Zaakid ipv Dagvaarding, ivm uniformering over verschillende rechtsmiddelen die ten slotte tot zaken leiden
eiser :: Document * NatuurlijkPersoon [UNI] PRAGMA "" "is aangebracht door" ""
MEANING "Een dagvaarding is betekend op verzoek van de eiser."
PURPOSE RELATION eiser[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub b Rv; Art. 258 lid 1 Sv; Art. 6:5 lid 1 sub a Awb"
{+ Het exploot vermeldt ten minste de naam, op wiens verzoek de betekening geschiedt.
 Het beroepschrift bevat ten minste de naam  van de indiener(s).
-}


producties :: Document * Aantal PRAGMA "Bij " "worden" "producties toegevoegd."
MEANING "Bij een dagvaarding kunnen een aantal producties zijn bijgevoegd."
PURPOSE RELATION producties[Document*Aantal] IN DUTCH REF "Art. 2 lid 3 Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken"
{+
-}


deurwaarder :: Document * NatuurlijkPersoon
PURPOSE RELATION deurwaarder[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub c Rv"
{+
-}


kantoordeurwaarder :: Document * Kantoordeurwaarder [UNI]
PURPOSE RELATION kantoordeurwaarder IN DUTCH REF "Art. 45 lid 3 sub c Rv"
{+
-}


-- Dit is de gedaagde, deze relateren aan Zaakid ipv Dagvaarding
ontvanger :: Document * NatuurlijkPersoon [UNI]
MEANING "Een dagvaarding wordt naar de ontvanger verstuurd."
PURPOSE RELATION ontvanger[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub d Rv; Art. 258 lid 1 Sv; Art. 260 lid 3 Sv"
{+ De dagvaarding wordt betekend aan de ontvanger.
-}

--RULE afschrift : 

-- Deze kan vervangen worden door een regel die de relatie 'dagvaarding' [Dagvaarding*Document] aan een relatie uit 'Dossiers' relateert, die iets over een bericht zegt bv.
afschrift :: Document * NatuurlijkPersoon
PURPOSE RELATION afschrift[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub e Rv"
{+
-}



--==============--
-- Zaakgegevens --
--==============--

zaakid :: Zaakid * Zaak [UNI] PRAGMA "" "heeft zaaksnummer" ""
MEANING "Een zaak wordt uniek gekenmerkt door een zaaksnummer."
PURPOSE RELATION zaakid[Zaakid*Zaak] IN DUTCH REF ""
{+ Een zaak heeft een uniek zaaksnummer.
-}

soortrecht :: Zaakid * "Soort recht" [UNI] PRAGMA "" "is geplaatst onder" ""
MEANING "Elke zaak valt onder een rechtsgbied."
PURPOSE RELATION soortrecht[Zaakid*"Soort recht"] IN DUTCH REF "Art. 2 lid 1 Sv"
{+ Alle zaken vallen in een bepaalde categorie.
-}

gedingdatum :: Zaakid * Datum
PURPOSE RELATION gedingdatum IN DUTCH REF "Art. 126 lid 1 Rv"
{+
-}

behandelendgerecht :: Zaakid * Gerecht PRAGMA "" "wordt behandeld door" ""
MEANING "Bij rechterlijke organen worden zaken behandeld en beslist."
PURPOSE RELATION behandelendgerecht[Zaakid*Gerecht] IN DUTCH REF "Art. 8:8 lid 1 Awb; Art. 8:8 lid 2 Awb; Art. 15 lid 1 Rv; Art. 15 lid 2 Rv; Art. 41 RO; Art. 111 sub e Rv"
{+ Zaken worden behandeld bij rechterlijke organen.
-}

gegevenAan :: Zaakid * Sector PRAGMA "" "wordt gegeven aan" ""
MEANING "Eke zaak wordt gegeven aan een sector."
PURPOSE RELATION gegevenAan[Zaakid*Sector] IN DUTCH REF ""
{+ 
-}

behandelwijze :: Zaakid * Behandelwijze PRAGMA "" "heeft als behandelwijze" ""
MEANING "Een zaak wordt volgens een bepaalde behandelwijze behandeld."
PURPOSE RELATION behandelwijze[Zaakid*Behandelwijze] IN DUTCH REF ""
{+ 
-}

behandeltype :: Zaakid * Behandeltype PRAGMA "" "heeft als behandeltype" ""
MEANING "Een zaak krijgt een bepaald behandeltype."
PURPOSE RELATION behandeltype[Zaakid*Behandeltype] IN DUTCH REF ""
{+ 
-}

griffierechtbetaald :: Zaakid * Griffierechtbetaald PRAGMA "In " "is de betaling van het griffierecht" ""
MEANING "In een zaak dient er griffierecht betaald te worden."
PURPOSE RELATION griffierechtbetaald IN DUTCH REF "Art. 3 lid 1 Wet griffierechten burgerlijke zaken; Art. 127a lid 1 Rv; Art. 8:41 Awb"
{+ De rechter houdt de zaak aan zolang de eiser het griffierecht niet heeft voldaan en de termijn genoemd in artikel 3, derde lid, van de Wet griffierechten burgerlijke zaken nog loopt.
-}

--========--
-- Kamers --
--========--

samenstelling :: Bezetting * Samenstelling PRAGMA "" "is" " samengesteld"
MEANING "Elke zaak wordt door een enkel- of meervoudige kamer behandeld."
PURPOSE RELATION samenstelling[Bezetting*Samenstelling] IN DUTCH REF "Art. 15 lid 2 Rv; Art. 17 lid 1 Rv; Art. 21 lid 2 Sv; Art. 21 lid 5 Sv; Art. 268 Sv; Art. 8:10 Awb"
{+ Bij de rechtbank worden zaken behandeld en beslist door een enkelvoudige kamer, en indien de zaak naar het oordeel van de enkelvoudige kamer ongeschikt is voor behandeling  en beslissing van één rechter, verwijst zij deze naar een meervoudige kamer, bestaande uit drie leden.

 Bij het gerechtshof worden zaken behandeld en beslist door een meervoudige kamer, bestaande uit drie raadsheren.

 Bij de Hoge Raad worden zaken behandeld en beslist door vijf leden van de meervoudige kamer.
-}


naambezetting :: Bezetting * Naambezetting PRAGMA "" "heeft als naam" ""

 
behandelend :: Zaakid * Bezetting PRAGMA "" "wordt behandeld door" ""
MEANING "Een zaak wordt ter behandeling voorgelegd aan een kamer."
PURPOSE RELATION behandelend[Zaakid*Bezetting] IN DUTCH REF "Art. 15 lid 1 Rv; Art. 16 lid 1 Rv; Art. 268 Sv; Art. 8:10 Awb"
{+ Zaken worden behandeld door enkel- of meervoudige kamers.
-}


bezetting :: Rechter * Bezetting PRAGMA "" "maakt deel uit van" ""


--==========--
-- Partijen --
--==========--

ispartij :: NatuurlijkPersoon * Partij


partijtype :: Partij * Partijtype PRAGMA "" "heeft de rol van" ""
MEANING "Een partij is betrokken in een bepaalde rol."
PURPOSE RELATION partijtype[Partij*Partijtype] IN DUTCH REF "Art. 12 lid 1 Sv; Art. 51i Sv; Art. 51f lid 1 Sv; Art. 8:26 lid 1 Awb; Art. 217 Rv; Art. 271 Rv"
{+ Bij een zaak betrokken partijen zijn te typeren naar een bepaalde rol. Voorbeelden hiervan zijn 'klager', 'gedaagde', 'eiser' , 'benadeelde', 'gevoegde' etc. Elke sector kent zijn eigen type partijen, welke in een sectorspecifieke tabel opgenomen kunnen worden.
-}


partij :: Partij * Zaakid PRAGMA "" "is betrokken bij" ""
MEANING "Een partij is betrokken bij een zaak."
PURPOSE RELATION partij[Partij*Zaakid] IN DUTCH REF ""
{+ Een natuurlijk persoon of rechtspersoon krijgt pas de status van partij als hij/zij betrokken is bij een zaak.
-}

deskundige :: NatuurlijkPersoon * Zaakid PRAGMA "" "is deskundige in" ""
MEANING "Een partij kan als deskundige benoemd worden bij een zaak."
PURPOSE RELATION deskundige[NatuurlijkPersoon*Zaakid] IN DUTCH REF "Art. 51i Sv"
{+ Op de wijze bij de wet bepaald wordt een deskundige benoemd met een opdracht tot het geven van informatie over of het doen van onderzoek op een terrein, waarvan hij specifieke of bijzondere kennis bezit.
-}

gemachtigde :: Partij * NatuurlijkPersoon
PURPOSE RELATION gemachtigde[Partij*NatuurlijkPersoon] IN DUTCH REF "Art. 111 sub b Rv"
{+
-}



--===================--
-- Wraken/verschonen --
--===================--
 
gewraakte :: Rechter * Document PRAGMA "" "wordt gewraakt in" ""
MEANING "Tegen elke rechter die een zaak behandeld kan een verzoek tot wraking worden ingediend."
PURPOSE RELATION gewraakte[Rechter*Document] IN DUTCH REF "Art. 36 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan er tegen een rechter een verzoek tot wraking worden ingediend.
-}
POPULATION gewraakte[Rechter*Document] CONTAINS
[
]

wraker :: Partij * Document  PRAGMA "" "is de indiener van" ""
MEANING "Op verzoek van een partij kan elk van de rechters die een zaak behandelen worden gewraakt."
PURPOSE RELATION wraker[Partij*Document] IN DUTCH REF "Art. 36 Rv"
{+ Partijen hebben het recht een wrakingsverzoek in te dienen.
-}
POPULATION wraker[Partij*Document] CONTAINS
[ 
]

wrakingsverzoek :: Document * Zaakid PRAGMA "" "heeft betrekking op de zaak" ""
MEANING "Een wrakingsverzoek vindt plaats tijdens de behandeling van een zaak."
PURPOSE RELATION wrakingsverzoek[Document*Zaakid] IN DUTCH REF "Art. 37 Rv"
{+ Een wrakingsverzoek kan alleen worden ingediend tijdens een lopende zaak.
-}

verschonende :: Rechter * Document PRAGMA "" "is afkomstig van" ""
MEANING "Elk verzoek tot verschoning is ingediend door een persoon."
PURPOSE RELATION verschonende[Rechter*Document] IN DUTCH REF "Art. 40 lid 1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan een rechter een verzoeken zich te mogen verschonen.
-}
POPULATION verschonende CONTAINS
[
]

verschoningsverzoek :: Document * Zaakid PRAGMA "" "wordt ingediend tijdens de behandeling van" ""
MEANING "Elk verzoek tot verschoning heeft betrekking op een zaak."
PURPOSE RELATION verschoningsverzoek[Document*Zaakid] IN DUTCH REF "Art. 40 lid 1 Rv; Art. 40 lid 3 Rv"
{+ Een verschoningsverzoek kan worden ingediend zodra een rechter is toegewezen aan de behandeling van de zaak
-}
POPULATION verschoningsverzoek CONTAINS
[
]

-----------------
-- Invarianten --
-----------------

RULE griffierechttype : I[Griffierechtbetaald] = 'voldaan'

RULE samenstellingkamers : I[Samenstelling] = 'Enkelvoudig' \/ 'Meervoudig'
MEANING "Kamers zijn enkel- of meervoudig."
MESSAGE "Er zijn alleen enkelvoudige en meervoudige kamers."
PURPOSE RULE "samenstellingkamers" IN DUTCH REF ""
{+
-}

RULE wraker : wraker;wrakingsverzoek |- partij
MEANING "Een wrakingsverzoek is ingediend door een partij, betrokken bij de zaak."
PURPOSE RULE "wraker" IN DUTCH REF ""
{+ Alleen betrokken partijen hebben het recht een wrakingsverzoek in te dienen.
-}

RULE wraking : gewraakte;wrakingsverzoek |- rechter;behandeling;behandeld~
MEANING "Een verzoek tot wraking betreft een rechter die optreedt tijdens de behandeling van de zaak."
PURPOSE RULE "wraking" IN DUTCH REF "Art. 36 Rv"
{+ Het wrakingsverzoek kan alleen ingediend worden tegen rechters die de zaak behandelen.
-}


RULE verschoond : verschonende;verschoningsverzoek |- rechter;behandeling;behandeld~
MEANING "Elk verschoningsverzoek wordt ingediend door de rechter die de zaak behandeld."
PURPOSE RULE "verschoond" IN DUTCH REF "Art. 40 lid 1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan elk van de rechters die een zaak behandelen, verzoeken zich te mogen verschonen
-}

--------------------
-- Specialisaties --
--------------------
SPEC Rechtbank ISA Gerecht
SPEC Gerechtshof ISA Gerecht


--------------------
-- Zaakdocumenten --
--------------------

aanhangig :: Document * Zaakid [UNI] PRAGMA "" "is aanhangig gemaakt als" ""
MEANING "Aan een aangebrachte dagvaarding wordt een zaaksnummer toegekend."
PURPOSE RELATION aanhangig[Document*Zaakid] IN DUTCH REF "Art. 125 Rv; Art. 258 lid 1 Sv; Art. 8:10 lid 1 Awb; Art. 128 lid 2 Rv; Art. 6:4 Awb"
{+ Het geding is aanhangig vanaf de dag van dagvaarding.
 Het door een beroepschrift ingesteld beroep wordt aanhangig gemaakt bij ontvangst, dit gebeurt in het elektronische verkeer door er een willekeurig systeemnummer aan toe te kennen.
-}

--========--
-- Feiten --
--========--
feit :: Document * Feit PRAGMA "" "vermeld dat het feit strafbaar is gesteld op basis van" ""
MEANING "Een dagvaarding vermeld het feit dat ten laste wordt gelegd."
PURPOSE RELATION feit[Document*Feit] IN DUTCH REF "Art. 261 lid 1 Sv; Art. 258 lid 3 Sv; Art. 111 sub d Rv; Art. 6:5 lid 1 sub d Awb"
{+ De dagvaarding behelst een opgave van het feit dat ten laste wordt gelegd.
 In een beroepschrift staan de gronden vermeld waarop het beroepschrift gebaseerd is.
-}

datumdelict :: Feit * Datum PRAGMA "" "is begaan om" ""
MEANING "Een feit is begaan omstreeks een bepaalde datum."
PURPOSE RELATION datumdelict[Feit*Datum] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt omstreeks welke datum het feit begaan zou zijn.
-}


tijdstipdelict :: Feit * Tijdstip PRAGMA "" "is begaan om" ""
MEANING "Een feit is begaan omstreeks een bepaald tijdstip."
PURPOSE RELATION tijdstipdelict[Feit*Tijdstip] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt omstreeks welk tijdstip het feit begaan zou zijn.
-}

plaatsdelict :: Feit * Adres PRAGMA "" "is begaan op locatie" ""
MEANING "Een feit is begaam op een locatie."
PURPOSE RELATION plaatsdelict[Feit*Adres] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt waar ter plaatse het feit begaan zou zijn.
-}

artikelvoorschrift :: Artikel * Feit PRAGMA "" "behelst de opgave van" ""
MEANING "Een feit is strafbaar op basis van wettelijke voorschriften."
PURPOSE RELATION artikelvoorschrift[Artikel*Feit] IN DUTCH REF "Art. 261 lid 1 Sv; Art. 6:5 lid 1 sub c Awb"
{+ De dagvaarding vermeldt de wettelijke voorschriften waarbij het feit strafbaar is gesteld. 
 Het exploot van dagvaarding vermeldt de eis dan wel de tenlastlegging van het feit en de wettelijke gronden of voorschriften van de eis.
 In een beroepschrift staat omschreven tegen welke beslissing of handeling men beroep aantekend.
-}

omschrijvingartikel :: Tekst * Artikel PRAGMA "" "is de omschrijving van" ""
MEANING "Een feit wordt omschreven om het te duiden."
PURPOSE RELATION omschrijvingartikel[Tekst*Artikel] IN DUTCH REF "Art. 261 lid 1 Sv; Art. 6:5 lid 1 sub c Awb"
{+ De opgave van het feit kan worden omschreven.
-}


omstandigheden :: Feit * Tekst PRAGMA "" "is begaan onder de volgende omstandigheden" ""
MEANING "Een feit kan zijn begaan onder bepaalde omstandigheden."
PURPOSE RELATION omstandigheden[Feit*Tekst] IN DUTCH REF "Art. 261 lid 2 Sv"
{+ De dagvaarding behelst de vermelding van de omstandigheden waaronder het feit zou zijn begaan.
-}

behandeladres :: Orgaan * Adres
PURPOSE RELATION behandeladres IN DUTCH REF "Art. 111 sub e Rv"
{+
-}

adresvoorstukken :: Document * Adres
PURPOSE RELATION adresvoorstukken IN DUTCH REF "Art. 111 sub e Rv"
{+
-}

roldatum :: Document * Datum [UNI]
PURPOSE RELATION roldatum IN DUTCH REF "Art. 111 sub f Rv"
{+
-}


tijdstip :: Document * Tijdstip
PURPOSE RELATION tijdstip IN DUTCH REF "Art. 111 sub f Rv"
{+
-}

verschijningswijze :: Document * NatuurlijkPersoon
PURPOSE RELATION verschijningswijze IN DUTCH REF "Art. 111 sub g Rv; Art. 111 sub h Rv"
{+
-}

verweerder :: Document * Orgaan

appointeringsvoorstel :: Appointeringsvoorstel * Document PRAGMA "" "bevat de zaak" ""
MEANING "Het Openbaar Ministerie appointeert zaken in een appointeringsvoorstel."
PURPOSE RELATION appointeringsvoorstel[Appointeringsvoorstel*Document] IN DUTCH REF "Art 5 lid 1 sub 1 Landelijk strafprocesreglement; Art 6 Landelijk strafprocesreglement; Art 7 lid 1 Landelijk strafprocesreglement"
{+ Het OM en de sectoren strafrecht van de rechtbanken appointeren zaken t.b.v. de planning.
-}

-----------------
-- Invarianten --
-----------------

{-
RULE compleetberoepschrift : beroepschrift |- indiener;V /\ indiener;woonplaats;V /\ dagtekening;V /\ omschrijvingberoep;V /\ grondenberoep;V --/\ verweerder;V
MEANING "Een beroepschrift bevat ten minste de naam en adresgegevens van de indiener, de dagtekening, de omschrijving van het besluit of de handeling en de gronden van het beroep."
PURPOSE RULE "compleetberoepschrift" IN DUTCH REF "Art. 6:5 lid 1 Awb"
{+ Wettelijk is vastgesteld dat het beroepschrift ten minste moet bevatten.
-}

ENDPATTERN


ENDCONTEXT