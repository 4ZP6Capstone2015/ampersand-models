CONTEXT Zaakgegevens_

INCLUDE "Zittinggegevens.adl"
INCLUDE "Persoonsgegevens.adl"


THEMES "Zaakgegevens"
META "authors" "Leon Chevalking"

--------------
-- Patterns --
--------------
PATTERN Zaakgegevens -- hoort in INDOORS thema "Registratie"
PURPOSE PATTERN "Zaakgegevens"
{+Het gegevensgebied "Zaakgegevens" bevat de gestructureerde gegevens van en over zaken.
Deze gegevens maken het zaaksgericht werken en procesbesturing mogelijk.
Onder zaken verstaan we niet alleen straf-, burgerlijke, bestuursrechterlijke zaken in eerste aanleg en in hoger beroep,
maar ook registerhandelingen, toezicht en andere wettelijke taken die door rechterlijke ambtenaren als zaak worden behandeld.
De zaaksdocumenten (de ongestructureerde gegevens) bevinden zich in dossiers (gegevensgebied: Dossiers).
Op de werkplek is een zaak als één geheel te raadplegen en te bewerken.
Tijdens het werken aan een zaak ziet een gebruiker zaaksgegevens en documenten in één scherm

In de pre-INDOORS situatie wordt het zaaksverloop in de sectorale systemen Civiel, NKP, COMPAS/GPS en NIAS in hoger beroep en Berber geregistreerd.
De gestructureerde gegevens zullen in INDOORS uiteindelijk in één zaakregistratie worden bijgehouden, met behoud van sectorale verschillen.
Het zaaksnummer (of parketnummer) is/wordt landelijk geüniformeerd.
-}

----------
-- Keys --
----------
KEY document : Document(kenmerk)
KEY bezetting : Bezetting(naambezetting)
KEY zaak : Zaakid (TXT "Zaak ", zaakid)
KEY partij : Partij(ispartij~;gbaVoornamen, TXT " ", ispartij~;gbaGeslachtsnaam)

---------------
-- Concepten --
---------------

--CONCEPT Zaak ""

CONCEPT Dagvaarding "Een dagvaarding is een oproep om voor een rechterlijk orgaan te verschijnen."

CONCEPT Partij "Een partij is een bij een zaak betrokken natuurlijk persoon, rechtspersoon of ook bij strafzaken, de vertegenwoordiger van het Openbaar Ministerie."

--CONCEPT Sector "" 

CONCEPT Exploot "Een exploot is een schriftelijk relaas of proces-verbaal van de deurwaarder dienend ten bewijze dat de voor bedoelde handeling voorgeschreven formaliteit door hem zijn nageleefd."

----------------------------
-- Relaties en populaties --
----------------------------

--=================--
-- Zaaksdocumenten --
--=================--

kenmerk :: Document * Kenmerk [UNI] PRAGMA "" "heeft als kenmerk" ""
MEANING "Een document heeft een uniek, onderscheidenlijk kenmerk."
PURPOSE RELATION kenmerk[Document*Kenmerk] IN DUTCH REF ""
{+ Om documenten van elkaar te kunnen onderscheiden hebben documenten een uniek, door de gebruiker gespecificeerd, kenmerk.
-}

aanbrengwijze :: Zaakid * Aanbrengwijze [UNI] PRAGMA "" "is aangebracht als" ""
MEANING "Elke zaak wordt op een vooraf gedefinieerde wijze aangebracht."
PURPOSE RELATION aanbrengwijze[Zaakid*Aanbrengwijze] IN DUTCH REF "Art. 125 Rv; Art. 258 lid 1 Sv; Art. 8:10 lid 1 Awb; Art. 128 lid 2 Rv; Art. 6:4 Awb"
{+ Door een zaak aan te brengen, is deze bekend bij het gerecht waar de zaak wordt aangebracht. 
-}

datumbetekening :: Document * Datum [UNI] PRAGMA "" "is betekend op" ""
MEANING "Een inleidend zaakstuk kan vereisen op een datum betekend te zijn."
PURPOSE RELATION datumbetekening[Document*Datum] IN DUTCH REF "Art. 45 lid 3a Rv; Art. 278 lid 4 Rv; Art. 585 lid 1 sub a Sv; Art. 6:5 lid 1 sub b Awb"
{+ Het document dat de zaak inleidt, een dagvaarding, exploot of beroepschrift, bevat ten minste de datum van betekening. Bij verzoekschriften wordt de datum aangetekend waarop de het verzoekschrift ter griffie is ingediend.
-}

producties :: Document * Aantal [UNI] PRAGMA "Bij " "worden" "producties toegevoegd"
MEANING "Bij een dagvaarding kunnen een aantal producties zijn bijgevoegd."
PURPOSE RELATION producties[Document*Aantal] IN DUTCH REF "Art. 3 lid 1 sub e Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken"
{+ Bij een dagvaarding kan een aantal producties horen die de dagvaarding toelichten. Dit zijn zaaksdocumenten die per post opgestuurd dienen te worden, maar waarvan het aantal wel digitaal aangegeven kan worden om de compleetheid van de poststukken controleerbaar te maken.
-}

deurwaarder :: Document * NatuurlijkPersoon [UNI] PRAGMA "Het exploot van " "wordt gedaan door door" ""
MEANING "De dagvaarding vermeldt de deurwaarder die het exploot gedaan heeft."
PURPOSE RELATION deurwaarder[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub c Rv"
{+ Het exploot van dagvaarding wordt gedaan door een deurwaarder.
-}

kantoordeurwaarder :: Document * Kantoordeurwaarder [UNI] PRAGMA "" "is exploot gedaan door een deurwaarder werkzaam voor" ""
MEANING "Het exploot vermeldt het kantoor van de deurwaarder."
PURPOSE RELATION kantoordeurwaarder IN DUTCH REF "Art. 45 lid 3 sub c Rv"
{+ Een deurwaarder voert zijn werkzaamheden uit in naam van een kantoor, dat op de dagvaarding vermeld dient te worden.
-}

gerichtaangedaagde :: Document * NatuurlijkPersoon [UNI] PRAGMA "" "is bestemd voor" ""
MEANING "Een dagvaarding vermeldt de naam voor wie die is bestemd."
PURPOSE RELATION gerichtaangedaagde[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub d Rv; Art. 258 lid 1 Sv; Art. 260 lid 3 Sv"
{+ De dagvaarding wordt betekend aan de ontvanger.
-}

woonplaatsgedaagde :: Document * Plaats [UNI] PRAGMA "" "vermeldt als woonplaats van de gedaagde" ""
MEANING "Een dagvaarding vermeldt de woonplaats van de persoon voor wie die is bestemd."
PURPOSE RELATION woonplaatsgedaagde[Document*Plaats] IN DUTCH REF "Art. 45 lid 3 sub d"
{+ De dagvaarding vermeldt de woonplaats van degene aan wie die is bestemd ter identificatie.
-}

afschriftnaar :: Document * NatuurlijkPersoon PRAGMA "" "is gelaten aan" ""
MEANING "Van een dagvaarding wordt een afschrift gelaten."
PURPOSE RELATION afschriftnaar[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 45 lid 3 sub e Rv; Art. 51 Sv"
{+ Het exploot vermeldt degene aan wie het afschrift van het exploot gelaten is. Dit geldt ook voor de dagvaarding in strafzaken, waar het afschrift naar de raadsvrouw/raadsman verstuurd wordt.
-}


--========--
-- Feiten --
--========--
aanleiding :: Document * Aanleiding PRAGMA "" "vermeldt als aanleiding" ""
MEANING "Een inleidend zaakstuk bevat de aanleiding van de zaak."
PURPOSE RELATION aanleiding[Document*Aanleiding] IN DUTCH REF "Art. 261 lid 1 Sv; Art. 258 lid 3 Sv; Art. 111 sub d Rv; Art. 6:5 lid 1 sub d Awb"
{+ Een inleidend zaakstuk bevat de aanleiding waarom het zaakstuk wordt ingediend.
-}

datumdelict :: Aanleiding * Datum PRAGMA "" "is begaan op" ""
MEANING "Een feit is begaan omstreeks een bepaalde datum."
PURPOSE RELATION datumdelict[Aanleiding*Datum] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt omstreeks welke datum het feit begaan zou zijn.
-}

plaatsdelict :: Aanleiding * Locatie PRAGMA "" "is begaan op locatie" ""
MEANING "Een feit vermeldt de plaats waar het feit begaan zou zijn."
PURPOSE RELATION plaatsdelict[Aanleiding*Locatie] IN DUTCH REF "Art. 261 lid 1 Sv"
{+ De dagvaarding vermeldt de plaats waar het feit begaan zou zijn, dit kan variëren van een specifieke locatie zoals een plaats tot een meer algemene plaatsaanduiding zoals een traject of een land.
-}

omschrijvingaanleiding :: Aanleiding * Tekst PRAGMA "" "is begaan onder de volgende omstandigheden:" ""
MEANING "Het feit wordt omschreven om het te duiden."
PURPOSE RELATION omschrijvingaanleiding[Aanleiding*Tekst] IN DUTCH REF "Art. 261 lid 1 Sv; Art. 261 lid 2 Sv"
{+ De aanleiding van het inleidend zaakstuk dient te worden omschreven. In het strafrecht spreken we van de opgave van het feit. In het bestuurs- en civielrecht spreken we van de omschrijving van het verzoek.
-}

gronden :: Aanleiding * Gronden PRAGMA "" "heeft als grond" ""
MEANING "De aanleiding kan strafbaar zijn op basis van één of meerdere wettelijke voorschriften."
PURPOSE RELATION gronden[Aanleiding*Gronden] IN DUTCH REF "Art. 1 lid 1 Sr; Art. 261 lid 1 Sv; Art. 6:5 lid 1 sub c Awb"
{+ De dagvaarding vermeldt het wettelijke voorschrift waarbij het feit strafbaar is gesteld. 
 In een beroepschrift staat omschreven tegen welke beslissing of handeling men beroep aantekend.
-}

behandeladres :: Orgaan * Adres PRAGMA "" "is gevestigd te" ""
MEANING "Een exploot vermeldt het adres van de behandelplaats."
PURPOSE RELATION behandeladres IN DUTCH REF "Art. 111 sub e Rv; Art. 258 lid 5 Sv; Art. 2 Sv"
{+ Het exploot van de dagvaarding dient te vermelden op welk adres de zaak behandeld gaat worden.
-}

adresvoorstukken :: Document * Adres PRAGMA "" "heeft als adres voor stukken vermeld:" ""
MEANING "Een dagvaarding vermeldt op welk adres stukken kunnen worden ingediend."
PURPOSE RELATION adresvoorstukken IN DUTCH REF "Art. 111 sub e Rv"
{+ Indien de zaak behandeld wordt op een nevenzittingsplaats waar geen stukken kunnen worden ingediend, vermeldt de dagvaarding expliciet het adres waar de stukken kunnen worden ingediend.
-}

-- Straf vermeldt ook de datum waarop de partijen dienen te verschijnen.
roldatum :: Document * Datum [UNI] PRAGMA "" "vermeldt als roldatum" ""
MEANING "Een dagvaarding vermeldt de roldatum waartegen wordt gedagvaard."
PURPOSE RELATION roldatum IN DUTCH REF "Art. 111 sub f Rv"
{+ De roldatum geeft het moment aan tot wanneer de ontvangende partij haar antwoord op de dagvaarding in moet dienen.
-}


tijdstip :: Document * Tijdstip PRAGMA "" "vermeldt als zittingstijdstip" ""
MEANING "Een dagvaarding kan het zittingstijdstip vermelden."
PURPOSE RELATION tijdstip IN DUTCH REF "Art. 111 sub f Rv; Art. 258 lid 5 Sv"
{+ Als er op de roldatum een terechtzitting plaatsvindt, vermeldt de dagvaarding het tijdstip van de zitting, zodat de ontvangende partij weet wanneer ze moet verschijnen.
-}


verschijningswijze :: Document * Verschijningswijze PRAGMA "" "vermeldt als verschijningswijze:" "" 
MEANING "De dagvaarding vermeldt de wijze waarop de gedaagde in het geding moet verschijnen."
PURPOSE RELATION verschijningswijze IN DUTCH REF "Art. 111 sub g Rv; Art. 111 sub h Rv"
{+ Partijen kunnen naar gelang het soort zaak op verschillende wijze in het geding verschijnen.
-}

appointeringsvoorstel :: Appointeringsvoorstel * Document PRAGMA "" "bevat het inleidend zaakstuk" ""
MEANING "Het Openbaar Ministerie appointeert zaken in een appointeringsvoorstel."
PURPOSE RELATION appointeringsvoorstel[Appointeringsvoorstel*Document] IN DUTCH REF "Art 5 lid 1 sub 1 Landelijk strafprocesreglement; Art 6 Landelijk strafprocesreglement; Art 7 lid 1 Landelijk strafprocesreglement"
{+ Het OM en de sectoren strafrecht van de rechtbanken en hoven appointeren zaken t.b.v. de planning.
-}

--==============--
-- Zaakgegevens --
--==============--

zaakid :: Zaakid * Zaak [UNI] PRAGMA "" "heeft zaaksnummer" ""
MEANING "Een zaak wordt uniek gekenmerkt door een zaaksnummer."
PURPOSE RELATION zaakid[Zaakid*Zaak] IN DUTCH REF ""
{+ Een zaak heeft een uniek zaaksnummer.
-}

soortrecht :: Zaakid * "Soort recht" [UNI] PRAGMA "" "is geplaatst onder" ""
MEANING "Elke zaak valt onder een bepaald soort recht."
PURPOSE RELATION soortrecht[Zaakid*"Soort recht"] IN DUTCH REF "Art. 2 lid 1 Sv"
{+ Alle zaken vallen in een bepaalde categorie.
-}

behandelendgerecht :: Zaakid * Gerecht [UNI] PRAGMA "" "wordt behandeld door" ""
MEANING "Bij rechterlijke organen worden zaken behandeld en beslist."
PURPOSE RELATION behandelendgerecht[Zaakid*Gerecht] IN DUTCH REF "Art. 8:8 lid 1 Awb; Art. 8:8 lid 2 Awb; Art. 15 lid 1 Rv; Art. 15 lid 2 Rv; Art. 41 RO; Art. 111 sub e Rv"
{+ Zaken worden behandeld bij rechterlijke organen.
-}

gegevenAan :: Zaakid * Sector [UNI] PRAGMA "" "wordt gegeven aan" ""
MEANING "Eke zaak wordt gegeven aan een sector."
PURPOSE RELATION gegevenAan[Zaakid*Sector] IN DUTCH REF ""
{+ Een sector binnen een gerecht wordt aangewezen om de zaak te behandelen. 
-}

-- Uit Sv af te leiden
behandelwijze :: Zaakid * Behandelwijze [UNI] PRAGMA "" "heeft als behandelwijze" ""
MEANING "Een zaak wordt volgens een bepaalde behandelwijze behandeld."
PURPOSE RELATION behandelwijze[Zaakid*Behandelwijze] IN DUTCH REF ""
{+ Er zijn landelijk bekende behandelwijzen waarop een zaak behandeld kan worden.
-}

behandeltype :: Zaakid * Behandeltype [UNI] PRAGMA "" "heeft als behandeltype" ""
MEANING "Een zaak krijgt een bepaald behandeltype."
PURPOSE RELATION behandeltype[Zaakid*Behandeltype] IN DUTCH REF ""
{+ Er zijn landelijk bekende behandeltypen waarop een zaak behandeld kan worden.
-}

hogerberoep :: Zaakid * Zaakid [UNI] PRAGMA "" "dient het hoger beroep van" ""
MEANING ""
PURPOSE RELATION hogerberoep[Zaakid*Zaakid] IN DUTCH REF "Art. 404 Sv"
{+
-}

--========--------
-- Griffierecht --
--==============--

griffierecht :: Zaakid * Griffierecht

hoogtegriffierecht :: Griffierecht * Hoogte



griffierechtbetaald :: Zaakid * Griffierechtbetaald [UNI] PRAGMA "In " "is de betaling van het griffierecht" ""
MEANING "In een zaak dient er griffierecht betaald te worden."
PURPOSE RELATION griffierechtbetaald IN DUTCH REF "Art. 3 lid 1 Wet griffierechten burgerlijke zaken; Art. 127a lid 1 Rv; Art. 8:41 Awb"
{+ De rechter houdt de zaak aan zolang de eiser het griffierecht niet heeft voldaan en de termijn genoemd in artikel 3, derde lid, van de Wet griffierechten burgerlijke zaken nog loopt.
-}

--========--
-- Kamers --
--========--

samenstelling :: Bezetting * Samenstelling [UNI] PRAGMA "" "is" " samengesteld"
MEANING "Een bezetting wordt door een enkel- of meervoudige kamer behandeld."
PURPOSE RELATION samenstelling[Bezetting*Samenstelling] IN DUTCH REF "Art. 15 lid 2 Rv; Art. 17 lid 1 Rv; Art. 21 lid 2 Sv; Art. 21 lid 5 Sv; Art. 268 Sv; Art. 8:10 Awb"
{+ Een bezetting is enkelvoudig of meervoudig samengesteld.
-}


naambezetting :: Bezetting * Naambezetting [UNI] PRAGMA "" "heeft als naam" ""

 
behandelend :: Zaakid * Bezetting [UNI] PRAGMA "" "wordt behandeld door" ""
MEANING "Een zaak wordt ter behandeling voorgelegd aan een kamer."
PURPOSE RELATION behandelend[Zaakid*Bezetting] IN DUTCH REF "Art. 15 lid 1 Rv; Art. 16 lid 1 Rv; Art. 268 Sv; Art. 8:10 Awb"
{+ Bij de gerechten worden zaken behandeld en beslist door een enkelvoudige of meervoudige kamer, bestaande uit drie leden.

 Bij de Hoge Raad worden zaken behandeld en beslist door drie of vijf leden van de meervoudige kamer.
-}


bezetting :: Rechter * Bezetting PRAGMA "" "maakt deel uit van" ""

--==========--
-- Partijen --
--==========--

ispartij :: NatuurlijkPersoon * Partij


partijtype :: Partij * Partijtype [UNI] PRAGMA "" "heeft de rol van" ""
MEANING "Een partij is betrokken in een bepaalde rol."
PURPOSE RELATION partijtype[Partij*Partijtype] IN DUTCH REF "Art. 12 lid 1 Sv; Art. 27 Sv; Art. 51i Sv; Art. 51f lid 1 Sv; Art. 8:26 lid 1 Awb; Art. 217 Rv; Art. 271 Rv"
{+ Bij een zaak betrokken partijen zijn te typeren naar een bepaalde rol. Voorbeelden hiervan zijn 'klager', 'gedaagde', 'eiser' , 'benadeelde', 'gevoegde', 'verdachte', 'Officier van Justitie' etc. Elke sector kent zijn eigen type partijen, welke in een sectorspecifieke tabel opgenomen kunnen worden.
-}


partij :: Partij * Zaakid PRAGMA "" "is betrokken bij" ""
MEANING "Een partij is betrokken bij een zaak."
PURPOSE RELATION partij[Partij*Zaakid] IN DUTCH REF "Art. 12 lid 1 Sv; Art. 27 Sv; Art. 51f lid 1 Sv; Art. 8:26 lid 1 Awb; Art. 217 Rv; Art. 271 Rv"
{+ Een natuurlijk persoon of rechtspersoon krijgt pas de status van partij als hij/zij betrokken is bij een zaak.
-}

deskundige :: NatuurlijkPersoon * Zaakid PRAGMA "" "is deskundige in" ""
MEANING "Een partij kan als deskundige benoemd worden bij een zaak."
PURPOSE RELATION deskundige[NatuurlijkPersoon*Zaakid] IN DUTCH REF "Art. 51i Sv"
{+ Op de wijze bij de wet bepaald wordt een deskundige benoemd met een opdracht tot het geven van informatie over of het doen van onderzoek op een terrein, waarvan hij specifieke of bijzondere kennis bezit.
-}

gemachtigde :: Partij * NatuurlijkPersoon PRAGMA "" "heeft als gemachtigde" "aangesteld"
MEANING "Een partij kan een gemachtigde aanstellen."
PURPOSE RELATION gemachtigde[Partij*NatuurlijkPersoon] IN DUTCH REF "Art. 111 sub b Rv"
{+ Een gemachtigde procedeert in naam van een partij.
-}

--===================--
-- Wraken/verschonen --
--===================--
 
gewraakte :: Rechter * Document PRAGMA "" "wordt gewraakt in" ""
MEANING "Tegen elke rechter die een zaak behandeld kan een verzoek tot wraking worden ingediend."
PURPOSE RELATION gewraakte[Rechter*Document] IN DUTCH REF "Art. 36 Rv; Art. 512 Sv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan er tegen een rechter een verzoek tot wraking worden ingediend.
-}

wraker :: Partij * Document  PRAGMA "" "is de indiener van" ""
MEANING "Op verzoek van een partij kan elk van de rechters die een zaak behandelen worden gewraakt."
PURPOSE RELATION wraker[Partij*Document] IN DUTCH REF "Art. 36 Rv"
{+ Partijen hebben het recht een wrakingsverzoek in te dienen.
-}

wrakingsverzoek :: Document * Zaakid PRAGMA "" "heeft betrekking op de zaak" ""
MEANING "Een wrakingsverzoek vindt plaats tijdens de behandeling van een zaak."
PURPOSE RELATION wrakingsverzoek[Document*Zaakid] IN DUTCH REF "Art. 37 Rv"
{+ Een schriftelijk wrakingsverzoek kan alleen worden ingediend vooraf aan de aavang van de zitting.
-}

verschonende :: Rechter * Document PRAGMA "" "is afkomstig van" ""
MEANING "Elk verzoek tot verschoning is ingediend door een rechter."
PURPOSE RELATION verschonende[Rechter*Document] IN DUTCH REF "Art. 40 lid 1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan een rechter een verzoeken zich te mogen verschonen.
-}

verschoningsverzoek :: Document * Zaakid PRAGMA "" "wordt ingediend tijdens de behandeling van" ""
MEANING "Elk verzoek tot verschoning heeft betrekking op een zaak."
PURPOSE RELATION verschoningsverzoek[Document*Zaakid] IN DUTCH REF "Art. 40 lid 1 Rv; Art. 40 lid 3 Rv"
{+ Een verschoningsverzoek kan worden ingediend zodra een rechter is toegewezen aan de behandeling van de zaak
-}

-----------------
-- Invarianten --
-----------------

RULE griffierechttype : I[Griffierechtbetaald] = 'voldaan'

RULE samenstellingkamers : I[Samenstelling] = 'Enkelvoudig' \/ 'Meervoudig'
MEANING "Kamers zijn enkel- of meervoudig."
MESSAGE "Er zijn alleen enkelvoudige en meervoudige kamers."
PURPOSE RULE "samenstellingkamers" IN DUTCH REF ""
{+
-}

RULE wraker : wraker;wrakingsverzoek |- partij
MEANING "Een wrakingsverzoek is ingediend door een partij, betrokken bij de zaak."
PURPOSE RULE "wraker" IN DUTCH REF ""
{+ Alleen betrokken partijen hebben het recht een wrakingsverzoek in te dienen.
-}

RULE wraking : gewraakte;wrakingsverzoek |- bezetting;houdtzitting;behandeling;behandeld~
MEANING "Een verzoek tot wraking betreft een rechter de zaak behandeld."
PURPOSE RULE "wraking" IN DUTCH REF "Art. 36 Rv"
{+ Het wrakingsverzoek kan alleen ingediend worden tegen rechters die de zaak behandelen.
-}


RULE verschoond : verschonende;verschoningsverzoek |- bezetting;houdtzitting;behandeling;behandeld~
MEANING "Elk verschoningsverzoek wordt ingediend door die rechter die de zaak behandeld."
PURPOSE RULE "verschoond" IN DUTCH REF "Art. 40 lid 1 Rv"
{+ Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan elk van de rechters die een zaak behandelen, verzoeken zich te mogen verschonen
-}

{-
RULE compleetberoepschrift : beroepschrift |- indiener;V /\ indiener;woonplaats;V /\ dagtekening;V /\ omschrijvingberoep;V /\ grondenberoep;V --/\ verweerder;V
MEANING "Een beroepschrift bevat ten minste de naam en adresgegevens van de indiener, de dagtekening, de omschrijving van het besluit of de handeling en de gronden van het beroep."
PURPOSE RULE "compleetberoepschrift" IN DUTCH REF "Art. 6:5 lid 1 Awb"
{+ Wettelijk is vastgesteld dat het beroepschrift ten minste moet bevatten.
-}

--------------------
-- Specialisaties --
--------------------
SPEC Rechtbank ISA Gerecht
SPEC Gerechtshof ISA Gerecht


ENDPATTERN


ENDCONTEXT