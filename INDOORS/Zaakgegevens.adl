CONTEXT Zaakgegevens

PATTERN Zaakgegevens -- hoort in INDOORS thema "Registratie"
PURPOSE PATTERN "Zaakgegevens"
{+Het gegevensgebied "Zaakgegevens" bevat de gestructureerde gegevens van en over zaken.
Deze gegevens maken het zaaksgericht werken en procesbesturing mogelijk.
Onder zaken verstaan we niet alleen straf-, burgerlijke, bestuursrechterlijke zaken en hoger beroep,
maar ook registerhandelingen, toezicht en andere wettelijke taken die door rechterlijke ambtenaren als zaak worden behandeld.
De zaaksdocumenten (de ongestructureerde gegevens) bevinden zich in dossiers (gegevensgebied: Dossiers).
Op de werkplek is een zaak als één geheel te raad-plegen en te bewerken.
Tijdens het werken aan een zaak ziet een gebruiker zaaksgegevens en documenten in één scherm

In de pre-INDOORS situatie wordt het zaaksverloop in de sectorale systemen Civiel, NKP, Straf en Berber geregistreerd.
De gestructureerde gegevens zullen in INDOORS uit-eindelijk in één zaakregistratie worden bijgehouden, met behoud van sectorale verschillen.
Het zaaksnummer is/wordt landelijk geüniformeerd.
-}

{-
RULE toevoegen: van;partij[Partij*Zaak] |- doc;zaaksdossier
MEANING "Voor elk bericht dat afkomstig is van een persoon behorende tot de partij van een zaak, geldt dat het bericht behoort tot een document dat in een zaaksdossier is opgenomen."
PURPOSE RULE toevoegen  IN DUTCH
{+Alle berichten van betrokken partijen in een zaak zitten in het zaaksdossier.
-}

{- SJ: gemaakt op 22 mei 2009 op basis van de notitie van Guus Harten
       "Een schets van een nieuw primair processysteem voor de bestuursrechtspraak"
       aangevuld op 20 februari 2010 op basis van het project startdocument VIRO Bestuursrecht
 -}
{-
De zaaksbehandeling is leidend
Het primaire proces (de zaaksbehandeling) is leidend.
Elementen van de bedrijfsvoering zijn ondergeschikt aan het primair proces en het onafhankelijk rechterlijk domein.
-}

eiser :: Partij * Zaak PRAGMA "" "is verzoekende partij in" ""
MEANING "Een persoon kan de rol van verzoekende partij vervullen in een zaak."
PURPOSE RELATION eiser[Partij*Zaak] IN DUTCH
{+In elke zaak is er een eisende partij. Afhankelijk van de zaaksoort heeft deze verschillende namen, zoals appellant, eiser, verzoeker, enz.
-}
POPULATION eiser CONTAINS
[	("Hendrik Pieter Cornelis van de Meene", "175262");
("S.A. Wiltschut", "AWB 08/3670");
("Roeland Petrus Dijkstra", "156583");
("B. Stolen", "11/123458");
("A. Angereden", "10/569423");
("O.P.G. Elicht", "11/489631")
]

gedaagde :: Partij * Zaak PRAGMA "" "is gedaagde in" ""
MEANING "Een persoon kan de rol van gedaagde vervullen in een zaak."
PURPOSE RELATION gedaagde[Partij*Zaak] IN DUTCH REF "Art. 27-1 Sv."
{+In elke zaak is er een gedaagde partij. Afhankelijk van de zaaksoort heet deze gedaagde, verdachte, of anderszins.
-}
POPULATION gedaagde CONTAINS
[	("Documentatie- en informatiecentrum Stichting De Stelling", "175262");
("De Raad voor Rechtsbijstand", "AWB 08/3670");
("Regiopolitie Brabant Zuid-Oost", "156583");
("F.R. Audeur", "11/123458");
("T.A. Sjesdief", "10/569423");
("B. Eul", "11/489631")
]

strafrechtsketennummer :: Strafrechtsketennummer -> Partij [UNI,TOT] PRAGMA "" "wordt toegekend aan" "in de rol van gedaagde."
MEANING "Eén strafrechtsketennummer wordt toegekend aan precies één gedaagde."
PURPOSE RELATION strafrechtsketennummer [Strafrechtsketennummer*Partij] IN DUTCH REF "Art. 27b-1 Sv."
{+Onze Minister van Justitie kent aan de verdachte na de vaststelling van zijn identiteit een strafrechtsketennummer toe, tenzij aan hem reeds een strafrechtsketennummer is toegekend.
-}
POPULATION strafrechtsketennummer CONTAINS
[	("004995", 	"T.A. Sjesdief");
("007487",	"B. Eul")
]

deskundige :: Partij * Zaak PRAGMA "" "is deskundige in" ""
MEANING "Een persoon kan als deskundige benoemd worden bij een zaak."
PURPOSE RELATION deskundige[Partij*Zaak] IN DUTCH REF "Art. 51i Sv."
{+Op de wijze bij de wet bepaald wordt een deskundige benoemd met een opdracht tot het geven van informatie over of het doen van onderzoek op een terrein, waarvan hij specifieke of bijzondere kennis bezit.
-}
POPULATION deskundige CONTAINS
[	("F. Eggink", "11/123458");
("J. Honkoop", "10/569423");
("R.J. Jansen", "11/489631")
]

benadeeld :: Partij * Zaak PRAGMA "" "is als benadeelde partij gevoegd in" ""
MEANING "Een persoon kan zich als een benadeelde partij bij een zaak voegen."
PURPOSE RELATION benadeeld[Partij*Zaak] IN DUTCH REF "Art. 51f-1,§2, SV."
{+In sommige soorten zaken kan een partij zich voegen als benadeelde. De benadeelde wordt in Art. 51a Sv met de term slachtoffer aangeduidt.
-}
POPULATION benadeeld CONTAINS
[	("B. Stolen", "11/123458");
("A. Angereden", "10/569423");
("O.P.G. Elicht", "11/489631")
]

{-
Tussengekomen partij?!
-}

gevoegde :: Partij * Zaak PRAGMA "" "is als belanghebbende gevoegd in" ""
MEANING "Een persoon kan zich als belanghebbende partij bij een zaak voegen."
PURPOSE RELATION gevoegde[Partij*Zaak] IN DUTCH REF "Art. 8:26-1, Awb."
{+In sommige soorten zaken kan een partij zich voegen als belanghebbende.
-}
POPULATION gevoegde CONTAINS
[	("X-CAGO B.V.", "402349");
("Telio Nederland B.V.", "397151");
("VIALIS B.V.", "388346")
]

isPartij :: "natuurlijkpersoon" * Partij PRAGMA "" "is als" "in de hoedanigheid van partij"
MEANING "Een partij is een rol die toegekend kan worden aan een natuurlijk persoon."
PURPOSE RELATION isPartij["natuurlijkpersoon"*Partij] IN DUTCH
{+
-}
POPULATION isPartij CONTAINS
[ ("Bert Stolen", "B. Stolen")
; ("Andre Angereden", "A. Angereden")
; ("Olaf Elicht", "O.P.G. Elicht")
; ("Barend Eul", "B. Eul")
; ("Tomas Sjesdief", "T.A. Sjesdief")
; ("Frits Audeur", "F.R. Audeur")
; ("Roeland Petrus Dijkstra", "Roeland Petrus Dijkstra")
; ("Sandra Anne Wiltschut", "S.A. Wiltschut")
; ("Hendrik Pieter Cornelis van de Meene", "Hendrik Pieter Cornelis van de Meene")
; ("", "")
; ("", "")
; ("", "")
; ("", "")
; ("", "")
; ("", "")
; ("", "")
]


partij :: Partij * Zaak PRAGMA "" "is partij in" ""
MEANING "Een persoon is een partij in een zaak."
PURPOSE RELATION partij[Partij*Zaak] IN DUTCH
{+Elke zaak behandelt een geschil tussen partijen.
-}
POPULATION partij[Partij*Zaak] CONTAINS
[ ("Hendrik Pieter Cornelis van de Meene", "175262")
; ("S.A. Wiltschut", "AWB 08/3670")
; ("Roeland Petrus Dijkstra", "156583")
; ("B. Stolen", "11/123458")
; ("A. Angereden", "10/569423")
; ("O.P.G. Elicht", "11/489631")
; ("Documentatie- en informatiecentrum Stichting De Stelling", "175262")
; ("De Raad voor Rechtsbijstand", "AWB 08/3670")
; ("Regiopolitie Brabant Zuid-Oost", "156583")
; ("F.R. Audeur", "11/123458")
; ("T.A. Sjesdief", "10/569423")
; ("B. Eul", "11/489631")
; ("X-CAGO B.V.", "402349")
; ("Telio Nederland B.V.", "397151")
; ("VIALIS B.V.", "388346")
]


klager :: Partij * Zaak PRAGMA "" "wordt als klager aangemerkt in" ""
MEANING "Een persoon kan zich als klagende partij worden aangemerkt in een zaak."
PURPOSE RELATION klager[Partij*Zaak] IN DUTCH REF "Art. 12-1 Sv."
{+Een rechstreeks belanghebbende kan schriftelijk beklag doen bij het gerechtshof wanneer een strafbaar feit niet vervolgd wordt, de vervolging niet voortgezet wordt, of    wanneer de vervolging plaatsvindt door het uitvaardigen van een strafbeschikking. 
-}


RULE Partij: partij[Partij * Zaak] = eiser \/ gedaagde \/ gevoegde \/ benadeeld
MEANING "Een partij (in strafrecht) kan zijn een eiser, of een gedaagde, of een gevoegde, of een benadeelde."
PURPOSE RULE Partij IN DUTCH
{+Partijen kunnen zijn: eiser, gedaagde, gevoegde of benadeelde partij (in strafrecht).
-}	
 
behandeling :: Zaak * Kamer PRAGMA "" "wordt behandeld door" ""
MEANING "Elke zaak wordt door een of meerdere kamers behandeld."
PURPOSE RELATION behandeling[Zaak*Kamer] IN DUTCH REF "Art. 15-1 en 15-2 en Rv en Art. 16-1 Rv en Art. 17-1 Rv en Art. 21-1 Sv en Art. 21-5 Sv en Art. 268 Sv. en Art. 8:10 Awb."
{+Volgens Wetboek Rv: Bij de rechtbank worden zaken behandeld en beslist door een enkelvoudige kamer, en indien de zaak naar het oordeel van de enkelvoudige kamer ongeschikt is voor behandeling  en beslissing van één rechter, verwijst zij deze naar een meervoudige kamer, bestaande uit drie leden.

 Bij het gerechtshof worden zaken behandeld en beslist door een meervoudige kamer, bestaande uit drie raadsheren.

 Bij de Hoge Raad worden zaken behandeld en beslist door vijf leden van de meervoudige kamer.
 
 Volgens Wetboek Sv: In alle gevallen waarin niet de beslissing door het rechterlijk college op de terechtzitting is voorgeschreven of aldaar ambtshalve wordt genomen, geschiedt de behandeling door de raadkamer. De samenstelling van de raadkamer is vastgesteld in Art 21-2 Sv.
 
 Behandeling door een enkelvoudige kamer van de rechtbank kan geschieden indien de zaak van eenvoudige aard is.
 
 Strafzaken worden behandeld en beslist door een meervoudige kamer, behoudens in de wet genoemde uitzonderingen.
 
 Volgens Wetboek Awb: De zaken die bij de rechtbank aanhangig worden gemaakt, worden in behandeling genomen door een enkelvoudige kamer.

 Indien een zaak naar het oordeel van de enkelvoudige kamer ongeschikt is voor behandeling door één rechter, verwijst zij deze naar een meervoudige kamer. De enkelvoudige kamer kan ook in andere gevallen een zaak naar een meervoudige kamer verwijzen.

     Indien een zaak naar het oordeel van de meervoudige kamer geschikt is voor verdere behandeling door één rechter, kan zij deze verwijzen naar een enkelvoudige kamer
-}
POPULATION behandeling CONTAINS
[	("11/123458", "Enkelvoudige kamer van de sector Civiel Recht van de Rechtbank 's-Gravenhage");
("10/569423", "Meervoudige kamer van de sector Strafrecht van de Rechtbank Leeuwarden");
("11/489631", "Enkelvoudige kamer van de sector Strafrecht van de Rechtbank Almelo")
]

uitspraak :: Uitspraak * Zaak PRAGMA "" "heeft betrekking op" ""
MEANING "Elke uitspraak geldt voor precies één zaak."
PURPOSE RELATION uitspraak[Uitspraak*Zaak] IN DUTCH REF "Art. 28-1 Rv"
{+Elke uitspraak die voor een specifieke zaak geldt, geschiedt in het openbaar. 
-}
POPULATION uitspraak CONTAINS
[	("BR9826", "11/123458");
("BU9872", "10/569423");
("BU2372", "11/489631")
]
 
wraking :: "Verzoek tot wraking" * Persoon PRAGMA "" "heeft betrekking op" "in de rol van rechter."
MEANING "Tegen elke rechter die een zaak behandeld kan een verzoek tot wraking worden ingediend."
PURPOSE RELATION wraking["Verzoek tot wraking"*Persoon] IN DUTCH REF "Art. 36 Rv."
{+Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan er tegen een rechter een verzoek tot wraking worden ingediend.
-}
POPULATION wraking CONTAINS
[ ("W11-14", "mr. E.M. Dil-Stork")
]

wrakingsverzoek :: "Verzoek tot wraking" * Partij  PRAGMA "" "is ingediend" ""
MEANING "Op verzoek van een partij kan elk van de rechters die een zaak behandelen worden gewraakt."
PURPOSE RELATION wrakingsverzoek["Verzoek tot wraking"*Partij] IN DUTCH REF "Art. 36 Rv."
{+Elk verzoek tot wraking is ingediend door ten minste één partij."
-}
POPULATION wrakingsverzoek CONTAINS
[ ("W11-14", "F.R. Audeur")
]

RULE Wraking: wrakingsverzoek;partij |- wraking;rechter~;dient~
MEANING "Elk verzoek tot wraking dat afkomstig is van een persoon die een partij is in een bepaalde zaak, betreft een persoon die als rechter optreedt tijdens de zitting van diezelfde zaak."
PURPOSE RULE Wraking IN DUTCH REF "Art. 36 Rv."
{+Het wrakingsverzoek afkomstig van een partij in een zaak, kan alleen ingediend worden tegen rechters die zitting nemen in die zaak.
-}

verschonen :: "Verzoek tot verschoning" * Persoon PRAGMA "" "heeft betrekking op" ""
MEANING "Elk verzoek tot verschoning is ingediend door een persoon."
PURPOSE RELATION verschonen["Verzoek tot verschoning"*Persoon] IN DUTCH REF "Art. 40-1 Rv."
{+Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan een rechter een verzoeken zich te mogen verschonen."
-}
POPULATION verschonen CONTAINS
[	("W09-12", "mr. J. Hielkema")
]


verschoningsverzoek :: "Verzoek tot verschoning" * Zaak PRAGMA "" "wordt ingediend tijdens de behandeling van" ""
MEANING "Elk verzoek tot verschoning heeft betrekking op precies één zaak."
PURPOSE RELATION verschoningsverzoek["Verzoek tot verschoning"*Zaak] IN DUTCH REF "Art. 40-1 en 40-3 Rv"
{+Een verschoningsverzoek kan worden ingediend zodra een rechter is toegewezen aan de zitting van de zaak
-}
POPULATION verschoningsverzoek CONTAINS
[	("W09-12", "10/569423")
]


RULE verschoond: verschoningsverzoek~;verschonen |- dient;kamer;bezetting~
MEANING "Elk verschoningsverzoek wordt ingediend door een rechter die een zaak behandeld en heeft betrekking op diezelfde rechter behandelend die zaak."
PURPOSE RULE verschoond IN DUTCH REF "Art. 40-1 Rv."
{+Op grond van feiten of omstandigheden waardoor de rechterlijke onpartijdigheid schade zou kunnen lijden kan elk van de rechters die een zaak behandelen, verzoeken zich te mogen verschonen
-}

gemachtigde :: Persoon * Machtiging [SUR] PRAGMA "" "is gemachtigd op grond van" ""
MEANING "Volgens het Burgerlijk procesrecht en het Bestuursprocesrecht kan een persoon zich laten vertegenwoordigen door een gemachtigde, veelal een advocaat. Volgens het Strafrecht is er sprake van een raadsheer, of raadslieden."
PURPOSE RELATION gemachtigde[Persoon*Machtiging] IN DUTCH REF "Art. 8:24-1 Awb en Art. 2:1-1 Awb, Art. 111-2 Rv en Art. 28-1, §2, Sv"
{+Een persoon is een gemachtigde op grond van één machtiging.
-}
POPULATION gemachtigde CONTAINS
[	("mr. S. Levelt", "brief 2011/44-4454");
("mr. I. van Driel", "brief 2011/46-2462");
("mr. L.L.A. Cox", "brief 2011/44-8568")
]

inzake :: Machtiging * Zaak [UNI] PRAGMA "" "heeft betrekking op" ""
MEANING "Elke machtiging heeft betrekking op precies één zaak."
PURPOSE RELATION inzake[Machtiging*Zaak] IN DUTCH REF "Art. 80-4 Rv."
{+Omdat een machtiging geldt voor een specifieke zaak, moet elke zaak waarvoor de machtiging geldt expliciet in de machtiging benoemd zijn.
-}
POPULATION inzake[Machtiging*Zaak] CONTAINS
[	("brief 2011/44-4454", "11/123458");
("brief 2011/46-2462", "10/569423");
("brief 2011/44-8568", "11/489631")
]	

door :: Machtiging -> Partij  [UNI, TOT] PRAGMA "" "is verleend door" ""
MEANING "Er is altijd één partij die een gegeven machtiging heeft verleend."
PURPOSE RELATION door[Machtiging*Partij] IN DUTCH REF "Art. 3:60-1 BW3."
{+Elke machtiging is verleend door één persoon.
-}
POPULATION door[Machtiging*Partij] CONTAINS
[	("brief 2011/44-4454", "B. Stolen");
("brief 2011/46-2462", "A. Angereden");
("brief 2011/44-8568", "O.P.G. Elicht")
]	

vertegenwoordigt :: "natuurlijkpersoon" * Partij PRAGMA "" "neemt de vertegenwoordiging op zich van" ""
MEANING "Een persoon kan als vertegenwoordiger ten minste één ander persoon vertegenwoordigen."
PURPOSE RELATION vertegenwoordigt["natuurlijkpersoon"*Partij] IN DUTCH REF "Art 8:24-1 Awb"
{+Partijen kunnen zich laten bijstaan of door een gemachtigde laten vertegenwoordigen.
-}
POPULATION vertegenwoordigt["natuurlijkpersoon"*Partij] CONTAINS
[	("Sebastiaan Levelt", "B. Stolen");
("Ilja van Driel", "A. Angereden");
("Lonneke Cox", "O.P.G. Elicht")
]	
  
rechtsgebied :: Zaak -> Rechtsgebied PRAGMA "" "is geplaatst onder" ""
MEANING "Elke zaak valt onder een rechtsgbied."
PURPOSE RELATION rechtsgebied[Zaak*Rechtsgebied] IN DUTCH REF "Art.2-1 Sv"
{+Alle zaken hebben een rechtsgebied (bestuursrecht, civiel, straf, familie, enz.).
-}
POPULATION rechtsgebied CONTAINS
[	("11/123458",	"Civiel");
("10/569423",	"Straf");
("11/489631",	"Straf");
("175262",		"Civiel");
("AWB 08/3670",	"Bestuursrecht");
("156583",		"Civiel");
("402349",		"Civiel");
("397151", 		"Civiel");
("388346", 		"Civiel")
]	
  
zaaksoort :: Zaak -> Zaaktype [UNI, TOT] PRAGMA "" "wordt ingedeeld onder" ""
MEANING "Elke zaak heeft één zaaktype."
PURPOSE RELATION zaaksoort[Zaak*Zaaktype] IN DUTCH
{+Alle zaken zijn in te delen naar zaaktype (eerste aanleg, beroep, hoger beroep, enz.). Alle zaaksoorten zijn in de enterprise architectuur benoemd.
-}
POPULATION zaaksoort CONTAINS
[	("11/123458", "Kort geding");
("10/569423", "Eerste aanleg");
("11/489631", "Eerste aanleg");
("175262", "Eerste aanleg");
("AWB 08/3670", "Eerste aanleg");
("156583", "Eerste aanleg");
("402349", "Kort geding");
("397151", "Kort geding");
("388346", "Kort geding")

]	

RULE procespartijen: door~;inzake |- partij
MEANING "Alle machtigingen in een zaak zijn verleend door iemand die partij is in die zaak" 
PURPOSE RULE procespartijen IN DUTCH
{+Een persoon die als partij betrokken is bij een zaak, kan alleen machtigingen verstreken voor de zaken waarin hij als partij betrokken is.
-}	

RULE zaaktyperingen: 
 I[Zaaktype] = 'Kort geding'    \/
               'Vovo'           \/
               'Eerste aanleg'  \/
               'Beroep'         \/
               'Hoger beroep'   \/
               'Bodemprocedure' \/
               'Cassatie'           -- o.g.v. Artikel 78 RO

SPEC Advocaat ISA Persoon
RULE gemachtigde : vertegenwoordigt;door~;gemachtigde~ |- isAdvocaat
MEANING "Elke gemachtigde vertegenwoordiger van een partij, is een ingeschreven advocaat."
	   
ENDPATTERN


PROCESS	"Procesregels zaken"

RULE gewraakt: wraking;bezetting;kamer~;dient~ |- wrakingsverzoek;partij
MEANING "Elke rechter die zitting neemt in een bepaalde zaak, kan worden gewroken door een persoon die de rol van partij heeft in die zaak."
PURPOSE RULE gewraakt IN DUTCH REF "Art. 36 Rv."
{+Het wrakingsverzoek tegen een persoon die als rechter zitting neemt in een zaak die zij behandelen, kan alleen zijn ingediend op verzoek van een partij die betrokken is bij die zaak.
-}

ENDPROCESS

ENDCONTEXT