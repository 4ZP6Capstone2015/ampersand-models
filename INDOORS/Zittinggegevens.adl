CONTEXT Zittinggegevens

INCLUDE "Zaakgegevens.adl"

THEMES Zittinggegevens--, "Rechterlijke indeling", "Wet Herziening Gerechterlijke Kaart"
META "authors" "Leon Chevalking"

PATTERN Zittinggegevens -- hoort in INDOORS thema "Registratie", functioneel gebied "Plannen"
PURPOSE PATTERN "Zittinggegevens"
{+Dit gegevensgebied bevat de gestructureerde gegevens van en over zittingen, behandelingen, gerechten en hun kamers, nevenvestigingsplaatsen enzovoorts.
De Raad registreert zittingen momenteel in de primaire processystemen (Berber, Civiel, GPS, NKP).
De gegevens over gerechten (vestigingsplaatsen, standplaatsen van rechterlijke ambtenaren, kamers, zalen, enz.)
zullen veranderen onder invloed van de herziening gerechtelijke kaart.
Deze worden als veranderlijke gegevens beschouwd, zodat zij in de INDOORS-situatie door gemandateerde gebruikers kunnen worden beheerd.
-}

--------------------
-- Specialisaties --
--------------------
{-
SPEC Rolzitting ISA Zitting
SPEC Terechtzitting ISA Zitting


SPEC Zitting ISA Gebeurtenis
SPEC Gerecht ISA Orgaan

-}

SPEC "Mondelinge behandeling" ISA Behandeling

----------
-- Keys --
----------

--KEY zitting : "Mondelinge behandeling"(TXT "De behandeling van ", behandeld~;zaakid)
KEY zaal : Zaal(zaalnaam~)
KEY rechter : Rechter (naamrechter)


---------------
-- Concepten --
---------------

CONCEPT "Mondelinge behandeling" "Een mondelinge behandeling is de gebeurtenis waarop één zaak of meerdere zaken gezamenlijk behandeld worden op een zitting. Vormen van mondelinge behandelingen zijn comparities, pleidooien, terechtzittingen en raadkamerprocedures" "Art. 87 Rv; Art. 88 Rv; Art. 134 Rv; Art. 269 Sv; Art. 21 Sv"
CONCEPT "Zitting" "Een zitting is de overkoepelende eenheid waarop één of meerdere zaaksbehandelingen plaatsvinden." ""
CONCEPT "Zaal" "Een zittingszaal is de plek waar de mondelinge behandeling plaatsvindt, dit hoeft niet altijd een zaal binnen een gerecht te zijn." 
--------------
-- Relaties --
--------------

behandeling :: Zitting * Behandeling [UNI] PRAGMA "" "bevat" ""
MEANING "Tijdens een zitting vinden één of meerdere mondelinge behandelingen plaats."
PURPOSE RELATION behandeling[Zitting*Behandeling] IN DUTCH REF ""
{+
-} 

zittingszaal :: Zitting * Zaal [UNI] PRAGMA "" "vindt plaats in" ""
MEANING "Een zitting vindt plaats in een zittingszaal."
PURPOSE RELATION zittingszaal[Zitting*Zaal] IN DUTCH REF ""
{+ 
-}

zaalnaam :: Zaalnaam -> Zaal[UNI] PRAGMA "" "is de zaalnaam van" ""

behandeld :: Zaakid * "Mondelinge behandeling" PRAGMA "" "wordt behandeld tijdens" ""
MEANING "Een mondelinge behandeling behandelt één zaak of meedere zaken gezamenlijk."
PURPOSE RELATION behandeld[Zaakid*"Mondelinge behandeling"] IN DUTCH REF "Art. 8:1 lid 1 Awb; Art. 8:8 lid 1 Awb; Art. 8:8 lid 2 Awb; Art. 15 lid 1 Rv; Art. 15 lid 2 Rv"  
{+ Een zaak kan mondeling worden behandeld op een zitting.
-}

procesverbaal :: "Mondelinge behandeling" * Document [UNI] PRAGMA "" "is vastgelegd in" ""
MEANING "De griffier maakt een proces-verbaal op van de mondelinge behandeling."
PURPOSE RELATION procesverbaal["Mondelinge behandeling"*Document] IN DUTCH REF "Art. 8:61 lid 3 Awb; Art. 88 lid 3 Rv; Art. 326 lid 1 Sv"
{+ De griffier houdt aantekening bij van hetgeen tijdens de behandeling aan de orde is gekomen.
-}

rechtertekent :: Rechter * Document PRAGMA "" "heeft" "ondertekend"
MEANING "De rechter ondertekent het proces-verbaal."
PURPOSE RELATION rechtertekent[Rechter*Document] IN DUTCH REF ""
{+ De rechter die de mondelinge behandeling doet, ondertekent na afloop het proces-verbaal.
-}

griffiertekent :: Griffier * Document [UNI] PRAGMA "" "heeft" "ondertekend"
MEANING "De griffier die tijdens de mondelinge behandeling aanwezig is, ondertekent het proces-verbaal."
PURPOSE RELATION griffiertekent[Griffier*Document] IN DUTCH REF ""
{+
-}

griffier :: Zitting * Griffier [UNI] PRAGMA "Bij " "wordt" "als griffier aangewezen"
MEANING "Voor een zitting wordt een griffier aangewezen."
PURPOSE RELATION griffier[Zitting*Griffier] IN DUTCH REF "Art. 268 lid 3 Sv; Art.326 lid 1 Sv"
{+ Voor elke zitting is ten minste één griffier aangewezen. 
-}

tolk :: NatuurlijkPersoon * "Mondelinge behandeling" PRAGMA "" "verleent bijstand bij" "als tolk"
MEANING "Tijdens een mondelinge behandeling kan een tolk aanwezig zijn."
PURPOSE RELATION tolk[NatuurlijkPersoon*"Mondelinge behandeling"] IN DUTCH REF "Art. 276 lid 1 Sv"
{+ Indien op de terechtzitting blijkt dat de bijstand van een tolk nodig is, beveelt de rechtbank de oproeping van een tolk. Partijen en het Openbaar Ministerie kunnen vooraf ook zelf een tolk aanstellen, die ter zitting aanwezig is.
-}

bewijsstuk :: Document * "Mondelinge behandeling" PRAGMA "" "is als bewijsstuk gepresenteerd tijdens" ""
MEANING "Elk fysiek bewijsstuk dat tijdens de mondelinge behandeling gepresenteerd wordt, dient opgenomen te zijn in een document."
PURPOSE RELATION bewijsstuk[Document*"Mondelinge behandeling"] IN DUTCH REF "Art. 339 Sv; Art. 344 Sv; Art. 344a Sv; Art. 2.10 Landelijk Procesreglement voor Civiele Dagvaardingszaken; Art. 5 Procesregeling Bestuursrecht 2010"
{+ Fysieke bewijsstukken die tijdens een zitting zijn gepresenteerd, dienen geregistreerd te staan in een document.
-}

verhinderdatumrechter :: Rechter * Datum PRAGMA "" "is verhinderd op" ""
MEANING "Een rechter kan verhinderdata voor zittingen opgeven."
PURPOSE RELATION verhinderdatumrechter[Rechter*Datum] IN DUTCH REF "Art. 2 lid 2 sub 1 Landelijk strafprocesreglement"
{+ Om zittingen efficiënt te kunnen plannen, moet er rekening gehouden worden met verhinderdata van rechters.
-}

verhinderdatumpartij :: Partij * Datum PRAGMA "" "is verhinderd op" ""
MEANING "Een partij kan verhinderdata voor een mondelinge behandeling opgeven."
PURPOSE RELATION verhinderdatumpartij[Partij*Datum] IN DUTCH REF "Art. 8 lid 2 Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken; Art. 16 lid 4 Procesregeling Bestuursrecht 2010"
{+ Om zittingen efficiënt te kunnen plannen, moet er rekening gehouden worden met verhinderdata van de betrokken partijen.
-}


geagendeerd :: Zitting -> Datum [UNI] PRAGMA "" "is geagendeerd op de zitting van" ""
MEANING "Zaken worden geagendeerd op de zittingsdatum waarop ze mondeling behandeld worden."
PURPOSE RELATION geagendeerd[Zitting*Datum] IN DUTCH REF "Art. 258 lid 5 Sv; Art. 8 lid 1 Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken"
{+ De rechtbank kan de datum en het aanvangs- en eindtijdstip van een zitting, waaronder begrepen een descente, ambtshalve bepalen.
-}

begintijd :: Zitting * Tijdstip [UNI] PRAGMA "" "vangt aan om" ""
MEANING "Een zitting vangt aan op een begintijd."
PURPOSE RELATION begintijd[Zitting*Tijdstip] IN DUTCH REF "Art. 8 lid 1 Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken"
{+ Tijdens het plannen van de zitting moet er rekening gehouden worden met de agenda's van o.a. de rechter die de zaak behandelt.
-}

eindtijd :: Zitting * Tijdstip [UNI] PRAGMA "" "is beëindigd om" ""
MEANING "Een zitting wordt beëindigd aan op de eindtijd."
PURPOSE RELATION eindtijd[Zitting*Tijdstip] IN DUTCH REF "Art. 8 lid 1 Landelijk procesreglement voor civiele dagvaardingszaken bij de rechtbanken"
{+ Tijdens het plannen van de zitting moet er rekening gehouden worden met de agenda's van o.a. de rechter die de zaak behandelt.
-}


onderzoekgesloten :: Zaakid * Datum PRAGMA "Het onderzoek in " "is gesloten op" ""
MEANING "De rechtbank sluit het onderzoek in de zaak, wanneer zij van oordeel is dat het onderzoek voltooid is."
PURPOSE RELATION onderzoekgesloten[Zaakid*Datum] IN DUTCH REF "Art. 8:65 lid 1 Awb; Art. 345 lid 1 Sv"
{+ Een onderzoek in de zaak wordt afgesloten, om aan te geven dat er geen verdere (mondelinge) behandeling nodig is en er uitspraak gedaan gaat worden.
-}

behandelinggesloten :: "Mondelinge behandeling" * Tijdstip [UNI] PRAGMA "" "is gesloten op" ""
MEANING "Elke mondelinge behandeling heeft een eindtijd."
PURPOSE RELATION behandelinggesloten["Mondelinge behandeling"*Tijdstip] IN DUTCH REF ""
{+ Een mondeling behandeling wordt expliciet afgesloten en dit tijdstip wordt in het proces-verbaal genoteerd.
-}

geschorst :: "Mondelinge behandeling" * Tijdstip PRAGMA "" "is geschorst vanaf:" ""
MEANING "Een mondelinge behandeling wordt op beval van de rechtbank geschorst."
PURPOSE RELATION geschorst["Mondelinge behandeling"*Tijdstip] IN DUTCH REF "Art. 281 Sv; Art. 8:64 lid 1 Awb; 8:64 lid 2 Awb"
{+ Indien het belang van het onderzoek dit vordert, beveelt de rechtbank de schorsing van het onderzoek voor bepaalde of onbepaalde tijd.
-}

hervat :: "Mondelinge behandeling" * Tijdstip PRAGMA "" "is hervat op:" ""
MEANING "Een mondelinge behandeling wordt op gegeven moment hervat."
PURPOSE RELATION hervat["Mondelinge behandeling"*Tijdstip] IN DUTCH REF "Art. 319 lid 1 Sv; Art. 18 Procesregeling bestuursrecht 2010"
{+ Nadat een mondelinge behandeling geschorst is, wordt die behandeling op een zeker moment hervat.
-}

-----------------
-- Invarianten --
-----------------

RULE beschikbaar : rechter;geagendeerd |- -verhinderdatumrechter
MEANING "Een rechter kan alleen op een zitting geplaatst worden als hij op die dag beschikbaar is."
MESSAGE "Op de geplande zittingsdatum is een rechter niet beschikbaar."
VIOLATION (TXT "Rechter ", TGT I[Rechter], TXT " is niet beschikbaar op ", SRC verhinderdatumrechter)
PURPOSE RULE "beschikbaar" IN DUTCH REF ""
{+ Een rechter kan alleen ter zitting verschijnen als hij beschikbaar is.
-}

-- RULE tekenenpv : rechter + griffier ondertekenen pv

{-
RULE uitnodiging : 
MEANING "De uitnodiging van een zitting
-}

ENDPATTERN


----------------
-- Pattern RI --
----------------

PATTERN "Rechterlijke indeling"

--------------
-- Relaties --
--------------

naamrechter :: Rechter * Rechternaam

rechter :: Rechter * Zitting PRAGMA "" "heeft houdt zitting" ""
MEANING "Degene die zitting heeft in een kamer draagt de titel van rechter."
PURPOSE RELATION rechter[Rechter*Zitting] IN DUTCH REF "Art. 50 t/m 56 RO"
{+ De bezetting van een kamer bestaat uit een of meerdere rechters.
-}


arrondisement :: Arrondissement * Rechtbank [UNI] PRAGMA "" "is de hoofdplaats van" ""
MEANING "De rechtbank is gevestigd in de hoofdplaats van het arrondissement."
PURPOSE RELATION arrondisement[Arrondissement*Rechtbank] IN DUTCH REF "Art. 41 lid 1 RO; Art. 3 RI; Art. 5 RI; Art. 7 RI; Art. 9 RI; Art. 11 RI"
{+-}

ressorteert :: Arrondissement * Ressort [UNI] PRAGMA "" "ressorteert onder" ""
MEANING "De rechtbank is gevestigd in de hoofdplaats van het arrondissement."
PURPOSE RELATION ressorteert[Arrondissement*Ressort] IN DUTCH REF "Art. 2 RI; Art. 4 RI; Art. 8 RI; Art. 10 RI"
{+ Elke rechtbank ressorteert onder een gerechtshof.
-}
  
ressort :: Gerechtshof * Ressort [UNI] PRAGMA "" " heeft " "als hoofdvestigingsplaats"
MEANING "Het gerechtshof is gevestigd in de hoofdplaats van het ressort."
PURPOSE RELATION ressort[Gerechtshof*Ressort] IN DUTCH REF "Art. 59 lid 1 RO; Art. 1 RI" 
{+ Elk gerecht heeft een hoofdvestigingsplaats.
-}
  
neven :: Plaats * Rechtbank [UNI] PRAGMA "" "is nevenvestigingsplaats van" ""
MEANING "Een nevenvestigingsplaats wordt aangewezen door de rechtbank."
PURPOSE RELATION neven[Plaats*Rechtbank] IN DUTCH REF "Art. 41 lid 2 RO; Bijlage 2 RO"
{+ Het bestuur van een rechterlijk orgaan kan zaken verdelen over de hoofdplaats en de nevenvestigings- en nevenzittingsplaatsen. 
-}

-----------------
-- Invarianten --
-----------------
{-
RULE "Artikel 59 lid 4 RO": locatie~;kamer;gerecht |- hoofdplaats~ \/ hoofdplaatsrechtbank;ressort \/ neven;ressort
MEANING "Elke zitting vindt plaats in de hoofdvestigingsplaats van een gerecht of een van de nevenvestigingsplaatsen (tekst checken, Artikel 59 lid 4 RO)"	
-}


{-	
RULE bevoegdheid: beslissing;van;jurisdictie |- bevoegd
PURPOSE RULE bevoegdheid IN DUTCH
{+ Een bestuurszaak dient bij de rechter die bij de zetel
van de gemeente, provincie, waterschap of politieregio hoort,
waar tegen bezwaar was ingesteld (voorafgaande aan de zaak bij de bestuursrechter)
(art. 8:7 Awb.)
-}

{-
RULE "bevoegdheid van gerechten": dient;kamer;gerecht |- bevoegd
 MEANING "Een zaak wordt behandeld in een gerecht dat daartoe bevoegd is."
-}
--Evt verplaatsen naar beslissingen 
-- RULE "besluit": Op basis van artikel 26 Rv, mag een rechter niet weigeren te beslissen, dit is een invariant op beslist.

ENDPATTERN

-------------
-- Wet HGK --
-------------

PATTERN "Wet Herziening Gerechterlijke Kaart"

PURPOSE PATTERN "Wet Herziening Gerechterlijke Kaart"
{+
-}

rechtsgebied :: Rechtbank * Arrondissement


rechtsgebied :: Gerechtshof * Ressort


ressorteertHGK :: Arrondissement * Ressort


-----------------
-- Invarianten --
-----------------

-- Artikel 21b lid 1 Wet HGK is een regel die het vaststellen van zittingsplaatsen bepaald.

ENDPATTERN

ENDCONTEXT