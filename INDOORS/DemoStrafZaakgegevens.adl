CONTEXT DemoStrafZaakgegevens

-------------------------------------
-- Include relevante andere demo's --
-------------------------------------
INCLUDE "DemoStrafblad.adl"

---------------------------------
-- Relaties voor het interface --
---------------------------------
appointeringsvoorstel :: Appointeringsvoorstel * Dagvaarding


--------------------------------------
-- Items voor in de zwarte menubalk --
--------------------------------------

INTERFACE Dagvaardingen FOR "Officier van Justitie": I[ONE]
BOX [ "Te registreren"  : V[ONE*Zaakid] \/ V[ONE*Zaakid];zaakid;zaakid~;aanhangig~;appointeringsvoorstel~;appointeringsvoorstel;aanhangig 
    , Dagvaardingen     : V[ONE*Dagvaarding];dagvaarding;dagvaarding~ /\ -(V[ONE*Dagvaarding];aanhangig;zaakid;zaakid~;aanhangig~)
    ]
    
INTERFACE "Dagvaardingen registreren" FOR Griffie: I[ONE]
BOX [ "Dagvaardingen zonder zaaknummer"     : V[ONE*Dagvaarding];dagvaarding;dagvaarding~ /\ -(V[ONE*Dagvaarding];aanhangig;zaakid;zaakid~;aanhangig~)
    ]
    
INTERFACE Zaken FOR "Juridisch medewerker" : I[ONE]
BOX [ "Verdeel zaken over kamers" : V[ONE*Zaakid]
    ]


----------------------------------------------------
-- Interfaces die nagenoeg gelijk aan elkaar zijn --
----------------------------------------------------

INTERFACE Appointeren (appointeringsvoorstel,aanhangig, zaakid, ontvanger, strafrechtsketennummer, feit, artikelvoorschrift, omschrijvingartikel, roldatum) FOR "Officier van Justitie" 
                                 : I[Appointeringsvoorstel]
BOX [ Appointeringsvoorstel      : appointeringsvoorstel
      BOX [ ""                        : aanhangig
            BOX[ Parketnummer         : zaakid
               ]
          , Verdachte             : ontvanger
          , Persoonsgegevens            : ontvanger
            BOX [ Naam              : I[NatuurlijkPersoon]
                , Woonplaats        : woonplaats
                , Strafketennummer  : strafrechtsketennummer~
                ]
          , "Wettelijke voorschriften" : feit
             BOX [ Artikel : artikelvoorschrift~
                 , "" : artikelvoorschrift~
                 BOX [ Omschrijving: omschrijvingartikel~
                     ]
                 ]
          , Roldatum             : roldatum
          ]
    ]


INTERFACE Dagvaarding FOR "Officier van Justitie", Griffie, "Juridisch medewerker", Rechter : I[Dagvaarding]
BOX [ Kenmerk                         : dagvaarding
    , "Datum betekening"              : datumbetekening
    , Verdachte                       : ontvanger
    BOX [ Naam                        : I[NatuurlijkPersoon]
        , Woonplaats                  : woonplaats
        , Strafketennummer            : strafrechtsketennummer~
        ]
    , Feit                            : feit
    BOX [ Tijdstip                    : tijdstipdelict
        , "Plaats delict"             : plaatsdelict
        , Omstandigheden              : omstandigheden        
        ]
    , "Wettelijke voorschriften"      : feit
      BOX [ Artikel : artikelvoorschrift~
          , "" : artikelvoorschrift~
            BOX [ Omschrijving: omschrijvingartikel~
                ]
          ]    
    , "Datum terechtzitting"          : roldatum
    ]


INTERFACE "Dagvaarding aanbrengen" (dagvaarding, aanhangig, datumbetekening, feit, tijdstipdelict, plaatsdelict, omstandigheden) FOR "Officier van Justitie" 
                                     : I[Zaakid]
BOX [ Parketnummer                   : zaakid
    , Dagvaarding                    : aanhangig~
      BOX [ Kenmerk                  : dagvaarding
          , "Datum betekening"       : datumbetekening
          , Verdachte                : ontvanger
          BOX [ Naam                 : I[NatuurlijkPersoon]
              , Woonplaats           : woonplaats
              , Strafketennummer     : strafrechtsketennummer~
              ]
         , Feit                      : feit
         BOX [ Tijdstip              : tijdstipdelict
             , "Plaats delict"       : plaatsdelict
             , Omstandigheden        : omstandigheden        
             ]
        , "Wettelijke voorschriften" : feit
          BOX [ Artikel : artikelvoorschrift~
              , "" : artikelvoorschrift~
               BOX [ Omschrijving: omschrijvingartikel~
                   ]
              ]    
        , "Datum terechtzitting"     : roldatum
         ]
    ]

INTERFACE "Dagvaarding inzien" FOR "Officier van Justitie" 
                            : I[Dagvaarding]
BOX [ ""                    : dagvaarding;dagvaarding~
      INTERFACE Dagvaarding
    ]
    

INTERFACE "Zaak bijwerken" (behandelendgerecht, kamer, samenstelling) FOR "Juridisch medewerker"
                                : I[Zaakid] /\ -(vonnis~;vonnis)
BOX [ Dagvaarding               : aanhangig~
      INTERFACE Dagvaarding
    , Zaaknummer                : zaakid 
    , "Behandelende rechtbank"  : behandelendgerecht
    , Kamer                     : kamer
    BOX [ Samenstelling         : samenstelling
        ]    
    ]

INTERFACE "Zaak afdoen" (vonnis) FOR Griffie
                                : I[Zaakid]
BOX [ Zaaknummer                : zaakid 
    , Dagvaarding               : aanhangig~
      INTERFACE Dagvaarding
    , "Behandelende rechtbank"  : behandelendgerecht
    , Kamer                     : kamer        
    , "Uitspraak (Referentie)"  : vonnis~    
    ]    

INTERFACE Zaak FOR Griffie, "Juridisch medewerker", Rechter : I[Zaakid]
BOX [ ""                                              : aanhangig~
      INTERFACE Dagvaarding
    , Zaaknummer                                      : zaakid
    , "Behandelende rechtbank"                        : behandelendgerecht
    , Kamer                                           : kamer    
    , "Vonnis (referentie)"                           : vonnis~    
    ]
    
INTERFACE "Zaak inzien" FOR Griffie, Rechter
                                : I[Zaakid]
BOX [ ""                                              : aanhangig~
      INTERFACE Dagvaarding
    , Zaaknummer                                      : zaakid
    , "Behandelende rechtbank"                        : behandelendgerecht
    , Kamer                                           : kamer    
    , "Vonnis (referentie)"                           : vonnis~    
    ]    
    
ENDCONTEXT