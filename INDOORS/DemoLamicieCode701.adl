CONTEXT DemoLamicieCode701 IN DUTCH

-------------------------------------
-- Include relevante andere demo's --
-------------------------------------
INCLUDE "INDOORS.adl"
INCLUDE "DemoZittinggegevens.adl"
INCLUDE "DemoBeslissingen.adl"

-------------------------------------
-- Include populatie --
-------------------------------------
INCLUDE "DemoLamicieCode701.pop"

---------------------------
-- Demo- en procesregels --
---------------------------

PROCESS Lamicie701proces



RULE documentkenmerk : I[Document] |- kenmerk;kenmerk~
MESSAGE "Het document heeft geen kenmerk."

RULE documenttype : I[Document] |- type;type~
MESSAGE "Het document heeft geen type."

RULE appointeringsvoorstelcompleet : appointeringsvoorstel;type;'Appointeringsvoorstel';type~ |- appointeringsvoorstel;roldatum;roldatum~
MESSAGE "Het appointeringsvoorstel bevat geen roldatum."

RULE dagvaardingdata : zaaksdocument~;type;'Dagvaarding';type~ |- zaaksdocument~;datumbetekening;datumbetekening~ \/ zaaksdocument~;roldatum;roldatum~
MESSAGE "Er ontbreekt een datum (betekening of roldatum) op de dagvaarding."

RULE dagvaardingfeit : zaaksdocument~;type;'Dagvaarding';type~ |- zaaksdocument~
MESSAGE "De aanleiding voor de dagvaarding ontbreekt."



ENDPROCESS

--------------------------------------
-- Items voor in de zwarte menubalk --
--------------------------------------

INTERFACE Dagvaardingen FOR "Officier van Justitie"
                    : I[ONE]
BOX 
[ 
   "Te registreren" : V[ONE*Zaakid] --\/ V[ONE*Zaakid];zaakid;zaakid~;aanhangig~;appointeringsvoorstel~;appointeringsvoorstel;aanhangig 
   , Dagvaardingen  : V[ONE*Document];type;'Dagvaarding';type~ --/\ -(V[ONE*Dagvaarding];aanhangig;zaakid;zaakid~;aanhangig~)
]
	
INTERFACE "Dagvaardingen registreren" FOR "Administratief medewerker"
                                     : I[ONE]
BOX 
[ 
   "Dagvaardingen zonder zaaknummer" : V[ONE*Document];type;'Dagvaarding';type~ /\ -(V[ONE*Document];type;'Dagvaarding';type~;zaaksdocument;zaakid;zaakid~;zaaksdocument~)
]


INTERFACE Zaken FOR "Poortraadsheer" 
                               : I[ONE]
BOX 
[ 
   "Appointeer zaken" : V[ONE*Zaakid];geappointeerdezaak~;geappointeerdezaak /\ -(V[ONE*Zaakid];geappointeerdezaak~;geappointeerdezaak;behandeld;(datumbehandeling;datumbehandeling~;behandeld~ \/ geschattebehandeltijd;geschattebehandeltijd~;behandeld~))
]

	
----------------------------------------------------
-- Interfaces die nagenoeg gelijk aan elkaar zijn --
----------------------------------------------------

INTERFACE "Appointeringsvoorstel indienen" (appointeringsvoorstel, type, kenmerk, geappointeerdezaak, roldatum, zaakid) FOR "Officier van Justitie" 
                         : I[Document]
BOX 
[ 
   Kenmerk               : kenmerk
,  Appointeringsvoorstel : appointeringsvoorstel~
   BOX 
   [ 
      "Parketnummer(s)"       : geappointeerdezaak
	  BOX
	  [
	     ""              : zaakid
	  ]
   ]
,  Type                  : type   
,  Roldatum              : roldatum   
]

INTERFACE "Appointeren" (behandelend, geschattebehandeltijd, datumbehandeling, behandeld, samenstelling, naambezetting) FOR Poortraadsheer
                         :I[Zaakid]
BOX 
[ 
   "Zaakgegevens"             : I[Zaakid]
   BOX
   [
      Parketnummer             : zaakid
   ,  Feit                     : aanleiding
         BOX 
         [ 
            Tijdstip           : datumdelict
         ,  "Plaats delict"    : plaatsdelict
         ,  Gronden            : gronden
         ,  Omstandigheden     : omschrijvingaanleiding
         ]
   ]   
,  "Gegevens zitting" : behandeld
   BOX
   [
      "Geschatte behandeltijd" : geschattebehandeltijd		 
   ,  Behandeldatum            : datumbehandeling
   ]
,  "Infomatie over Kamer"  : behandelend
   BOX
   [
      Samenstelling         : samenstelling
   ,  "Naam kamer"          : naambezetting
   ]
]

INTERFACE "Dagvaarding aanbrengen" (kenmerk, zaaksdocument, zaakid, datumbetekening, gerichtaangedaagde, aanleiding, datumdelict, plaatsdelict, gronden, omschrijvingaanleiding) FOR "Officier van Justitie" 
                               : I[Document]
BOX 
[ 
   Kenmerk                     : kenmerk
,  Type                        : type	 
,  Zaaksdocument               : zaaksdocument
   BOX
   [   
      Parketnummer             : zaakid
   ,  Feit                     : aanleiding
         BOX 
         [ 
            Tijdstip           : datumdelict
         ,  "Plaats delict"    : plaatsdelict
         ,  Gronden            : gronden
         ,  Omstandigheden     : omschrijvingaanleiding
         ]
   ]
,  "Datum betekening"          : datumbetekening
,  "In ontvangst genomen door" : gerichtaangedaagde
   BOX 
   [ 
      Voornaam                 : gbaVoornamen
   ,  Achternaam               : gbaGeslachtsnaam
   ,  Adres                    : woonadres
      BOX
      [
	     Straatnaam            : straatnaam
      ,  Huisnummer            : huisnummer
      ,  Woonplaats            : woonplaats
      ,  Postcode              : postcode
      ]
   ]
,  "Datum terechtzitting"      : roldatum
]

INTERFACE "Dagvaarding inzien" FOR "Officier van Justitie", "Juridisch medewerker", "Administratief medewerker", Advocaat
                               : I[Document]
BOX 
[ 
   Kenmerk                     : kenmerk
,  Zaaksdocument               : zaaksdocument
   BOX
   [   
      Parketnummer             : zaakid
   ,  Feit                        : aanleiding
      BOX 
      [ 
         Tijdstip                 : datumdelict
      ,  "Plaats delict"          : plaatsdelict
      ,  Gronden                  : gronden
      ,  Omstandigheden           : omschrijvingaanleiding
      ]
   ]
,  "Datum betekening"          : datumbetekening
,  "In ontvangst genomen door" : gerichtaangedaagde
   BOX 
   [ 
      Naam                     : I[NatuurlijkPersoon]
   ,  Adres                    : woonadres
      BOX
      [
	     Straatnaam            : straatnaam
      ,  Huisnummer            : huisnummer
      ,  Woonplaats            : woonplaats
      ,  Postcode              : postcode
      ]
   ]
,  "Datum terechtzitting"      : roldatum
]

ENDCONTEXT