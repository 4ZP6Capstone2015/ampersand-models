CONTEXT Advocatentableau

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd, is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten. BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen. De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Art. 1
--lid 1
inschrijving :: Inschrijving * Persoon PRAGMA "" "heeft betrekking op" ""
	MEANING "Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten."
	PURPOSE RELATION inschrijving[Inschrijving*Persoon] IN DUTCH REF "Art. 1-1, §1, Advw."
	{+Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel "advocaat" te mogen voeren.
	-}
	POPULATION inschrijving CONTAINS
		[ 	("0001", "mr. S. Levelt");
			("0002", "mr. I. van Driel");
			("0003", "mr. A. Dekkers");
			("0004", "mr. L.L.A. Cox")
		]

--Procesregels
RULE advocaat: isAdvocaat |- isBeëdigd;beëdiging;inschrijving
	MEANING "Elke persoon die de titel advocaat draagt is beëdigd en is op het advocatentableau ingeschreven."
	PURPOSE RULE "advocaat"
	{+Een persoon kan de titel advocaat voeren als hij als advocaat beëdigd is.
	-}

RULE verzoekTotAdvocaat : inschrijving~ |-	isVerstuurdDoor~;verzoekTotInschrijving
	MEANING "Aan elke inschrijving van een advocaat is een verzoek van inschrijving voorafgegaan."
	PURPOSE RULE verzoekTotAdvocaat REF "Art. 2-1, §1, Advw."
	{+Het initiatief om tot advocaat ingeschreven te worden ligt bij de persoon, daarom moet hij een verzoek van inschrijving indienen.
	-}


--Wie doet de inschrijving? Griffie rechtbank --> Rol toevoegen.	
RULE beëdigdeAdvocaat: inschrijving~ |- isBeëdigd;beëdiging
	MEANING "Elke ingeschreven advocaat is beëdigd."
	PURPOSE RULE "beëdigdeAdvocaat" --REF
	{+Herschrijven
	-}

--Art.2
--lid 1
dientVerzoekIn :: Persoon * Rechtbank PRAGMA "" "dient een verzoek tot inschrijving van advocaat in bij" ""
	MEANING "Een persoon dient zijn verzoek van inschrijving tot advocaat in bij maximaal één rechtbank"
	PURPOSE RELATION dientVerzoekIn[Persoon*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Elk persoon kan aan de voorzitter van de rechtbank van het arrondissement waarin de verzoeker kantoor wenst te houden schriftelijk inschrijving als advocaat te verzoeken..
	-}
	POPULATION dientVerzoekIn CONTAINS
		[	("mr. S. Levelt", "Rechtbank van Amsterdam");
			("mr. I. van Driel", "Rechtbank Rotterdam");	
			("mr. A. Dekkers", "Rechtbank van Utrecht");
			("mr. L.L.A. Cox", "Rechtbank van Utrecht")
		]

	
isGerichtAan :: "Verzoek van inschrijving" -> Rechtbank [UNI,TOT] PRAGMA "" "is gericht aan" ""
	MEANING "Elk verzoek van inschrijving is gericht aan precies één rechtbank."
	PURPOSE RELATION isGerichtAan["Verzoek van inschrijving"*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Een verzoek is gericht aan een specifieke rechtbank, daar waar de verzoeker kantoor wenst te houden.
	-}
	POPULATION isGerichtAan CONTAINS
		[	("V001", "Rechtbank van Amsterdam");
			("V002", "Rechtbank Rotterdam");
			("V003", "Rechtbank van Utrecht");
			("V004", "Rechtbank van Utrecht")
		]

	
isVerstuurdDoor :: "Verzoek van inschrijving" * Persoon PRAGMA "" "is verstuurd door" ""
	MEANING "Elk verzoek van inschrijving op het advocatentableau is verstuurd door één persoon."
	PURPOSE RELATION isVerstuurdDoor["Verzoek van inschrijving"*Persoon] IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen.
	-}
	POPULATION isVerstuurdDoor CONTAINS
		[	("V001", "mr. S. Levelt");
			("V002", "mr. I. van Driel");
			("V003", "mr. A. Dekkers");
			("V004", "mr. L.L.A. Cox")
		]
	
verzoekTotInschrijving :: "Verzoek van inschrijving" * Inschrijving PRAGMA "" "heeft geleid tot" ""
	MEANING "Een verzoek van inschrijving kan leiden tot één inschrijving."
	PURPOSE RELATION verzoekTotInschrijving["Verzoek van inschrijving"*Inschrijving] IN DUTCH
	{+Een inschrijving op het advocatentableau kan plaatsvinden als er een verzoek tot inschrijving ingediend is.
	-}
	POPULATION verzoekTotInschrijving CONTAINS
		[	("V001", "0001");
			("V002", "0002");
			("V003", "0003");
			("V004", "0004")
		]

RULE "Art. 2-1 Advw.": isGerichtAan |- isVerstuurdDoor;dientVerzoekIn
	MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de rechtbank waar de betreffende persoon kantoor wenst te houden."
	PURPOSE RULE "Art. 2-1 Advw."
	{+De wet schrijft voor dat inschrijvingsverzoeken aan de rechtbank worden gericht.
	-}
 
	
-- Art. 3
isBeëdigd :: Persoon * Beëdiging PRAGMA "" "is beëdigd op" ""
	MEANING "Elke advocaat heeft ten minste één beëdiging ondergaan."
	PURPOSE RELATION isBeëdigd[Persoon*Beëdiging] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Een advocaat wiens verzoek tot inschrijving geaccepteerd is door het betreffende rechtbank, wordt door de voorzitter van het rechtbank officieel als advocaat beëdigd.
	-}
	POPULATION isBeëdigd CONTAINS
		[ 	("mr. S. Levelt", "B001");
			("mr. I. van Driel", "B002");
			("mr. A. Dekkers", "B003");
			("mr. L.L.A. Cox", "B004")
		]
	
	
locatie :: Beëdiging -> Locatie [TOT] PRAGMA "" "heeft plaatsgevonden op" ""
	MEANING "Elke beëdiging vindt plaats op precies één locatie."
	PURPOSE RELATION locatie[Beëdiging*Locatie] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Een beëdiging van een advocaat vindt plaats op de locatie van de rechtbank waarin de advocaat kantoor wil gaan houden."
	-}
	POPULATION locatie[Beëdiging*Locatie] CONTAINS
		[ 	("B001", "Amsterdam");
			("B002", "Rotterdam");
			("B003", "Utrecht");
			("B004", "Utrecht")
		]

	
datum ::  Beëdiging -> Datum [TOT] PRAGMA "" "geschiedde op" ""
	MEANING "Elke beëdiging geschiedde op precies één datum."
	PURPOSE RELATION datum[Beëdiging*Datum] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+De datum waarop de beëdiging plaats gevonden heeft, is de datum waarop de beëdigide de titel van advocaat mag voeren.
	-}
	POPULATION datum CONTAINS
		[ 	("B001", "25-08-1990");
			("B002", "12-02-2009");
			("B003", "06-10-2007");
			("B004", "18-04-2009")
		]


	
wasBetrokkenBij :: Rechtbank * Beëdiging PRAGMA "" "was betrokken bij" ""
	MEANING "Elke rechtbank is betrokken bij het behandelen van alle beëdigingingen van personen die succesvol verzocht hebben op deze rechtbank als advocaat ingeschreven te worden."
	PURPOSE RELATION wasBetrokkenBij[Rechtbank*Beëdiging] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+De rechtbank is verantwoordelijk voor het beëedigen van de personen  die verzocht hebben bij de rechtbank kantoor te houden als advocaat."
	-}
	POPULATION wasBetrokkenBij CONTAINS
		[ 	("Rechtbank van Amsterdam", "B001");
			("Rechtbank Rotterdam", "B002");
			("Rechtbank van Utrecht", "B003");
			("Rechtbank van Utrecht", "B004")
		]


beëdiging :: Beëdiging * Inschrijving PRAGMA "" "heeft betrekking op" ""
	MEANING "Elke inschrijving heeft een beëdiging als basis."
	PURPOSE RELATION beëdiging[Beëdiging*Inschrijving] IN DUTCH
	{+Om te kunnen zeggen op welke beëdiging een inschrijving is gebasseerd, gebruiken we het woord basis.
	-}
	POPULATION beëdiging CONTAINS
		[	("B001", "0001");
			("B002", "0002");
			("B003", "0003");
			("B004", "0004")
		]
--Procesregel		
RULE "Art. 3-1 Advw.": isBeëdigd;wasBetrokkenBij~ |- dientVerzoekIn
	MEANING "Elke advocaat die beëdigd wordt door een betrokken rechtbank, heeft tot die rechtbank ook het inschrijvingsverzoek gericht."
	PURPOSE RULE "Art. 3-1 Advw."
	{+Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is.
	-}
	
-- Art. 8
schrappingDoor :: "Verzoek tot schrapping" * Persoon [TOT] PRAGMA "" "is gedaan door" ""
	MEANING "Elk verzoek tot schrapping is gedaan door één persoon."
	PURPOSE RELATION schrappingDoor["Verzoek tot schrapping"*Persoon] IN DUTCH REF "Art. 8-2, §1, Advw."
	{+Een schrappingsverzoek moet schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf, of door rechtbank waar de advocaat kantoor houdt.
	-}
	POPULATION schrappingDoor CONTAINS
		[ 	("S001", "mr. A. Dekkers");
			("S002", "drs. S. Verhoeven");
			("S003", "mr. S. Levelt")
		]

schrappingVan :: "Verzoek tot schrapping" * Persoon [TOT] PRAGMA "" "heeft betrekking op" ""
	MEANING "Elk verzoek tot schrapping heeft betrekking op precies één advocaat."
	PURPOSE RELATION schrappingVan["Verzoek tot schrapping"*Persoon] IN DUTCH REF "Art. 8-2, §1, Advw."
	{+Een schrappingsverzoek heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden.
	-}
	POPULATION schrappingVan CONTAINS
		[ 	("S001", "mr. A. Dekkers");
			("S002", "mr. I. van Driel");
			("S003", "mr. S. Levelt")
		]

-- Art. 12
isOndergebracht :: Persoon * Kantoor PRAGMA "" "is ondergebracht bij" ""
	MEANING "Elke advocaat is ondergebracht bij het kantoor waar hij of zij werkzaam is."
	PURPOSE RELATION isOndergebracht [Persoon*Kantoor] IN DUTCH REF "Art. 12-1, §2, Advw."
	{+Het kantoor waar een advocaat werkzaam is, staat geregistreerd op het advocatentableau."
	-}
	POPULATION isOndergebracht CONTAINS
		[ 	("mr. S. Levelt", "Wieringa advocaten");
			("mr. I. van Driel", "Haulussy advocaten");
			("mr. A. Dekkers", "Kuipers, Jonkers, van den Berg");
			("mr. L.L.A. Cox", "ATM Advocaten Utrecht")
		]
	
kantoor :: Kantoor * Rechtbank PRAGMA "" "valt onder" ""
	MEANING "Elk kantoor valt onder één rechtbank." 
	PURPOSE RELATION kantoor[Kantoor*Rechtbank] IN DUTCH REF "Art. 12-1, §2, Advw."
	{+Een kantoor moet bij de wet onder één rechtbank bestaan. 
	-}	
	POPULATION kantoor CONTAINS
		[ 	("Wieringa advocaten", "Rechtbank van Amsterdam");
			("Haulussy advocaten", "Rechtbank Rotterdam");
			("Kuipers, Jonkers, van den Berg", "Rechtbank van Utrecht");
			("ATM Advocaten Utrecht", "Rechtbank van Utrecht")
		]

		
--Deze relatie vervangt tijdelijk de SPEC isA van advocaat naar persoon
isAdvocaat :: Persoon * Persoon PRAGMA "" "heeft als" "de rol van advocaat."
	MEANING "Een persoon kan de rol van één advocaat vervullen."
	PURPOSE RELATION isAdvocaat[Persoon*Persoon] IN DUTCH
	{+De rol van advocaat kan alleen vervuld worden door een persoon.
	-}
	POPULATION isAdvocaat CONTAINS
	[ 		("mr. S. Levelt", "mr. S. Levelt");
			("mr. I. van Driel", "mr. I. van Driel");
			("mr. A. Dekkers", "mr. A. Dekkers");
			("mr. L.L.A. Cox", "mr. L.L.A. Cox")
	]

ENDPATTERN

--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatieeisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

barnummer :: Barnummer -> Persoon PRAGMA "" "wordt toegekend" "in de hoedanigheid van advocaat."
	MEANING "Elk barnummer wordt toegekend aan precies één advocaat."
	PURPOSE RELATION barnummer[Barnummer*Persoon] IN DUTCH
	{+Een barnummer dient als uniek identificatienummer voor een advocaat.
	-}
	POPULATION barnummer CONTAINS
	[	("A23968", "mr. S. Levelt");
		("A26815", "mr. I. van Driel");
		("A16378", "mr. A. Dekkers");
		("A24763", "mr. L.L.A. Cox")
	]
	
kantoornummer :: Kantoornummer -> Kantoor PRAGMA "" "wordt toegekend" ""
	MEANING "Elk kantoornummer wordt toegekend aan precies één kantoor."
	PURPOSE RELATION kantoornummer[Kantoornummer*Kantoor] IN DUTCH
	{+Een kantoornummer dient als uniek identificatienummer voor een kantoor.
	-}
	POPULATION kantoornummer CONTAINS
	[ 	("K52347", "Wieringa advocaten");
		("K45317", "Haulussy advocaten");
		("K48933", "Kuipers, Jonkers, van den Berg");
		("K12493", "ATM Advocaten Utrecht")
	]

kantooradres :: Kantoor -> Adres PRAGMA "" "is gevestigd op" ""
	MEANING "Elk kantoor heeft precies één adres."
	PURPOSE RELATION kantooradres[Kantoor*Adres] IN DUTCH
	{+Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden.
	-}
	POPULATION kantooradres CONTAINS
	[ 	("Wieringa advocaten", "Herengracht 425-429, 1017 BR, Amsterdam");
		("Haulussy advocaten", "Westblaak 5f, 3001 AC, Rotterdam");
		("Kuipers, Jonkers, van den Berg", "Mariahoek 4, 3511 LD, Utrecht");
		("ATM Advocaten Utrecht", "Herculesplein 213, 3584 AA, Utrecht")
	]
	
begindatum :: Persoon * Begindatum PRAGMA "" "is zijn of haar werkzaamheden begonnen vanaf" ""
	MEANING "Elk persoon heeft één begindatum waarop de werkzaamheden beginnen."
	PURPOSE RELATION begindatum[Persoon*Begindatum] IN DUTCH
	{+Een persoon begint op een zekere datum zijn of haar werkzaamheden.
	-}
	POPULATION begindatum[Persoon*Begindatum] CONTAINS
	[ 	("mr. S. Levelt", "06-12-1999");
		("mr. I. van Driel", "13-05-1995");
		("mr. A. Dekkers", "20-01-1988");
		("mr. L.L.A. Cox", "31-07-2001")
	]

begindatum :: Begindatum * Kantoor PRAGMA "" "is van toepassing op" ""
	MEANING "Een begindatum is van toepassing op de werkzaamheden bij een kantoor."
	PURPOSE RELATION begindatum[Begindatum*Kantoor] IN DUTCH
	{+Werkzaamheden hebben betrekking op de aanstelling bij een kantoor.
	-}
	POPULATION begindatum[Begindatum*Kantoor] CONTAINS
	[ 	("06-12-1999", "Wieringa advocaten");
		("13-05-1995", "Haulussy advocaten");
		("20-01-1988", "Kuipers, Jonkers, van den Berg");
		("31-07-2001", "ATM Advocaten Utrecht")
	]
	
RULE werktbij : isOndergebracht |- begindatum[Persoon*Begindatum];begindatum[Begindatum*Kantoor]
	MEANING "Als een advocaat is ondergebracht bij een kantoor, dan is de begindatum van de werkzaamheden van de advocaat gelijk aan de begindatum waarop het kantoor de werkzaamheden van de advocaat registreert."
	PURPOSE RULE "werktbij"
	{+Een advocaat werkt vanaf de begindatum bij het kantoor waar hij of zij is ondergebracht.
	-}

	
-- Lelijke code die toegevoegd is om het zoeken mogelijk te maken
zoek :: Zoek * Zoek
 = [ ("zoek", "zoek")
   ]
   
zoekOpNaam :: Zoek * Persoon

zoekOpBAR :: Zoek * Barnummer
	
ENDPATTERN

INTERFACE Overview : I[ONE]
	BOX [ 	Advocaten				: V[ONE*Persoon];isAdvocaat,
			Kantoren				: V[ONE*Kantoor]
		]
		
INTERFACE Zoeken : I[ONE]
	BOX [	"Zoek op BAR-nummer"	: V[ONE*Zoek],
			"Zoek op naam"			: V[ONE*Zoek]
		]

INTERFACE "Zoek op BAR-nummer" : I[Zoek]
	BOX [ "BAR-nummer"				: zoekOpBAR
			BOX [ "BAR-nummer"			: barnummer
				BOX [
					"BAR-nummer"			: barnummer~,
					Advocaat 				: isAdvocaat,
					Inschrijvingsverzoek	: isVerstuurdDoor~,
					Beëdiging				: isBeëdigd,
					Kantoor					: isOndergebracht
					]
				]
		]		
		
INTERFACE "Geavanceerd zoeken" : I[Zoek]
	BOX [	"Gezochte naam"			: zoekOpNaam
			BOX	[
				"BAR-nummer"			: barnummer~,
				Advocaat 				: isAdvocaat,
				Inschrijvingsverzoek	: isVerstuurdDoor~,
				Beëdiging				: isBeëdigd,
				Kantoor					: isOndergebracht
				]
		]


INTERFACE Advocaten : I[Persoon]
	BOX	[	"BAR-Nummer"			: barnummer~,
			Advocaat 				: isAdvocaat,
			Inschrijvingsverzoek	: isVerstuurdDoor~,
			Beëdiging				: isBeëdigd,
			Kantoor					: isOndergebracht
		]
		
INTERFACE Kantoren : I[Kantoor]
	BOX	[	Kantooradres			: kantooradres,
			Rechtbank				: kantoor,
			Advocaten				: isOndergebracht~
		]
		
INTERFACE Rechtbanken : I[Rechtbank]
	BOX [	Kantoren				: kantoor~,
			Beëdigingen				: wasBetrokkenBij,
			Inschrijvingsverzoeken	: isGerichtAan~
				BOX [	"Indiener van" 					: isVerstuurdDoor
							BOX [	Inschrijvingsverzoek				: isVerstuurdDoor~],
						"Verwijst naar inschrijving"	: verzoekTotInschrijving
						
					]
		]
	
INTERFACE Beëdigingen : I[Beëdiging]
	BOX [	Beëdigde				: isBeëdigd~,
			Datum					: datum,
			Locatie					: locatie,
			Rechtbank				: wasBetrokkenBij~
		]
			
INTERFACE Inschrijvingsverzoeken : I["Verzoek van inschrijving"]
	BOX	[	"Ingediend door"		: isVerstuurdDoor,
			"Ingediend bij"			: isGerichtAan
		]
			
ENDCONTEXT