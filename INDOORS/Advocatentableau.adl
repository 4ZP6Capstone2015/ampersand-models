CONTEXT Advocatentableau


--INCLUDE "Persoonsgegevens.adl"

THEMES Advocatentableau

----------------------
-- Advocatentableau --
----------------------

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

----------
-- Keys --
----------

KEY advocaat : Inschrijving (isadvocaat;gbaVoornamen, TXT " ", isadvocaat;gbaGeslachtsnaam)
KEY kantoor : Kantoornummer(kantoornaam)


---------------
-- Concepten --
---------------

CONCEPT Verzoeker "Een ieder die schriftelijk verzoekt om ingeschreven te worden als advocaat." "Art. 2 lid 1 Advw"

CONCEPT Advocaat "Een raadsheer die de praktijk uitoefent overeenkomstig de bevoegdheden en vereisten, bij de Wetboeken van Burgerlijke Rechtsvordering en Strafvordering en bij de bijzondere wetten en besluiten gegeven en gevorderd, en overeenkomstig deze wet en de daarop berustende verordeningen en besluiten." "Art. 10 Advw"

CONCEPT "Verzoekt tot inschrijving" "Een door de verzoeker opgesteld document gericht aan de president van de rechtbank waarin verzocht wordt tot inschrijving als advocaat." "Art. 2 lid 1 Advw"

CONCEPT Inschrijving "De gebeurtenis die de advocatuur van de ingeschrevene bevestigt." "Art. 1 lid 1 Advw; Art. 1 lid 2 Advw"

CONCEPT Beëdiging "De gebeurtenis waarop de verzoeker de eed of belofte aflegt waarna de verzoeker als advocaat op het tableau ingeschreven kan worden." "Art. 3 lid 1 Advw"

CONCEPT "BAR nummer" "Uniek, persoonlijk nummer dat bij inschrijving aan advocaten toegekend wordt, om administratieve handelingen te vergemakkelijken" ""

CONCEPT Kantoor "Het kantoor van de advocaat geldt voor al zijn handelingen als zodanig als gekozen woonplaats." "Art. 12 lid 3 Advw"

CONCEPT Schrapping "Gebeurtenis die de advocaat uit zijn functie ontheft waarmee de rechten, plichten en bevoegdheden van advocaten niet meer van toepassing zijn op de geschrapte persoon" "Art. 8 Advw"

CONCEPT Schorsing "Gebeurtenis die de advocaat tijdelijk zijn bevoegdheden als advcaat ontneemt." "Art. 16 Advw; Art. 48 lid 2 sub c Advw"

--------------
-- Relaties --
--------------

verzoektotinschrijving :: Document * NatuurlijkPersoon[UNI] PRAGMA "" "is het verzoek tot inschrijving als advocaat van" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION verzoektotinschrijving[Document*NatuurlijkPersoon] IN DUTCH REF "Art. 2 lid 1 Advw"
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door een persoon.
-}


inschrijvingsverzoek :: Document * Inschrijving [UNI] PRAGMA "" "verwijst naar de feitelijke inschrijving van" ""
MEANING "Het inschrijvingsverzoek wordt schriftelijk ingediend."
PURPOSE RELATION inschrijvingsverzoek[Document*Inschrijving] IN DUTCH REF "Art.2 lid 1 Advw"
{+ Inschrijving op het advocatentableau wordt schriftelijk verzocht.
-}


gerichtaan :: Inschrijving -> Rechtbank [UNI] PRAGMA "Het verzoek tot inschrijving van " "is gericht aan de president van de" ""
MEANING "Een verzoek tot inschrijving als advocaat is gericht aan de president van de rechtbank."
PURPOSE RELATION gerichtaan[Inschrijving*Rechtbank] IN DUTCH REF "Art. 2 lid 1 Advw"
{+ Ieder is bevoegd aan de voorzitter van de rechtbank schriftelijk inschrijving als advocaat te verzoeken.
-}


inschrijvingseis :: Inschrijving * Inschrijvingseis [UNI] PRAGMA "Het verzoek tot inschrijving van " "" "aan de inschrijvingseisen"
MEANING "Een verzoek tot inschrijving als advocaat moet voldoen aan de wettelijke gestelde eisen."
PURPOSE RELATION inschrijvingseis[Inschrijving*Inschrijvingseis] IN DUTCH REF "Art. 2 Advw"
{+ De advocatenwet specificeert eisen waaraan inschrijvingsverzoeken moeten voldoen.
-}


inschrijvingsstatus :: Inschrijving -> Inschrijvingsstatus [UNI] PRAGMA "De inschrijving van " "heeft de status" ""
MEANING "De inschrijving van een advocaat op het landelijk advocaten tableau heeft een status."
PURPOSE RELATION inschrijvingsstatus[Inschrijving*Inschrijvingsstatus] IN DUTCH REF "Art. 1 lid 2 Advw; Art. 8 Advw; Art. 16 Advw"
{+ De status van een ingeschreven advocaat geeft aan welke bevoegdheden de ingeschrevene heeft.
-}


beëdigd :: Beëdiging -> Inschrijving [UNI] PRAGMA "" "is beëdiging van" ""
MEANING "Om als advocaat ingeschreven te worden op het tableau dient men beëdigd te worden."
PURPOSE RELATION beëdigd[Beëdiging*Inschrijving] IN DUTCH REF "Art. 3 lid 1 Advw."
{+ Een advocaat wordt beëdigd op requisitoir van het openbaar ministerie.
-}


locatieBeëdiging :: Beëdiging -> Rechtbank [UNI] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van de verzoeker vindt plaats op een rechtbank."
PURPOSE RELATION locatieBeëdiging[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3 lid 1 Advw."
{+ Een verzoeker wordt door de rechtbank beëdigd.
-}


isadvocaat :: Inschrijving -> NatuurlijkPersoon [UNI] PRAGMA "Het verzoek tot inschrijving van " "heeft geleid tot de inschrijving van" ""
MEANING "Elke advocaat staat geregistreerd op het tableau van de Nederlandse Orde van Advocaten."
PURPOSE RELATION isadvocaat[Inschrijving*NatuurlijkPersoon] IN DUTCH REF "Art. 1 lid 1 Advw."
{+ De advocaten worden ingeschreven op het tableau van de Nederlandse Orde van Advocaten.
-}


houdtkantoor :: Inschrijving * Kantoornummer [UNI] PRAGMA "" "staat ingeschreven bij advocatenkantoor" "" 
MEANING "Een advocaat is verplicht kantoor te houden." 
PURPOSE RELATION houdtkantoor[Inschrijving*Kantoornummer] IN DUTCH REF "Art. 12 lid 1 Advw."
{+ Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden.
-}


barnummer :: Barnummer -> Inschrijving [UNI] PRAGMA "Barnummer " "is toegekend aan" ""
MEANING "Elke advocaat heeft een uniek BAR-nummer."
PURPOSE RELATION barnummer[Barnummer*Inschrijving] IN DUTCH REF ""
{+ Ingeschreven advocaten hebben een uniek nummer waarmee koppelingen gelegd kunnen worden met externe applicaties om advocaten uniek te identificeren.
-}


schrappingvan :: Bericht * Inschrijving [UNI] PRAGMA "" "bevat de beslissing tot schrapping van" ""
MEANING "Van een beslissing tot schrapping wordt een bericht verstuurd."
PURPOSE RELATION schrappingvan[Bericht*Inschrijving] IN DUTCH REF "Art. 8 lid 2 Advw"
{+ Van de beslissing tot schrapping wordt door de griffier van de rechtbank kennisgegeven aan de secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

schorsingvan :: Bericht * Inschrijving [UNI] PRAGMA "" "bevat de beslissing tot schorsing van" ""
MEANING "Van een beslissing tot schorsing wordt een bericht verstuurd."
PURPOSE RELATION schorsingvan[Bericht*Inschrijving] IN DUTCH REF "Art. 16 Advw"
{+ De griffier van het gerecht geeft van de schorsing kennis aan de secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}



schorsingopheffen :: Bericht * Inschrijving [UNI] PRAGMA "" "bevat de beslissing tot opheffing van de schorsing van" ""
MEANING "Van een beslissing tot opheffing van schorsing wordt een bericht verstuurd."
PURPOSE RELATION schorsingopheffen[Bericht*Inschrijving] IN DUTCH REF "Art. 16 Advw"
{+ De griffier van het gerecht geeft van de opheffing schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}



-----------------
-- Invarianten --
-----------------

--Beëdigingen vinden plaats op de rechtbank waar het inschrijvingsverzoek naar verzonden is
RULE beëdigingslocatie : locatieBeëdiging~ |- gerichtaan~;V
MEANING "De rechtbank waar de verzoeker beëdigd wordt, is gelijk aan de rechtbank aan wier president het verzoek tot inschrijving gericht is."
MESSAGE "De rechtbank waar de locatie plaatsvindt komt niet overeen met de rechtbank waaraan het inschrijvingsverzoek gericht is."
PURPOSE RULE "beëdigingslocatie" IN DUTCH REF "Art. 3 lid 1 Advw"
{+ De rechtbank waar het verzoek tot inschrijving aan gericht is, is de rechtbank waar de beëdiging wordt gepland en gehouden.
-}

--Verzoekers die nog niet gescreend zijn, mogen niet worden beëdigd
RULE screening : beëdigd~ |- inschrijvingseis;V
MEANING "Een te beëdigden verzoeker heeft voldaan aan de inschrijvingseisen."
MESSAGE "De inschrijvingseisen van de verzoeker zijn nog niet gecontroleerd of voldoen niet."
PURPOSE RULE screening IN DUTCH REF ""
{+ De Nederlandse Orde van Advocaten controleert of de verzoeker aan de inschrijvingseisen voldoet, voordat de verzoeker mag worden beëdigd.
-} 

RULE schrappingvanadvocaat : schrappingvan~ |- isadvocaat;V
MEANING "Een bericht dat de schrapping van een advocaat betreft, kan alleen over een op het tableau verkerende advocaat gaan."
PURPOSE RULE "schrappingvanadvocaat" IN DUTCH REF ""
{+ Om de geldigheid van of beslissingen tot schrapping te controleren, kan gesteld worden dat deze alleen over op het tableau ingeschreven advocaten mogen gaan.
-}

RULE schorsingvanadvocaat : schorsingvan~ |- isadvocaat;V
MEANING "Een bericht dat de schorsing van een advocaat bevat kan alleen over een op het tableau verkerende advocaat gaan."
PURPOSE RULE schorsingvanadvocaat IN DUTCH REF ""
{+ Om de geldigheid van beslissingen tot schorsing te controleren, kan gesteld worden dat deze alleen over op het tableau ingeschreven advocaten mogen gaan.
-}


RULE schorsingopheffen : schorsingopheffen~ |- schorsingvan~;V /\ -schrappingvan~;V
MEANING "Een bericht dat de opheffing van een schorsing bevat kan alleen een geschorste advocaat betreffen."
PURPOSE RULE "schorsingopheffen" IN DUTCH REF ""
{+ Om volgordelijkheid van het proces tot opheffing van schorsing te bewaken, kunnen berichten die opheffing van een schorsing betreffen alleen over geschorste advocaten gaan.
-}


RULE statusvaninschrijving: I[Inschrijvingsstatus] = 'voorwaardelijk' \/ 'onvoorwaardelijk' \/ 'geregistreerd' \/ 'geschrapt' \/ 'geschorst'
MEANING "De inschrijving als advocaat geschiedt voorwaardelijk of onvoorwaardelijk."
MESSAGE "Volgens Artikel 1 lid 2 van de Advocatenwet is de inschrijving van een advocaat 'voorwaardelijk' of 'onvoorwaardelijk'."
PURPOSE RULE statusvaninschrijving IN DUTCH REF "Art. 1 lid 2 Advw"
{+ De inschrijvingsstatus van een advocaat is wettelijk bepaald als 'voorwaardelijk' of 'onvoorwaardelijk'
-}

RULE inschrijvingseistypen: I[Inschrijvingseis] = 'voldoet'
MEANING "Aan inschrijvingseisen moet uiteindelijk worden voldaan."
MESSAGE "Een inschrijvingseis kan de status hebben van 'voldoet'."
PURPOSE RULE "inschrijvingseistypen" IN DUTCH REF "Art 2 Advw"
{+ De status van de inschrijvingseis(en) bepaalt de status van het inschrijvingsverzoek
-}

--------------------
-- Specialisaties --
--------------------
SPEC Beëdiging ISA Gebeurtenis

ENDPATTERN

--------------------------------
-- Applicatiespecifieke eisen --
--------------------------------
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornaam :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornaam[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}


kantooradres :: Kantoornummer -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoorlocatie heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoornummer*Adres] IN DUTCH
{+ Elke kantoorlocatie heeft precies één adres.
-}


ENDPATTERN

ENDCONTEXT