CONTEXT Advocatentableau


INCLUDE "Persoonsgegevens.adl"
INCLUDE "Gebeurtenis.adl"


PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

----------
-- Keys --
----------

KEY advocaat : Inschrijving (advocaat;naam)
KEY kantoor : Kantoornummer(kantoornaam)


---------------
-- Concepten --
---------------

CONCEPT Verzoeker "Een ieder die schriftelijk verzoekt om ingeschreven te worden als advocaat." "Art. 2 lid 1"

CONCEPT Advocaat "Een raadsheer die de praktijk uitoefent overeenkomstig de bevoegdheden en vereisten, bij de Wetboeken van Burgerlijke Rechtsvordering en Strafvordering en bij de bijzondere wetten en besluiten gegeven en gevorderd, en overeenkomstig deze wet en de daarop berustende verordeningen en besluiten." "Art. 10 Advw"

CONCEPT "Verzoekt tot inschrijving" "Een door de verzoeker opgesteld document gericht aan de president van de rechtbank waarin verzocht wordt tot inschrijving als advocaat." "Art. 2 lid 1 Advw"

CONCEPT Inschrijving "De gebeurtenis die de advocatuur van de ingeschrevene bevestigt." "Art. 1 lid 1; Art. 1 lid 2; Advw"

CONCEPT Beëdiging "De gebeurtenis waarop de verzoeker de eed of belofte aflegt waarna de verzoeker als advocaat op het tableau ingeschreven kan worden." "Art. 3 lid 1 Advw"

CONCEPT "BAR nummer" "Uniek, persoonlijk nummer dat bij inschrijving aan advocaten toegekend wordt, om administratieve handelingen te vergemakkelijken" "http://barbeheer.nl"

CONCEPT Kantoor "Het kantoor van de advocaat geldt voor al zijn handelingen als zodanig als gekozen woonplaats." "Art. 12 lid 3 Advw"

CONCEPT Schrapping "Gebeurtenis die de advocaat uit zijn functie ontheft waarmee de rechten, plichten en bevoegdheden van advocaten niet meer van toepassing zijn op de geschrapte persoon" "Art. 8 Advw"

CONCEPT Schorsing "Gebeurtenis die de advocaat tijdelijk zijn bevoegdheden als advcaat ontneemt." "Art. 16 Advw; Art. 48 lid 2 sub c"

--------------
-- Relaties --
--------------

verzoeker :: Document * Persoon [UNI] PRAGMA "Het verzoek tot inschrijving met kenmerk " "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION verzoeker[Document*Persoon] IN DUTCH REF "Art. 2 lid 1 Advw"
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION verzoeker CONTAINS
[ ("Vti1", "4")
; ("Vti2", "5")
; ("Vti3", "6")
; ("Vti4", "7")
]

inschrijvingsverzoek :: Inschrijving * Document PRAGMA "Het verzoek tot inschrijving van " "heeft kenmerk" ""
MEANING "Inschrijving op het advocatentableau wordt schriftelijk verzocht."
PURPOSE RELATION inschrijvingsverzoek IN DUTCH REF "Art.2 lid 1"
{+ Het inschrijvingsverzoek wordt schriftelijk ingediend.
-}
POPULATION inschrijvingsverzoek CONTAINS
[ ("Inschrijving 1", "Vti1")
; ("Inschrijving 2", "Vti2")
; ("Inschrijving 3", "Vti3")
; ("Inschrijving 4", "Vti4")
] 

gerichtaan :: Inschrijving -> Rechtbank [UNI] PRAGMA "Het verzoek tot inschrijving van " "is gericht aan de president van de" ""
MEANING "Een verzoek tot inschrijving als advocaat is gericht aan de president van de rechtbank."
PURPOSE RELATION gerichtaan[Inschrijving*Rechtbank] IN DUTCH REF "Art. 2 lid 1 Advw"
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION gerichtaan[Inschrijving*Rechtbank] CONTAINS
[ ("Inschrijving 1", "Rechtbank Amsterdam")
; ("Inschrijving 2", "Rechtbank Rotterdam")
; ("Inschrijving 3", "Rechtbank Utrecht")
; ("Inschrijving 4", "Rechtbank Utrecht")
]

kantoorlocatie :: Inschrijving -> Adres [UNI] PRAGMA "Het inschrijvingsverzoek van " "vermeld de kantoorlocatie" ""
MEANING "Een advocaat is verplicht kantoor te houden."
PURPOSE RELATION kantoorlocatie[Inschrijving*Adres] IN DUTCH REF "Art. 2 lid 1 Advw."
{+ Advocaten mogen slechts bij één kantoor ingeschreven staan.
-}
POPULATION kantoorlocatie[Inschrijving*Adres] CONTAINS
[ ("Inschrijving 1", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("Inschrijving 2", "Westblaak 5f, 3001 AC, Rotterdam")
; ("Inschrijving 3", "Mariahoek 4, 3511 LD, Utrecht")
; ("Inschrijving 4", "Herculesplein 213, 3584 AA, Utrecht")
]

inschrijvingseis :: Inschrijving * Inschrijvingseis [UNI] PRAGMA "Het verzoek tot inschrijving van " "heeft inschrijvingseisen die" ""
MEANING "Een verzoek tot inschrijving als advocaat moet gecontroleerd worden aan de wettelijk gestelde eisen."
PURPOSE RELATION inschrijvingseis[Inschrijving*Inschrijvingseis] IN DUTCH REF "Art. 2 Advw"
{+
-}
POPULATION inschrijvingseis CONTAINS
[ ("Inschrijving 1", "voldoen")
; ("Inschrijving 2", "voldoen")
; ("Inschrijving 3", "voldoen")
; ("Inschrijving 4", "voldoen")
]

inschrijvingsstatus :: Inschrijving * Inschrijvingsstatus PRAGMA "De inschrijving van " "heeft de status" ""
MEANING "De inschrijving van een advocaat op het landelijk advocaten tableau heeft de status 'voorwaardelijk' of 'onvoorwaadelijk'."
PURPOSE RELATION inschrijvingsstatus[Inschrijving*Inschrijvingsstatus]
{+ De status van een ingeschreven advocaat indiceert of er rondom zijn inschrijving nog handelingen verricht dienen te worden.
-}
POPULATION inschrijvingsstatus CONTAINS
[ ("Inschrijving 1", "onvoorwaardelijk")
; ("Inschrijving 2", "voorwaardelijk")
; ("Inschrijving 3", "onvoorwaardelijk")
; ("Inschrijving 4", "onvoorwaardelijk")
]

beëdigd :: Beëdiging -> Inschrijving [UNI] PRAGMA "" "is beëdigd tijdens gebeurtenis" ""
MEANING "De beëdiging van de verzoeker geschiedt op basis van het inschrijvingsverzoek."
PURPOSE RELATION beëdigd[Beëdiging*Inschrijving] IN DUTCH REF "Art. 3 lid 1 Advw."
{+ Een advocaat wordt beëdigd op requisitoir van het openbaar ministerie.
-}
POPULATION beëdigd CONTAINS
[ ("Be1", "Inschrijving 1")
; ("Be2", "Inschrijving 2")
; ("Be3", "Inschrijving 3")
; ("Be4", "Inschrijving 4")
]

locatieBeëdiging :: Beëdiging -> Rechtbank [UNI] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een aspirant advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatieBeëdiging[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3 lid 1 Advw."
{+
-}
POPULATION locatieBeëdiging CONTAINS
[ ("Be1", "Rechtbank Amsterdam")
; ("Be2", "Rechtbank Rotterdam")
; ("Be3", "Rechtbank Utrecht")
; ("Be4", "Rechtbank Utrecht")
]

advocaat :: Inschrijving -> Persoon [UNI] PRAGMA "Het verzoek tot inschrijving van " "heeft geleid tot de inschrijving van" ""
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION advocaat[Inschrijving*Persoon] IN DUTCH REF "Art. 1 lid 1 Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION advocaat CONTAINS
[ ("Inschrijving 1", "4")
; ("Inschrijving 2", "5")
; ("Inschrijving 3", "6")
; ("Inschrijving 4", "7")
]

barnummer :: Barnummer -> Inschrijving [UNI] PRAGMA "Barnummer " "is toegekend aan" ""
MEANING "Elke advocaat heeft een uniek BAR-nummer."
PURPOSE RELATION barnummer[Barnummer*Inschrijving] IN DUTCH REF ""
{+
-}
POPULATION barnummer CONTAINS
[ ("A23968", "Inschrijving 1")
; ("A26815", "Inschrijving 2")
; ("A16378", "Inschrijving 3")
; ("A24763", "Inschrijving 4")
]



schrapper :: Document * Inschrijving PRAGMA "Het verzoek tot schrapping met kenmerk " "is ingediend door" ""
MEANING "Een schrapping kan schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf."
PURPOSE RELATION schrapper[Document*Inschrijving] IN DUTCH REF "Art. 8 lid 2 Advw."
{+
-}
POPULATION schrapper[Document*Inschrijving] CONTAINS
[ ("Vts1", "Inschrijving 3")
; ("Vts3", "Inschrijving 1")
]

schrappingDoor :: Beslissing * Orgaan PRAGMA "De beslissing " "is genomen door de" ""
MEANING "Tot schrapping kan worden besloten door de rechtbank waar de advocaat kantoor houdt of de Raad van State."
PURPOSE RELATION schrappingDoor[Beslissing*Orgaan] IN DUTCH REF "Art. 8 lid 2 Advw."
{+
-}
POPULATION schrappingDoor[Beslissing*Orgaan] CONTAINS
[ ("Vts2", "Raad van State")
]

schrapping :: Document * Inschrijving PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrapping heeft betrekking op een ingeschreven advocaat."
PURPOSE RELATION schrapping[Document*Inschrijving] IN DUTCH REF "Art. 8 lid 2 Advw."
{+
-}
POPULATION schrapping CONTAINS
[ ("Vts1", "Inschrijving 3")
; ("Vts2", "Inschrijving 2")
; ("Vts3", "Inschrijving 1")
]


houdtkantoor :: Inschrijving * Kantoornummer [UNI] PRAGMA "" "staat ingeschreven bij advocatenkantoor" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtkantoor[Inschrijving*Kantoornummer] IN DUTCH REF "Art. 12 lid 1 Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtkantoor CONTAINS
[ ("Inschrijving 1", "K52347")
; ("Inschrijving 2", "K45317")
; ("Inschrijving 3", "K48933")
; ("Inschrijving 4", "K12493")
]

kantoor :: Kantoornummer * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoorlocatie valt binnen het rechtsgebied van een rechtbank." 
PURPOSE RELATION kantoor[Kantoornummer*Rechtbank] IN DUTCH REF "Art. 12 lid 1 Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("K52347", "Rechtbank Amsterdam")
; ("K45317", "Rechtbank Rotterdam")
; ("K48933", "Rechtbank Utrecht")
; ("K12493", "Rechtbank Utrecht")
]


schorsing :: Document * Inschrijving PRAGMA "" "heeft betrekking op" ""
MEANING "Onder bepaalde voorwaarden kan een advocaat worden geschorst."
PURPOSE RELATION schorsing[Document*Inschrijving] IN DUTCH REF "Art. 16 Advw."
{+ Een geschorste advocaat mag gedurende de schorsing de titel van advocaat niet voeren.
-}

schorsing :: Beslissing * Orgaan PRAGMA "De beslissing tot schorsing " "is genomen door" ""
MEANING "Een gerecht kan beslissen om een advocaat te schorsen."
PURPOSE RELATION schorsing[Beslissing*Orgaan] IN DUTCH REF "Art. 16 Advw."
{+ De griffier van het gerecht geeft van de schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

schorsingOpheffen :: Beslissing * Orgaan PRAGMA "De beslissing het opheffen van de schorsing " "is genomen door" ""
MEANING "Een gerecht kan beslissen een schorsing op te heffen."
PURPOSE RELATION schorsingOpheffen[Beslissing*Orgaan] IN DUTCH REF "Art. 16 Advw."
{+ De griffier van het gerecht geeft van de opheffing schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

-----------------
-- Invarianten --
-----------------

--Beëdigingen vinden plaats op de rechtbank waar het inschrijvingsverzoek naar verzonden is
RULE beëdigingslocatie : locatieBeëdiging~ |- gerichtaan~;V
MESSAGE "De rechtbank waar de locatie plaatsvindt komt niet overeen met de rechtbank waaraan het inschrijvingsverzoek gericht is."

--Verzoekers die nog niet gescreend zijn, mogen niet worden beëdigd
RULE screening : beëdigd~ |- inschrijvingseis;V
MEANING "Een beëdigde verzoeker heeft voldaan aan de inschrijvingseisen."
MESSAGE "De inschrijvingseisen van de verzoeker zijn nog niet gecontroleerd of voldoen niet."
PURPOSE RULE screening IN DUTCH REF ""
{+ De Nederlandse Orde van Advocaten controleert of de verzoeker aan de inschrijvingseisen voldoet, voordat de verzoeker mag worden beëdigd.
-} 

RULE statusvaninschrijving: I[Inschrijvingsstatus] = 'voorwaardelijk' \/ 'onvoorwaardelijk'
MEANING "De inschrijving als advocaat geschiedt voorwaardelijk of onvoorwaardelijk."
MESSAGE "Volgens Artikel 1 lid 2 van de Advocatenwet is de inschrijving van een advocaat 'voorwaardelijk' of 'onvoorwaardelijk'."
PURPOSE RULE statusvaninschrijving IN DUTCH REF "Art. 1 lid 2 Advw."
{+ De inschrijvingsstatus van een advocaat is wettelijk bepaald als 'voorwaardelijk' of 'onvoorwaardelijk'
-}

RULE inschrijvingseistypen: I[Inschrijvingseis] = 'voldoen'
MEANING "Aan inschrijvingseisen kan worden voldaan."
MESSAGE "Een inschrijvingseis kan de status hebben van 'voldoet'."
PURPOSE RULE "inschrijvingseistypen" IN DUTCH REF "Art 2, Advw."
{+ Het type van de inschrijvingseis(en) bepaalt de status van het inschrijvingsverzoek
-}

--------------------
-- Specialisaties --
--------------------
SPEC Beëdiging ISA Gebeurtenis

ENDPATTERN

--------------------------------
-- Applicatiespecifieke eisen --
--------------------------------
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornaam :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornaam[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornaam CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoornummer -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoorlocatie heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoornummer*Adres] IN DUTCH
{+ Elke kantoorlocatie heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("K52347", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("K45317", "Westblaak 5f, 3001 AC, Rotterdam")
; ("K48933", "Mariahoek 4, 3511 LD, Utrecht")
; ("K12493", "Herculesplein 213, 3584 AA, Utrecht")
]

ENDPATTERN

ENDCONTEXT