CONTEXT Advocatentableau


INCLUDE "Persoonsgegevens.adl"


PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Specialisatie en overerving
SPEC Beëdiging ISA Gebeurtenis

KEY advocaat : Inschrijving (advocaat;naam)
KEY kantoor : Kantoornummer(kantoornaam)

--Relaties en regels

--Art. 1
--lid 1
advocaat :: Inschrijving * Persoon [UNI] PRAGMA "De inschrijving van " "bevestigt de advocatuur van" ""
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION advocaat[Inschrijving*Persoon] IN DUTCH REF "Art. 1 lid 1, §1, Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION advocaat CONTAINS
[ ("Inschrijving 1", "4")
; ("Inschrijving 2", "5")
; ("Inschrijving 3", "6")
; ("Inschrijving 4", "7")
]

barnummer :: Barnummer * Inschrijving [UNI] PRAGMA "Barnummer " "is toegekend aan" ""
MEANING "Elke advocaat heeft een uniek BAR-nummer."
PURPOSE RELATION barnummer[Barnummer*Inschrijving] IN DUTCH REF ""
{+
-}
POPULATION barnummer CONTAINS
[ ("A23968", "Inschrijving 1")
; ("A26815", "Inschrijving 2")
; ("A16378", "Inschrijving 3")
; ("A24763", "Inschrijving 4")
]

inschrijvingsstatus :: Inschrijving * Inschrijvingsstatus PRAGMA "" "heeft de status" ""
MEANING "De inschrijving van een advocaat op het landelijk advocaten tableau heeft de status 'voorwaardelijk' of 'onvoorwaadelijk'."
PURPOSE RELATION inschrijvingsstatus[Inschrijving*Inschrijvingsstatus]
{+ De status van een ingeschreven advocaat indiceert of er rondom zijn inschrijving nog handelingen verricht dienen te worden.
-}
POPULATION inschrijvingsstatus CONTAINS
[ ("Inschrijving 1", "onvoorwaardelijk")
; ("Inschrijving 2", "voorwaardelijk")
; ("Inschrijving 3", "onvoorwaardelijk")
; ("Inschrijving 4", "onvoorwaardelijk")
]


RULE statusvaninschrijving: I[Inschrijvingsstatus] = 'voorwaardelijk' \/ 'onvoorwaardelijk'
MEANING "De inschrijving als advocaat geschiedt voorwaardelijk of onvoorwaardelijk."
MESSAGE "Volgens Artikel 1 lid 2 van de Advocatenwet is de inschrijving van een advocaat 'voorwaardelijk' of 'onvoorwaardelijk'."
PURPOSE RULE statusvaninschrijving IN DUTCH REF "Art. 1 lid 2, §1, Advw."
{+ De inschrijvingsstatus van een advocaat is wettelijk bepaald als 'voorwaardelijk' of 'onvoorwaardelijk'
-}

--Art.2
--lid 1
verzoeker :: Document * Persoon [UNI] PRAGMA "Verzoek tot inschrijving met kenmerk " "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION verzoeker[Document*Persoon] IN DUTCH REF "Art. 2 lid 1, §1, Advw"
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION verzoeker CONTAINS
[ ("Vti1", "4")
; ("Vti2", "5")
; ("Vti3", "6")
; ("Vti4", "7")
]

inschrijvingsverzoek :: Inschrijving * Document PRAGMA "De inschrijving van " "is gebaseerd op" ""
MEANING "Een ingeschreven advocaat heeft een verzoek tot inschrijving als advocaat ingediend."
PURPOSE RELATION inschrijvingsverzoek IN DUTCH REF ""
{+ Een inschrijving op het advocatentableau is te herleiden naar het verzoek tot inschrijving als advocaat.
-}
POPULATION inschrijvingsverzoek CONTAINS
[ ("Inschrijving 1", "Vti1")
; ("Inschrijving 2", "Vti2")
; ("Inschrijving 3", "Vti3")
; ("Inschrijving 4", "Vti4")
] 

ontvanger :: Document * Rechtbank [UNI] PRAGMA "Verzoek tot inschrijving met kenmerk " "is gericht aan de president van de" ""
MEANING "Een verzoek tot inschrijving als advocaat is gericht aan de president van de rechtbank."
PURPOSE RELATION ontvanger[Document*Rechtbank] IN DUTCH REF "Art. 2 lid 1, §1, Advw"
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION ontvanger[Document*Rechtbank] CONTAINS
[ ("Vti1", "Rechtbank Amsterdam")
; ("Vti2", "Rechtbank Rotterdam")
; ("Vti3", "Rechtbank Utrecht")
; ("Vti4", "Rechtbank Utrecht")
]

kantoorlocatie :: Persoon * Adres [UNI] PRAGMA "" "gaat kantoor houden op" ""
MEANING "Een advocaat is verplicht kantoor te houden."
PURPOSE RELATION kantoorlocatie[Persoon*Adres] IN DUTCH REF "Art. 2 lid 1, §1, Advw."
{+
Advocaten mogen slechts bij één kantoor ingeschreven staan.
-}
POPULATION kantoorlocatie CONTAINS
[ ("4", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("5", "Westblaak 5f, 3001 AC, Rotterdam")
; ("6", "Mariahoek 4, 3511 LD, Utrecht")
; ("7", "Herculesplein 213, 3584 AA, Utrecht")
]

inschrijvingseis :: Inschrijving * Inschrijvingseis [UNI] PRAGMA "De inschrijvingseisen van " "" ""
MEANING "Een verzoek tot inschrijving als advocaat moet gecontroleerd worden aan de wettelijk gestelde eisen."
PURPOSE RELATION inschrijvingseis[Inschrijving*Inschrijvingseis] IN DUTCH REF "Art. 2, §1, Advw"
{+
-}
POPULATION inschrijvingseis CONTAINS
[ ("Inschrijving 1", "voldoen")
; ("Inschrijving 2", "voldoen")
; ("Inschrijving 3", "voldoen")
; ("Inschrijving 4", "voldoen")
]

RULE inschrijvingseistypen: I[Inschrijvingseis] = 'voldoen'
MEANING "Aan inschrijvingseisen kan worden voldaan."
MESSAGE "Een inschrijvingseis kan de status hebben van 'voldoet'."
PURPOSE RULE "inschrijvingseistypen" IN DUTCH REF "Art 2, §1, Advw."
{+ Het type van de inschrijvingseis(en) bepaalt de status van het inschrijvingsverzoek
-}


-- Art. 3
beëdigd :: Inschrijving * Beëdiging [UNI] PRAGMA "" "is beëdigd tijdens gebeurtenis" ""
MEANING "Om ingeschreven te worden als advocaat dient er een beëdiging plaats te hebben gevonden."
PURPOSE RELATION beëdigd[Inschrijving*Beëdiging] IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Een advocaat wordt beëdigd op requisitoir van het openbaar ministerie.
-}
POPULATION beëdigd CONTAINS
[ ("Inschrijving 1", "Be1")
; ("Inschrijving 2", "Be2")
; ("Inschrijving 3", "Be3")
; ("Inschrijving 4", "Be4")
]

locatieBeëdiging :: Beëdiging * Rechtbank [UNI] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een aspirant advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatieBeëdiging[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+
-}
POPULATION locatieBeëdiging CONTAINS
[ ("Be1", "Rechtbank Amsterdam")
; ("Be2", "Rechtbank Rotterdam")
; ("Be3", "Rechtbank Utrecht")
; ("Be4", "Rechtbank Utrecht")
]

--Invarianten

--Verzoekers die nog niet gescreend zijn, mogen niet worden beëdigd
RULE screening : beëdigd |- inschrijvingseis;V
MEANING "Een beëdigde verzoeker heeft voldaan aan de inschrijvingseisen."
MESSAGE "De inschrijvingseisen van de verzoeker zijn nog niet gecontroleerd of voldoen niet."
PURPOSE RULE screening IN DUTCH REF ""
{+ De Nederlandse Orde van Advocaten controleert of de verzoeker aan de inschrijvingseisen voldoet, voordat de verzoeker mag worden beëdigd.
-} 


--Beëdigingen vinden plaats op basis van het corresponderende inschrijvingsverzoek

-- Art. 8
schrapper :: Document * Inschrijving PRAGMA "Het verzoek tot schrapping met kenmerk " "is ingediend door" ""
MEANING "Een schrapping kan schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf."
PURPOSE RELATION schrapper[Document*Inschrijving] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrapper[Document*Inschrijving] CONTAINS
[ ("Vts1", "Inschrijving 3")
; ("Vts3", "Inschrijving 1")
]

schrappingDoor :: Beslissing * Orgaan PRAGMA "De beslissing " "is genomen door de" ""
MEANING "Tot schrapping kan worden besloten door de rechtbank waar de advocaat kantoor houdt of de Raad van State."
PURPOSE RELATION schrappingDoor[Beslissing*Orgaan] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrappingDoor[Beslissing*Orgaan] CONTAINS
[ ("Vts2", "Raad van State")
]

schrapping :: Document * Inschrijving PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrapping heeft betrekking op een ingeschreven advocaat."
PURPOSE RELATION schrapping[Document*Inschrijving] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrapping CONTAINS
[ ("Vts1", "Inschrijving 3")
; ("Vts2", "Inschrijving 2")
; ("Vts3", "Inschrijving 1")
]


-- Art. 12
houdtkantoor :: Inschrijving * Kantoornummer [UNI] PRAGMA "" "staat ingeschreven bij advocatenkantoor" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtkantoor[Inschrijving*Kantoornummer] IN DUTCH REF "Art. 12 lid 1, §2, Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtkantoor CONTAINS
[ ("Inschrijving 1", "K52347")
; ("Inschrijving 2", "K45317")
; ("Inschrijving 3", "K48933")
; ("Inschrijving 4", "K12493")
]

kantoor :: Kantoornummer * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoorlocatie valt binnen het rechtsgebied van een rechtbank." 
PURPOSE RELATION kantoor[Kantoornummer*Rechtbank] IN DUTCH REF "Art. 12 lid 1, §2, Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("K52347", "Rechtbank Amsterdam")
; ("K45317", "Rechtbank Rotterdam")
; ("K48933", "Rechtbank Utrecht")
; ("K12493", "Rechtbank Utrecht")
]


--Art. 16
schorsing :: Document * Inschrijving PRAGMA "" "heeft betrekking op" ""
MEANING "Onder bepaalde voorwaarden kan een advocaat worden geschorst."
PURPOSE RELATION schorsing[Document*Inschrijving] IN DUTCH REF "Art. 16, §2, Advw."
{+ Een geschorste advocaat mag gedurende de schorsing de titel van advocaat niet voeren.
-}

schorsing :: Beslissing * Orgaan PRAGMA "De beslissing " "is genomen door" ""
MEANING "Een gerecht kan beslissen om een advocaat te schorsen."
PURPOSE RELATION schorsing[Beslissing*Orgaan] IN DUTCH REF "Art. 16, §2, Advw."
{+ De griffier van het gerecht geeft van de schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

schorsingOpheffen :: Beslissing * Orgaan PRAGMA "De beslissing " "is genomen door" ""
MEANING "Een gerecht kan beslissen een schorsing op te heffen."
PURPOSE RELATION schorsingOpheffen[Beslissing*Orgaan] IN DUTCH REF "Art. 16, §2, Advw."
{+ De griffier van het gerecht geeft van de opheffing schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

ENDPATTERN


PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}


--Procesregels
ROLE Secretaris MAINTAINS ingeschrevenAdvocaat
ROLE Griffier MAINTAINS moetwordenbeëdigd, opleidingseisvoldaan

RULE moetwordenbeëdigd:  inschrijvingseis |- beëdigd; V
MEANING "Een inschrijvingsverzoek waarvan gestelde opleidingseis gecontroleerd is, moet uiteindelijk leiden tot een beëdiging."
MESSAGE "Er zijn inschrijvingsverzoeken die nog niet tot een beëdiging hebben geleid, als bedoeld in Art. 3 lid 1, Advw."
PURPOSE RULE moetwordenbeëdigd IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Geaccepteerde inschrijvingsverzoeken leiden tot beëdigingen.
-}


RULE ingeschrevenAdvocaat: beëdigd |- barnummer~;V
MEANING "Elke beëdigde advocaat moet uiteindelijk op het tableau worden ingeschreven."
MESSAGE "De beëdigde advocaat dient ingeschreven te worden op het tableau. (Art. 3-1, §1, Advw.)"
PURPOSE RULE ingeschrevenAdvocaat IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ De griffier van de rechtbank licht de secretaris van de algemene raad in over de beëdiging ingelicht, zodat de secretaris de advocaat op het tableau in kan schrijven.
-}


RULE opleidingseisvoldaan: inschrijvingseis |- inschrijvingseis;(I[Inschrijvingseis]='voldoen')
MEANING "Een inschrijvingsverzoek wordt geaccepteerd als de indiener aan de inschrijvingseisen voldoet."
MESSAGE "Het inschrijvingsverzoek is nog niet gecontroleerd op alle inschrijvingseisen. (Art. 2, Advw.)"
PURPOSE RULE "opleidingseisvoldaan" IN DUTCH REF "Art. 2, §1, Advw."
{+ Een inschrijvingsverzoek kan alleen worden ingeschreven als het aan de inschrijvingseisen voldoet.
-}

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornaam :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornaam[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornaam CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoornummer -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoorlocatie heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoornummer*Adres] IN DUTCH
{+ Elke kantoorlocatie heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("K52347", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("K45317", "Westblaak 5f, 3001 AC, Rotterdam")
; ("K48933", "Mariahoek 4, 3511 LD, Utrecht")
; ("K12493", "Herculesplein 213, 3584 AA, Utrecht")
]

zoeken :: Zoek * Zoek
POPULATION zoeken CONTAINS
[ ("", "Zoek")
]

zoekOpNaam :: Zoek * Inschrijving

zoekOpBAR :: Zoek * Barnummer

ENDPATTERN

ENDCONTEXT