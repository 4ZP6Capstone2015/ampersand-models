CONTEXT Advocatentableau

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Specialisatie en overerving
SPEC Beëdiging ISA Gebeurtenis
--SPEC Inschrijvingsverzoek ISA Document

KEY verzoek : Inschrijvingsverzoek(isVerstuurdDoor;advocaatnaam~,isGerichtAan,kantoorVerzoek)
KEY advocaatnaam : Advocaat (advocaatnaam;naam)
KEY beëdiging : Beëdiging (isBeëdigd;advocaatnaam~,locatieBeëdiging)
KEY kantoor : Kantoornummer(kantoornaam)


--Relaties en regels

--Art. 1
--lid 1
advocaatnaam :: Advocaat * Persoon [TOT,UNI, INJ] PRAGMA "" "is toegekend aan" "in de hoedanigheid van advocaat"
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION advocaatnaam[Advocaat*Persoon] IN DUTCH REF "Art. 1-1, §1, Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION advocaatnaam CONTAINS
[ ("23968", "4")
; ("26815", "5")
; ("16378", "6")
; ("24763", "7")
]


RULE statussen: I[Inschrijvingsstatus] = 'voorwaardelijk' \/ 'onvoorwaardelijk'

inschrijvingsstatus :: Advocaat * Inschrijvingsstatus

barnummer :: Advocaat -> Barnummer
POPULATION barnummer CONTAINS
[ ("23968", "A23968")
; ("26815", "A26815")
; ("16378", "A16378")
; ("24763", "A24763")
]

RULE "uniek BAR-nummer" : barnummer;barnummer~ |- I[Advocaat]
MEANING "Elke advocaat heeft een uniek BAR-nummer"
MESSAGE "Dit BAR-nummer is al aan een andere advocaat toegewezen."

RULE beëdigdeAdvocaat: advocaatnaam~;advocaatnaam |- isBeëdigd~;isBeëdigd
MEANING "Elke ingeschreven advocaat is beëdigd."
MESSAGE "Een advocaat kan niet worden ingeschreven als hij nog niet is beëdigd. (Art. 3-1, §1, Advw.)"
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Als de secretaris van de algemene raad de advocaat op het tableau inschrijft, is hij door de griffier van de rechtbank over de beëdiging ingelicht.
-}

--Art.2
--lid 1
kantoorVerzoek :: Inschrijvingsverzoek * Kantoornummer [TOT] PRAGMA "Indiener van " "wil kantoor houden bij" ""
MEANING "Het inschrijvingsverzoek bevat de kantoornaam waar de inschrijvende kantoor wil houden."
PURPOSE RELATION kantoorVerzoek[Inschrijvingsverzoek*Kantoornummer] IN DUTCH REF "Art. 2-1, §1, Advw."
{+
Advocaten mogen slechts bij één kantoor ingeschreven staan.
-}
POPULATION kantoorVerzoek CONTAINS
[ ("Vvi1", "K52347")
; ("Vvi2", "K45317")
; ("Vvi3", "K48933")
; ("Vvi4", "K12493")
]


isGerichtAan :: Inschrijvingsverzoek -> Rechtbank [TOT] PRAGMA "" "is gericht aan de" ""
MEANING "Een verzoek van inschrijving tot advocaat is gericht aan de voorzitter van een rechtbank."
PURPOSE RELATION isGerichtAan[Inschrijvingsverzoek*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw"
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION isGerichtAan CONTAINS
[ ("Vvi1", "Rechtbank Amsterdam")
; ("Vvi2", "Rechtbank Rotterdam")
; ("Vvi3", "Rechtbank Utrecht")
; ("Vvi4", "Rechtbank Utrecht")
]

isVerstuurdDoor :: Inschrijvingsverzoek * Persoon [TOT] PRAGMA "" "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION isVerstuurdDoor[Inschrijvingsverzoek*Persoon] IN DUTCH REF "Art. 2-1, §1, Advw"
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION isVerstuurdDoor CONTAINS
[ ("Vvi1", "4")
; ("Vvi2", "5")
; ("Vvi3", "6")
; ("Vvi4", "7")
]

inschrijvingseis :: Inschrijvingsverzoek * Inschrijvingseis PRAGMA "" "heeft inschrijvingseisen die" ""
MEANING ""
PURPOSE RELATION inschrijvingseis[Inschrijvingsverzoek*Inschrijvingseis] IN DUTCH REF "Art. 2, §1, Advw"
{+
-}
POPULATION inschrijvingseis CONTAINS
[ ("Vvi1", "zijn gecontroleerd")
; ("Vvi2", "zijn gecontroleerd")
; ("Vvi3", "zijn gecontroleerd")
; ("Vvi4", "zijn gecontroleerd")
]

RULE kantoorverzoek: kantoorVerzoek~;isGerichtAan |- kantoor
MEANING "Het in het inschrijvingsverzoek vermeldde kantoor moet vallen onder een rechtbank waar het inschrijvingsverzoek aan is gericht."
MESSAGE "De locatie van de rechtbank komt niet overeen met de locatie van het kantoor, m.a.w. het kantoor valt niet onder het arrondisement van de rechtbank. (Art. 3-1, §1, Advw)"
PURPOSE RULE "kantoorverzoek" IN DUTCH REF "Art. 3-1, §1, Advw"
{+ 
-}

{-
RULE "Art. 2-1 Advw.": kantoorVerzoek;kantoor |- isGerichtAan
MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de voorzitter van de rechtbank waar de betreffende persoon kantoor wenst te houden. (Art 2-1, §1, Advw.)"
MESSAGE "De locatie van de rechtbank komt niet overeen met de locatie van het kantoor, m.a.w. het kantoor valt niet onder het arrondisement van de rechtbank."
PURPOSE RULE "Art. 2-1 Advw." IN DUTCH REF "Art 2-1, §1, Advw"
{+ Inschrijvingsverzoeken moeten aan de voorzitter van de rechtbank worden gericht, waar de indiener kantoor wenst te houden.
-}


RULE inschrijvingseistypen: I[Inschrijvingseis] = 'zijn gecontroleerd' \/ 'niet zijn gecontroleerd'
MEANING ""
MESSAGE ""
PURPOSE RULE "inschrijvingseistypen" IN DUTCH REF "Art 2, §1, Advw."
{+
-}


-- Art. 3
isBeëdigd :: Beëdiging *  Persoon [TOT,UNI] PRAGMA "" "bevestigt de beëdigding van" ""
MEANING "Een advocaat wordt beëdigigd op requisitoir van het openbaar ministerie."
PURPOSE RELATION isBeëdigd[Beëdiging*Persoon] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Elke aspirant advocaat moet een beëdiging ondergaan.
-}
POPULATION isBeëdigd CONTAINS
[ ("Be1", "4")
; ("Be2", "5")
; ("Be3", "6")
; ("Be4", "7")
]

locatieBeëdiging :: Beëdiging -> Rechtbank [TOT] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een aspirant advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatieBeëdiging[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3-1, §1, Advw."
{+
-}
POPULATION locatieBeëdiging CONTAINS
[ ("Be1", "Rechtbank Amsterdam")
; ("Be2", "Rechtbank Rotterdam")
; ("Be3", "Rechtbank Utrecht")
; ("Be4", "Rechtbank Utrecht")
]

behoortBij :: Inschrijvingsverzoek * Beëdiging [SUR, INJ] PRAGMA "De indiener van " "heeft geleid tot" ""
MEANING "De advocaten worden door de rechtbank beëdigd aan wier voorzitter zij een verzoek tot inschrijving hebben gedaan."
PURPOSE RELATION behoortBij[Inschrijvingsverzoek*Beëdiging] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een verzoek van inschrijving kan leiden tot een beëdiging. 
-}
POPULATION behoortBij CONTAINS
[ ("Vvi1", "Be1")
; ("Vvi2", "Be2")
; ("Vvi3", "Be3")
; ("Vvi4", "Be4")
]


--Invarianten
RULE wilkantoorhouden: isBeëdigd~ |- isVerstuurdDoor~;kantoorVerzoek;kantoor;locatieBeëdiging~
MEANING "De advocaten worden door de rechtbank aan wier voorzitter zij een verzoek tot inschrijving hebben gedaan beëdigd."
MESSAGE "Het kantoor van de kandidaat bevindt zich niet in het arrondissement waar de kandidaat beëdigd is, wat op grond van Art. 3 lid 1, Advw. wél vereist is."
PURPOSE RULE "wilkantoorhouden" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is.
-}

--Regel die eigenlijk vervangen moet worden door ECA-regel
RULE locatiebeëdiging: locatieBeëdiging~;locatie |- hoofdplaatsrechtbank~
MEANING "De beëdigingslocatie moet overeenkomen met de locatie van de rechtbank."
MESSAGE "De locatie waar de beëdiging plaatsvindt komt niet overeen met de rechtbank waar de beëdigde zich heeft aangemeld voor inschrijving. Dat is in strijd met Art. 3 lid 1, Advw."
PURPOSE RULE locatiebeëdiging IN DUTCH REF "Art. 3-1, §1, Advw."
{+ De locatie van de rechtbank waar de beëdiging plaatsvindt moet gelijk zijn aan de locatie waar de rechtbank gevestigd is.
-}

-- Art. 8
schrappingDoor :: Schrapping * Advocaat PRAGMA "" "is  verzocht door" ""
MEANING "Een schrapping kan schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf."
PURPOSE RELATION schrappingDoor[Schrapping*Advocaat] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingDoor[Schrapping*Advocaat] CONTAINS
[ ("Vts1", "16378")
; ("Vts3", "23968")
]

schrappingDoor :: Schrapping * Orgaan PRAGMA "" "is besloten door" ""
MEANING "Tot schrapping kan worden besloten door de rechtbank waar de advocaat kantoor houdt of de Raad van State."
PURPOSE RELATION schrappingDoor[Schrapping*Orgaan] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingDoor[Schrapping*Orgaan] CONTAINS
[ ("Vts2", "Raad van State")
]

schrappingVan :: Schrapping * Advocaat [TOT] PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrapping heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden."
PURPOSE RELATION schrappingVan[Schrapping*Advocaat] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingVan CONTAINS
[ ("Vts1", "16378")
; ("Vts2", "26815")
; ("Vts3", "23968")
]


-- Art. 12
houdtkantoor :: Advocaat * Kantoornummer [TOT] PRAGMA "Advocaat met advocaatnaam " "staat ingeschreven bij" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtkantoor[Advocaat*Kantoornummer] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtkantoor CONTAINS
[ ("23968", "K52347")
; ("26815", "K45317")
; ("16378", "K48933")
; ("24763", "K12493")
]

kantoor :: Kantoornummer * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoor valt onder een rechtbank." 
PURPOSE RELATION kantoor[Kantoornummer*Rechtbank] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("K52347", "Rechtbank Amsterdam")
; ("K45317", "Rechtbank Rotterdam")
; ("K48933", "Rechtbank Utrecht")
; ("K12493", "Rechtbank Utrecht")
]


--Art. 16
schorsingVan :: Schorsing * Advocaat PRAGMA "" "" ""
MEANING ""
PURPOSE RELATION schorsingVan[Schorsing*Advocaat] IN DUTCH REF "Art. 16, §2, Advw."
{+
-}

ENDPATTERN


PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}


--Procesregels
ROLE Secretaris MAINTAINS ingeschrevenAdvocaat
ROLE Griffier MAINTAINS moetwordenbeëdigd, opleidingseisvoldaan, ingeschrevenAdvocaat

RULE moetwordenbeëdigd: isVerstuurdDoor~;inschrijvingseis |- isBeëdigd~;behoortBij~;inschrijvingseis;(I[Inschrijvingseis]='zijn gecontroleerd')
MEANING "Een inschrijvingsverzoek waarvan gestelde opleidingseis gecontroleerd is, moet uiteindelijk leiden tot een beëdiging."
MESSAGE "De volgende inschrijvingsverzoek(en) wachten op beëdiging als bedoeld in Art. 3 lid 1, Advw."
PURPOSE RULE moetwordenbeëdigd IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Geaccepteerde inschrijvingsverzoeken leiden tot beëdigingen.
-}


RULE ingeschrevenAdvocaat: isBeëdigd~;isBeëdigd |- advocaatnaam~;advocaatnaam 
MEANING "Elke ingeschreven advocaat is beëdigd."
MESSAGE "De beëdigde advocaat dient ingeschreven te worden op het tableau. (Art. 3-1, §1, Advw.)"
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ De griffier van de rechtbank licht de secretaris van de algemene raad in over de beëdiging ingelicht, zodat de secretaris de advocaat op het tableau in kan schrijven.
-}


RULE opleidingseisvoldaan: isVerstuurdDoor~;inschrijvingseis |- isVerstuurdDoor~;inschrijvingseis;(I[Inschrijvingseis]='zijn gecontroleerd')
MEANING "Een inschrijvingsverzoek wordt geaccepteerd als de indiener aan de inschrijvingseisen voldoet."
MESSAGE "Het inschrijvingsverzoek is nog niet gecontroleerd op alle inschrijvingseisen. (Art. 2, Advw.)"
PURPOSE RULE "opleidingseisvoldaan" IN DUTCH REF "Art. 2, §1, Advw."
{+ Een inschrijvingsverzoek kan alleen worden ingeschreven als het aan de inschrijvingseisen voldoet.
-}

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornaam :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornaam[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornaam CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoornummer -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoornummer*Adres] IN DUTCH
{+ Elk kantoor heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("K52347", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("K45317", "Westblaak 5f, 3001 AC, Rotterdam")
; ("K48933", "Mariahoek 4, 3511 LD, Utrecht")
; ("K12493", "Herculesplein 213, 3584 AA, Utrecht")
]

zoeken :: Zoek * Zoek
POPULATION zoeken CONTAINS
[ ("", "Zoek")
]

zoekOpNaam :: Zoek * Persoon

zoekOpBAR :: Zoek * Advocaat




ENDPATTERN

ENDCONTEXT