CONTEXT Advocatentableau

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Specialisatie en overerving
--SPEC Beëdiging ISA Gebeurtenis


--Relaties en regels

--Art. 1
--lid 1
inschrijving :: Inschrijving * Advocaat [UNI] PRAGMA "" "heeft betrekking op" ""
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION inschrijving[Inschrijving*Advocaat] IN DUTCH REF "Art. 1-1, §1, Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION inschrijving CONTAINS
[ ("0001", "mr. S. Levelt")
; ("0002", "mr. I. van Driel")
; ("0003", "mr. A. Dekkers")
; ("0004", "mr. L.L.A. Cox")
]


RULE Advocaten: inschrijving~;inschrijving = isAdvocaat~;isAdvocaat
MEANING "De advocaten worden ingeschreven op het tableau van de Nederlandse orde van advocaten."
PURPOSE RULE Advocaten IN DUTCH REF "Art. 1-1, §1, Advw."
{+ Alle advocaten staan geregistreerd op het tableau van de Nederlandse orde van advocaten, waarmee alle ingeschreven personen op het tableau van de Nederlandse orde van advocaten  als advocaat aangemerkt kunnen worden.
-}

--Art.2
--lid 1
{-
wilKantoorHoudenBij :: Advocaat * Rechtbank [INJ] PRAGMA "" "wil kantoor houden bij" ""
MEANING "Een ieder is bevoegd zich te richten tot de voorzitter van de rechtbank waar de verzoeker kantoor wenst te houden."
PURPOSE RELATION wilKantoorHoudenBij["natuurlijkpersoon"*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
Advocaten mogen slechts bij één rechtbank kantoor houden.

POPULATION wilKantoorHoudenBij CONTAINS
[ ("mr. S. Levelt", "Rechtbank Amsterdam")
; ("mr. I. van Driel", "Rechtbank Rotterdam")
; ("mr. A. Dekkers", "Rechtbank Utrecht")
; ("mr. L.L.A. Cox", "Rechtbank Utrecht")
]
-- SJ: Mag een advocaat aan meerdere rechtbanken kantoor houden? Waar in de W&R staat dat?
-}


isGerichtAan :: "Verzoek van inschrijving" -> Rechtbank [TOT] PRAGMA "" "is gericht aan de" ""
MEANING "Een verzoek van inschrijving tot advocaat is gericht aan de voorzitter van een rechtbank."
PURPOSE RELATION isGerichtAan["Verzoek van inschrijving"*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION isGerichtAan CONTAINS
[ ("V001", "Rechtbank Amsterdam")
; ("V002", "Rechtbank Rotterdam")
; ("V003", "Rechtbank Utrecht")
; ("V004", "Rechtbank Utrecht")
]


isVerstuurdDoor :: "Verzoek van inschrijving" * "natuurlijkpersoon" [TOT] PRAGMA "" "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION isVerstuurdDoor["Verzoek van inschrijving"*"natuurlijkpersoon"] IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION isVerstuurdDoor CONTAINS
[ ("V001", "Sebastiaan Levelt")
; ("V002", "Ilja van Driel")
; ("V003", "Anne Dekkers")
; ("V004", "Lonneke Cox")
]

RULE "Art. 2-1 Advw.": isVerstuurdDoor;isAdvocaat;houdtKantoor;kantoor |- isGerichtAan
MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de voorzitter van de rechtbank waar de betreffende persoon kantoor wenst te houden."
PURPOSE RULE "Art. 2-1 Advw." IN DUTCH REF "Art 2-1, §1, Advw."
{+ Inschrijvingsverzoeken moeten, op wettelijke gronde, aan de voorzitter van de rechtbank worden gericht.
-}
 

-- Art. 3
isBeëdigd :: Beëdiging *  "natuurlijkpersoon" [TOT] PRAGMA "" "bevestigt de beëdigding van" ""
MEANING "Een advocaat wordt beëdigigd op requisitoir van het openbaar ministerie."
PURPOSE RELATION isBeëdigd[Beëdiging*"natuurlijkpersoon"] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Elke aspirant advocaat moet een beëdiging ondergaan.
-}
POPULATION isBeëdigd CONTAINS
[ ("B001", "Sebastiaan Levelt")
; ("B002", "Ilja van Driel")
; ("B003", "Anne Dekkers")
; ("B004", "Lonneke Cox")
]


datum ::  Beëdiging -> Datum [TOT] PRAGMA "" "geschiedde op" ""
MEANING "De datum waarop de beëdiging plaats gevonden heeft, is de datum waarop de beëdigide de titel van advocaat mag voeren."
PURPOSE RELATION datum[Beëdiging*Datum] IN DUTCH REF "Art. 3-1, §1, Advw."
{+
-}
POPULATION datum CONTAINS
[ ("B001", "25-08-1990")
; ("B002", "12-02-2009")
; ("B003", "06-10-2007")
; ("B004", "18-04-2009")
]


locatie :: Beëdiging -> Rechtbank [TOT] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatie[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een beëdiging van een advocaat vindt plaats op de locatie van de rechtbank waarin de advocaat kantoor wil gaan houden.
Omdat alleen beëdigde personen advocaat zijn, wordt de beëdiging van elke advocaat geregistreerd.
-}

POPULATION locatie[Beëdiging*Rechtbank] CONTAINS
[ ("B001", "Rechtbank Amsterdam")
; ("B002", "Rechtbank Rotterdam")
; ("B003", "Rechtbank Utrecht")
; ("B004", "Rechtbank Utrecht")
]


--Invarianten
RULE "Art. 3-1 Advw.": isAdvocaat~;isBeëdigd~;locatie |- houdtKantoor;kantoor
MEANING "Elke advocaat die beëdigd is door een betrokken rechtbank, wil kantoor houden bij die rechtbank."
PURPOSE RULE "Art. 3-1 Advw." IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is.
-}

-- Art. 8
schrappingDoor :: "Verzoek tot schrapping" * "natuurlijkpersoon" [TOT] PRAGMA "" "is gedaan door" ""
MEANING "Een schrappingsverzoek moet schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf, of door de rechtbank waar de advocaat kantoor houdt."
PURPOSE RELATION schrappingDoor["Verzoek tot schrapping"*"natuurlijkpersoon"] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingDoor CONTAINS
[ ("S001", "Anne Dekkers")
; ("S002", "Sanne Verhoeven")
; ("S003", "Sebastiaan Levelt")
]

schrappingVan :: "Verzoek tot schrapping" * Advocaat [TOT] PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrappingsverzoek heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden."
PURPOSE RELATION schrappingVan["Verzoek tot schrapping"*Advocaat] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingVan CONTAINS
[ ("S001", "mr. A. Dekkers")
; ("S002", "mr. I. van Driel")
; ("S003", "mr. S. Levelt")
]

-- Art. 12
houdtKantoor :: Advocaat * Kantoor [UNI] PRAGMA "" "staat ingeschreven bij" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtKantoor[Advocaat*Kantoor] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtKantoor CONTAINS
[ ("mr. S. Levelt", "Wieringa advocaten")
; ("mr. I. van Driel", "Haulussy advocaten")
; ("mr. A. Dekkers", "Kuipers, Jonkers, van den Berg")
; ("mr. L.L.A. Cox", "ATM Advocaten Utrecht")
]

kantoor :: Kantoor * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoor valt onder de bevoegdheid van een specifieke rechtbank." 
PURPOSE RELATION kantoor[Kantoor*Rechtbank] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("Wieringa advocaten", "Rechtbank Amsterdam")
; ("Haulussy advocaten", "Rechtbank Rotterdam")
; ("Kuipers, Jonkers, van den Berg", "Rechtbank Utrecht")
; ("ATM Advocaten Utrecht", "Rechtbank Utrecht")
]


--Deze relatie vervangt tijdelijk de SPEC isA van advocaat naar persoon
isAdvocaat :: "natuurlijkpersoon" * Advocaat PRAGMA "" "is als" "in de hoedanigheid van advocaat"
MEANING "Een advocaat is een rol die toegekend kan worden aan een natuurlijk persoon met de meester-titel."
PURPOSE RELATION isAdvocaat["natuurlijkpersoon"*Advocaat] IN DUTCH
{+
-}
POPULATION isAdvocaat CONTAINS
[ ("Sebastiaan Levelt", "mr. S. Levelt")
; ("Ilja van Driel", "mr. I. van Driel")
; ("Anne Dekkers", "mr. A. Dekkers")
; ("Lonneke Cox", "mr. L.L.A. Cox")
]


ENDPATTERN


PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}

verzoekTotBeëdiging :: "Verzoek van inschrijving" * Beëdiging PRAGMA "" "heeft geleid tot" ""
MEANING "Elke beëdiging heeft een verzoek van inschrijving als basis."
PURPOSE RELATION verzoekTotBeëdiging["Verzoek van inschrijving"*Beëdiging] IN DUTCH
{+ Een inschrijving op het advocatentableau kan plaatsvinden als er een verzoek tot inschrijving ingediend is.
-}
POPULATION verzoekTotBeëdiging CONTAINS
[ ("V001", "B001")
; ("V002", "B002")
; ("V003", "B003")
; ("V004", "B004")
]

verzoekTotInschrijving :: "Verzoek van inschrijving" * Inschrijving PRAGMA "" "heeft geleid tot" ""
MEANING "Elke inschrijving heeft een beëdiging als basis."
PURPOSE RELATION verzoekTotInschrijving["Verzoek van inschrijving"*Inschrijving] IN DUTCH
{+ Een inschrijving op het advocatentableau kan plaatsvinden als er een verzoek tot inschrijving ingediend is.
-}
POPULATION verzoekTotInschrijving CONTAINS
[ ("V001", "0001")
; ("V002", "0002")
; ("V003", "0003")
; ("V004", "0004")
]


beëdiging :: Beëdiging * Inschrijving PRAGMA "" "heeft betrekking op" ""
MEANING "Elke inschrijving heeft een beëdiging als basis."
PURPOSE RELATION beëdiging[Beëdiging*Inschrijving] IN DUTCH
{+ Om te kunnen zeggen op welke beëdiging een inschrijving is gebasseerd, gebruiken we het woord \"basis\".
-}
POPULATION beëdiging CONTAINS
[ ("B001", "0001")
; ("B002", "0002")
; ("B003", "0003")
; ("B004", "0004")
]

--Procesregels
ROLE Secretaris MAINTAINS verzoekTotBeëdiging, beëdigdeAdvocaat, ingeschrevenEnBeëdigd, verzoekingeschreven, beëdigingingeschreven, beëdigde

RULE verzoekTotBeëdiging : isBeëdigd~ |- isVerstuurdDoor~;verzoekTotBeëdiging
MEANING "Aan elke beëdiging van een advocaat is een verzoek van inschrijving voorafgegaan."
PURPOSE RULE verzoekTotBeëdiging IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Het initiatief om tot advocaat ingeschreven te worden ligt bij de persoon, daarom moet hij een verzoek van inschrijving indienen.
-}


--Wie doet de inschrijving? Griffie rechtbank --> Rol toevoegen.
RULE beëdigdeAdvocaat: isAdvocaat;inschrijving~ |- isBeëdigd~;beëdiging
MEANING "Elke ingeschreven advocaat is beëdigd."
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Als de secretaris van de algemene raad de advocaat op het tableau inschrijft, is hij door de griffier van de rechtbank over de beëdiging ingelicht.
-}

RULE ingeschrevenEnBeëdigd: inschrijving;isAdvocaat~ |- beëdiging~;isBeëdigd
MEANING "Een persoon die de titel advocaat mag voeren is ten minste één keer als advocaat beëdigd."
PURPOSE RULE "beëdigde" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een ingeschreven advocaat moet door een rechtbank beëdigd zijn.
-}

RULE beëdigde: beëdiging~;isBeëdigd |- ingeschrevene
MEANING "Een persoon die ten minste één keer als advocaat beëdigd is, mag de titel advocaat voeren."
PURPOSE RULE "beëdigde" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een succesvolle beëdiging moet altijd tot een inschrijving op het tableau leiden.
-}

RULE verzoekingeschreven : isVerstuurdDoor;isAdvocaat |- behoortBij

RULE beëdigingingeschreven : isBeëdigd;isAdvocaat |- beëdigingBehoortBij  

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

ingeschrevene :: Inschrijving -> "natuurlijkpersoon"
POPULATION ingeschrevene CONTAINS
[ ("0001", "Sebastiaan Levelt")
; ("0002", "Ilja van Driel")
; ("0003", "Anne Dekkers")
; ("0004", "Lonneke Cox")
]

bar :: Barnummer -> Inschrijving
POPULATION bar CONTAINS
[ ("A23968", "0001")
; ("A26815", "0002")
; ("A16378", "0003")
; ("A24763", "0004")
]

inschrijvingkantoor :: Inschrijving -> Kantoor
POPULATION inschrijvingkantoor CONTAINS
[ ("0001", "Wieringa advocaten")
; ("0002", "Haulussy advocaten")
; ("0003", "Kuipers, Jonkers, van den Berg")
; ("0004", "ATM Advocaten Utrecht")
]

barnummer :: Barnummer * Advocaat [UNI] PRAGMA "" "is toegekend aan" "in de hoedanigheid van advocaat"
MEANING "Een barnummer dient als uniek identificatienummer voor een advocaat."
PURPOSE RELATION barnummer[Barnummer*Advocaat] IN DUTCH
{+ Een barnummer dient als uniek identificatienummer voor een advocaat.
-}
POPULATION barnummer CONTAINS
[ ("A23968", "mr. S. Levelt")
; ("A26815", "mr. I. van Driel")
; ("A16378", "mr. A. Dekkers")
; ("A24763", "mr. L.L.A. Cox")
]

kantoornummer :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornummer[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornummer CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoor -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoor*Adres] IN DUTCH
{+ Elk kantoor heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("Wieringa advocaten", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("Haulussy advocaten", "Westblaak 5f, 3001 AC, Rotterdam")
; ("Kuipers, Jonkers, van den Berg", "Mariahoek 4, 3511 LD, Utrecht")
; ("ATM Advocaten Utrecht", "Herculesplein 213, 3584 AA, Utrecht")
]

begindatum :: Advocaat * Begindatum [UNI] PRAGMA "" "is zijn of haar werkzaamheden begonnen vanaf" ""
MEANING "Een advocaat begint op een zekere datum zijn of haar werkzaamheden."
PURPOSE RELATION begindatum[Advocaat*Begindatum] IN DUTCH
{+ Elke advocaat heeft één begindatum waarop de werkzaamheden beginnen.
-}
POPULATION begindatum[Advocaat*Begindatum] CONTAINS
[ ("mr. S. Levelt", "06-12-1999")
; ("mr. I. van Driel", "13-05-1995")
; ("mr. A. Dekkers", "20-01-1988")
; ("mr. L.L.A. Cox", "31-07-2001")
]

begindatum :: Begindatum * Kantoor PRAGMA "Op" "is een advocaat in dienstgetreden bij" ""
MEANING "Een begindatum is van toepassing op de werkzaamheden bij een kantoor."
PURPOSE RELATION begindatum[Begindatum*Kantoor] IN DUTCH
{+ Werkzaamheden hebben betrekking op de aanstelling bij een kantoor.
-}
POPULATION begindatum[Begindatum*Kantoor] CONTAINS
[ ("06-12-1999", "Wieringa advocaten")
; ("13-05-1995", "Haulussy advocaten")
; ("20-01-1988", "Kuipers, Jonkers, van den Berg")
; ("31-07-2001", "ATM Advocaten Utrecht")
]

behoortBij :: "Verzoek van inschrijving" * Advocaat PRAGMA "" "behoort bij" ""
POPULATION behoortBij CONTAINS
[ ("V001", "mr. S. Levelt")
; ("V002", "mr. I. van Driel")
; ("V003", "mr. A. Dekkers")
; ("V004", "mr. L.L.A. Cox")
]

beëdigingBehoortBij :: Beëdiging * Advocaat PRAGMA "" "behoort bij" ""
POPULATION beëdigingBehoortBij CONTAINS
[ ("B001", "mr. S. Levelt")
; ("B002", "mr. I. van Driel")
; ("B003", "mr. A. Dekkers")
; ("B004", "mr. L.L.A. Cox")
]

RULE werktbij : begindatum[Advocaat*Begindatum];begindatum[Begindatum*Kantoor] |- houdtKantoor
MEANING "Een advocaat werkt vanaf de begindatum bij het kantoor waar hij of zij is ondergebracht."
PURPOSE RULE "werktbij"
{+ Als een advocaat is ondergebracht bij een kantoor, dan is de begindatum van de werkzaamheden van de advocaat gelijk aan de begindatum waarop het kantoor de werkzaamheden van de advocaat registreert.
-}

RULE inschrijvingsverzoek : behoortBij |- isVerstuurdDoor;isAdvocaat


zoekOpNaam :: Zoek * Advocaat

zoekOpBAR :: Zoek * Barnummer


ENDPATTERN

ENDCONTEXT