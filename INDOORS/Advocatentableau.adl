CONTEXT Advocatentableau

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Specialisatie en overerving
SPEC Beëdiging ISA Gebeurtenis


--Relaties en regels

--Art. 1
--lid 1
barnummer :: Barnummer * Persoon [UNI] PRAGMA "" "is toegekend aan" "in de hoedanigheid van advocaat"
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION barnummer[Barnummer*Persoon] IN DUTCH REF "Art. 1-1, §1, Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION barnummer CONTAINS
[ ("A23968", "Sebastiaan Levelt")
; ("A26815", "Ilja van Driel")
; ("A16378", "Anne Dekkers")
; ("A24763", "Lonneke Cox")
]

RULE beëdigdeAdvocaat: barnummer~;barnummer |- isBeëdigd~;isBeëdigd
MEANING "Elke ingeschreven advocaat is beëdigd."
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Als de secretaris van de algemene raad de advocaat op het tableau inschrijft, is hij door de griffier van de rechtbank over de beëdiging ingelicht.
-}

--Art.2
--lid 1
kantoorVerzoek :: "Verzoek van inschrijving" * Kantoor [INJ] PRAGMA "Indiener van " "wil kantoor houden bij" ""
MEANING "Het inschrijvingsverzoek bevat de kantoornaam waar de inschrijvende kantoor wil houden."
PURPOSE RELATION kantoorVerzoek["Verzoek van inschrijving"*Kantoor] IN DUTCH REF "Art. 2-1, §1, Advw."
{+
Advocaten mogen slechts bij één kantoor ingeschreven staan.
-}
POPULATION kantoorVerzoek CONTAINS
[ ("Vvi1", "Wieringa advocaten")
; ("Vvi2", "Haulussy advocaten")
; ("Vvi3", "Kuipers, Jonkers, van den Berg")
; ("Vvi4", "ATM Advocaten Utrecht")
]



isGerichtAan :: "Verzoek van inschrijving" -> Rechtbank [TOT] PRAGMA "" "is gericht aan de" ""
MEANING "Een verzoek van inschrijving tot advocaat is gericht aan de voorzitter van een rechtbank."
PURPOSE RELATION isGerichtAan["Verzoek van inschrijving"*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION isGerichtAan CONTAINS
[ ("Vvi1", "Rechtbank Amsterdam")
; ("Vvi2", "Rechtbank Rotterdam")
; ("Vvi3", "Rechtbank Utrecht")
; ("Vvi4", "Rechtbank Utrecht")
]



isVerstuurdDoor :: "Verzoek van inschrijving" * Persoon [TOT] PRAGMA "" "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION isVerstuurdDoor["Verzoek van inschrijving"*Persoon] IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION isVerstuurdDoor CONTAINS
[ ("Vvi1", "Sebastiaan Levelt")
; ("Vvi2", "Ilja van Driel")
; ("Vvi3", "Anne Dekkers")
; ("Vvi4", "Lonneke Cox")
]

RULE "Art. 2-1 Advw.": kantoorVerzoek;kantoor |- isGerichtAan
MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de voorzitter van de rechtbank waar de betreffende persoon kantoor wenst te houden."
PURPOSE RULE "Art. 2-1 Advw." IN DUTCH REF "Art 2-1, §1, Advw."
{+ Inschrijvingsverzoeken moeten aan de voorzitter van de rechtbank worden gericht, waar de indiener kantoor wenst te houden.
-}

-- Art. 3
isBeëdigd :: Beëdiging *  Persoon [TOT] PRAGMA "" "bevestigt de beëdigding van" ""
MEANING "Een advocaat wordt beëdigigd op requisitoir van het openbaar ministerie."
PURPOSE RELATION isBeëdigd[Beëdiging*Persoon] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Elke aspirant advocaat moet een beëdiging ondergaan.
-}
POPULATION isBeëdigd CONTAINS
[ ("Be1", "Sebastiaan Levelt")
; ("Be2", "Ilja van Driel")
; ("Be3", "Anne Dekkers")
; ("Be4", "Lonneke Cox")
]

locatieRechtbank :: Beëdiging -> Rechtbank [TOT] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatieRechtbank[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een beëdiging van een advocaat vindt plaats op de locatie van de rechtbank waarin de advocaat kantoor wil gaan houden.
Omdat alleen beëdigde personen advocaat zijn, wordt de beëdiging van elke advocaat geregistreerd.
-}

POPULATION locatieRechtbank CONTAINS
[ ("Be1", "Rechtbank Amsterdam")
; ("Be2", "Rechtbank Rotterdam")
; ("Be3", "Rechtbank Utrecht")
; ("Be4", "Rechtbank Utrecht")
]


--Invarianten
RULE "Art. 3-1 Advw.": isBeëdigd~;locatieRechtbank |- isVerstuurdDoor~;kantoorVerzoek;kantoor
MEANING "Elke advocaat die beëdigd is door een betrokken rechtbank, wil kantoor houden bij die rechtbank."
PURPOSE RULE "Art. 3-1 Advw." IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is.
-}

RULE verzoekTotBeëdiging : isBeëdigd~;isBeëdigd |- isVerstuurdDoor~;isVerstuurdDoor
MEANING "Aan elke beëdiging van een advocaat is een verzoek van inschrijving voorafgegaan."
PURPOSE RULE verzoekTotBeëdiging IN DUTCH REF "Art. 2-1, §1, Advw."
{+ Een beëdiging van een advocaat kan alleen plaats vinden als er ook een inschrijvingsverzoek ingediend is door die persoon.
-}

-- Art. 8
schrappingDoor :: "Verzoek tot schrapping" * Persoon [TOT] PRAGMA "" "is gedaan door" ""
MEANING "Een schrappingsverzoek moet schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf, of door de rechtbank waar de advocaat kantoor houdt."
PURPOSE RELATION schrappingDoor["Verzoek tot schrapping"*Persoon] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingDoor CONTAINS
[ ("Vts1", "Anne Dekkers")
; ("Vts2", "Sanne Verhoeven")
; ("Vts3", "Sebastiaan Levelt")
]

schrappingVan :: "Verzoek tot schrapping" * Persoon [TOT] PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrappingsverzoek heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden."
PURPOSE RELATION schrappingVan["Verzoek tot schrapping"*Persoon] IN DUTCH REF "Art. 8-2, §1, Advw."
{+
-}
POPULATION schrappingVan CONTAINS
[ ("Vts1", "Anne Dekkers")
; ("Vts2", "Ilja van Driel")
; ("Vts3", "Sebastiaan Levelt")
]

-- Art. 12
houdtkantoor :: Barnummer * Kantoor [TOT] PRAGMA "Advocaat met barnummer " "staat ingeschreven bij" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtkantoor[Barnummer*Kantoor] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtkantoor CONTAINS
[ ("A23968", "Wieringa advocaten")
; ("A26815", "Haulussy advocaten")
; ("A16378", "Kuipers, Jonkers, van den Berg")
; ("A24763", "ATM Advocaten Utrecht")
]

kantoor :: Kantoor * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoor valt onder de bevoegdheid van een specifieke rechtbank." 
PURPOSE RELATION kantoor[Kantoor*Rechtbank] IN DUTCH REF "Art. 12-1, §2, Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("Wieringa advocaten", "Rechtbank Amsterdam")
; ("Haulussy advocaten", "Rechtbank Rotterdam")
; ("Kuipers, Jonkers, van den Berg", "Rechtbank Utrecht")
; ("ATM Advocaten Utrecht", "Rechtbank Utrecht")
]

RULE houdtKantoor: barnummer~;houdtkantoor |- isVerstuurdDoor~;kantoorVerzoek
MEANING "Elke advocaat houdt kantoor bij het kantoor dat vermeld staat in het inschrijvingsverzoek."
PURPOSE RULE "houdtKantoor" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ Waar de advocaat kantoor houdt, komt overeen met het kantoor dat in het inschrijvingsverzoek vermeld staat. 
-}

ENDPATTERN


PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}


--Procesregels
ROLE Secretaris MAINTAINS beëdigdeAdvocaat, wilkantoorhouden

--Wie doet de inschrijving? Griffie rechtbank --> Rol toevoegen.
RULE ingeschrevenAdvocaat: isBeëdigd~;isBeëdigd |- barnummer~;barnummer 
MEANING "Elke ingeschreven advocaat is beëdigd."
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ De griffier van de rechtbank licht de secretaris van de algemene raad in over de beëdiging ingelicht, zodat de secretaris de advocaat op het tableau in kan schrijven.
-}

RULE wilkantoorhouden: isVerstuurdDoor~;kantoorVerzoek |- barnummer~;houdtkantoor
MEANING "Het kantoor wat in het inschrijvingsverzoek vermeld staat, wordt uiteindelijk het kantoor waar de advocaat daadwerkelijk kantoor houdt."
PURPOSE RULE "wilkantoorhouden" IN DUTCH REF "Art. 3-1, §1, Advw."
{+ 
-}

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornummer :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornummer[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornummer CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoor -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoor*Adres] IN DUTCH
{+ Elk kantoor heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("Wieringa advocaten", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("Haulussy advocaten", "Westblaak 5f, 3001 AC, Rotterdam")
; ("Kuipers, Jonkers, van den Berg", "Mariahoek 4, 3511 LD, Utrecht")
; ("ATM Advocaten Utrecht", "Herculesplein 213, 3584 AA, Utrecht")
]

zoeken :: Zoek * Zoek
POPULATION zoeken CONTAINS
[ ("", "Zoek")
]

zoekOpNaam :: Zoek * Persoon

zoekOpBAR :: Zoek * Barnummer




ENDPATTERN

ENDCONTEXT