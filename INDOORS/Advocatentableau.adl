CONTEXT Advocatentableau

--INCLUDE "Dossiers.adl"
--INCLUDE "Zaakgegevens.adl"
--INCLUDE "Persoonsgegevens.adl"
--INCLUDE "Zittinggegevens.adl"
--INCLUDE "Registers.adl"
--INCLUDE "Gebeurtenis.adl"
--INCLUDE "Berichten.adl"

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau" REF "http://barbeheer.nl"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd,
is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten.
BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen.
De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Specialisatie en overerving
SPEC Beëdiging ISA Gebeurtenis
--SPEC Inschrijvingsverzoek ISA Document

--KEY verzoek : Inschrijvingsverzoek(TXT "Verstuurd door ", verstuurdDoor;naam)
KEY advocaat : Advocaat (advocaat;naam)
--KEY beëdiging : Beëdiging (TXT "De beëdiging van " beëdigd;naam, TXT " te ", locatieBeëdiging)
KEY kantoor : Kantoornummer(kantoornaam)


--Relaties en regels

--Art. 1
--lid 1
advocaat :: Advocaat * Persoon [TOT,UNI,INJ] PRAGMA "" "representeert de advocatenrol van" ""
MEANING "Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel advocaat te mogen voeren."
PURPOSE RELATION advocaat[Advocaat*Persoon] IN DUTCH REF "Art. 1 lid 1, §1, Advw."
{+ Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten.
-}
POPULATION advocaat CONTAINS
[ ("mr. S. Levelt", "4")
; ("mr. I. van Driel", "5")
; ("mr. A. Dekkers", "6")
; ("mr. L.L.A. Cox", "7")
]


RULE inschrijvingsstatussen: I[Inschrijvingsstatus] = 'voorwaardelijk' \/ 'onvoorwaardelijk'
MEANING "De inschrijving als advocaat geschiedt voorwaardelijk of onvoorwaardelijk."
MESSAGE "Volgens Artikel 1 lid 2 van de Advocatenwet is de inschrijving van een advocaat 'voorwaardelijk' of 'onvoorwaardelijk'."
PURPOSE RULE inschrijvingsstatussen IN DUTCH REF "Art. 1 lid 2, §1, Advw."
{+ De inschrijvingsstatus van een advocaat is wettelijk bepaald als 'voorwaardelijk' of 'onvoorwaardelijk'
-}

inschrijvingsstatus :: Advocaat * Inschrijvingsstatus PRAGMA "Aan de inschrijving van " "is de inschrijvingsstatus toegekend van" ""
MEANING "De inschrijving van een advocaat op het landelijk advocaten tableau heeft de status 'voorwaardelijk' of 'onvoorwaadelijk'."
PURPOSE RELATION inschrijvingsstatus[Advocaat*Inschrijvingsstatus]
{+ De status van een ingeschreven advocaat indiceert of er rondom zijn inschrijving nog handelingen verricht dienen te worden.
-}

barnummer :: Advocaat -> Barnummer PRAGMA "" "is verbonden aan BAR-nummer" ""
MEANING "Een advocaat heeft een uniek BAR-nummer."
PURPOSE RELATION barnummer[Advocaat*Barnummer] IN DUTCH REF ""
{+
-}
POPULATION barnummer CONTAINS
[ ("mr. S. Levelt", "A23968")
; ("mr. I. van Driel", "A26815")
; ("mr. A. Dekkers", "A16378")
; ("mr. L.L.A. Cox", "A24763")
]

RULE "uniek BAR-nummer" : barnummer;barnummer~ |- I[Advocaat]
MEANING "Elke advocaat heeft een uniek BAR-nummer"
MESSAGE "Dit BAR-nummer is al aan een andere advocaat toegewezen."
PURPOSE RULE "uniek BAR-nummer" IN DUTCH REF ""
{+ Een BAR-nummer is uniek toegekend aan een enkele advocaat.
-}

RULE beëdigdeAdvocaat: advocaat~;advocaat |- beëdigd~;beëdigd
MEANING "Elke ingeschreven advocaat is beëdigd."
MESSAGE "Een advocaat kan niet worden ingeschreven als hij nog niet is beëdigd. (Art. 3-1, §1, Advw.)"
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Als de secretaris van de algemene raad de advocaat op het tableau inschrijft, is hij door de griffier van de rechtbank over de beëdiging ingelicht.
-}

--Art.2
--lid 1
kantoorVerzoek :: Inschrijvingsverzoek -> Kantoornummer PRAGMA "Indiener van " "wil kantoor houden bij" ""
MEANING "Het inschrijvingsverzoek bevat de kantoornaam waar de inschrijvende kantoor wil houden."
PURPOSE RELATION kantoorVerzoek[Inschrijvingsverzoek*Kantoornummer] IN DUTCH REF "Art. 2 lid 1, §1, Advw."
{+
Advocaten mogen slechts bij één kantoor ingeschreven staan.
-}
POPULATION kantoorVerzoek CONTAINS
[ ("Vti1", "K52347")
; ("Vti2", "K45317")
; ("Vti3", "K48933")
; ("Vti4", "K12493")
]


gerichtAan :: Inschrijvingsverzoek -> Rechtbank PRAGMA "" "is gericht aan de" ""
MEANING "Een verzoek van inschrijving tot advocaat is gericht aan de voorzitter van een rechtbank."
PURPOSE RELATION gerichtAan[Inschrijvingsverzoek*Rechtbank] IN DUTCH REF "Art. 2 lid 1, §1, Advw"
{+ Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
-}
POPULATION gerichtAan CONTAINS
[ ("Vti1", "Rechtbank Amsterdam")
; ("Vti2", "Rechtbank Rotterdam")
; ("Vti3", "Rechtbank Utrecht")
; ("Vti4", "Rechtbank Utrecht")
]

verstuurdDoor :: Inschrijvingsverzoek -> Persoon PRAGMA "" "is verstuurd door" ""
MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
PURPOSE RELATION verstuurdDoor[Inschrijvingsverzoek*Persoon] IN DUTCH REF "Art. 2 lid 1, §1, Advw"
{+ Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
-}
POPULATION verstuurdDoor CONTAINS
[ ("Vti1", "4")
; ("Vti2", "5")
; ("Vti3", "6")
; ("Vti4", "7")
]

inschrijvingseis :: Inschrijvingsverzoek * Inschrijvingseis PRAGMA "" "heeft inschrijvingseisen die" "zijn"
MEANING "Inschrijvingseisen van een inschrijvingsverzoek zijn controleerbaar."
PURPOSE RELATION inschrijvingseis[Inschrijvingsverzoek*Inschrijvingseis] IN DUTCH REF "Art. 2, §1, Advw"
{+
-}
POPULATION inschrijvingseis CONTAINS
[ ("Vti1", "gecontroleerd")
; ("Vti2", "gecontroleerd")
; ("Vti3", "gecontroleerd")
; ("Vti4", "gecontroleerd")
]

RULE kantoorverzoek: kantoorVerzoek~;gerichtAan |- kantoor
MEANING "Het in het inschrijvingsverzoek vermeldde kantoor moet vallen onder een rechtbank waar het inschrijvingsverzoek aan is gericht."
MESSAGE "De locatie van de rechtbank komt niet overeen met de locatie van het kantoor, m.a.w. het kantoor valt niet onder het arrondissement van de rechtbank. (Art. 3 lid 1, §1, Advw)"
PURPOSE RULE "kantoorverzoek" IN DUTCH REF "Art. 3-1, §1, Advw"
{+ 
-}

{-
RULE "Art. 2-1 Advw.": kantoorVerzoek;kantoor |- gerichtAan
MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de voorzitter van de rechtbank waar de betreffende persoon kantoor wenst te houden. (Art 2-1, §1, Advw.)"
MESSAGE "De locatie van de rechtbank komt niet overeen met de locatie van het kantoor, m.a.w. het kantoor valt niet onder het arrondissement van de rechtbank."
PURPOSE RULE "Art. 2-1 Advw." IN DUTCH REF "Art 2 lid 1, §1, Advw"
{+ Inschrijvingsverzoeken moeten aan de voorzitter van de rechtbank worden gericht, waar de indiener kantoor wenst te houden.
-}


RULE inschrijvingseistypen: I[Inschrijvingseis] = 'gecontroleerd' \/ 'niet gecontroleerd'
MEANING "Een inschrijvingseis is gecontroleerd of niet gecontroleerd."
MESSAGE "Een inschrijvingseis kan geen andere status hebben dan 'gecontroleerd' of 'niet gecontroleerd'."
PURPOSE RULE "inschrijvingseistypen" IN DUTCH REF "Art 2, §1, Advw."
{+ Het type van de inschrijvingseis(en) bepaalt de status van het inschrijvingsverzoek
-}


-- Art. 3
beëdigd :: Beëdiging *  Persoon [TOT,UNI] PRAGMA "" "bevestigt de beëdigding van" ""
MEANING "Een advocaat wordt beëdigigd op requisitoir van het openbaar ministerie."
PURPOSE RELATION beëdigd[Beëdiging*Persoon] IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Elke aspirant advocaat moet een beëdiging ondergaan.
-}
POPULATION beëdigd CONTAINS
[ ("Be1", "4")
; ("Be2", "5")
; ("Be3", "6")
; ("Be4", "7")
]

locatieBeëdiging :: Beëdiging -> Rechtbank [TOT] PRAGMA "" "heeft plaatsgevonden op de" ""
MEANING "Het beëdigen van een aspirant advocaat vindt plaats op de rechtbank waarbij de aspirant advocaat kantoor wenst te houden."
PURPOSE RELATION locatieBeëdiging[Beëdiging*Rechtbank] IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+
-}
POPULATION locatieBeëdiging CONTAINS
[ ("Be1", "Rechtbank Amsterdam")
; ("Be2", "Rechtbank Rotterdam")
; ("Be3", "Rechtbank Utrecht")
; ("Be4", "Rechtbank Utrecht")
]

behoortBij :: Inschrijvingsverzoek * Beëdiging [SUR, INJ] PRAGMA "De indiener van " "heeft geleid tot" ""
MEANING "De advocaten worden door de rechtbank beëdigd aan wier voorzitter zij een verzoek tot inschrijving hebben gedaan."
PURPOSE RELATION behoortBij[Inschrijvingsverzoek*Beëdiging] IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Een verzoek van inschrijving kan leiden tot een beëdiging. 
-}
POPULATION behoortBij CONTAINS
[ ("Vti1", "Be1")
; ("Vti2", "Be2")
; ("Vti3", "Be3")
; ("Vti4", "Be4")
]


--Invarianten
RULE wilkantoorhouden: beëdigd~ |- verstuurdDoor~;kantoorVerzoek;kantoor;locatieBeëdiging~
MEANING "Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is."
MESSAGE "Het kantoor van de kandidaat bevindt zich niet in het arrondissement waar de kandidaat beëdigd is, wat op grond van Art. 3 lid 1, Advw. wél vereist is."
PURPOSE RULE "wilkantoorhouden" IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Een advocaat die kantoor wenst te houden, dient beëdigd te worden bij de rechtbank waar het kantoor onder valt.
-}

--Regel die eigenlijk vervangen moet worden door ECA-regel
RULE locatiebeëdiging: locatieBeëdiging~;locatie |- hoofdplaatsrechtbank~
MEANING "De beëdigingslocatie moet overeenkomen met de locatie van de rechtbank."
MESSAGE "De locatie waar de beëdiging plaatsvindt komt niet overeen met de rechtbank waar de beëdigde zich heeft aangemeld voor inschrijving. Dat is in strijd met Art. 3 lid 1, Advw."
PURPOSE RULE locatiebeëdiging IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ De locatie van de rechtbank waar de beëdiging plaatsvindt moet gelijk zijn aan de locatie waar de rechtbank gevestigd is.
-}

--Alleen inschrijvingsverzoeken waar de inschrijvingseisen van zijn goedgekeurd mogen leiden tot beëdigingen
RULE magalleenwordenbeëdigd: beëdigd~;behoortBij~;inschrijvingseis |- verstuurdDoor~;inschrijvingseis;(I[Inschrijvingseis]='gecontroleerd')
MEANING "Een inschrijvingsverzoek waarvan gestelde de inschrijvingseisen gecontroleerd en goedbevonden zijn, moet uiteindelijk leiden tot een beëdiging."
MESSAGE "Deze persoon mag niet beëdigd worden, omdat de eisen van zijn/haar inschrijvingsverzoek niet gecontroleerd zijn conform Artikel 2 van de Advocatenwet."
PURPOSE RULE magalleenwordenbeëdigd IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Alleen inschrijvingsverzoeken waar de inschrijvingseisen van zijn goedgekeurd mogen leiden tot beëdigingen.
-}

--Beëdigingen vinden plaats op basis van het corresponderende inschrijvingsverzoek
RULE corresponderendinschrijvingsverzoek: behoortBij |- verstuurdDoor;beëdigd~
MEANING "Een beëdiging vindt plaats op basis van een eerder gedaan corresponderend inschrijvingsverzoek."
MESSAGE "De persoon die het inschrijvingsverzoek heeft ingediend, komt niet overeen met de beëdigde persoon."
PURPOSE RULE corresponderendinschrijvingsverzoek IN DUTCH REF ""
{+ Beëdigingen vinden plaats op basis van het corresponderende inschrijvingsverzoek
-}

-- Art. 8
schrappingDoor :: Schrapping * Advocaat PRAGMA "" "is  verzocht door" ""
MEANING "Een schrapping kan schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf."
PURPOSE RELATION schrappingDoor[Schrapping*Advocaat] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrappingDoor[Schrapping*Advocaat] CONTAINS
[ ("Vts1", "mr. A. Dekkers")
; ("Vts3", "mr. S. Levelt")
]

schrappingDoor :: Schrapping * Orgaan PRAGMA "De beslissing " "is genomen door" ""
MEANING "Tot schrapping kan worden besloten door de rechtbank waar de advocaat kantoor houdt of de Raad van State."
PURPOSE RELATION schrappingDoor[Schrapping*Orgaan] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrappingDoor[Schrapping*Orgaan] CONTAINS
[ ("Vts2", "Raad van State")
]

schrappingVan :: Schrapping * Advocaat [TOT] PRAGMA "" "heeft betrekking op" ""
MEANING "Een schrapping heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden."
PURPOSE RELATION schrappingVan[Schrapping*Advocaat] IN DUTCH REF "Art. 8 lid 2, §1, Advw."
{+
-}
POPULATION schrappingVan CONTAINS
[ ("Vts1", "mr. A. Dekkers")
; ("Vts2", "mr. I. van Driel")
; ("Vts3", "mr. S. Levelt")
]


-- Art. 12
houdtkantoor :: Advocaat * Kantoornummer [TOT] PRAGMA "Advocaat " "staat ingeschreven bij" "" 
MEANING "Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden."
PURPOSE RELATION houdtkantoor[Advocaat*Kantoornummer] IN DUTCH REF "Art. 12 lid 1, §2, Advw."
{+ Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden
-}
POPULATION houdtkantoor CONTAINS
[ ("mr. S. Levelt", "K52347")
; ("mr. I. van Driel", "K45317")
; ("mr. A. Dekkers", "K48933")
; ("mr. L.L.A. Cox", "K12493")
]

kantoor :: Kantoornummer * Rechtbank [UNI] PRAGMA "" "valt onder de" ""
MEANING "Een kantoor valt onder een rechtbank." 
PURPOSE RELATION kantoor[Kantoornummer*Rechtbank] IN DUTCH REF "Art. 12 lid 1, §2, Advw."
{+ Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
-}
POPULATION kantoor CONTAINS
[ ("K52347", "Rechtbank Amsterdam")
; ("K45317", "Rechtbank Rotterdam")
; ("K48933", "Rechtbank Utrecht")
; ("K12493", "Rechtbank Utrecht")
]


--Art. 16
schorsingVan :: Schorsing * Advocaat PRAGMA "" "heeft betrekking op" ""
MEANING "Onder bepaalde voorwaarden kan een advocaat worden geschorst."
PURPOSE RELATION schorsingVan[Schorsing*Advocaat] IN DUTCH REF "Art. 16, §2, Advw."
{+ Een geschorste advocaat mag gedurende de schorsing de titel van advocaat niet voeren.
-}

schorsing :: Schorsing * Orgaan PRAGMA "De beslissing " "is genomen door" ""
MEANING "Een gerecht kan beslissen om een advocaat te schorsen."
PURPOSE RELATION schorsing[Schorsing*Orgaan] IN DUTCH REF "Art. 16, §2, Advw."
{+ De griffier van het gerecht geeft van de schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

schorsingOpheffen :: Schorsing * Orgaan PRAGMA "De beslissing " "is genomen door" ""
MEANING "Een gerecht kan beslissen een schorsing op te heffen."
PURPOSE RELATION schorsingOpheffen[Schorsing*Orgaan] IN DUTCH REF "Art. 16, §2, Advw."
{+ De griffier van het gerecht geeft van de opheffing schorsing kennis aan secretaris van de algemene raad met het oog op de verwerking op het tableau.
-}

ENDPATTERN


PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}


--Procesregels
ROLE Secretaris MAINTAINS ingeschrevenAdvocaat
ROLE Griffier MAINTAINS moetwordenbeëdigd, opleidingseisvoldaan

RULE moetwordenbeëdigd: verstuurdDoor~;inschrijvingseis |- beëdigd~;behoortBij~;inschrijvingseis;(I[Inschrijvingseis]='gecontroleerd')
MEANING "Een inschrijvingsverzoek waarvan gestelde opleidingseis gecontroleerd is, moet uiteindelijk leiden tot een beëdiging."
MESSAGE "Er zijn inschrijvingsverzoeken die nog niet tot een beëdiging hebben geleid, als bedoeld in Art. 3 lid 1, Advw."
PURPOSE RULE moetwordenbeëdigd IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ Geaccepteerde inschrijvingsverzoeken leiden tot beëdigingen.
-}


RULE ingeschrevenAdvocaat: beëdigd~;beëdigd |- advocaat~;advocaat 
MEANING "Elke ingeschreven advocaat is beëdigd."
MESSAGE "De beëdigde advocaat dient ingeschreven te worden op het tableau. (Art. 3-1, §1, Advw.)"
PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3 lid 1, §1, Advw."
{+ De griffier van de rechtbank licht de secretaris van de algemene raad in over de beëdiging ingelicht, zodat de secretaris de advocaat op het tableau in kan schrijven.
-}


RULE opleidingseisvoldaan: verstuurdDoor~;inschrijvingseis |- verstuurdDoor~;inschrijvingseis;(I[Inschrijvingseis]='gecontroleerd')
MEANING "Een inschrijvingsverzoek wordt geaccepteerd als de indiener aan de inschrijvingseisen voldoet."
MESSAGE "Het inschrijvingsverzoek is nog niet gecontroleerd op alle inschrijvingseisen. (Art. 2, Advw.)"
PURPOSE RULE "opleidingseisvoldaan" IN DUTCH REF "Art. 2, §1, Advw."
{+ Een inschrijvingsverzoek kan alleen worden ingeschreven als het aan de inschrijvingseisen voldoet.
-}

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatie-eisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

kantoornaam :: Kantoornummer -> Kantoor [UNI] PRAGMA "" "is toegekend aan" ""
MEANING "Een kantoornummer dient als uniek identificatienummer voor een kantoor."
PURPOSE RELATION kantoornaam[Kantoornummer*Kantoor] IN DUTCH
{+ Elk kantoornummer wordt toegekend aan precies één kantoor.
-}
POPULATION kantoornaam CONTAINS
[ ("K52347", "Wieringa advocaten")
; ("K45317", "Haulussy advocaten")
; ("K48933", "Kuipers, Jonkers, van den Berg")
; ("K12493", "ATM Advocaten Utrecht")
]

kantooradres :: Kantoornummer -> Adres [UNI] PRAGMA "" "is gevestigd op" ""
MEANING "Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden."
PURPOSE RELATION kantooradres[Kantoornummer*Adres] IN DUTCH
{+ Elk kantoor heeft precies één adres.
-}
POPULATION kantooradres CONTAINS
[ ("K52347", "Herengracht 425-429, 1017 BR, Amsterdam")
; ("K45317", "Westblaak 5f, 3001 AC, Rotterdam")
; ("K48933", "Mariahoek 4, 3511 LD, Utrecht")
; ("K12493", "Herculesplein 213, 3584 AA, Utrecht")
]

zoeken :: Zoek * Zoek
POPULATION zoeken CONTAINS
[ ("", "Zoek")
]

zoekOpNaam :: Zoek * Persoon

zoekOpBAR :: Zoek * Advocaat




ENDPATTERN

ENDCONTEXT