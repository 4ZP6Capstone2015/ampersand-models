CONTEXT Advocatentableau

PATTERN Advocatentableau
PURPOSE PATTERN "Advocatentableau"
{+
Het Beheer Advocaten Registratie (BAR), ook wel advocatentableau genoemd, is een bij convenant ingesteld samenwerkingsverband tussen de Rechtspraak, de Raad voor Rechtsbijstand en de Nederlandse orde van advocaten. BAR voorziet in actuele en correcte gegevens over de advocatuur voor de drie partijen. De gegevens die gedeeld worden zijn beëdigings-, kantoor-, adres en communicatiegegevens. (bron:http://barbeheer.nl)
-}

--Art. 1
--lid 1
inschrijving :: Inschrijving * Advocaat PRAGMA "" "heeft betrekking op" ""
	MEANING "Elke advocaat staat geregistreerd in het tableau van de Nederlandse orde van advocaten."
	PURPOSE RELATION inschrijving[Inschrijving*Advocaat] IN DUTCH REF "Art. 1-1, §1, Advw."
	{+Een advocaat moet ingeschreven zijn op het tableau van de Nederlandse orde van advocaten om de titel "advocaat" te mogen voeren.
	-}
	POPULATION inschrijving CONTAINS
		[ 	("0001", "mr. S. Levelt");
			("0002", "mr. I. van Driel");
			("0003", "mr. A. Dekkers");
			("0004", "mr. L.L.A. Cox")
		]



--Art.2
--lid 1
{-
wilKantoorHoudenBij :: Persoon * Rechtbank PRAGMA "" "wil kantoor houden bij" ""
	MEANING "Een ieder is bevoegd zich te richten tot de voorzitter van de rechtbank waar de verzoeker kantoor wenst te houden."
	PURPOSE RELATION wilKantoorHoudenBij[Persoon*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
	Advocaten mogen slechts bij één rechtbank kantoor houden.

	POPULATION wilKantoorHoudenBij CONTAINS
		[	("mr. S. Levelt", "Rechtbank van Amsterdam");
			("mr. I. van Driel", "Rechtbank Rotterdam");	
			("mr. A. Dekkers", "Rechtbank van Utrecht");
			("mr. L.L.A. Cox", "Rechtbank van Utrecht")
		]
-}

	
isGerichtAan :: "Verzoek van inschrijving" -> Rechtbank [UNI,TOT] PRAGMA "" "is gericht aan" ""
	MEANING "Een verzoek van inschrijving tot advocaat is gericht aan de voorzitter van een rechtbank."
	PURPOSE RELATION isGerichtAan["Verzoek van inschrijving"*Rechtbank] IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Inschrijvingsverzoeken tot advocaat worden enkel en alleen behandeld door rechtbanken.
	-}
	POPULATION isGerichtAan CONTAINS
		[	("V001", "Rechtbank van Amsterdam");
			("V002", "Rechtbank Rotterdam");
			("V003", "Rechtbank van Utrecht");
			("V004", "Rechtbank van Utrecht")
		]

	
isVerstuurdDoor :: "Verzoek van inschrijving" * Persoon PRAGMA "" "is verstuurd door" ""
	MEANING "Een persoon die als advocaat ingeschreven wil worden op het advocatentableau, dient daartoe een verzoek tot inschrijving in te dienen."
	PURPOSE RELATION isVerstuurdDoor["Verzoek van inschrijving"*Persoon] IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Een verzoek van inschrijving tot advocaat dient altijd ingediend te zijn door één persoon.
	-}
	POPULATION isVerstuurdDoor CONTAINS
		[	("V001", "Sebastiaan Levelt");
			("V002", "Ilja van Driel");
			("V003", "Anne Dekkers");
			("V004", "Lonneke Cox")
		]
	
RULE "Art. 2-1 Advw.": isVerstuurdDoor;isAdvocaat;houdtKantoor;kantoor |- isGerichtAan
	MEANING "Iedereen die als advocaat ingeschreven wil worden, dient een verzoek tot inschrijving te richten aan de voorzitter van de rechtbank waar de betreffende persoon kantoor wenst te houden."
	PURPOSE RULE "Art. 2-1 Advw." IN DUTCH REF "Art 2-1, §1, Advw."
	{+Inschrijvingsverzoeken moeten, op wettelijke gronde, aan de voorzitter van de rechtbank worden gericht.
	-}
 
	
-- Art. 3
isBeëdigd :: Advocaat * Beëdiging PRAGMA "" "is beëdigd op" ""
	MEANING "Een advocaat beëdigigd op requisitoir van het openbaar ministerie."
	PURPOSE RELATION isBeëdigd[Advocaat*Beëdiging] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Elke aspirant advocaat moet een beëdiging ondergaan.
	-}
	POPULATION isBeëdigd CONTAINS
		[ 	("mr. S. Levelt", "B001");
			("mr. I. van Driel", "B002");
			("mr. A. Dekkers", "B003");
			("mr. L.L.A. Cox", "B004")
		]
	
	
locatie :: Beëdiging -> Locatie [TOT] PRAGMA "" "heeft plaatsgevonden op" ""
	MEANING "Een beëdiging van een advocaat vindt plaats op de locatie van de rechtbank waarin de advocaat kantoor wil gaan houden."
	PURPOSE RELATION locatie[Beëdiging*Locatie] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+
	-}
	POPULATION locatie[Beëdiging*Locatie] CONTAINS
		[ 	("B001", "Amsterdam");
			("B002", "Rotterdam");
			("B003", "Utrecht");
			("B004", "Utrecht")
		]

	
datum ::  Beëdiging -> Datum [TOT] PRAGMA "" "geschiedde op" ""
	MEANING "De datum waarop de beëdiging plaats gevonden heeft, is de datum waarop de beëdigide de titel van advocaat mag voeren."
	PURPOSE RELATION datum[Beëdiging*Datum] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+
	-}
	POPULATION datum CONTAINS
		[ 	("B001", "25-08-1990");
			("B002", "12-02-2009");
			("B003", "06-10-2007");
			("B004", "18-04-2009")
		]

	
wasBetrokkenBij :: Rechtbank * Beëdiging PRAGMA "" "was betrokken bij" ""
	MEANING "De rechtbank aan wier voorzitter een verzoek tot inschrijving is gedaan behandeld op requisitoir van het openbaar ministerie de beëdiging."
	PURPOSE RELATION wasBetrokkenBij[Rechtbank*Beëdiging] IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Beëdigingen vinden plaats op de rechtbank waar het inschrijvingsverzoek aan gericht is.
	-}
	POPULATION wasBetrokkenBij CONTAINS
		[ 	("Rechtbank van Amsterdam", "B001");
			("Rechtbank Rotterdam", "B002");
			("Rechtbank van Utrecht", "B003");
			("Rechtbank van Utrecht", "B004")
		]


--Invarianten		
RULE "Art. 3-1 Advw.": isBeëdigd;wasBetrokkenBij~ |- houdtKantoor;kantoor
	MEANING "Elke advocaat die beëdigd wordt door een betrokken rechtbank, wil kantoor houden bij die rechtbank."
	PURPOSE RULE "Art. 3-1 Advw."
	{+Een advocaat dient kantoor te houden bij de rechtbank waar het inschrijvingsverzoek ingediend is.
	-}
	
RULE beëdigde: beëdiging~;isBeëdigd~ |- inschrijving
	MEANING "Een persoon die ten minste één keer als advocaat beëdigd is mag de titel advocaat voeren."
	PURPOSE RULE "beëdigde" IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Een succesvolle beëdiging moet altijd tot een inschrijving op het tableau leiden.
	-}
	
-- Art. 8
schrappingDoor :: "Verzoek tot schrapping" * Persoon [TOT] PRAGMA "" "is gedaan door" ""
	MEANING "Een schrappingsverzoek moet schriftelijk ingediend worden bij de secretaris van de algemene raad door de advocaat zelf, of door rechtbank waar de advocaat kantoor houdt."
	PURPOSE RELATION schrappingDoor["Verzoek tot schrapping"*Persoon] IN DUTCH REF "Art. 8-2, §1, Advw."
	{+
	-}
	POPULATION schrappingDoor CONTAINS
		[ 	("S001", "Anne Dekkers");
			("S002", "Sanne Verhoeven");
			("S003", "Sebastiaan Levelt")
		]

schrappingVan :: "Verzoek tot schrapping" * Advocaat [TOT] PRAGMA "" "heeft betrekking op" ""
	MEANING "Een schrappingsverzoek heeft betrekking op een advocaat die van het advocatentableau geschrapt moet worden."
	PURPOSE RELATION schrappingVan["Verzoek tot schrapping"*Advocaat] IN DUTCH REF "Art. 8-2, §1, Advw."
	{+
	-}
	POPULATION schrappingVan CONTAINS
		[ 	("S001", "mr. A. Dekkers");
			("S002", "mr. I. van Driel");
			("S003", "mr. S. Levelt")
		]

-- Art. 12
houdtKantoor :: Advocaat * Kantoor PRAGMA "" "staat ingeschreven bij" "" --[INJ, UNI, TOT]
	MEANING "Advocaten zijn wettelijk verplicht kantoor te houden."
	PURPOSE RELATION houdtKantoor[Advocaat*Kantoor] IN DUTCH REF "Art. 12-1, §2, Advw."
	{+Een advocaat mag slechts bij één rechtbank op één locatie kantoor houden"
	-}
	POPULATION houdtKantoor CONTAINS
		[ 	("mr. S. Levelt", "Wieringa advocaten");
			("mr. I. van Driel", "Haulussy advocaten");
			("mr. A. Dekkers", "Kuipers, Jonkers, van den Berg");
			("mr. L.L.A. Cox", "ATM Advocaten Utrecht")
		]
	
kantoor :: Kantoor * Rechtbank PRAGMA "" "valt onder" ""
	MEANING "Een kantoor valt onder de bevoegdheid van een specifieke rechtbank." 
	PURPOSE RELATION kantoor[Kantoor*Rechtbank] IN DUTCH REF "Art. 12-1, §2, Advw."
	{+Een kantoorlocatie moet bij de wet onder één rechtbank bestaan. 
	-}	
	POPULATION kantoor CONTAINS
		[ 	("Wieringa advocaten", "Rechtbank van Amsterdam");
			("Haulussy advocaten", "Rechtbank Rotterdam");
			("Kuipers, Jonkers, van den Berg", "Rechtbank van Utrecht");
			("ATM Advocaten Utrecht", "Rechtbank van Utrecht")
		]

		
--Deze relatie vervangt tijdelijk de SPEC isA van advocaat naar persoon
isAdvocaat :: Persoon * Advocaat PRAGMA "" "heeft als" "de rol van advocaat."
	MEANING "Een persoon kan de rol van één advocaat vervullen."
	PURPOSE RELATION isAdvocaat[Persoon*Advocaat] IN DUTCH
	{+De rol van advocaat kan alleen vervuld worden door een persoon.
	-}
	POPULATION isAdvocaat CONTAINS
	[ 		("Sebastiaan Levelt", "mr. S. Levelt");
			("Ilja van Driel", "mr. I. van Driel");
			("Anne Dekkers", "mr. A. Dekkers");
			("Lonneke Cox", "mr. L.L.A. Cox")
	]

ENDPATTERN



PROCESS Inschrijven

PURPOSE PROCESS "Inschrijven"
{+
In dit pattern worden de procesrelaties en procesregels die betrekking hebben op het advocatentableau beschreven.
-}

verzoekTotBeëdiging :: "Verzoek van inschrijving" * Beëdiging PRAGMA "" "heeft geleid tot" ""
	MEANING "Elke beëdiging heeft een verzoek van inschrijving als basis."
	PURPOSE RELATION verzoekTotBeëdiging["Verzoek van inschrijving"*Beëdiging] IN DUTCH
	{+Een inschrijving op het advocatentableau kan plaatsvinden als er een verzoek tot inschrijving ingediend is.
	-}
	POPULATION verzoekTotBeëdiging CONTAINS
		[	("V001", "B001");
			("V002", "B002");
			("V003", "B003");
			("V004", "B004")
		]
{-
verzoekTotInschrijving :: "Verzoek van inschrijving" * Inschrijving PRAGMA "" "heeft geleid tot" ""
	MEANING "Elke inschrijving heeft een beëdiging als basis."
	PURPOSE RELATION verzoekTotInschrijving["Verzoek van inschrijving"*Inschrijving] IN DUTCH
	Een inschrijving op het advocatentableau kan plaatsvinden als er een verzoek tot inschrijving ingediend is.
	
	POPULATION verzoekTotInschrijving CONTAINS
		[	("V001", "0001");
			("V002", "0002");
			("V003", "0003");
			("V004", "0004")
		]
-}

beëdiging :: Beëdiging * Inschrijving PRAGMA "" "heeft betrekking op" ""
	MEANING "Elke inschrijving heeft een beëdiging als basis."
	PURPOSE RELATION beëdiging[Beëdiging*Inschrijving] IN DUTCH
	{+Om te kunnen zeggen op welke beëdiging een inschrijving is gebasseerd, gebruiken we het woord \"basis\".
	-}
	POPULATION beëdiging CONTAINS
		[	("B001", "0001");
			("B002", "0002");
			("B003", "0003");
			("B004", "0004")
		]		
		
--Procesregels
ROLE Secretaris MAINTAINS verzoekTotBeëdiging, beëdigdeAdvocaat, ingeschrevenEnBeëdigd

RULE verzoekTotBeëdiging : isBeëdigd |- isAdvocaat~;isVerstuurdDoor~;verzoekTotBeëdiging
	MEANING "Aan elke beëdiging van een advocaat is een verzoek van inschrijving voorafgegaan."
	PURPOSE RULE verzoekTotBeëdiging IN DUTCH REF "Art. 2-1, §1, Advw."
	{+Het initiatief om tot advocaat ingeschreven te worden ligt bij de persoon, daarom moet hij een verzoek van inschrijving indienen.
	-}


--Wie doet de inschrijving? Griffie rechtbank --> Rol toevoegen.	
RULE beëdigdeAdvocaat: inschrijving~ |- isBeëdigd;beëdiging
	MEANING "Elke ingeschreven advocaat is beëdigd."
	PURPOSE RULE "beëdigdeAdvocaat" IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Als de secretaris van de algemene raad de advocaat op het tableau inschrijft, is hij door de griffier van de rechtbank over de beëdiging ingelicht.
	-}

RULE ingeschrevenEnBeëdigd: inschrijving |- beëdiging~;isBeëdigd~
	MEANING "Een persoon die de titel advocaat mag voeren is ten minste één keer als advocaat beëdigd."
	PURPOSE RULE "beëdigde" IN DUTCH REF "Art. 3-1, §1, Advw."
	{+Een ingeschreven advocaat moet door een rechtbank beëdigd zijn.
	-}

ENDPROCESS


--Applicatiespecifieke eisen. Deze zijn niet terug te vinden in de wet
PATTERN LAT
PURPOSE PATTERN "LAT"
{+
In dit pattern worden specifieke applicatieeisen opgenomen. De relaties en eisen komen uit het technisch ontwerp (TO) van de Landelijke AdvocatenTabel (LAT) zoals opgesteld per september 2007 (versie 2.0).
Relaties en regels zijn gebasseerd op het datamodel en vinden wellicht niet hun grond in de wet.
-}

persID :: PersoonID * Persoon
	POPULATION persID CONTAINS
	[	("1", "mr. S. Levelt");
		("2", "mr. I. van Driel");
		("3", "mr. A. Dekkers");
		("4", "mr. L.L.A. Cox")
	]

barnummer :: Barnummer -> Advocaat PRAGMA "" "wordt toegekend" "in de hoedanigheid van advocaat."
	MEANING "Elk barnummer wordt toegekend aan precies één advocaat."
	PURPOSE RELATION barnummer[Barnummer*Advocaat] IN DUTCH
	{+Een barnummer dient als uniek identificatienummer voor een advocaat.
	-}
	POPULATION barnummer CONTAINS
	[	("A23968", "mr. S. Levelt");
		("A26815", "mr. I. van Driel");
		("A16378", "mr. A. Dekkers");
		("A24763", "mr. L.L.A. Cox")
	]
	
kantoornummer :: Kantoornummer -> Kantoor PRAGMA "" "wordt toegekend" ""
	MEANING "Elk kantoornummer wordt toegekend aan precies één kantoor."
	PURPOSE RELATION kantoornummer[Kantoornummer*Kantoor] IN DUTCH
	{+Een kantoornummer dient als uniek identificatienummer voor een kantoor.
	-}
	POPULATION kantoornummer CONTAINS
	[ 	("K52347", "Wieringa advocaten");
		("K45317", "Haulussy advocaten");
		("K48933", "Kuipers, Jonkers, van den Berg");
		("K12493", "ATM Advocaten Utrecht")
	]

kantooradres :: Kantoor -> Adres PRAGMA "" "is gevestigd op" ""
	MEANING "Elk kantoor heeft precies één adres."
	PURPOSE RELATION kantooradres[Kantoor*Adres] IN DUTCH
	{+Een kantoor heeft een vestigingsadres waarop het kantoor aangeschreven kan worden.
	-}
	POPULATION kantooradres CONTAINS
	[ 	("Wieringa advocaten", "Herengracht 425-429, 1017 BR, Amsterdam");
		("Haulussy advocaten", "Westblaak 5f, 3001 AC, Rotterdam");
		("Kuipers, Jonkers, van den Berg", "Mariahoek 4, 3511 LD, Utrecht");
		("ATM Advocaten Utrecht", "Herculesplein 213, 3584 AA, Utrecht")
	]
	
begindatum :: Advocaat * Begindatum PRAGMA "" "is zijn of haar werkzaamheden begonnen vanaf" ""
	MEANING "Elk persoon heeft één begindatum waarop de werkzaamheden beginnen."
	PURPOSE RELATION begindatum[Advocaat*Begindatum] IN DUTCH
	{+Een persoon begint op een zekere datum zijn of haar werkzaamheden.
	-}
	POPULATION begindatum[Advocaat*Begindatum] CONTAINS
	[ 	("mr. S. Levelt", "06-12-1999");
		("mr. I. van Driel", "13-05-1995");
		("mr. A. Dekkers", "20-01-1988");
		("mr. L.L.A. Cox", "31-07-2001")
	]

begindatum :: Begindatum * Kantoor PRAGMA "" "is van toepassing op" ""
	MEANING "Een begindatum is van toepassing op de werkzaamheden bij een kantoor."
	PURPOSE RELATION begindatum[Begindatum*Kantoor] IN DUTCH
	{+Werkzaamheden hebben betrekking op de aanstelling bij een kantoor.
	-}
	POPULATION begindatum[Begindatum*Kantoor] CONTAINS
	[ 	("06-12-1999", "Wieringa advocaten");
		("13-05-1995", "Haulussy advocaten");
		("20-01-1988", "Kuipers, Jonkers, van den Berg");
		("31-07-2001", "ATM Advocaten Utrecht")
	]
	
RULE werktbij : begindatum[Advocaat*Begindatum];begindatum[Begindatum*Kantoor] |- houdtKantoor
	MEANING "Als een advocaat is ondergebracht bij een kantoor, dan is de begindatum van de werkzaamheden van de advocaat gelijk aan de begindatum waarop het kantoor de werkzaamheden van de advocaat registreert."
	PURPOSE RULE "werktbij"
	{+Een advocaat werkt vanaf de begindatum bij het kantoor waar hij of zij is ondergebracht.
	-}

	
-- Lelijke code die toegevoegd is om het zoeken en aanmaken mogelijk te maken
zoek :: Zoek * Zoek
 = [ ("zoek", "zoek")
   ]
   
zoekOpNaam :: Zoek * Advocaat

zoekOpBAR :: Zoek * Barnummer

	
ENDPATTERN

INTERFACE Overview : I[ONE]
	BOX [ 	Advocaten				: V[ONE*Advocaat],
			Kantoren				: V[ONE*Kantoor]
		]
		
INTERFACE Zoeken : I[ONE]
	BOX [	"Zoek op BAR-nummer"	: V[ONE*Zoek],
			"Zoek op naam"			: V[ONE*Zoek]
		]

INTERFACE "Zoek op BAR-nummer" : I[Zoek]
	BOX [ "BAR-nummer"				: zoekOpBAR
			BOX [ "BAR-nummer"			: barnummer
				BOX [
					"BAR-nummer"			: barnummer~,
					Advocaat 				: isAdvocaat~,
					Inschrijvingsverzoek	: isAdvocaat~;isVerstuurdDoor~,
					Beëdiging				: isBeëdigd,
					Kantoor					: houdtKantoor
					]
				]
		]		
		
INTERFACE "Geavanceerd zoeken" : I[Zoek]
	BOX [	"Gezochte naam"			: zoekOpNaam
			BOX	[
				"BAR-nummer"			: barnummer~,
				Advocaat 				: isAdvocaat~,
				Inschrijvingsverzoek	: isAdvocaat~;isVerstuurdDoor~,
				Beëdiging				: isBeëdigd,
				Kantoor					: houdtKantoor
				]
		]


INTERFACE Advocaten : I[Advocaat]
	BOX [	"BAR-Nummer"			: barnummer~,
			Advocaat 				: isAdvocaat~,
			Inschrijvingsverzoek	: isAdvocaat~;isVerstuurdDoor~,
			Beëdiging				: isBeëdigd,
			Kantoor					: houdtKantoor
		]
			
INTERFACE Kantoren : I[Kantoor]
	BOX	[	Kantooradres			: kantooradres,
			Rechtbank				: kantoor,
			Advocaten				: houdtKantoor~
		]
		
INTERFACE Rechtbanken : I[Rechtbank]
	BOX [	Kantoren				: kantoor~,
			Beëdigingen				: wasBetrokkenBij,
			Inschrijvingsverzoeken	: isGerichtAan~
				BOX [	"Indiener van" 					: isVerstuurdDoor
							BOX [	Inschrijvingsverzoek				: isVerstuurdDoor~],
						"Verwijst naar beëdiging"		: verzoekTotBeëdiging
						
					]
		]
	
INTERFACE Beëdigingen : I[Beëdiging]
	BOX [	Beëdigde				: isBeëdigd~,
			Datum					: datum,
			Locatie					: locatie,
			Rechtbank				: wasBetrokkenBij~
		]
			
INTERFACE Inschrijvingsverzoeken : I["Verzoek van inschrijving"]
	BOX	[	"Ingediend door"		: isVerstuurdDoor
			BOX [ "Wil kantoor houden bij"				: isAdvocaat;houdtKantoor
				],
			"Ingediend bij"			: isGerichtAan
		]

ENDCONTEXT