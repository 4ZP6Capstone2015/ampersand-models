CONTEXT DemoZittinggegevens

INCLUDE "Processen.adl"

-----------------
-- Inlogproces --
-----------------

sessionUsername :: SESSION * Rechternaam [UNI]
sessionPassword :: SESSION * Password [UNI]


  
password :: Rechter * Password =
  [ ("Rechter 10", "de")
  ; ("Rechter 9", "vv")
  ]

PROCESS Login

RULE login: '_SESSION'[SESSION];sessionUsername;naamrechter~;password = '_SESSION';sessionPassword
MESSAGE "Incorrect password"
-- this rule guarantees that any user in sessionUsername will have entered a correct password
ENDPROCESS


----------------
-- Interfaces --
----------------

INTERFACE "Login"(sessionUsername, sessionPassword) FOR Rechter 
                            : V[ONE*SESSION];'_SESSION'[SESSION]
BOX [ "Username"            : sessionUsername
    , "Password"            : sessionPassword
    , "Currently logged in" : sessionUsername;naamrechter~;naamrechter
    ]
	
INTERFACE "Mijn pagina" (verhinderdatumrechter) FOR Rechter 
                                : I[ONE]
BOX [ Zittingrooster            : V;'_SESSION';sessionUsername;naamrechter~;rechter;zittinghouden
    BOX [ Zaaknummer            : dient~
        , Zaal                  : zittingszaal
        , Kamer                 : zittinghouden~
        BOX [ "Rechter(s)"      : rechter~
            ]
        , Griffier              : griffier 
        , "Geagendeerd op"      : geagendeerd 
        , Tijdstip              : begintijd
		, pv                    : procesverbaal
        ]
	, "Persoonlijke informatie" : V; '_SESSION';sessionUsername;naamrechter~
	BOX [Vakantiedagen          : verhinderdatumrechter
        ]
	]

INTERFACE Zittingen FOR Griffie : I[ONE]
BOX [ "Af te ronden zittingen" : V[ONE*Zitting] /\ -(V[ONE*Zitting];procesverbaal;procesverbaal~)
    ] 	


INTERFACE "Zitting plannen" (dient, zittingszaal, zittinghouden, griffier, rechter, sector, rechtbank[Kamerid*Orgaan], geagendeerd, begintijd) FOR Griffie
                       : I[Zitting]
BOX [ Zaaknummer       : dient~
    , Zaal             : zittingszaal
	, Kamer            : zittinghouden~
    BOX [ "Rechter(s)" : rechter~
	    , Sector       : sector
        , Rechtbank    : rechtbank
	    ]
    , Griffier         : griffier 
    , "Geagendeerd op" : geagendeerd 
    , Tijdstip         : begintijd
    ]

INTERFACE "Uitnodiging versturen"
                       : I[Zitting]
BOX [ Genodigde        : dient~;aanhangig~;eiser
    , Zaaknummer       : dient~
    , Zaal             : zittingszaal
	, Kamer            : zittinghouden~
	BOX [ Rechtbank    : rechtbank
        ]
    , "Geagendeerd op" : geagendeerd 
    , Tijdstip         : begintijd
    ]
	
INTERFACE "Zitting afronden" (procesverbaal) FOR Griffie
                       : I[Zitting]
BOX [ Zaaknummer       : dient~
    , Zaal             : zittingszaal
	, Kamer            : zittinghouden~
    BOX [ "Rechter(s)" : rechter~
	    ]
    , Griffier         : griffier 
    , "Geagendeerd op" : geagendeerd 
    , Tijdstip         : begintijd
	, "Proces-verbaal" : procesverbaal
    ]

ENDCONTEXT