CONTEXT DemoZittinggegevens

INCLUDE "INDOORS.adl"
INCLUDE "DemoZittinggegevens.pop"

----------
-- Keys --
----------
--KEY zaak: Zaakid(zaakid)
KEY mondelingebehandeling : "Mondelinge behandeling"(TXT "De behandeling van ", behandeld~;zaakid)
KEY zitting : Zitting(TXT "Zitting van: ", houdtzitting~;naambezetting, TXT ", Op datum: ", geagendeerd, TXT ", Van: ", begintijd, TXT " tot: ", eindtijd)
--KEY bezetting : Bezetting(naambezetting)


----------------------
-- Demo invarianten --
----------------------
RULE minimalevereistenzitting : I[Zitting] |- begintijd;begintijd~ /\ eindtijd;eindtijd~ /\ geagendeerd;geagendeerd~ /\ houdtzitting~;naambezetting;naambezetting~;houdtzitting 
MESSAGE "Er missen essentiele zittinggegevens. Controleer de naam van de bezetting, aanvangstijdstip, eindtijd en datum van de zitting."

RULE procesverbaal : procesverbaal |- behandeld~;zaaksdocument~
MESSAGE "Het proces-verbaal refereert naar het dossier van een andere zaak."

RULE kenmerkpv : I[Document] |- kenmerk;kenmerk~
MESSAGE "Het proces-verbaal moet een kenmerk hebben."
VIOLATION (TXT "Huidige document")

RULE behandelingsluiten : I["Mondelinge behandeling"];behandeld~;zaaksdocument~;type;'Proces-verbaal';type~;zaaksdocument;behandeld |- behandelinggesloten;behandelinggesloten~
MEANING "Een behandeling waarvan het proces-verbaal is opgemaakt, moet zijn gesloten."
MESSAGE "Het proces-verbaal moet de sluitingstijd van de behandeling bevatten."

-----------------------
-- Demo procesregels --
-----------------------

PROCESS "Zitting plannen"

ROLE "Administratief medewerker" MAINTAINS zittingszaal, griffier, completebezetting, consistentiegriffier
ROLE "Juridisch medewerker" MAINTAINS beslissingenpv, griffiertekentpv, consistentiegriffier

RULE zittingszaal : I[Zitting] |- zittingszaal;zittingszaal~
MESSAGE "Een zittingszaal ontbreekt voor de volgende zitting(en)."
VIOLATION (TXT "Zitting van: ", TGT houdtzitting~, TXT ", Op datum: ", TGT geagendeerd, TXT ", Van: ", TGT begintijd, TXT " tot: ", TGT eindtijd)

RULE griffier : I[Zitting] |- griffier;griffier~
MESSAGE "Er is nog geen griffier toegekend aan de zitting. (Art. 88 lid 3 Rv)"
VIOLATION (TXT "Zitting van: ", TGT houdtzitting~, TXT ", Op datum: ", TGT geagendeerd, TXT ", Van: ", TGT begintijd, TXT " tot: ", TGT eindtijd)

RULE completebezetting : I[Bezetting] |- samenstelling;samenstelling~
MESSAGE "De samenstelling van de bezetting is incompleet."
VIOLATION (TGT houdtzitting)

RULE beslissingenpv : I[Document] |- beslissing;beslissing~
MESSAGE "Het proces-verbaal bevat geen belissingen."
VIOLATION (TXT "Een proces-verbaal in: ", TGT zaaksdocument)

RULE griffiertekentpv : zaaksdocument;zaaksdocument~;type;'Proces-verbaal';type~;zaaksdocument;zaaksdocument~ |- griffiertekent~;griffiertekent
MEANING "Een behandeling waarvan het proces-verbaal is opgemaakt, moet zijn gesloten."
MESSAGE "De griffier dient de volgende processen-verbaal te ondertekenen. (Art. 88 lid 3 Rv)"

RULE consistentiegriffier : griffiertekent;zaaksdocument |- griffier~;behandeling;behandeld~
MESSAGE "De griffier ter zitting is niet de griffier die het proces-verbaal ondertekent heeft."
VIOLATION (TXT "Griffier ter zitting: ", TGT behandeld;behandeling~;griffier, TXT ". Het proces-verbaal is ondertekend door: ", TGT zaaksdocument~;griffiertekent~)

ENDPROCESS

-----------------
-- Inlogproces --
-----------------

sessionGebruikersnaam :: SESSION * Rechternaam [UNI]
sessionWachtwoord :: SESSION * Wachtwoord [UNI]


wachtwoord :: Rechter * Wachtwoord =
  [ ("Rechter 10", "de")
  ; ("Rechter 9", "vv")
  ]

PROCESS Login

RULE login: '_SESSION'[SESSION];sessionGebruikersnaam;naamrechter~;wachtwoord = '_SESSION';sessionWachtwoord
MESSAGE "Ongeldig wachtwoord"
-- this rule guarantees that any user in sessionGebruikersnaam will have entered a correct password
ENDPROCESS

--------------------------------------
-- Items voor in de zwarte menubalk --
--------------------------------------

INTERFACE "Login"(sessionGebruikersnaam, sessionWachtwoord) FOR Rechter 
                        : V[ONE*SESSION];'_SESSION'[SESSION]
BOX [ "Gebruikersnaam"  : sessionGebruikersnaam
    , "Wachtwoord"      : sessionWachtwoord
    , "Nu ingelogd als" : sessionGebruikersnaam;naamrechter~;naamrechter
    ]

INTERFACE "Mijn pagina" (verhinderdatumrechter, rechtertekent) FOR Rechter 
                                : I[ONE]
BOX [ Zittingen                 : V;'_SESSION';sessionGebruikersnaam;naamrechter~;bezetting;houdtzitting
	    BOX [ Zaal              : zittingszaal
            , Zaken             : behandeling
			BOX [ Zaaknummer    : behandeld~
			    ]
			, Bezetting         : houdtzitting~
            BOX [ "Rechter(s)"  : bezetting~
                ]
            , Griffier          : griffier 
            , "Geagendeerd op"  : geagendeerd 
            , Begintijd         : begintijd
            , Eindtijd          : eindtijd
            ]
	, "Persoonlijke informatie" : V; '_SESSION';sessionGebruikersnaam;naamrechter~
	BOX [Vakantiedagen          : verhinderdatumrechter
        ]
    , "Processen-verbaal"       : V;'_SESSION';sessionGebruikersnaam;naamrechter~;bezetting;houdtzitting;behandeling;behandeld~;zaaksdocument~;type;'Proces-verbaal';type~;zaaksdocument;zaaksdocument~
	BOX [ "Ondertekend door rechter" : rechtertekent~
	    ]
	   
	]	

INTERFACE "Proces-verbaal" FOR "Juridisch medewerker" : I[ONE]
BOX [ "Proces verbaal opmaken voor:" : V[ONE*"Mondelinge behandeling"] /\ -(V[ONE*"Mondelinge behandeling"];behandelinggesloten;behandelinggesloten~)
    ] 
	
INTERFACE Zittingen FOR "Administratief medewerker" : I[ONE]
BOX [ "Geplande zittingen"            : V[ONE*Zitting]
      BOX
	  [
	  Zitting                         : I[Zitting]
	  , "Zaken op geplande zitting"   : behandeling;behandeld~
      ]
    , "Af te ronden behandelingen"    : V[ONE*"Mondelinge behandeling"];behandelinggesloten;behandelinggesloten~ /\ -(V[ONE*"Mondelinge behandeling"];procesverbaal;procesverbaal~)
    ] 	
	
----------------
-- Interfaces --
----------------

INTERFACE "Zitting plannen" (zittingszaal, behandeling, behandeld, houdtzitting, bezetting, naambezetting, samenstelling, bezetting, griffier, geagendeerd, begintijd, eindtijd) FOR "Administratief medewerker" 
                            : I[Zitting]
BOX [ Zaal                  : zittingszaal
    , Zaken                 : behandeling
	BOX [ "Behandeling van" : behandeld~
	    ]
    , Bezetting             : houdtzitting~
    BOX [ "Naam bezetting"  : naambezetting
        , Samenstelling     : samenstelling
        , "Rechter(s)"      : bezetting~
        ]
    , Griffier              : griffier
    , Datum                 : geagendeerd
    , Aanvangstijdstip      : begintijd
    , Eindtijd              : eindtijd	
    ]
	

INTERFACE "Proces-verbaal opmaken" (zaaksdocument, kenmerk, beslissing, griffiertekent, type, behandelinggesloten) FOR "Juridisch medewerker"
                                : I["Mondelinge behandeling"]
BOX [ Zaak                      : behandeld~
      BOX [ Zaaknummer          : behandeld;behandeld~
	      , "Proces-verbaal"    : zaaksdocument~
	      BOX [ Behandeling     : zaaksdocument;behandeld
		      , "Type document" : type
              , Kenmerk         : kenmerk
			  , Beslissingen    : beslissing
			  , Griffier        : griffiertekent~
              ]
          ]
    , "Behandeling gesloten om" : behandelinggesloten		  
    ]					   

INTERFACE "Behandeling afronden" (zaaksdocument, procesverbaal) FOR "Administratief medewerker"
                             : I["Mondelinge behandeling"];(behandelinggesloten;behandelinggesloten~)
BOX [ Zaak                   : behandeld~
      BOX [ Zaaknummer       : behandeld;behandeld~
          , "Proces-verbaal" : zaaksdocument~
	      BOX [ Dossier      : procesverbaal~ 
              , Beslissingen : beslissing
              ]
          ]			  
    ]

ENDCONTEXT