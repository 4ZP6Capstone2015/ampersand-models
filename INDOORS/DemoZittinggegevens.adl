CONTEXT DemoZittinggegevens

INCLUDE "Processen.adl"

-----------------
-- Inlogproces --
-----------------

{- RJ: Het hele inlogproces, inclusief de username password e.d. zou je kunnen vervangen door gebruik te maken van

INCLUDE "..\VOGDemo\Identificatiemiddelen.adl"

en voeg je eigen populaties apart toe (dus NIET in dat adl bestand, maar ergens anders). 
Je moet dan wel iets regelen voor de 'Rechternaam' (moet een Userid worden) 
-}

--Onderstaande relaties zijn ge-renamed om conflicten met COVOG op te lossen.
sessionGebruikersnaam :: SESSION * Rechternaam [UNI]
sessionWachtwoord :: SESSION * Wachtwoord [UNI]


wachtwoord :: Rechter * Wachtwoord =
  [ ("Rechter 10", "de")
  ; ("Rechter 9", "vv")
  ]

PROCESS Login

RULE login: '_SESSION'[SESSION];sessionGebruikersnaam;naamrechter~;wachtwoord = '_SESSION';sessionWachtwoord
MESSAGE "Ongeldig wachtwoord"
-- this rule guarantees that any user in sessionGebruikersnaam will have entered a correct password
ENDPROCESS


----------------
-- Interfaces --
----------------

INTERFACE "[Login]"(sessionGebruikersnaam, sessionWachtwoord) FOR Rechter  -- Renamed to resolve conflict with Login in Identificatiemiddelen.adl
                        : V[ONE*SESSION];'_SESSION'[SESSION]
BOX [ "Gebruikersnaam"  : sessionGebruikersnaam
    , "Wachtwoord"      : sessionWachtwoord
    , "Nu ingelogd als" : sessionGebruikersnaam;naamrechter~;naamrechter
    ]
	
INTERFACE "Mijn pagina" (verhinderdatumrechter) FOR Rechter 
                                : I[ONE]
BOX [ Zaakoverzicht             : V;'_SESSION';sessionGebruikersnaam;naamrechter~;rechter;zittinghouden
    BOX [ Zaaknummer            : behandeling;behandeld~
        , Zaal                  : zittingszaal
        , Kamer                 : zittinghouden~
        BOX [ "Rechter(s)"      : rechter~
            ]
        , Griffier              : griffier 
        , "Geagendeerd op"      : geagendeerd 
        , Tijdstip              : begintijd
		, pv                    : behandeling;procesverbaal
        ]
	, "Persoonlijke informatie" : V; '_SESSION';sessionGebruikersnaam;naamrechter~
	BOX [Vakantiedagen          : verhinderdatumrechter
        ]
	]

INTERFACE Zittingen FOR Griffie : I[ONE]
BOX [ "Af te ronden behandelingen"  : V[ONE*"Mondelinge behandeling"] /\ -(V[ONE*"Mondelinge behandeling"];procesverbaal;procesverbaal~)
    ] 	


INTERFACE "Zitting plannen" (zittingszaal, behandeling, zittinghouden, griffier, rechter, geagendeerd, begintijd) FOR Griffie 
                       : I[Zitting]
BOX [ Zaal             : zittingszaal
    , Zaken            : behandeling
    , Kamer            : zittinghouden~
    BOX [ "Rechter(s)" : rechter~
	    ]
    , Griffier         : griffier
    , Datum            : geagendeerd
    , Aanvangstijdstip : begintijd
    ]
	
	
INTERFACE "Uitnodiging versturen"
                       : I[Zitting]
BOX [ Genodigde        : behandeling;behandeld~;aanhangig~;eiser
    , Zaaknummer       : behandeling;behandeld~
    , Zaal             : zittingszaal
	, Kamer            : zittinghouden~
	BOX [ "Rechter(s)" : rechter~
        ]
    , Datum            : geagendeerd 
    , Aanvangstijd     : begintijd
    ]
	
INTERFACE "Behandeling afronden" (procesverbaal) FOR Griffie
                       : I["Mondelinge behandeling"]
BOX [ Zaaknummer       : behandeld~
	, "Proces-verbaal" : procesverbaal
    ]

ENDCONTEXT