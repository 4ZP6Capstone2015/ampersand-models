CONTEXT Overzichten

INCLUDE "Diensten.adl"
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Producten.adl"
INCLUDE "wettelijkeTaken.adl"

PATTERN Diensten
   RELATION naam[Dienst*DienstNaam] [UNI,TOT] PRAGMA "De naam van dienst " " is '" "'."
   RELATION omschrijving[Dienst*Tekst] [UNI] 
   RELATION sub[Dienst*Dienst] [UNI]
   RELATION naamSub[Dienst*DienstNaam][UNI]
ENDPATTERN
PATTERN Gebeurtenissen
   RELATION naam[Gebeurtenis*GebeurtenisNaam] [UNI,TOT] PRAGMA "De naam van gebeurtenis " " is '" "'."
   RELATION toelichting[Gebeurtenis*Tekst] [UNI] 
ENDPATTERN
PATTERN "Wettelijke Taken"
   RELATION naam[WettelijkeTaak*TaakNaam] [UNI,TOT] PRAGMA "De naam van wettelijke taak " " is '" "'."
ENDPATTERN
INTERFACE Diensten : V[ONE*Dienst]
  BOX [ Dienst : I
      , naam   : naam
      ]
INTERFACE Gebeurtenissen : V[ONE*Gebeurtenis]
  BOX [ Gebeurtenis : I
      , naam   : naam
      ]
INTERFACE WettelijkeTaken : V[ONE*WettelijkeTaak]
  BOX [ "Wettelijke Taak" : I
      , naam   : naam
      ]

PROCESS DienstenBeheer

  ROLE overzichtGebruiker MAINTAINS dummyRule
  PURPOSE RULE dummyRule {+ Workaround om een overzichts-gebruiker te hebben.-}
  RULE dummyRule : I[Dienst] |- I[Dienst] 
  

  ROLE dienstBeheer MAINTAINS omschrijfDienst
  RULE omschrijfDienst : I[Dienst] |- omschrijving;omschrijving~
  MEANING "Elke dienst is beschreven"
  MESSAGE "Elke dienst dient een omschrijving te hebben."
  VIOLATION (TXT "Dienst ", SRC naam, TXT " bevat geen omschrijving.") 

ENDPROCESS


INTERFACE "Beheren Dienstgegevens" (naam[Dienst*DienstNaam], sub[Dienst*Dienst]) FOR dienstBeheer : I[Dienst]
  BOX [ Dienst : naam
      , "omschrijving" : omschrijving
      , sub: sub 
        BOX [ naam : naam
            ]
        
      ]

PROCESS ScriptBeheer
  ROLE ADLModelleur MAINTAINS subNaamOvereenstemming
  RULE subNaamOvereenstemming : naamSub[Dienst*DienstNaam] |- sub;naam
  MEANING "De subnaam komt overeen met de naam van de sub"
  MESSAGE "De subnaam komt niet overeen met de gekozen code voor de Dienst"

  ROLE ADLModelleur MAINTAINS naamProducentOvereenstemming
  RULE naamProducentOvereenstemming : naamProducentbusinessfunctie |- producentbusinessfunctie;naam
  MEANING "De producentnaam komt overeen met de naam van de producent"
  MESSAGE "De producentnaam komt niet overeen met de gekozen code voor de Dienst"

  ROLE ADLModelleur MAINTAINS dienstNaamConsumentOvereenstemming
  RULE dienstNaamConsumentOvereenstemming : dienstNaamConsumentbusinessfunctie |- cons;naam
  MEANING "De naam van de consumentenactiviteit komt overeen met de naam van de dienst"
  MESSAGE "De consumentenactiviteitnaam kom niet overeen met de gekozen code voor de Dienst"

  ROLE ADLModelleur MAINTAINS wTaakNaamOvereenstemming
  RULE wTaakNaamOvereenstemming : naamSub[WettelijkeTaak*TaakNaam] |- sub;naam
  MEANING "De subnaam komt overeen met de naam van de dienst"
  MESSAGE "De consumentenactiviteitnaam kom niet overeen met de gekozen code voor de Dienst"
ENDPROCESS

INTERFACE dienst1 (naamSub[Dienst*DienstNaam],sub[Dienst*Dienst]) FOR ADLModelleur : I[Dienst]
  BOX [ Dienst : I
      , naam: naam
      , naamSub : naamSub
      , sub: sub 
        BOX [ id : I
            , naam : naam
            ]
      , childs: sub~
        BOX [ id : I
            , naam : naam
            ]
      ]

INTERFACE gebeurtenis (naamProducentbusinessfunctie, producentbusinessfunctie)
  FOR ADLModelleur: I[Gebeurtenis]
  BOX [ Gebeurtenis : I
      , Naam: naam
      , Rol : rol
      , "naam business functie": naamProducentbusinessfunctie
      , "business functie": producentbusinessfunctie
         BOX [ id : I
             , naam : naam
             ]
      ]
INTERFACE wettelijkeTaak1 (naamProducentbusinessfunctie, producentbusinessfunctie)
  FOR ADLModelleur: I[WettelijkeTaak]
  BOX [ "wettelijke taak" : I
      , Naam: naam
      , naamSub : naamSub
      , sub: sub 
        BOX [ id : I
            , naam : naam
            ]
      , childs: sub~
        BOX [ id : I
            , naam : naam
            ]
      ]
INTERFACE dienst FOR overzichtGebruiker : I[Dienst]
  BOX [ "dienst"           : naam
      , "onderdeel van"    : sub
        BOX [ "" : naam
            ]
      , "onderverdeeld in" : sub~
        BOX [ "" : naam
            ]
      , "omschrijving"     : omschrijving
      , "grondslag"        : grondslag
      ]
INTERFACE wettelijkeTaak FOR overzichtGebruiker : I[WettelijkeTaak]
  BOX [ "wettelijke taak"  : naam
      , "onderdeel van"    : sub
        BOX [ "" : naam
            ]
      , "onderverdeeld in" : sub~
        BOX [ "" : naam
            ]
      , "omschrijving"     : omschrijving
      , "grondslag"        : grondslag
      , "bron"             : bron
      ]

ENDCONTEXT