CONTEXT VIRO IN DUTCH
{- SJ, 30 augustus 2010 -}

PURPOSE PATTERN A0003 IN DUTCH REF "UtI VIRO"
{+Een zaak kan pas worden afgesloten nadat de gegevens van de uitspraak zijn geregistreerd en een afschrift van de uitspraak is verzonden.
Het verzenden van een kopie van de uitspraak aan partijen is een wettelijk vereiste. 

Het afsluiten van een zaak is een belangrijk registratiemoment,
omdat de zaak hiermee uitstroomt en de financiering daarmee samenhangt.-}

PATTERN A0003 --  Registratie van uitspraken

  deel :: Tekst * Tekst PRAGMA "" " maakt deel uit van ".
  tekst :: Uitspraak -> Tekst PRAGMA "" " heeft " " als tekst".
  dictum :: Uitspraak -> Tekst PRAGMA "" " heeft " " als dictum".
  overweging :: Uitspraak * Tekst PRAGMA "" " heeft " " als overweging".
  mededeling :: Uitspraak * Tekst PRAGMA "" " heeft " " als mededeling".
  grosse :: Bericht * Uitspraak PRAGMA "" " is grosse van ".
  doc :: Bericht -> Tekst PRAGMA "" " luidt ".
  
  RULE "dictum": dictum;deel |- tekst
  RULE "overweging": overweging;deel |- tekst
  RULE "grosse": tekst = grosse~;doc
  
PURPOSE RELATION dictum IN DUTCH REF "UtI VIRO"
{+Het dictum is de beslissing die de rechtbank heeft genomen.-}

PURPOSE RULE "grosse" IN DUTCH REF "UtI VIRO"
{+De grosse is een afschrift van de uitspraak ten behoeve van een betrokken partij, een deskundige, of andere belanghebbende.-}
PURPOSE RULE "overweging" IN DUTCH REF "UtI VIRO"
{+Alle overwegingen van de rechtbank dienen in de uitspraak te worden vermeld.-}
PURPOSE RULE "dictum" IN DUTCH REF "UtI VIRO"
{+Het Dictum staat altijd onderaan een uitspraak,
na de Overwegingen en andere mededelingen zoals het Procesverloop.-}

ENDPATTERN

ENDCONTEXT