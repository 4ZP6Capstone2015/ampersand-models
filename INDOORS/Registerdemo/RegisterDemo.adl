CONTEXT RegisterDemo

--INCLUDE "INDOORS.adl"

INCLUDE "advocatentableau.adl"
INCLUDE "persoonsgegevens.adl"
INCLUDE "gebeurtenis.adl"
INCLUDE "zittinggegevens.adl"


INTERFACE Advocatentableau : I[ONE]
BOX [ Advocaten            : V[ONE*Barnummer];barnummer
    , Kantoren             : V[ONE*Kantoornummer];kantoornummer
    ]

	
INTERFACE "Advocaat zoeken" : I[ONE]
BOX [ ""                    : V[ONE*Zoek]
    ]

	
INTERFACE " op BAR-nummer" (zoekOpBAR)         
                                   : I[Zoek]
BOX [ "Record voor"                : zoekOpBAR
    BOX [ "BAR-nummer"             : barnummer;barnummer~
	    , ""                       : barnummer;barnummer~
	    BOX [ Naam                 : barnummer
            , Geboortedatum        : barnummer;bsn;geboortedatum
            , Inschrijvingsverzoek : barnummer;isVerstuurdDoor~
            BOX [ "Gericht aan"    : isGerichtAan
		        , Kantoor          : kantoorVerzoek
                ]
                , Beëdiging        : barnummer;isBeëdigd~
            BOX [ Rechtbank        : locatieRechtbank
	            , Beëdigingsdatum  : datum
                ]
            , Kantoor              : houdtkantoor
            ]
        ]
    ]


INTERFACE " op naam" (zoekOpNaam)
                                   : I[Zoek]
BOX [ "Record voor"                : zoekOpNaam
    BOX	[ "BAR-nummer"             : barnummer~    
        , ""                       : barnummer~
        BOX [ Naam                 : barnummer
            , Geboortedatum        : barnummer;bsn;geboortedatum
            , Inschrijvingsverzoek : barnummer;isVerstuurdDoor~
            BOX [ "Gericht aan"    : isGerichtAan
		        , Kantoor          : kantoorVerzoek
                ]
            , Beëdiging            : barnummer;isBeëdigd~
            BOX [ Rechtbank        : locatieRechtbank
	            , Beëdigingsdatum  : datum
                ]
            , Kantoor              : houdtkantoor
            ]
        ]
    ]

	
INTERFACE "Advocaat toevoegen" (barnummer, houdtkantoor)
                               : I[Barnummer]
BOX [ Naam                     : barnummer
    BOX [ ""                   : I[Persoon]
        , Inschrijvingsverzoek : isVerstuurdDoor~
        BOX [ "Gericht aan"    : isGerichtAan
		    , Kantoor          : kantoorVerzoek
            ]
        , Beëdiging            : isBeëdigd~
        BOX [ Rechtbank        : locatieRechtbank
	        , Beëdigingsdatum  : datum
            ]
        ]
    , "Houdt kantoor bij"      : houdtkantoor
    ]
	
	
INTERFACE Advocaat (houdtkantoor)          
                            : I[Persoon]
BOX [ "BAR-Nummer"          : barnummer~
    , Naam                  : barnummer~;barnummer
    , Geboortedatum         : bsn;geboortedatum
    , Inschrijvingsverzoek	: isVerstuurdDoor~
    BOX [ "Gericht aan"     : isGerichtAan
        , "Verstuurd door"  : isVerstuurdDoor
		, Kantoor           : kantoorVerzoek
        ]
    , Beëdiging             : isBeëdigd~
    BOX [ Rechtbank         : locatieRechtbank
	    , Beëdigingsdatum   : datum
        ]
    , Kantoorinfomatie      : barnummer~
	BOX[ Kantoor            : houdtkantoor
       , "Gevestigd te"     : houdtkantoor;kantooradres
       ]
    ]		

	
INTERFACE Kantoor (kantooradres, kantoor)   
				   : I[Kantoor]
BOX[ Kantoor       : kantoornummer~;kantoornummer
   , Kantooradres  : kantooradres
   , Rechtbank     : kantoor
   , Advocaten     : houdtkantoor~;barnummer
   ]

	
INTERFACE "Overzicht per rechtbank" 
                             : I[Rechtbank]
BOX [ Rechtbank              : kantoor~;kantoor
    , Kantoren               : kantoor~
    , Beëdigingen            : locatieRechtbank~
	BOX [ Beëdigde           : isBeëdigd
        , Beëdigingsdatum    : datum
        ]
    , Inschrijvingsverzoeken : isGerichtAan~
    BOX [ "Ingediend door"   : isVerstuurdDoor
        , "Gericht aan"      : kantoorVerzoek
        ]
    ]

	
INTERFACE Beëdiging (isBeëdigd, behoortBij, datum, locatieRechtbank, locatie)			
                           : I[Beëdiging]
BOX [ Beëdigde             : isBeëdigd
    , Inschrijvingsverzoek : behoortBij~
	BOX [ Kantoor          : kantoorVerzoek
	    ]
    , Datum                : datum
	, Rechtbank            : locatieRechtbank
    , Locatie              : locatie
    ]

	
INTERFACE Inschrijvingsverzoek (isVerstuurdDoor, isGerichtAan, kantoorVerzoek)
                                : I["Verzoek van inschrijving"]
BOX	[ "Ingediend door"          : isVerstuurdDoor
    , "Ingediend bij"           : isGerichtAan
    , "Gaat kantoor houden bij" : kantoorVerzoek
    ]

ENDCONTEXT