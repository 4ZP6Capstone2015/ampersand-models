CONTEXT Managementinformatie LATEX
	META "authors" "Sebastiaan Hobers, Roger Hage, Lode Cornelissen POC Ampersand"
	
	
	
INTERFACE "Overzicht" :I[ONE] {- -}
BOX [ 	"Aanvraag" : V[ONE*Aanvraag]
		--,"De beoordeling" : resulteertIn
	]
	
	
INTERFACE "Overzicht aanvragen" : I[Aanvraag]
BOX [ 	"Aanvraag" : I
		,"Beoordeling" : resulteertIn
		,"Product" : heeftBetrekkingOp
		,"Taak": hoortBij~
]	

INTERFACE "Overzicht taken" : I[Taak]
BOX [ 	"Taak" : I
		,"Taakstatus" : heeftEen
		,"Team" : toegewezenAan
		,"Medewerker": behandeld~
]	

INTERFACE "Overzicht team" : I[Team]
BOX [ 	"Team" : I
		,"Medewerker" : ingedeeldIn~
]	
	
--ROLE Teamleider MAINTAINS "xxxt"	

PURPOSE PATTERN Managementinformatie
	{+
	Dit hoofdstuk beschrijft de taal zoals die wordt gebruikt om de managementinformatie omtrent processen te tonen aan de gebruiker.
	Waar mogelijk verwijst de tekst naar de bron, zodat de lezer afwijkingen die hij of zij constateert kan traceren.
	Daar waar de tekst afwijkt van de bron is die bron uiteraard meer gezaghebbend dan deze tekst.
	
	Functionele eisen zullen worden opgesteld in de hier gedefinieerde taal.
	-}
	
	

PATTERN Managementinformatie

	PURPOSE CONCEPT Medewerker REF "Personeelsgids, begrippenlijst"
   	{+
   		Wanneer sprake is van uitval naar aaleiding van een beoordeling, ontstaat er een taak waar iemand naar moet gaan kijken. 
   		Om deze reden wordt het concept "Medewerker" gebruikt.     
   	-}
   	CONCEPT Medewerker "Degene (m/v) die een fulltime of parttime, vaste of tijdelijke arbeidsovereenkomst heeft met ASR Nederland N.V., en toegang heeft tot de medewerkerapplicatie adidas." "Personeelsgids, begrippenlijst"
   	
	PURPOSE CONCEPT Aanvraag REF "Ergens"
   	{+
   		Te doen.
   		Algemene tekst bedenken over een aanvraag. Daarna voor een verhaal voor de specifieke aanvragen
   		Het is mijn insziens (SH) niet nodig om elke aanvraag apart te benoemen. 
   		
   		Er worden vier soorten aanvragen onderkend, een offerteaanvraag, een contractaanvraag, een mutatieaanvraag en een claimaanvraag.
   	-}
   	CONCEPT Aanvraag "Het verzoek tot behandeling" "xxxxx"
   	
   	PURPOSE CONCEPT OfferteAanvraag REF "LSD DAVPAC QuotationApplication, LSD INC QuotationApplication"
   	{+
   		   
   	-}
   	CONCEPT OfferteAanvraag "Het verzoek tot een offerte van een AOV en/of een ziektekosten product(en)" "LSD DAVPAC QuotationApplication, LSD INC QuotationApplication"
   
   	PURPOSE CONCEPT ContractAanvraag REF "LSD DAVPAC ContractApplication, LSD INC ContractApplication"
   	{+
   		  
   	-}
   	CONCEPT ContractAanvraag "Het verzoek tot het afsluiten van een AOV en/of een ziektekostencontract" "LSD DAVPAC ContractApplication, LSD INC ContractApplication"
   
   	PURPOSE CONCEPT MutatieAanvraag REF "LSD DAVPAC Endorsment"
   	{+
   		
   	-}
   	CONCEPT MutatieAanvraag "Het verzoek tot een administratieve wijziging" "LSD DAVPAC Endorsment"
   
    PURPOSE CONCEPT ClaimAanvraag REF "LSD INC ClaimApplication"
   	{+
   		   
   	-}
   	CONCEPT ClaimAanvraag "Het verzoek tot het registeren van een claim in de claimadministratie" "LSD INC ClaimApplication"
   	
   
   	PURPOSE CONCEPT Beoordeling REF "Ergens"
   	{+
   		Een aanvraag die binnenkomt moet uiteindelijk beoordeeld worden.
   		Het kan zijn dat een aanvraag standaard geaccepteerd wordt.
   		Het kan echter ook zijn dat beoordeling door een extern systeem plaatsvindt.
   		Het resultaat van een beoordeling kan zijn dat de aanvraag naar een medeweker moet om het risico te analyseren.
   		Een ander resultaat kan zijn dat de medewerker zelf een aanvraag heeft aangemaakt en deze verder wil behandelen vanuit de medewerkerapplicatie
   		Om deze informatie te registeren over te informeren is het concept "beoordeling" gebruikt.   
   	-}
   	CONCEPT Beoordeling "Nog iets voor bedenken wat zinvol is" "xxxxx"	
   
	PURPOSE CONCEPT Product REF "Marketoffering"
   	{+
   		De aanvragen die het NGA systeem binnen komen hebben allen betrekking op een product.
   		De enige uitzondering hierop is een claimaanvraag. In deze aanvraag is geen productinformatie bekend.
   		Om deze reden gebruiken we het concept "Product".
   		
   		Een product is commerciele product 
   		Denk hierbij aan een verzuim of werknemer WIA product    
   	-}
   	CONCEPT Product "Het commerciele product dat aangeboden wordt aan de klant" "Marketoffering"	
   
	PURPOSE CONCEPT Taak REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		Een taak is datgene dat een medewerker in zijn inbox krijgt.
   		De taak verschijnt alleen als er een medewerker naar moet kijken.    
   	-}
   	CONCEPT Taak "De uitgevallen taak naar aanleiding van een beoordeelde aanvraag" "xxxxx"

	PURPOSE CONCEPT Taakstatus REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		Een Taak heeft in het systeem een status, zoals bijvoorbeeld de status "open". 
   		Om deze reden gebruiken we het concept "Taakstatus".
   	-}
   	CONCEPT Taakstatus "De mogelijke status" "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	   	
   	PURPOSE CONCEPT Team REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		Een team is een clustering van medewerkers die dezelfde werkzaamheden binnen de organisatie vervullen.  
   		Binnen een team is nog onderscheid tussen junior,medior,senior. Dit is (nog) niet in scope voor dit ontwerp.   
   	-}
   	CONCEPT Team "Medewerkers met dezelfde verantwoordelijkheden" "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	
   	
   	   	
   	
	-----------------------------------------------------------
	--		De relatie isAContract
	-----------------------------------------------------------
	PURPOSE RELATION isAContract[ContractAanvraag*Aanvraag] REF "LSD DAVPAC ContractApplication, LSD INC ContractApplication"
	{+
		Nu alle definities helder zijn, kunnen de afspraken die gemaakt zijn worden beschreven.
		
		Een contractaanvraag is een vorm van een aanvraag.
	-}
	RELATION isAContract[ContractAanvraag*Aanvraag][TOT]
	PRAGMA "" " is een "
	MEANING "De uitspraak ``Contractaanvraag $c$ is een aanvraag $a$.'' behoort tot de gemeenschappelijke taal."

	-----------------------------------------------------------
	--		De relatie resulteertIn
	-----------------------------------------------------------
	PURPOSE RELATION resulteertIn[Aanvraag*Beoordeling] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Een aanvraag kan hoogstens een beoordeling hebben.
	-}
	RELATION resulteertIn[Aanvraag*Beoordeling]  [UNI]
	PRAGMA "" " reslulteert in "
	MEANING "De uitspraak ``Aanvraag $a$ resulteert in beoordeling $b$.'' behoort tot de gemeenschappelijke taal."

	-----------------------------------------------------------
	--		De relatie heeftBetrekkingOp
	-----------------------------------------------------------
	PURPOSE RELATION heeftBetrekkingOp[Aanvraag*Product] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Een aanvraag heeft betrekking op ten minste 1 product
		

		Er is 1 specifieke aanvraag. Die kan behoren bij 1 of meerdere producten.
	-}
	RELATION heeftBetrekkingOp[Aanvraag*Product]  [UNI,TOT,SUR]
	PRAGMA "" " heeft betrekking op "
	MEANING "De uitspraak ``Aanvraag $a$ heeft betrekking op product $p$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie hoortBij
	-----------------------------------------------------------
	PURPOSE RELATION hoortBij[Taak*Aanvraag] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Een taak hoort bij een aanvraag.
		
		Er is 1 taak die behoort bij 1 aanvraag. Een aanvraag hoeft niet te lijden tot een taak
	-}
	RELATION hoortBij[Taak*Aanvraag] [UNI,TOT,INJ] 
	PRAGMA "" " hoort bij "
	MEANING "De uitspraak ``Taak $t$ hoort bij aanvraag $a$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie heeftEen
	-----------------------------------------------------------
	PURPOSE RELATION heeftEen[Taak*Taakstatus] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+
		Een taak heeft altijd een status
		
		Een taak heeft altijd een status. Hierover is discussie
	-}
	RELATION heeftEen[Taak*Taakstatus]  
	PRAGMA "" " heeft een "
	MEANING "De uitspraak ``Taak $t$ heeft status $s$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie toegewezenAan
	-----------------------------------------------------------
	PURPOSE RELATION toegewezenAan[Taak*Team] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+
		Een taak is altijd toegewezen aan een team
		
	-}
	RELATION toegewezenAan[Taak*Team]  [UNI,TOT]
	PRAGMA "" " is toegewezen aan "
	MEANING "De uitspraak ``Taak $t$ is toegewezen aan team $x$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie behandeld
	-----------------------------------------------------------
	PURPOSE RELATION behandeld[Medewerker*Taak] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+
		Een taak wordt door een medewerker behandeld.
		Het kan zijn dat een medewerker geen taak heeft behandeld.
		TODO kardinaliteit
	-}
	RELATION behandeld[Medewerker*Taak]  [UNI]
	PRAGMA "" " behandeld "
	MEANING "De uitspraak ``Medewerker $m$ behandeld taak $t$.'' behoort tot de gemeenschappelijke taal."
		
	-----------------------------------------------------------
	--		De relatie ingedeeldInø
	-----------------------------------------------------------
	PURPOSE RELATION ingedeeldIn[Medewerker*Team] REF "xxx"
	{+
		Een medewerker is ingedeeld in een team
		TODO kardinaliteit
	-}
	RELATION ingedeeldIn[Medewerker*Team]  [UNI,TOT]
	PRAGMA "" " is ingedeeld in "
	MEANING "De uitspraak ``Medewerker $m$ is ingedeeld in team $t$.'' behoort tot de gemeenschappelijke taal."
		

	-----------------------------------------------------------
	--		De regel Aanvraag dient beoordeelt te worden
	-----------------------------------------------------------	
	PURPOSE RULE "Aanvraag dient beoordeelt te worden" REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Elke aanvraag die binnenkomt zal beoordeelt moeten worden. 
		Sommige aanvragen kunnen meteen als beoordeling geaccepteerd krijgen. Denk hierbij aan het toegeven van een deelnemer op een contract.
		Andere aanvragen dienen beoordeelt te worden door een extern systeem of uiteindelijk door een medewerker alvorens ze geaccepteerd worden.
	-}
	RULE "Aanvraag dient beoordeelt te worden" : I |- resulteertIn;resulteertIn~
	MEANING "Als een aanvraag wordt ingediend, dan mag deze aanvraag niet onbeoordeeld blijven."
	MESSAGE "De aanvraag heeft nog geen beoordeling."
	VIOLATION (TXT "Aanvraag ", SRC I, TXT " dient beoordeelt te worden")




ENDPATTERN

POPULATION resulteertIn[Aanvraag*Beoordeling] CONTAINS
     [("Aanvraag 15", "Verwezen")
	; ("Aanvraag 16", "Geaccepteerd")
	; ("Aanvraag 17", "Exclusief")
	 ]
	 
POPULATION heeftBetrekkingOp[Aanvraag*Product] CONTAINS
     [("Aanvraag 15", "WNWIA")
	; ("Aanvraag 16", "WGWIA")
	; ("Aanvraag 17", "Ziektekosten")
	 ]	 

POPULATION hoortBij[Taak*Aanvraag] CONTAINS
     [("Taak 15", "Aanvraag 15")
	; ("Taak 20", "Aanvraag 16")
	 ]		 
	
POPULATION heeftEen[Taak*Taakstatus] CONTAINS
     [("Taak 15", "Open")
	; ("Taak 20", "Gesloten")
	 ]	

POPULATION toegewezenAan[Taak*Team] CONTAINS
     [("Taak 15", "FrontOffice")
	; ("Taak 20", "Claim")
	 ]	 
	 
POPULATION  behandeld[Medewerker*Taak] CONTAINS
     [("Sebastiaan", "Taak 15")
	; ("Roger", "Taak 20")
	 ]	 	 
	 
POPULATION  ingedeeldIn[Medewerker*Team] CONTAINS
     [("Sebastiaan", "FrontOffice")
     ;("Lode", "FrontOffice")
	; ("Roger", "Claim")
	; ("Stef", "Claim")
	 ]		 
 

ENDCONTEXT