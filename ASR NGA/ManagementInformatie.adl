CONTEXT Managementinformatie IN DUTCH LATEX
	META "authors" "Sebastiaan Hobers, Roger Hage, Lode Cornelissen POC Ampersand"
	
	
	
INTERFACE "Overzicht" :I[ONE] {- -}
BOX [ 	"Klantverzoek" : V[ONE*Klantverzoek]
		--,"De beoordeling" : resulteertIn
	]
	
	
INTERFACE "Overzicht aanvragen" (resulteertIn, heeftBetrekkingOp, hoortBij)
FOR Tussenpersoon : I[Klantverzoek]
BOX [ 	"Klantverzoek" : I
		,"Beoordeling" : resulteertIn
		,"Product" : heeftBetrekkingOp
		,"Taak": hoortBij~
]	

INTERFACE "Overzicht taken" (heeftEen, toegewezenAan, behandeld) : I[Taak]
BOX [ 	"Taak" : I
		,"Taakstatus" : heeftEen
		,"Team" : toegewezenAan
		,"Medewerker": behandeld~
]	

INTERFACE "Overzicht team" : I[Team]
BOX [ 	"Team" : I
		,"Medewerker" : ingedeeldIn~
]	

-- Uitgezet voor het genereren van de PDF 
-- uitzoeken met Stef hoe hier mee om te gaan
-- PROCESS "Indienen klantverzoek"	
-- ROLE Tussenpersoon MAINTAINS "Klantverzoek dient beoordeeld te worden"	
-- ENDPROCESS



PURPOSE PATTERN Managementinformatie
	{+
	Dit hoofdstuk beschrijft de taal zoals die wordt gebruikt om de managementinformatie omtrent processen te tonen aan de gebruiker.
	Waar mogelijk verwijst de tekst naar de bron, zodat de lezer afwijkingen die hij of zij constateert kan traceren.
	Daar waar de tekst afwijkt van de bron is die bron uiteraard meer gezaghebbend dan deze tekst.
	
	Functionele eisen zullen worden opgesteld in de hier gedefinieerde taal.
	
	Bij het opstellen van dit ontwerp wordt voldaan aan een viertal requirements. 
	Het eerste requirement is inzicht in het aantal verschillende klantverzoeken (offerte, contractaanvraag, etc) die binnenkomen. 
	Van de binnengekomen klantverzoeken moet bijgehouden worden hoeveel worden straight through verwerkt zijn en hoeveel verwezen.
	Het tweede requirement gaat over het inzicht in de werkvoorraad. Hievoor is het van belang inzicht te krijgen in de taken met status open.
	Het inzicht moet uitgesplitst worden naar teamniveau en medewerkerniveau. 
	Een ander onderdeel van het tweede requirement is dat men ook moet kunnen zien wat de werkvoorraad in het verleden was.
	Het derde requirement gaat over doorlooptijden. Wat is de doorlooptijd van een uitgevallen taak. Dit is dan uitgesplitst naar het type taak.
	Op dit moment zijn twee typen taken onderkent. Namelijk een taak voor de uitval offerte en een taak voor de uitval van een claim.
	Het vierde en laatste requirement gaat over medewerkers. Hoeveel taken heeft een medewerker afgehandeld, wat is zijn throughput.   
	-}
	
	

PATTERN Managementinformatie

	PURPOSE CONCEPT Medewerker REF "Personeelsgids, begrippenlijst"
   	{+
   		    
   	-}
   	CONCEPT Medewerker "Een medewerker is degene (m/v) die een fulltime of parttime, vaste, tijdelijke of inhuur arbeidsovereenkomst heeft met ASR Nederland N.V.." "Personeelsgids, begrippenlijst"
   	
	PURPOSE CONCEPT Klantverzoek REF "Requirements overzicht.ppt"
   	{+
   		   		
   	-}
   	CONCEPT Klantverzoek "Een klantverzoek is het verzoek tot verwerking van een werkproces." "Requirements overzicht.ppt"
   	
   	PURPOSE CONCEPT Offerte REF "Requirements overzicht.ppt"
   	{+
   		   
   	-}
   	CONCEPT Offerte "Het verzoek tot een offerte van een AOV en/of een ziektekosten product(en)" "Requirements overzicht.ppt"
   
   	PURPOSE CONCEPT ContractAanvraag REF "LSD DAVPAC ContractApplication, LSD INC ContractApplication"
   	{+
   		  
   	-}
   	CONCEPT ContractAanvraag "Het verzoek tot het afsluiten van een AOV en/of een ziektekostencontract" "Requirements overzicht.ppt"
   
   	PURPOSE CONCEPT AdministratieveWijziging REF "LSD DAVPAC Endorsment"
   	{+
   		
   	-}
   	CONCEPT AdministratieveWijziging "Het verzoek tot een administratieve wijziging" "Requirements overzicht.ppt"
   
    PURPOSE CONCEPT EersteZiektemelding REF "LSD INC ClaimApplication"
   	{+
   		   
   	-}
   	CONCEPT EersteZiektemelding "Ziekmelding 48 uur na 1e ziektedag doorgeven" "Requirements overzicht.ppt"
   	
   
   	PURPOSE CONCEPT Beoordeling REF "Ergens"
   	{+
   		Een klantverzoek die binnenkomt moet uiteindelijk beoordeeld worden.
   		Het kan zijn dat een klantverzoek standaard geaccepteerd wordt.
   		Het kan echter ook zijn dat beoordeling door een extern systeem plaatsvindt.
   		Het resultaat van een beoordeling kan zijn dat een klantverzoek naar een medeweker moet om het risico te analyseren.
   		Om de informatie zichtbaar te maken is het concept "beoordeling" gebruikt.   
   	-}
   	CONCEPT Beoordeling "Over de definitie van een beoordeling dient nog een uitspraak gedaan te worden." "xxxxx"	
   
	PURPOSE CONCEPT Product REF "Marketoffering"
   	{+
   		  
   	-}
   	CONCEPT Product "Onder product wordt verstaan hetgene dat de klant bij ASR kan afsluiten en op zijn polis ziet." "Marketoffering"	
   
	PURPOSE CONCEPT Taak REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		    
   	-}
   	CONCEPT Taak "Een taak is datgene dat de medewerker in zijn inbak ziet als werk dat hij of zij moet doen." "xxxxx"

	PURPOSE CONCEPT Taakstatus REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		
   	-}
   	CONCEPT Taakstatus "Een taakstatus is de status die een taak op dit moment heeft" "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	   	
   	PURPOSE CONCEPT Team REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	{+
   		
   	-}
   	CONCEPT Team "Een team is een cluster van medewerkers met dezelfde verantwoordelijkheden" "DFO NGA Generieke functionaliteit medewerkerapplicatie"
   	
   	
   	-----------------------------------------------------------
	--		ISA relaties
	-----------------------------------------------------------
	SPEC ContractAanvraag ISA Klantverzoek
	SPEC Offerte ISA Klantverzoek
	SPEC EersteZiektemelding ISA Klantverzoek
	SPEC AdministratieveWijziging ISA Klantverzoek
   	
	-----------------------------------------------------------
	--		De relatie isAContract
	-----------------------------------------------------------
--	PURPOSE RELATION isAContract[ContractAanvraag*Klantverzoek] REF "LSD DAVPAC ContractApplication, LSD INC ContractApplication"
	--{+
		
		
	--	Een contractaanvraag is een vorm van een klantverzoek.
	---}
	--RELATION isAContract[ContractAanvraag*Klantverzoek][TOT]
	--PRAGMA "" " is een "
	--MEANING "De uitspraak ``Contractaanvraag $c$ is een klantverzoek $a$.'' behoort tot de gemeenschappelijke taal."

	-----------------------------------------------------------
	--		De relatie resulteertIn
	-----------------------------------------------------------
	PURPOSE RELATION resulteertIn[Klantverzoek*Beoordeling] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Nu alle definities helder zijn, kunnen de afspraken die gemaakt zijn worden beschreven.
		
	-}
	RELATION resulteertIn[Klantverzoek*Beoordeling]  [UNI]
	PRAGMA "" " resulteert in de beoordeling "
	MEANING "De uitspraak ``Klantverzoek $a$ resulteert in beoordeling $b$.'' behoort tot de gemeenschappelijke taal."

	-----------------------------------------------------------
	--		De relatie heeftBetrekkingOp
	-----------------------------------------------------------
	PURPOSE RELATION heeftBetrekkingOp[Klantverzoek*Product] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		

	-}
	RELATION heeftBetrekkingOp[Klantverzoek*Product]  [SUR]
	PRAGMA "" " heeft betrekking op het product "
	MEANING "De uitspraak ``Klantverzoek $a$ heeft betrekking op product $p$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie hoortBij
	-----------------------------------------------------------
	PURPOSE RELATION hoortBij[Taak*Klantverzoek] REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
	
	-}
	RELATION hoortBij[Taak*Klantverzoek] [UNI,TOT] 
	PRAGMA "" " hoort bij "
	MEANING "De uitspraak ``Taak $t$ hoort bij klantverzoek $a$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie heeftEen
	-----------------------------------------------------------
	PURPOSE RELATION heeftEen[Taak*Taakstatus] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+

	-}
	RELATION heeftEen[Taak*Taakstatus]  
	PRAGMA "" " heeft een de status "
	MEANING "De uitspraak ``Taak $t$ heeft status $s$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie toegewezenAan
	-----------------------------------------------------------
	PURPOSE RELATION toegewezenAan[Taak*Team] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+
		
	-}
	RELATION toegewezenAan[Taak*Team]  [UNI,TOT]
	PRAGMA "" " is toegewezen aan het team "
	MEANING "De uitspraak ``Taak $t$ is toegewezen aan team $x$.'' behoort tot de gemeenschappelijke taal."
	
	-----------------------------------------------------------
	--		De relatie behandeld
	-----------------------------------------------------------
	PURPOSE RELATION behandeld[Medewerker*Taak] REF "DFO NGA Generieke functionaliteit medewerkerapplicatie"
	{+

	-}
	RELATION behandeld[Medewerker*Taak]  [UNI]
	PRAGMA "" " behandeld de taak "
	MEANING "De uitspraak ``Medewerker $m$ behandelt taak $t$.'' behoort tot de gemeenschappelijke taal."
		
	-----------------------------------------------------------
	--		De relatie ingedeeldInø
	-----------------------------------------------------------
	PURPOSE RELATION ingedeeldIn[Medewerker*Team] REF "xxx"
	{+

	-}
	RELATION ingedeeldIn[Medewerker*Team]  [UNI,TOT]
	PRAGMA "" " is ingedeeld in het team "
	MEANING "De uitspraak ``Medewerker $m$ is ingedeeld in team $t$.'' behoort tot de gemeenschappelijke taal."
		

	-----------------------------------------------------------
	--		De regel klantverzoek dient beoordeelt te worden
	-----------------------------------------------------------	
	PURPOSE RULE "Klantverzoek dient beoordeeld te worden" REF "LSD INC QuotationApplication, LSD INC ContractApplication, LSD INC ClaimApplication, LSD DAVPAC Endorsement"
	{+
		Naast afspraken, zijn er ook regels beschreven die als afspraak uitgedrukt worden. Deze worden hieronder beschreven.
	-}
	RULE "Klantverzoek dient beoordeeld te worden" : I |- resulteertIn;resulteertIn~
	MEANING "Als een klantverzoek wordt ingediend, dan mag deze klantverzoek niet onbeoordeeld blijven."
	MESSAGE "De klantverzoek heeft nog geen beoordeling."
	VIOLATION (TXT "Klantverzoek ", SRC I, TXT " dient beoordeelt te worden")




ENDPATTERN

--POPULATION resulteertIn[Offerte*Beoordeling] CONTAINS
--     [("Offerte van de heer Jansen", "Verwezen")
--	 ]

POPULATION resulteertIn[Klantverzoek*Beoordeling] CONTAINS
     [("Offerte van de heer Pietersen", "Verwezen")
    ; ("Offerte van de heer Jansen", "Verwezen")
	; ("Ziekmelding van mevrouw Karelsen door de gemeente Amersfoort", "Verwezen")
	; ("Administratieve wijziging van mevrouw Karelsen", "Geaccepteerd")
	; ("Offerte van de heer Adelse", "Exclusief")
	 ]
	 
POPULATION heeftBetrekkingOp[Klantverzoek*Product] CONTAINS
     [("Offerte van de heer Jansen", "WNWIA")
     ;("Offerte van de heer Jansen", "WGWIA")
     ;("Offerte van de heer Jansen", "Ziektekosten")
     ;("Offerte van de heer Pietersen", "WNWIA")
     ;("Administratieve wijziging van mevrouw Karelsen", "WGWIA")
	 ]	 

POPULATION hoortBij[Taak*Klantverzoek] CONTAINS
     [("Uitval Offerte | Te hoog risico | taaknummer 15", "Offerte van de heer Pietersen")
	; ("Uitval claim | Er is een risico | taaknummer 20", "Ziekmelding van mevrouw Karelsen door de gemeente Amersfoort")
	 ]		 
	
POPULATION heeftEen[Taak*Taakstatus] CONTAINS
     [("Uitval Offerte | Te hoog risico | taaknummer 15", "Open")
	; ("Uitval claim | Er is een risico | taaknummer 20", "Gesloten")
	 ]	

POPULATION toegewezenAan[Taak*Team] CONTAINS
     [("Uitval Offerte | Te hoog risico | taaknummer 15", "FrontOffice")
	; ("Uitval claim | Er is een risico | taaknummer 20", "Claim")
	 ]	 
	 
POPULATION  behandeld[Medewerker*Taak] CONTAINS
     [("Sebastiaan", "Uitval Offerte | Te hoog risico | taaknummer 15")
	; ("Roger", "Uitval claim | Er is een risico | taaknummer 20")
	 ]	 	 
	 
POPULATION  ingedeeldIn[Medewerker*Team] CONTAINS
     [("Sebastiaan", "FrontOffice")
     ;("Lode", "FrontOffice")
	; ("Roger", "Claim")
	; ("Stef", "Claim")
	 ]		 
 

ENDCONTEXT