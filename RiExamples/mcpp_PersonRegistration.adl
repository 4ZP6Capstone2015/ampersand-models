CONTEXT PersonRegistration
-- The purpose of this file is to play around with VIEW functionalities. To that end, we use a very common pattern that appear to suit most use-cases.
-----------------------------------------------------------

-----------------------------------------------------------
------------------------------------------------------------
-- This is a complete Ampersand module
-- that is, including process and interfaces
-- for Account management and Logging into Sessions.
------------------------------------------------------------
PATTERN Accounts

CONCEPT Account "een record waarin gegevens zijn opgenomen van een (enkele) persoon"
PURPOSE CONCEPT Account IN DUTCH
{+Er zijn verschillende redenen op basis waarvan informatiesystemen relaties moeten kunnen leggen met individuele personen. Het gaat dan bijvoorbeeld om:

* het expliciet kunnen beleggen van verantwoordelijkheden
* het al dan niet mogen uitvoeren van services

Hierom voeren we het concept 'Account' in.-}

CONCEPT Userid "een aanduiding voor een account die gebruikt kan worden door de business."
PURPOSE CONCEPT Userid IN DUTCH
{+Om de business in staat te stellen namen van (gebruikers)accounts te onderscheiden van namen van andersoortige concepten, introduceren we het concept Userid.-}

userid :: Account -> Userid
PRAGMA "" "staat in de business bekend als"
PURPOSE RELATION userid[Account*Userid] IN DUTCH
{+Om de business in staat te stellen om zekere accounts aan te duiden, moet aan elk daarvan een naam zijn toegekend.-}

VIEW AccountNamen: Account(userid[Account*Userid])
--RULE "INJ(userid)": userid; userid~ |- I[Account)
--MEANING "Een account is identificeerbaar door zijn naam. Dat wil zeggen: als de naam vastligt, dan hoort daar hooguit één account."

password :: Account -> Password
PRAGMA "Het wachtwoord dat bij " " hoort is "
PURPOSE RELATION password IN DUTCH
{+Aan elk account is een wachtwoord (password) gekoppeld waarvan wordt aangenomen dat deze - behalve door het systeem - alleen door die persoon gekend wordt die het account mag gebruiken (= in een sessie mag activeren). Op basis van deze aanname kunnen we zeggen dat een user zich met behulp van dit password kan authenticeren.-}

roles :: Account * Role
PRAGMA "Aan " " is " " toegekend"
PURPOSE RELATION roles IN DUTCH
{+Al hetgeen onder (de verantwoordelijkheid van) een account kan gebeuren, moet gecontroleerd kunnen worden. Rollen spelen daar een centrale rol in - zie de Role-Based Access Control (RBAC) standaard van NIST. Rollen moeten daarin toegekend kunnen worden aan wat ze 'users' noemen. Hier zou het equivalent daarvan de UserID zijn, hetgeen het business equivalent van een Account is. Daarom koppelen we rollen aan een account (c.q. accounts aan rollen).-}

ENDPATTERN
-----------------------------------------------------------
PROCESS "Account Management"

emailaddr :: Account * EmailAddr
PRAGMA "Email voor de eigenaar van " " kan worden gestuurd naar "

RULE "email adres invullen": I[Account] |- emailaddr;V
MEANING "Van elk account moet tenminste 1 emailadres van diens eigenaar bekend zijn."
ROLE User MAINTAINS "email adres invullen"

--!Omdat prototype.exe verwacht dat alle ROLEs zijn gedefinieerd (lees: gebruikt), moeten we een procesregel maken voor AccountBeheerders om bij te houden:
RULE "rol(len) toekennen": I[Account] |- roles;V
ROLE AccountAdmin MAINTAINS "rol(len) toekennen"

ENDPROCESS
-----------------------------------------------------------
PATTERN SessionLogin

CONCEPT Session "een communicatiekanaal tussen een applicatie en een gebruiker."
PURPOSE CONCEPT Session IN DUTCH
{+Als meerdere gebruikers tegelijkertijd van eenzelfde applicatie gebruik willen maken, moet de communicatie die deze gebruikers (webclients) met de applicatie hebben, netjes uitelkaar worden gehouden.-}

RULE "the 'Session' concept is a singleton": I[Session] = 'SESSION'
MEANING "Naast 'SESSION' bestaan geen andere sessies."

sessionUser :: Session * Userid [UNI]
PRAGMA "De gebruiker van " " is ingelogd onder "
MEANING "Onder de 'sessionUser' van een sessie verstaan we (de business identifier van) het account waarvan de eigenaar verantwoordelijk gehouden wordt voor al hetgeen er in die sessie gebeurt."

sessionPassword :: Session * Password [UNI]
PRAGMA "De gebruiker van " " heeft zich geauthenticeerd met behulp van "

RULE "user authentication": sessionUser |- sessionPassword;password~;userid
MEANING "Bij inloggen moet een geldige (Userid,Password) combinatie worden opgegeven."

ENDPATTERN
-----------------------------------------------------------
--[Interfaces voor alle users]------------------------------

-- INTERFACE die je moet gebruiken om nieuwe Sessions mee te maken:
INTERFACE Login (sessionUser,sessionPassword): V[ONE*Session]
BOX [ userid : sessionUser
    , password : sessionPassword
    , sessionRoles : sessionUser;userid~;roles
    ]

-- INTERFACE voor gebruikers om hun eigen accountgegevens aan te passen
PURPOSE INTERFACE "My Account" IN DUTCH
{+Om bestaande accounts te kunnen wijzigen is het nodig dat er een INTERFACE bestaat waarmee dit kan. Het aanbrengen van wijzigingen in een account moet in eerste aanleg kunnen gebeuren door

* beheerders, d.w.z. AccountAdmins
* de eigenaar, d.w.z. degene die met dat account kan inloggen.

Echter de eigenaar mag niet zelf bepalen welke rollen hij zoal heeft; ook mag hij de zijn userid niet veranderen. Daarom moet er een interface bestaan die individuele gebruikers de gelegenheid biedt de voor hen wijzigbare velden aan te passen.-}
INTERFACE "My Account" (password,emailaddr): V[Userid*Session];'SESSION';sessionUser;userid~
BOX[ "userid" : userid
   , "password" : password
   , "email adres" : emailaddr
   , "rol(len)" : roles
   ]
-----------------------------------------------------------
--[AccountAdmin Interfaces]--------------------------------
INTERFACE "Account" (userid,password,emailaddr,roles): I[Account]; (I/\V;'SESSION';sessionUser;userid~;roles;'AccountAdmin';V)

BOX[ "userid" : I
   , "password" : password
   , "email adres" : emailaddr
   , "rol(len)" : roles
   , "person" : personalAccount~
   ]
-----------------------------------------------------------
-----------------------------------------------------------
PATTERN People

CONCEPT Person "someone of flesh and blood"

firstName :: Person * Text [UNI] -- everyone has a first name, but it may not be registered
lastName :: Person -> Text -- we assume that the last name is registered, for everyone
characteristics :: Person * Text -- as in the real world, we may use other characteristics to identify people

VIEW "People": Person(Name: firstName, TXT " ", lastName, TXT ", with characteristics: ", characteristics)


--RULE "VIEW Persons":  ((-firstName ! firstName~) /\ (firstName ! -firstName~))  /\  ((-lastName ! lastName~) /\ (lastName ! -lastName~))  /\  ((-characteristics ! characteristics~) /\ (characteristics ! -characteristics~))  |- I[Person]
ENDPATTERN
-----------------------------------------------------------
PATTERN "Personal Accounts"

personalAccount :: Person * Account PRAGMA "" " may login using "

RULE "personal account integrity": personalAccount = personalAccount;(I /\ roles;'User';V)
MEANING "Every personal account must have been assigned the 'User' role and vice versa."
MESSAGE "Please make sure this account has the role 'User' assigned, since it is a personal account"

ENDPATTERN
-----------------------------------------------------------
PATTERN Searching
PURPOSE PATTERN Searching
{+For searching people, we need a simple facility. This pattern implements such a facility (using a singleton concept 'SearchQuery'
-- Om te zoeken: druk op Edit in de gewenste zoek interface (bv. "Zoek op naam"),
-- voeg de zoekterm toe met Insert New, vul de term in en druk op Save.
-- NB. Na het zoeken moet de zoekterm weer verwijderd worden.
-}

RULE "SearchQuery is a singleton": I[SearchQuery] = 'SearchQuery'
searchQuery :: SearchQuery * SearchQuery = [ ("SearchQuery", "SearchQuery") ]

searchForPerson :: SearchQuery * Person

ENDPATTERN
-----------------------------------------------------------
INTERFACE "Search a persoon" (searchForPerson) : I[SearchQuery]
BOX [ "Klantnr" : searchForPerson
      BOX [ "Voornaam" : firstName
          , "Achternaam" : lastName
          , "Karakteristieken" : characteristics
          ]
    ]
-----------------------------------------------------------
PROCESS HRM

RULE "TOT-lastName": I[Person] |- lastName;lastName~
ROLE HRMOfficer MAINTAINS "TOT-lastName"

ENDPROCESS
-----------------------------------------------------------




INTERFACE "Overview": I[ONE]; (I /\ V;'SESSION';sessionUser;userid~;roles;'HRMOfficer';V)

BOX[ "People" : V[ONE*Person]
   , "Accounts" : V[ONE*Account]
   , "Search" : V[ONE*SearchQuery]
   ]




INTERFACE "Person" (firstName,lastName,characteristics): I[Person]; (I /\ V;'SESSION';sessionUser;userid~;roles;'HRMOfficer';V)

BOX[ "Person" : I
   , "First name" : firstName
   , "Last name" : lastName
   , "Characteristics": characteristics
   , "Account" : personalAccount
   ]
-----------------------------------------------------------
--[By default, the administrator is logged in]-------------
POPULATION sessionUser[Session*Userid] CONTAINS [ ("SESSION", "admin") ]
POPULATION sessionPassword[Session*Password] CONTAINS [ ("SESSION", "admin") ]

--[Account registry contents]------------------------------
POPULATION userid[Account*Userid] CONTAINS [ ("Account_0", "admin") ]
POPULATION password[Account*Password] CONTAINS [ ("Account_0", "admin") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_0", "User") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_0", "AccountAdmin") ]

POPULATION userid[Account*Userid] CONTAINS [ ("Account_1", "pukp") ]
POPULATION password[Account*Password] CONTAINS [ ("Account_1", "******") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_1", "User") ]

POPULATION userid[Account*Userid] CONTAINS [ ("Account_2", "klaassenj") ]
POPULATION password[Account*Password] CONTAINS [ ("Account_2", "*******") ]
POPULATION emailaddr[Account*EmailAddr] CONTAINS [ ("Account_2", "j.klaassen@acme.com") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_2", "User") ]

POPULATION userid[Account*Userid] CONTAINS [ ("Account_3", "klaassenk") ]
POPULATION password[Account*Password] CONTAINS [ ("Account_3", "*******") ]
POPULATION emailaddr[Account*EmailAddr] CONTAINS [ ("Account_3", "katrijn@acme.com") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_3", "User") ]

POPULATION userid[Account*Userid] CONTAINS [ ("Account_4", "apache01") ]
POPULATION password[Account*Password] CONTAINS [ ("Account_4", "********") ]
POPULATION emailaddr[Account*EmailAddr] CONTAINS [ ("Account_4", "j.klaassen@acme.com") ]
POPULATION roles[Account*Role] CONTAINS [ ("Account_4", "Webserver") ]

--[Person Registry Contents] ------------------------------
POPULATION firstName CONTAINS [ ("P_001", "Rieks") ]
POPULATION lastName CONTAINS [ ("P_001", "Joosten") ]
POPULATION characteristics CONTAINS [ ("P_001", "Over 18") ]
POPULATION characteristics CONTAINS [ ("P_001", "Lives in Groningen area") ]
POPULATION personalAccount CONTAINS [ ("P_001", "Account_0") ]
POPULATION roles CONTAINS [ ("Account_0", "HRMOfficer") ]

POPULATION firstName CONTAINS [ ("P_002", "Stef") ]
POPULATION lastName CONTAINS [ ("P_002", "Joosten") ]

POPULATION lastName CONTAINS [ ("P_003", "Joosten") ]
POPULATION characteristics CONTAINS [ ("P_003", "Over 18") ]
POPULATION characteristics CONTAINS [ ("P_003", "Lives in Peize area") ]

POPULATION firstName CONTAINS [ ("P_004", "Martijn") ]
POPULATION lastName CONTAINS [ ("P_004", "Schrage") ]
POPULATION characteristics CONTAINS [ ("P_004", "aka Oblosys") ]

POPULATION firstName CONTAINS [ ("P_005", "Piet") ]
POPULATION lastName CONTAINS [ ("P_005", "Puk") ]
POPULATION personalAccount CONTAINS [ ("P_005", "Account_1") ]
POPULATION roles CONTAINS [ ("Account_1", "HRMOfficer") ]

POPULATION firstName CONTAINS [ ("P_006", "Jan") ]
POPULATION lastName CONTAINS [ ("P_006", "Klaassen") ]
POPULATION personalAccount CONTAINS [ ("P_006", "Account_2") ]

POPULATION firstName CONTAINS [ ("P_007", "Katrijn") ]
POPULATION lastName CONTAINS [ ("P_007", "Klaassen") ]
POPULATION personalAccount CONTAINS [ ("P_007", "Account_3") ]
-----------------------------------------------------------
ENDCONTEXT
