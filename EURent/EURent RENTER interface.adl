CONTEXT EURentRENTERInterfaces
-----------------------------------------------------------
INTERFACE "User Overview" 
   ( sessionUser
   , sessionToday
   , sessionNewUserRC
-- Edit Contract Details
   , contractedStartDate
   , contractedEndDate
   , contractedCarType
   , contractedPickupBranch
   , contractedDropoffBranch
-- Edit Customer Details
   , rcRenter
   , rcDriver
   , validDrivingLicense
-- Other stuff
   , rcUserRequestedQ
   ) FOR User : V[ONE*SESSION];'_SESSION'[SESSION]
BOX[ "Session context" : I
   BOX[ "User" : sessionUser
      , "Todays date" : sessionToday
      ]
   , "My rental requests" : sessionUser;rcRenter~;(I /\ -rentalHasBeenStarted)
   BOX[ "Contract details" : I[RentalCase] INTERFACE "Edit Contract Details"
      , "Customer details" : I[RentalCase] INTERFACE "Edit Customer Details"
      , "Projected costs"  : I[RentalCase] INTERFACE "Show Projected Costs"
      , "Submit this rental request?" : rcUserRequestedQ
      ]
   , "My current rentals" : sessionUser;rcRenter~;(I /\ -rentalHasBeenEnded)
   BOX[ "Contract details" : I[RentalCase] INTERFACE "Show Contract Details"
      , "Customer details" : I[RentalCase] INTERFACE "Show Customer Details"
      , "Projected costs"  : I[RentalCase] INTERFACE "Show Projected Costs"
      ]
-- ,"My current rental contracts" : sessionUser;(rcDriver \/ rcRenter)~;(I /\ -rentalHasBeenEnded) INTERFACE "Show Contract Details"
-- ,"My current customer details" : sessionUser;(rcDriver \/ rcRenter)~;(I /\ -rentalHasBeenEnded) INTERFACE "Show Customer Details"
-- ,"My current projected costs"  : sessionUser;(rcDriver \/ rcRenter)~;(I /\ -rentalHasBeenEnded) INTERFACE "Show Projected Costs"
   , "My rental history"  : sessionUser;(rcDriver \/ rcRenter)~;(I /\  rentalHasBeenEnded)
   ]
-----------------------------------------------------------
sessionNewUserRC  :: SESSION * RentalCase [INJ,UNI]

INTERFACE "New User Rental" 
   ( sessionUser
   , sessionToday
   , sessionNewUserRC
-- Edit Contract Details
   , contractedStartDate
   , contractedEndDate
   , contractedCarType
   , contractedPickupBranch
   , contractedDropoffBranch
-- Edit Customer Details
   , rcRenter
   , rcDriver
   , validDrivingLicense
-- Other stuff
   , rcUserRequestedQ
   ) FOR User : V[ONE*SESSION];'_SESSION'[SESSION]
BOX[ "Session context" : I
   BOX[ "Who I am" : sessionUser
      , "Todays date" : sessionToday
      ]
   , "New Rental Request" : I
   BOX[ "Please fill in the following" : sessionNewUserRC
      BOX[ "Contract details" : I[RentalCase] INTERFACE "Edit Contract Details"
         , "Customer details" : I[RentalCase] INTERFACE "Edit Customer Details"
         , "Projected costs"  : I[RentalCase] INTERFACE "Show Projected Costs"
         , "Submit this rental request?" : rcUserRequestedQ
         ]
      ]
   ]
-----------------------------------------------------------
PROCESS "New Rental Car Handling of User Interface"
PURPOSE PROCESS "New Rental Car Handling of User Interface"
{+This process describes the automated features for filling in or changing the contents of forms that are presented in the interface 'New User Rental'. The assumption is that this interface is provided over the Internet, allowing users to request a rental in advance (see P2:1) from any location of their choosing (e.g. at home).-}

ROLE User MAINTAINS "Submit rental request"
RULE "Submit rental request": '_SESSION'[SESSION];sessionNewUserRC |- sessionNewUserRC;rcUserRequestedQ;'Yes';V
VIOLATION (TXT "The new rental request must be submitted.")

PURPOSE RULE "By default, the user will be the renter"
{+When a rental request is submitted, and the renter is not specified, the user that submits the request is assumed to be the renter.-}
ROLE ExecEngine MAINTAINS "Fill in default renter"
RULE "Fill in default renter": I /\ rcUserRequestedQ;'Yes';rcUserRequestedQ~ |- rcRenter;rcRenter~
VIOLATION (TXT "{EX} InsPair;rcRenter;RentalCase;", SRC I, TXT ";Person;", TGT sessionNewUserRC~;sessionUser)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT