CONTEXT EURentInterfaces
-----------------------------------------------------------
sessionBranch :: SESSION * Branch [UNI]
sessionToday  :: SESSION * Date [UNI]
sessionRC     :: SESSION * RentalContract [INJ,UNI]

INTERFACE "OVERVIEW" 
   ( sessionBranch
   , sessionToday
   , sessionRC
   , rcStartDate
   , rcEndDate
   , rcCarType
-- , rcPickupBranch
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , validDrivingLicense
-- , carAvailableAt
-- , carType
   , rcIssuedCar
   , rcStartDate
-- , rcReturnedCar
-- , rcActualEndDate
   , rcActualDropoffBranch
   , rentalIsPaidQ
   ) : V[ONE*SESSION];'_SESSION'[SESSION]
BOX[ "Select branch" : I
   BOX[ "Branch" : sessionBranch
      , "Location" : sessionBranch;branchLocation
      , "Todays date" : sessionToday
      , "Rental" : sessionRC
      ]
   , "Stock" : sessionBranch;carAvailableAt~
   , "Rentals to be picked up" : sessionBranch;rcPickupBranch~;(I /\ -rentalHasStarted)
   , "Rentals to be dropped off" : sessionBranch;rcDropoffBranch~;(I /\ -(rcReturnedCar;rcReturnedCar~))
   ]


INTERFACE Car: I[Car]
BOX[ "Car" : I
   , "Type" : carType
   BOX[ "Make" : brand
      , "Model" : model
      ]
   , "In stock at" : carAvailableAt
   , "Rented" : rcIssuedCar~;(rentalHasStarted /\ -rentalHasEnded)
   BOX[ "Renter" : rcRenter
      , "Driver" : rcDriver
      , "Start date" : rcStartDate
      , "End date"   : rcEndDate
      , "Drop-off"   : rcDropoffBranch
      ]
   ]

INTERFACE "Renting a car" --! Transactions [INVULLEN: B-Txx]
   ( rcStartDate
   , rcEndDate
   , rcCarType
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , validDrivingLicense
   ) FOR BranchOffice : I[RentalContract]
BOX[ "Contracted start of rental" : I
   BOX[ "Pick-up branch" : V[RentalContract*SESSION];'_SESSION';sessionBranch
      , "Desired start date" : rcStartDate
      , "Car type" : rcCarType
      ]
   , "Contracted end of rental" : I 
   BOX[ "Drop-off branch" : rcDropoffBranch
      , "Foreseen end date" : rcEndDate
      ]
   , "Customer details" : I
   BOX[ "Renter (Name)" : rcRenter
      , "Driver" : rcDriver
      , "VALID driving license" : rcDriver
      BOX[ "License number" : validDrivingLicense
         ]
      ]
   ]

PROCESS X
ROLE ExecEngine MAINTAINS "Auto fill in fields of new rental contract"
RULE "Auto fill in fields of new rental contract": sessionRC |- sessionRC;(I /\ rcPickupBranch;rcPickupBranch~ /\ rcStartDate;rcStartDate~)
VIOLATION (TXT "{EX} InsPair;rcPickupBranch;RentalContract;", TGT I, TXT ";Branch;", SRC sessionBranch
          ,TXT "{EX} InsPair;rcStartDate;RentalContract;", TGT I, TXT ";Date;", SRC sessionToday
          ,TXT "{EX} DelPair;sessionRC;SESSION;", SRC I, TXT ";RentalContract;", TGT I
          )

ROLE ExecEngine MAINTAINS "Auto fill in fields of returned cars"
RULE "Auto fill in fields of new rental contract": rcReturnedCar |- (I /\ rcActualDropoffBranch;rcActualDropoffBranch~ /\ rcActualEndDate;rcActualEndDate~);rcReturnedCar
VIOLATION (TXT "{EX} InsPair;rcActualDropoffBranch;RentalContract;", SRC I, TXT ";Branch;", SRC sessionRC~;sessionBranch
          ,TXT "{EX} InsPair;rcActualEndDate;RentalContract;", SRC I, TXT ";Date;", SRC sessionRC~;sessionToday
          )
ENDPROCESS 

INTERFACE "Returning a car" --! Transactions [INVULLEN: B-Txx]
   ( rcEndDate
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , rcReturnedCar
   ) FOR BranchOffice : I[RentalContract]
BOX[ "Rental start" : I
   BOX[ "Issued car" : rcIssuedCar
      , "Car type" : rcCarType
      , "Pick-up branch" : rcPickupBranch
      , "Start date" : rcStartDate
      ]
   , "Customer details" : I
   BOX[ "Renter (Name)" : rcRenter
      , "Driver" : rcDriver
      BOX[ "Name" : I
         , "(Valid) driving license" : validDrivingLicense
         ]
      ]
   , "Drop off details" : I
   BOX[ "Contract drop-off branch" : rcDropoffBranch
      , "Contract drop-off end date" : rcEndDate
      , "Returned car" : rcReturnedCar
      , "Actual drop-off branch" : rcActualDropoffBranch
      , "Acutal drop-off date" : rcActualEndDate
      ]
   , "Bill" : I
   BOX[ "Basic Charge" : I
      BOX[ "Number of days" : rentalPeriod
         , "Tariff per day" : rcIssuedCar;carType;rentalTariffPerDay
         , "Basic charge" : rentalBasicCharge
         ]
      , "Penalty charge for exceeding this duration" : I
      BOX[ "Number of days" : rentalExcessPeriod
         , "Tariff per day" : rcIssuedCar;carType;excessTariffPerDay
         , "Penalty charge" : rentalPenaltyCharge
         ]
      , "Penalty charge for dropping off at different location": I
      BOX[ "Contracted drop-off branch" : rcDropoffBranch
         , "Actual drop-off branch" : V[RentalContract*SESSION];'_SESSION';sessionBranch
--       , "Distance (km)": (rcActualDropoffBranch;distbranch~ /\ rcDropoffBranch;distbranch~);distance
--       , "Distance tariff" : yyy
         , "Penalty charge" : rentalLocationPenaltyCharge
         ]
     , "TOTAL charge" : rentalCharge
     , "Bill is paid (Yes/No)" : rentalIsPaidQ
     ]
   ]
-----------------------------------------------------------
ENDCONTEXT