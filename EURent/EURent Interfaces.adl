CONTEXT EURentInterfaces
------------------------------------------------------------

INTERFACE "OVERVIEW" : I[ONE]
BOX[ "Rental cases"  : V[ONE*RentalCase]
   , "Branches"      : V[ONE*Branch]
   ]
--[General Session Variables]-------------------------------
sessionUser   :: SESSION * Person [UNI]
sessionToday  :: SESSION * Date [UNI]

--[Basic Views for use within Interfaces]-------------------

INTERFACE "Show Contract Details" : I[RentalCase]
BOX[ "Pick-up branch" : contractedPickupBranch
   , "Drop-off branch" : contractedDropoffBranch
   , "Start date" : contractedStartDate
   , "End date" : contractedEndDate
   , "Car type" : contractedCarType
   , "Issued car" : rcIssuedCar
   ]

INTERFACE "Edit Contract Details"
   ( contractedPickupBranch
   , contractedDropoffBranch
   , contractedStartDate
   , contractedEndDate
   , contractedCarType
   ) : I[RentalCase]
BOX[ "* Pick-up branch" : contractedPickupBranch
   , "* Drop-off branch" : contractedDropoffBranch
   , "* Start date" : contractedStartDate
   , "* End date" : contractedEndDate
   , "* Car type" : contractedCarType
   , "Available car types at pick-up branch" : contractedPickupBranch;carAvailableAt~;carType
   ]
-----------------------------------------------------------

INTERFACE "Show Customer Details" : I[RentalCase]
BOX[ "* Driver" : rcDriver
   , "* VALID driving license" : rcDriver
   BOX[ "License number" : validDrivingLicense
      ]
   , "Renter (if different from driver)" : rcRenter
   ]

INTERFACE "Edit Customer Details"
   ( rcDriver
   , validDrivingLicense
   , rcRenter
   ) : I[RentalCase]
BOX[ "* Driver" : rcDriver
   , "* VALID driving license" : rcDriver
   BOX[ "License number" : validDrivingLicense
      ]
   , "Renter (if different from driver)" : rcRenter
   ]
-----------------------------------------------------------

INTERFACE "Car Assignment"
   ( contractedCarType
   , rcIssuedCar
   ) : I[RentalCase]
BOX[ "Available Types" : sessionNewBranchRC~;sessionBranch;carAvailableAt~;carType
   BOX[ "Type"     : I
      , "Euro/day" : rentalTariffPerDay
      ]
   , "Selected Type"  : contractedCarType
   , "Available cars" : contractedCarType;carType~ /\ sessionNewBranchRC~;sessionBranch;carAvailableAt~
   , "Selected car"   : rcIssuedCar
   ]
-----------------------------------------------------------

INTERFACE "Show Billing Details" : I[RentalCase]
BOX[ "Part 1: Basic Charge" : I
   BOX[ "Number of days" : rentalPeriod
      , "Tariff per day" : rcIssuedCar;carType;rentalTariffPerDay
      , "Basic charge" : rentalBasicCharge
      ]
   , "Part 2: Penalty charge for exceeding contracted duration" : I
   BOX[ "Number of days" : rentalExcessPeriod
      , "Tariff per day" : rcIssuedCar;carType;excessTariffPerDay
      , "Penalty charge" : rentalPenaltyCharge
      ]
   , "Part 3: Penalty charge for uncontractual drop-off": I
   BOX[ "Contracted drop-off branch" : contractedDropoffBranch
      , "Actual drop-off branch" : rcDroppedOffBranch
      , "Distance (km)": (rcDroppedOffBranch;distbranch~ /\ contractedDropoffBranch;distbranch~);distance
--       , "Distance tariff" : yyy
      , "Penalty charge" : rentalLocationPenaltyCharge
      ]
  , "TOTAL charge" : rentalCharge
  ]

-----------------------------------------------------------

INTERFACE "Show Drop-off Details" : I[RentalCase]
BOX[ "Contract drop-off branch" : contractedDropoffBranch
   , "Contract drop-off end date" : contractedEndDate
   , "Returned car" : rcDroppedOffCar
   , "Actual drop-off branch" : rcDroppedOffBranch
   , "Acutal drop-off date" : rcDroppedOffDate
   ]

INTERFACE "Edit Drop-off Details"
   ( contractedDropoffBranch
   , contractedEndDate
   , rcDroppedOffCar
   , rcDroppedOffBranch
   , rcDroppedOffDate
   ) : I[RentalCase]
BOX[ "Contract drop-off branch" : contractedDropoffBranch
   , "Contract drop-off end date" : contractedEndDate
   , "Returned car" : rcDroppedOffCar
   , "Actual drop-off branch" : rcDroppedOffBranch
   , "Acutal drop-off date" : rcDroppedOffDate
   ]
-----------------------------------------------------------
PROCESS "Computing Projected Costs"
--[computing the period of the projected rental]--
projectedRentalPeriod :: RentalCase * Integer [UNI] -- Duration (in days) of rental period
ROLE ExecEngine MAINTAINS "Trigger projected rental period computation" -- Result = 1 + MAX(0, (Contracted end date - Contracted start date))
RULE "Trigger projected rental period computation": 
   I /\ contractedStartDate;contractedStartDate~ /\ contractedEndDate;contractedEndDate~
|- (contractedStartDate;earliestDate~ /\ contractedEndDate;latestDate~);V
VIOLATION (TXT "{EX} NewStruct;CompNrDays" -- atom generated automatically (with time stamp)
               ,TXT ";earliestDate;CompNrDays;NULL;Date;", SRC contractedStartDate
               ,TXT ";latestDate;CompNrDays;NULL;Date;", SRC contractedEndDate
           )
ROLE ExecEngine MAINTAINS "projectedRentalPeriod computation"
RULE "projectedRentalPeriod computation": (contractedStartDate;earliestDate~ /\ contractedEndDate;latestDate~);computedRentalPeriod |- projectedRentalPeriod
VIOLATION (TXT "{EX} InsPair;projectedRentalPeriod;RentalCase;", SRC I, TXT ";Integer;", TGT I)

--[computing the charge of the projected rental]--
projectedBasicCharge :: RentalCase * Amount [UNI]
ROLE ExecEngine MAINTAINS "Trigger projected basic charge computation"
RULE "Trigger projected basic charge computation": I[RentalCase] /\ projectedRentalPeriod;projectedRentalPeriod~ /\ contractedCarType;contractedCarType~
|- (projectedRentalPeriod;ctcNrOfDays~ /\ contractedCarType;rentalTariffPerDay;ctcDailyAmount~);V
VIOLATION (TXT "{EX} NewStruct;CompTariffedCharge" -- atom generated automatically (with time stamp)
               ,TXT ";ctcNrOfDays;CompTariffedCharge;NULL;Integer;", SRC projectedRentalPeriod
               ,TXT ";ctcDailyAmount;CompTariffedCharge;NULL;Amount;", SRC contractedCarType;rentalTariffPerDay
           )
ROLE ExecEngine MAINTAINS "projectedBasicCharge computation"
RULE "projectedBasicCharge computation": (projectedRentalPeriod;ctcNrOfDays~ /\ contractedCarType;rentalTariffPerDay;ctcDailyAmount~);computedTariffedCharge |- projectedBasicCharge
VIOLATION (TXT "{EX} InsPair;projectedBasicCharge;RentalCase;", SRC I, TXT ";Amount;", TGT I)
ENDPROCESS
-----------------------------------------------------------
INTERFACE "Show Projected Costs" : I[RentalCase]
BOX[ "Daily charge (Eur)" : contractedCarType;rentalTariffPerDay
   , "Period (days)"      : projectedRentalPeriod
   , "Rental charge (Eur)": projectedBasicCharge
   ]
-----------------------------------------------------------
ENDCONTEXT