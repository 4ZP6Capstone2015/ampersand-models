CONTEXT EURentInterfaces
-----------------------------------------------------------
sessionBranch :: SESSION * Branch [UNI]
sessionNewRC  :: SESSION * RentalContract [UNI]
sessionCar    :: SESSION * Car [UNI]

INTERFACE "OVERVIEW" 
   ( sessionBranch
   , sessionNewRC
   , rcStartDate
   , rcEndDate
   , rcCarType
-- , rcPickupBranch
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , validDrivingLicense
-- , carAvailableAt
-- , carType
   , rcIssuedCar
   , rcStartDate
-- , rcReturnedCar
-- , rcActualEndDate
   , rcActualDropoffBranch
   , rentalIsPaidQ
   ) : V[ONE*SESSION];'_SESSION'[SESSION]
BOX[ "Select branch" : I
   BOX[ "Branch" : sessionBranch
      , "Location" : sessionBranch;branchLocation
      , "New car rental" : sessionNewRC
      BOX[ "Contracted start of rental" : I
         BOX[ "Car type" : rcCarType
            , "Start date" : rcStartDate  --! misschien automatisch invullen?
            , "Foreseen end date" : rcEndDate
            , "Drop-off branch" : rcDropoffBranch
            ]
         , "Customer details" : I
         BOX[ "Renter (Name)" : rcRenter
            , "Driver" : rcDriver
            , "VALID driving license" : rcDriver
            BOX[ "License number" : validDrivingLicense
               ]
            ]
         ]
      ]
   , "Stock" : sessionBranch;carAvailableAt~
   , "Rentals to be picked up" : sessionBranch;rcPickupBranch~;(I /\ -rentalHasStarted)
   , "Rentals to be dropped off" : sessionBranch;rcDropoffBranch~;(I /\ -(rcReturnedCar;rcReturnedCar~))
   ]


INTERFACE Car: I[Car]
BOX[ "Car" : I
   , "Type" : carType
   BOX[ "Make" : brand
      , "Model" : model
      ]
   , "In stock at" : carAvailableAt
   , "Rented" : rcIssuedCar~;(rentalHasStarted /\ -rentalHasEnded)
   BOX[ "Renter" : rcRenter
      , "Driver" : rcDriver
      , "Start date" : rcStartDate
      , "End date"   : rcEndDate
      , "Drop-off"   : rcDropoffBranch
      ]
   ]

INTERFACE "Renting a car" --! Transactions [INVULLEN: B-Txx]
   ( rcStartDate
   , rcEndDate
   , rcCarType
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , validDrivingLicense
   ) FOR BranchOffice : I[RentalContract]
BOX[ "Contracted start of rental" : I
   BOX[ "Pick-up branch" : V[RentalContract*SESSION];'_SESSION';sessionBranch
      , "Desired start date" : rcStartDate
      , "Car type" : rcCarType
      ]
   , "Contracted end of rental" : I 
   BOX[ "Drop-off branch" : rcDropoffBranch
      , "Foreseen end date" : rcEndDate
      ]
   , "Customer details" : I
   BOX[ "Renter (Name)" : rcRenter
      , "Driver" : rcDriver
      , "VALID driving license" : rcDriver
      BOX[ "License number" : validDrivingLicense
         ]
      ]
   ]

PROCESS X
ROLE ExecEngine MAINTAINS "Auto fill in fields of new rental contract"
RULE "Auto fill in fields of new rental contract": sessionNewRC |- sessionNewRC;(I /\ rcPickupBranch;rcPickupBranch~)
VIOLATION (TXT "{EX} InsPair;rcPickupBranch;RentalContract;", TGT I, TXT ";Branch;", SRC sessionBranch
          ,TXT "{EX} DelPair;sessionNewRC;SESSION;", SRC I, TXT ";RentalContract;", TGT I
          )
ENDPROCESS 

INTERFACE "Returning a car" --! Transactions [INVULLEN: B-Txx]
   ( rcEndDate
   , rcDropoffBranch
   , rcRenter
   , rcDriver
   , rcReturnedCar
   ) FOR BranchOffice : I[RentalContract]
BOX[ "Contract details" : I
   BOX[ "Start date" : I
      BOX[ "Issued car" : rcIssuedCar
         , "Car type" : rcCarType
         , "Pick-up branch" : rcPickupBranch
         , "Start date" : rcStartDate
         ]
      , "Contracted end of rental" : I 
      BOX[ "Drop-off branch" : rcDropoffBranch
         , "Foreseen end date" : rcEndDate
         ]
      ]
   , "Customer details" : I
   BOX[ "Renter (Name)" : rcRenter
      , "Driver" : rcDriver
      BOX[ "Name" : I
         , "(Valid) driving license" : validDrivingLicense
         ]
      ]
   , "Drop off details" : I
   BOX[ "Acutal drop-off date" : rcActualEndDate
      , "Returned car" : rcReturnedCar
      ]
   , "Bill" : I
   BOX[ "Basic Charge" : I
      BOX[ "Number of days" : rentalPeriod
         , "Tariff per day" : rcIssuedCar;carType;rentalTariffPerDay
         , "Basic charge" : rentalBasicCharge
         ]
{-      , "Penalty charge for exceeding this duration" : I
      BOX[ "Number of days" : rentalExcessPeriod
         , "Tariff per day" : rcIssuedCar;carType;rentalExcessTariffPerDay
         , "Penalty charge" : rentalPenaltyCharge
      , "Penalty charge for dropping off at different location" 
      BOX[ "Contracted drop-off branch" : rcDropoffBranch
         , "Actual drop-off branch" : V[RentalContract*SESSION];'_SESSION';sessionBranch
         , "Distance (km)": xxx
         , "Distance tariff" : yyy
         , "Penalty charge" : rentalLocationPenaltyCharge
         ]
-}
     , "Grand total" : rentalCharge
     ]
   , "Bill is paid (Yes/No)" : rentalIsPaidQ
   ]
-----------------------------------------------------------
ENDCONTEXT