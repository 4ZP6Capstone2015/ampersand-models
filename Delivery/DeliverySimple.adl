CONTEXT DeliverySimple
{-

Compile with: 

prototype DeliverySimple.adl --css=DeliverySimple.css ..

-}

INTERFACE Overview : I[ONE]
BOX[ "Alle klanten" : V[ONE*KlantNr]
     BOX [ naam : klantNaam ]
   , "Alle leveranciers" : V[ONE*LeverancierNr] 
     BOX [ naam : leverancierNaam ]
   , "Alle producten" : V[ONE*ProductNr]
     BOX [ naam : productNaam ]
   , "Alle bestellingen" : V[ONE*BestellingNr] 
     BOX [ ProductNr : bestellingVan BOX [ naam : productNaam ] 
         , KlantNr : bestellingDoor BOX [ naam : klantNaam ] 
         , LeverancierNr : bestellingBij BOX [ naam : leverancierNaam ] 
     ]
   , "Zoeken" : V[ONE*Zoek]
   ]
     
INTERFACE KlantNr (klantNaam, klantAdres, klantWoonplaats, bestellingOntvangen) : I[KlantNr]
BOX [ Naam : klantNaam
    , Straat : klantAdres
    , Woonplaats : klantWoonplaats
    , "Nog niet geaccepteerde bestellingen" : bestellingDoor~ /\ -(V; bestellingGeaccepteerd~)
    , "te ontvangen bestellingen" : bestellingDoor~ /\ (V; bestellingGeaccepteerd~) /\ -bestellingOntvangen~
    , "Ontvangen bestellingen" : bestellingOntvangen~
    ]

INTERFACE LeverancierNr (leverancierNaam, levert, productNaam, productPrijs, bestellingGeaccepteerd) : I[LeverancierNr]
BOX [ Naam : leverancierNaam
    , Producten : levert
      BOX [ Naam : productNaam
          , Prijs : productPrijs
          ]
    , "Te accepteren bestellingen" : bestellingBij~ /\ -bestellingGeaccepteerd~
    , "Geaccepteerde bestellingen" : bestellingGeaccepteerd~
    ]

INTERFACE ProductNr (productNaam, productPrijs) : I[ProductNr]
BOX [ Naam : productNaam
    , Prijs : productPrijs
    , Leveranciers : levert~
     BOX [ naam : leverancierNaam ]
    ]

INTERFACE BestellingNr (bestellingDoor, bestellingBij, bestellingVan, klantNaam, klantAdres, klantWoonplaats) : I[BestellingNr]
BOX [ Klant : bestellingDoor
      BOX [ Naam : klantNaam
          , Adres : klantAdres
          , Woonplaats : klantWoonplaats
          ]
    , Leverancier : bestellingBij
      BOX [ naam : leverancierNaam ]
    , Product: bestellingVan
      BOX [ Naam : productNaam
          , Prijs : productPrijs
          ]
    ]


-- Zoeken

-- Met behulp van een singleton concept Zoek en een hulprelatie kunnen
-- we een eenvoudige zoekfaciliteit simuleren.

-- Om te zoeken: druk op Edit in de gewenste zoek interface (bv. "Zoek op naam"), 
-- voeg de zoekterm toe met Insert New, vul de term in en druk op Save.
-- NB. Na het zoeken moet de zoekterm weer verwijderd worden. 

zoek :: Zoek * Zoek
 = [ ("zoek", "zoek")
   ]

zoekOpNr :: Zoek * KlantNr

INTERFACE "Zoek klant op nr" (zoekOpNr) : I[Zoek]
BOX [ "Klantnr" : zoekOpNr 
      BOX [ Naam : klantNaam
          , Adres : klantAdres
          , Woonplaats : klantWoonplaats
          ]
    ]

zoekOpNaam :: Zoek * Naam

INTERFACE "Zoek op naam" (zoekOpNaam) : I[Zoek]
BOX [ "Gezochte Naam" : zoekOpNaam 
    , "Gevonden klanten" : zoekOpNaam ; klantNaam~
      BOX [ Naam : klantNaam
          , Adres : klantAdres
          , Woonplaats : klantWoonplaats
          ]
    , "Gevonden leveranciers" : zoekOpNaam ; leverancierNaam~
      BOX [ Naam : leverancierNaam
          , Producten : levert
            BOX [ naam : productNaam ]
          ]
    , "Gevonden producten" : zoekOpNaam ; productNaam~
      BOX [ Naam : productNaam
          , Prijs : productPrijs
          , Leveranciers : levert~
            BOX [ naam : leverancierNaam ]
         ]
    ]
  
  

-- Relaties en populaties

klantNaam :: KlantNr -> Naam 
  = [ ("Klant_1"      , "Martijn")
    ; ("Klant_2"      , "Stef")
    ]

klantAdres :: KlantNr -> Adres 
  = [ ("Klant_1"      , "Kerkstraat")
    ; ("Klant_2"      , "Dorpsstraat")
    ]
    
klantWoonplaats :: KlantNr -> Woonplaats 
  = [ ("Klant_1"      , "Utrecht")
    ; ("Klant_2"      , "Enschede")
    ]
    
leverancierNaam :: LeverancierNr -> Naam 
  = [ ("Leverancier_1", "Rubber inc.")
    ; ("Leverancier_2", "Mario's pizzas")
    ]
    
levert :: LeverancierNr * ProductNr 
  = [ ("Leverancier_1", "Product_1")
    ; ("Leverancier_1", "Product_2")
    ; ("Leverancier_1", "Product_3")
    ; ("Leverancier_2", "Product_4")
    ; ("Leverancier_2", "Product_5")
    ; ("Leverancier_2", "Product_3")
    ]

productNaam :: ProductNr -> Naam
  = [ ("Product_1", "Stuiterbal")
    ; ("Product_2", "Elastiekje")
    ; ("Product_3", "Rubberen kip")
    ; ("Product_4", "Pizza Margherita")
    ; ("Product_5", "Broodje Mario")
    ]

productPrijs :: ProductNr -> Prijs
  = [ ("Product_1", "0,50 euro")
    ; ("Product_2", "0,02 euro")
    ; ("Product_3", "6,95 euro")
    ; ("Product_4", "8,50 euro")
    ; ("Product_5", "4,50 euro")
    ]

bestellingDoor :: BestellingNr -> KlantNr   
--  = [ ("Bestelling_1", "Klant_1") ]
bestellingBij :: BestellingNr -> LeverancierNr   
--  = [ ("Bestelling_1", "Leverancier_1") ]
bestellingVan :: BestellingNr -> ProductNr
--  = [ ("Bestelling_1", "Product_1") ]


PROCESS Bestellen

bestellingGeaccepteerd :: BestellingNr * LeverancierNr [UNI] -- een bestelling kan nooit door meerdere leveranciers geaccepteerd zijn
--  = [ ("Bestelling_1", "Leverancier_1") ]

bestellingOntvangen :: BestellingNr * KlantNr [UNI] -- een bestelling kan nooit door meerdere klanten ontvangen zijn
--  = [ ("Bestelling_1", "Klant_1") ]


RULE bestellingInAssortiment : bestellingVan |- bestellingBij; levert 
MEANING "Een bestelling bij een leverancier moet in het assortiment zitten"
MESSAGE "Er is bestelling van een product dat niet bij de leverancier in het assortiment zit"

RULE accepteerAlleenEigen : bestellingGeaccepteerd |- bestellingBij
MEANING "Iedere leverancier kan alleen eigen bestellingen accepteren"
MESSAGE "Een leverancier heeft een bestelling bij een andere leverancier geaccepteerd"

RULE allesGeaccepteerd: I |- bestellingGeaccepteerd; bestellingGeaccepteerd~ -- == TOT
MEANING "Alle bestellingen zijn geaccepteerd"
MESSAGE "Nog niet alle bestellingen zijn geaccepteerd"

RULE ontvangAlleenEigen: bestellingOntvangen |- bestellingDoor
MEANING "Een klant kan alleen eigen bestellingen ontvangen"
MESSAGE "Een klant heeft een bestelling van een andere klant ontvangen"

RULE allesOntvangen: bestellingGeaccepteerd |- bestellingOntvangen; V
MEANING "Alle geaccepteerde bestellingen zijn ontvangen"
MESSAGE "Er is een geaccepteerde bestelling die nog niet is ontvangen"

ROLE Leverancier MAINTAINS allesGeaccepteerd

ROLE Klant MAINTAINS allesOntvangen

ENDPROCESS

ENDCONTEXT