CONTEXT Session
{- Simple script to showcase session functionality.

To login, go to interface Login, press Edit and add & fill out user + password fields. 
The Documents interface will only show the documents that the current user has access to.
The Edit account interface allows a user to change his/her account details, except for the username.

Currently, this script works.
Try logging in with two different users.
You won't be able to do that within one browser, but just open a second browser of a different brand (e.g. Chrome and Firefox) and you will find that it is possible.
-}

CONCEPT SESSION  "A session represents a user inside an information system. It is used to administer any activity in the system to the user who performs that activity."
CONCEPT User     "A user is a named individual who uses an information system by logging in."
CONCEPT Username "A username is a unique name by which a user is known inside the system"
CONCEPT Password "A password is a string that can be kept secret by a user, and must be used in order to obtain a new session."
CONCEPT Name     "Name"
CONCEPT Document "Document"

sessionUsername :: SESSION * UserName [UNI]
sessionPassword :: SESSION * Password [UNI]

username :: User -> UserName
POPULATION username[User*UserName] CONTAINS
  [ ("user_1", "martijn")
  ; ("user_2", "stef")
  ]
  
password :: User -> Password
POPULATION password[User*Password] CONTAINS
  [ ("user_1", "zz")
  ; ("user_2", "pw")
  ]

fullName :: User * Name [UNI]
POPULATION fullName[User*Name] CONTAINS
  [ ("user_1", "Martijn Schrage")
  ; ("user_2", "Stef Joosten")
  ]
  
documents :: User * Document [SUR]
POPULATION documents[User*Document] CONTAINS
  [ ("user_1", "Oblomov corporate secrets")
  ; ("user_1", "Martijn's thesis")
  ; ("user_1", "Bug-report guidelines")
  ; ("user_2", "Bug-report guidelines")
  ; ("user_2", "AMMBR paper")
  ]


PROCESS Login
RULE login: sessionUsername;username~ = sessionPassword;password~
MESSAGE "Incorrect password"
-- this rule guarantees that any user in sessionUsername will have entered a correct password
ROLE beheerder MAINTAINS "Every user has a name"
RULE "Every user has a name" : I |- fullName;fullName~

ENDPROCESS

INTERFACE "Login"(sessionUsername, sessionPassword) FOR user: V[ONE*SESSION];'_SESSION'
BOX [ "Username"  : sessionUsername
    , "Password"  : sessionPassword
    , "Currently logged in" : sessionUsername;username~;fullName
    ]

INTERFACE "Documents" FOR user: V[ONE*SESSION];'_SESSION'
BOX [ "Welcome"   : sessionUsername;username~;fullName
    , "Documents" : sessionUsername;username~;documents
    ]

INTERFACE "Edit account"(fullName, password, sessionPassword) FOR user : V[ONE*SESSION];'_SESSION';sessionUsername;username~
BOX [ "Username"  : username
    , "Full name" : fullName
    , "Password"  : password
    , "Session"   : username;sessionUsername~            -- this is a bit of a hack: to allow changing the
      BOX [ "Repeat password" : sessionPassword          -- password, the sessionPassword needs to change
          ]                                              -- simultaneously, or rule login will fail.   
    ]

INTERFACE "Internals" FOR beheerder: I[ONE]
BOX [ "Active sessions" : V[ONE*SESSION]
      BOX [ "Session id" : I[SESSION]
          , "Username"   : sessionUsername
          ]
    ]

INTERFACE "New user" (username,fullName,password) FOR beheerder: I[User]
      BOX [ "Username"  : username
          , "Full name" : fullName
          , "Password"  : password
          ]

ENDCONTEXT