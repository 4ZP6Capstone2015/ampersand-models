CONTEXT Session
{- Simple script to showcase session functionality, linked to Ticket #375.

To login, go to interface Login, press Edit and add & fill out user + password fields. 
The Documents interface will only show the documents that the current user has access to.
The Edit account interface allows a user to change his/her account details, except for the username.

Currently, this script does not work, because Ampersand does not allow the user to specify the current session.
The problem is this: try logging in with two different users.
You won't be able to do that within your browser, but just open a second browser and you will find that it is possible.
That is because different browsers get different session identifiers, so all of a sudden there are two sessions.
However, there is no way to restrict each session to its own data.
Suppose we had an atom '_SESSION'[SESSION], which would exist within an interface on run time,
we would most likely have a properly working system...
-}

CONCEPT SESSION  "Session"
CONCEPT User     "User"
CONCEPT Username "Username"
CONCEPT Password "Password"
CONCEPT Name     "Name"
CONCEPT Document "Document"

sessionUsername :: SESSION * UserName [UNI]
sessionPassword :: SESSION * Password [UNI]

username :: User -> UserName
POPULATION username[User*UserName] CONTAINS
  [ ("user_1", "martijn")
  ; ("user_2", "stef")
  ]
  
password :: User -> Password
POPULATION password[User*Password] CONTAINS
  [ ("user_1", "zz")
  ; ("user_2", "pw")
  ]

fullName :: User * Name [UNI]
POPULATION fullName[User*Name] CONTAINS
  [ ("user_1", "Martijn Schrage")
  ; ("user_2", "Stef Joosten")
  ]
  
documents :: User * Document [SUR]
POPULATION documents[User*Document] CONTAINS
  [ ("user_1", "Oblomov corporate secrets")
  ; ("user_1", "Martijn's thesis")
  ; ("user_1", "Bug-report guidelines")
  ; ("user_2", "Bug-report guidelines")
  ; ("user_2", "AMMBR paper")
  ]


PROCESS Login
RULE login: sessionUsername;username~ = sessionPassword;password~
MESSAGE "Incorrect password"
-- this rule guarantees that any user in sessionUsername will have entered a correct password
ROLE beheerder MAINTAINS "Every user has a name"
RULE "Every user has a name" : I |- fullName;fullName~

ENDPROCESS

INTERFACE "Login"(sessionUsername, sessionPassword) FOR user: V[ONE*SESSION];'_SESSION'
BOX [ "Username"  : sessionUsername
    , "Password"  : sessionPassword
    , "Currently logged in" : sessionUsername;username~;fullName
    ]
    
INTERFACE "Documents" FOR user: V[ONE*SESSION];'_SESSION'
BOX [ "Welcome"   : sessionUsername;username~;fullName
    , "Documents" : sessionUsername;username~;documents
    ]

INTERFACE "Edit account"(fullName, password, sessionPassword) FOR user : V[ONE*SESSION];'_SESSION';sessionUsername;username~
BOX [ "Username"  : username
    , "Full name" : fullName
    , "Password"  : password
    , "Session"   : username;sessionUsername~            -- this is a bit of a hack: to allow changing the
      BOX [ "Repeat password" : sessionPassword          -- password, the sessionPassword needs to change
          ]                                              -- simultaneously, or rule login will fail.   
    ]

INTERFACE "Internals" FOR beheerder: I[ONE]
BOX [ "Active sessions" : V[ONE*SESSION]
      BOX [ "Session id" : I[SESSION]
          , "Username"   : sessionUsername
          ]
    ]

INTERFACE "New user" (username,fullName,password) FOR beheerder: I[User]
      BOX [ "Username"  : username
          , "Full name" : fullName
          , "Password"  : password
          ]

ENDCONTEXT