CONTEXT OnderwijsInspectie
-- INCLUDE "Stamtabellen.adl"
INCLUDE "basisonderwijs.adl" 
INCLUDE "toezichtPopulaties.adl"
LATEX

META "authors" "Stefan Hersbach"
META "authors" "Han Joosten"
META "authors" "Stef Joosten"
META "authors" "Richard ter Mors"


THEMES "Toezicht"
THEMES "Sanctionering"
THEMES "Handhaving"
PURPOSE CONTEXT OnderwijsInspectie REF "www.onderwijsinspectie.nl"
{+Elk kind heeft recht op goed onderwijs. Leerlingen, studenten en ouders moeten erop kunnen vertrouwen dat het onderwijs op een school goed is. Het schoolbestuur is verantwoordelijk voor de kwaliteit van het onderwijs en moet zich verantwoorden over de resultaten. Hierbij gaat het om resultaten in brede zin; krijgen alle leerlingen onderwijs van voldoende kwaliteit, voldoen scholen aan wet- en regelgeving en hebben ze hun financiën op orde? De Inspectie van het Onderwijs houdt hierop toezicht. Daarnaast rapporteert ze gevraagd en ongevraagd over ontwikkelingen binnen het onderwijs, met als doel het onderwijs als geheel te verbeteren.
-}

PATTERN "Toezicht"
PURPOSE PATTERN Toezicht REF "www.onderwijsinspectie.nl"
{+De inspectie bewaakt de kwaliteit van het onderwijs op individuele scholen in het primair, voortgezet en speciaal onderwijs. Ze houdt ook toezicht op de kwaliteit van opleidingen in het beroepsonderwijs en de volwasseneneducatie en het stelsel van het hoger onderwijs.

Toezicht is het verzamelen van informatie over de vraag of een handeling of zaak voldoet aan de daaraan gestelde eisen, het zich daarna vormen van een oordeel daarover en het eventueel naar aanleiding daarvan interveniëren.

Het toezicht van de inspectie is risicogericht. Dat wil zeggen: scholen met risico's, zoals minder goede leerresultaten, krijgen meer toezicht.-}

bevoegdTotControleVan :: Inspecteur * Voorschrift
PRAGMA "" " bevoegd tot controle van "
MEANING "Een inspecteur kan bevoegd zijn tot het beoordelen van naleving van een voorschrift"
PURPOSE RELATION bevoegdTotControleVan[Inspecteur * Voorschrift] REF "Artikel 3 lid 1"
{+Het toezicht op het onderwijs is opgedragen aan de inspectie van het onderwijs. Zij houdt middels haar inspecteurs toezicht op de naleving van het bepaalde wettelijke voorschriften.-}

naar :: Onderzoek * OvT 
PRAGMA "" " naar "

geschrevenDoor :: Rapport * Inspecteur [UNI,TOT]
PRAGMA "" " geschreven door " 
MEANING "Elk rapport is geschreven onder verantwoordelijkheid van één inspecteur."
PURPOSE RELATION geschrevenDoor[Rapport * Inspecteur] REF "Artikel 20"
{+De inspectie verricht regulier onderzoek of nader onderzoek zoals beschreven in artikel 20 van de WOT. De resultaten van dergelijke onderzoeken worden vastgelegd in een rapport. Hoewel het onderzoek gedaan kan zijn door een team van inspecteurs, is er steeds één die de eindverantwoordelijkheid van het rapport heeft. -}

leidtTot :: Onderzoek * Rapport [UNI]
PRAGMA " leidt tot "
MEANING "Elk onderzoek leidt uiteindelijk tot één rapport"
PURPOSE RELATION leidtTot [Onderzoek * Rapport] REF "Artikel 20 lid 1"
{+Het oordeel naar aanleiding van een onderzoek worden vastgesteld in een inspectierapport.-}

van ::  Tekortkoming * Voorschrift [UNI,TOT]
PRAGMA "" " van " 
MEANING "Elke tekortkoming is van één voorschrift."
PURPOSE RELATION van[Tekortkoming * Voorschrift] REF "Artikel 20 lid 2"
{+Indien naar het oordeel van de inspecite een voorschrift niet is nageleefd, dan moet dat in het rapport worden vermeld. Wanneer dit voor meerdere voorschriften geldt, dan zal voor elk van die voorschriften een vermelding worden opgenomen. Dit impliceert, dat elke tekortkoming 
over een enkel voorschrift gaat.-}

isVermeldIn :: Tekortkoming * Rapport [UNI,TOT]
PRAGMA "" " is vermeld in " 
MEANING "Elke tekortkoming staat vermeld in een inspectierapport"
PURPOSE RELATION isVermeldIn [Tekortkoming * Rapport] REF "Artikel 20 lid 2"
{+In het rapport worden de onderzoeksconclusies vastgelegd. Indien de inspectie oordeelt dat een bij of krachtens een onderwijswet gegeven voorschrift niet is nageleefd, is er sprake van een tekortkoming. Deze worden vermeld in het inspectierapport.-} 

betreft :: Tekortkoming * OvT [TOT,UNI]
PRAGMA "" " betreft "
MEANING "Elke tekortkoming heeft betrekking op één object van toezicht."
PURPOSE RELATION betreft [Tekortkoming*OvT] 
{+Wanneer tijdens een onderzoek wordt geconstateerd dat een object van toezicht een bepaald voorschrift niet wordt nageleefd, dan wordt deze tekortkoming opgenomen in het inspectierapport. Omdat duidelijk moet zijn waarop de tekortkoming betrekking heeft, wordt dit vastgelegd.-}


omschrijving :: Tekortkoming * Tekst [UNI]
PRAGMA "" " omschreven als "

onderdeelVan :: OvT * OvT 
PRAGMA "" " valt onder "
MEANING "Een OvT is onderdeel van een structuur met meerdere OvT elementen."  
PURPOSE RELATION betreft [Tekortkoming*OvT] REF "IBIS documentatie"
{+Theoretisch kan een onderzoek over meedere elementen worden gedaan. Een object van toezicht (OvT) is onderdeel van een organisatorische hiërarchie. In deze structuur van elementen komen vervolgens meerdere OvT's voor.-}

betreft :: Rapport * OvT [TOT]
PRAGMA  "" " betreft " 
MEANING "Elk rapport wordt gecommuniceerd met één object van toezicht."
PURPOSE RELATION betreft [Rapport * OvT] 
{+De conclusies uit het onderzoek gaan over een of meerdere samenhangende objecten van toezicht. Er is echter steeds één object van toezicht waaraan wordt gecommuniceerd. 
Indien er in het onderzoek meerdere objecten van toezicht worden meegenomen, dan vallen die objecten van toezicht daar allemaal onder.-}

vastgesteld :: Rapport * Datum [UNI] 
PRAGMA "" " is vastgesteld op "
MEANING "Elk rapport kan worden vastgesteld op een datum."
PURPOSE RELATION vastgesteld [Rapport * Datum] REF "Toezichtkader PO/VO 2012"
{+Ieder onderzoek wordt binnen zeven werkweken afgesloten met een conceptrapport. Het bestuur krijgt vier werkweken de gelegenheid op het conceptrapport te reageren. Na hoor- en wederhoor wordt het definitieve rapport door de inspectie vastgesteld.-}

escaleren :: Tekortkoming * JaNee [UNI]
MEANING "Een tekortkoming die niet tijdig is hersteld kan worden geescaleerd."
PURPOSE RELATION escaleren [Tekortkoming*JaNee] REF "Toezichtkader PO/VO 2012"
{+Een tekortkoming kan ertoe leiden dat de instelling van de inspectie een opdracht tot herstel krijgt. Als dat herstel niet of onvoldoende tijdig plaatsvindt kan deze worden geescaleerd. De precieze inhoud van die escalatie is afhankelijk van de wettelijke regel die overtreden wordt. -}

KEY Tekortkoming : Tekortkoming (TXT " vermelding van ", van, TXT " betreft ", betreft )



--- Concepten ---
  CONCEPT Inspecteur "Een inspecteur is een opziener, toezichthouder: inspecteur van het onderwijs." "van Dale"
  PURPOSE CONCEPT Inspecteur LATEX
  {+Een van de taken van de inspectie is het houden van toezicht op individuele scholen en instellingen. Dit gebeurt door personen die daartoe zijn aangesteld.-}

  CONCEPT Voorschrift "Een voorschrift is een aanwijzing waaraan men zich moet houden." "van Dale"
  PURPOSE CONCEPT Voorschrift LATEX
  {+Het houden van toezicht gaat over het beoordelen en bevorderen van de naleving van bij of krachtens een onderwijswet gegeven voorschriften. -}

  CONCEPT OvT "Een object van toezicht is een (deel van een) school of instelling, of een bevoegd bestuur." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT OvT LATEX
  {+De inspectie van het onderwijs houdt toezicht op de kwaliteit van het onderwijs in Nederland. Hieronder valt het toezicht op scholen en instellingen en haar bestuur. Bij instellingen voor beroeps- en volwasseneneducatie wordt vaak ook nog onderscheid gemaakt naar de verschillende onderwijsrichtingen.-}

  CONCEPT Tekortkoming "Een tekortkoming is datgene waarin iemand niet aan de eisen of verwachtingen voldoet" "van Dale"
  PURPOSE CONCEPT Tekortkoming
  {+Tijdens een onderzoek kan blijken dat een object van toezicht zich niet houdt aan een voorschrift. -}

  CONCEPT Rapport "Een rapport is een inspectierapport dat door een inspecteur opgesteld wordt na onderzoek op een object van toezicht." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT Rapport LATEX
  {+In het rapport wordt het oordeel van de inspectie vastgelegd naar aanleiding van het onderzoek.-}
  
  SPEC Instelling ISA OvT
  SPEC Bestuur ISA OvT

--  RULE "object van toezicht" : I[OvT] |- I[Bestuur] \/ I[Instelling]
--  MEANING "Elk object van toezicht is ofwel een bestuur ofwel een instelling, maar niet beide." 

ENDPATTERN

PROCESS Toezichthouden
  ROLE Inspecteur MAINTAINS "Vaststellen Rapport"
  RULE "Vaststellen Rapport" : I[Onderzoek] |- leidtTot;vastgesteld;vastgesteld~;leidtTot~
  MEANING "Elk onderzoek leidt tot een rapport dat is vastgesteld" 
  MESSAGE "Artikel 20 wot zegt dat de onderzoeksresultaten moeten worden vastgesteld"
  VIOLATION (TXT "Voor ", SRC I[Onderzoek], TXT " bestaat nog geen vastgesteld rapport.")

  ROLE HoofdInspecteur MAINTAINS "Escalaties"
  RULE "Escalaties" : escaleren |- gevolgVan~;gevolgVan;escaleren
  MEANING "De Hoofdinspecteur beoordeelt tekortkomingen die daartoe zijn aangewezen"
  MESSAGE "De volgende tekortkomingen zijn voorgedragen voor een sanctietraject"
  VIOLATION ( TXT "Er is nog geen sanctietraject gestart voor ",SRC I[Tekortkoming])

  ROLE "Juridisch medewerker" MAINTAINS "Opstellen voorgenomen besluit"
  RULE "Opstellen voorgenomen besluit" : I[Sanctietraject] |- totOpleggingVan~;totOpleggingVan
  MEANING "Voor elk sanctietraject wordt een voorlopig besluit opgesteld"
  VIOLATION (TXT "Voor " , SRC I , TXT " is nog geen voorlopig besluit opgesteld ")
ENDPROCESS 


PATTERN "Handhaving"
PURPOSE PATTERN Handhaving REF "Jaarwerkplan 2012"
{+Handhaving is voor de inspectie een breed begrip. Het omvat alle activiteiten die 
erop gericht zijn dat het onderwijs voldoet aan de eisen die eraan worden gesteld. 
Daarbij vormen de kwaliteit van het onderwijs en het naleven van wettelijke 
voorschriften de belangrijkste aangrijpingspunten. 

In veruit de meeste gevallen volstaat dat de inspectie de tekortkomingen signaleert 
en het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. De school brengt de 
verbeteringen aan en de inspectie onderzoekt het resultaat ervan. Dit is het 
zogenaamde aangepaste toezicht. Dit duurt tot de tekortkomingen in voldoende 
mate zijn opgeheven.

Bij erngstigere tekortkomingen kan de inspectie 
ook gebruik maken van de wettelijke handhavingsmogelijkheden die daarmee 
samenhangen. -}

--PURPOSE CONTEXT OnderwijsInspectie REF "www.rijksoverheid.nl"
--{+De Algemene wet bestuursrecht (afgekort Awb) is een Nederlandse wet die de algemene regels bevat voor de verhouding tussen de overheid en de individuele burgers, bedrijven. 
--De Awb vermeldt hoe een besluit moet worden voorbereid, hoe het moet worden bekendgemaakt en binnen welke termijn een besluit genomen moet worden.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met Awb als referentie, wordt de Algemene wet bestuursrecht bedoeld.
--In de Wet op het onderwijstoezicht (afgekort WOT) is vastgelegd op welke de manier toezicht wordt gehouden op de kwaliteit van het onderwijs in Nederland. 
--Hierin staat dat scholen in de eerste plaats zelf verantwoordelijk zijn voor de kwaliteit van het onderwijs en dat de Inspectie van het Onderwijs hierop toezicht.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met WOT als referentie, wordt de  Wet op het onderwijstoezicht bedoeld.-}




nav :: Verbeterafspraak * Tekortkoming
PRAGMA "voor " " is afgesproken "
MEANING "Elke verbeterafspraak is gemaakt naar aanleiding van een tekortkoming."
PURPOSE RELATION nav [Verbeterafspraak*Tekortkoming] REF "Jaarplan 2012"
{+In veel gevallen volstaat het dat de inspectie het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. Omdat een afspraak tot verbetering altijd een aanleiding heeft, bestaat de volgende afspraak.-}


met :: Verbeterafspraak * OvT
PRAGMA "" " gemaakt met "
MEANING "Elke verbeterafspraak is gemaakt met een bestuur of met de leiding van een instelling."
PURPOSE RELATION met[Verbeterafspraak * OvT]
{+Omdat het van belang is om te weten met wie een verbeterafspraak is gemaakt, leggen we dat vast.-}

opgeheven :: Tekortkoming * Tekortkoming [PROP]
MEANING "Een tekortkoming kan zijn opgeheven." 
PURPOSE RELATION opgeheven REF "Jaarplan 2012" 
{+Wanneer de verbeteringen zijn aangebracht kan de inspectie vaststellen dat 
de tekortkoming in voldoende mate is opgeheven. Het moet mogelijk zijn om dat te registreren.-}

RULE afspreken : I[Tekortkoming] |- nav~;met;betreft~;isVermeldIn~
MEANING "Voor elke tekortkoming, vermeld in een rapport betreffende een instelling of bestuur, dient een verbeterafspraak te zijn gemaakt met die instelling of bestuur." 


--- CONCEPTEN ---
 
ENDPATTERN
--isVermeldIn :: Overtreding * Rapport
--PRAGMA "" " is vermeld in " 
--MEANING "Als er overtredingen worden vastgesteld tijdens de controle dan worden deze vastgelegd in een rapport."
--PURPOSE RELATION isVermeldIn[Overtreding * Rapport] REF "Awb Artikel 5 lid 48.1"
--{+Het bestuursorgaan en de voor de overtreding bevoegde toezichthouder kunnen van de overtreding een rapport opmaken.-}



PATTERN Sanctionering
PURPOSE PATTERN Sanctionering REF "www.onderwijsinspectie.nl"
{+Handhaving omvat een groot aantal activiteiten die erop zijn gericht te bevorderen dat scholen de regels naleven. Als wij de juiste voorwaarden hebben geschapen voor scholen om conform de regels te opereren, komt eventueel een stringentere invulling van het handhavingsbeleid aan de orde bij die scholen waar dat niet bij helpt. We praten dan over sanctioneren.

Het opleggen van bestuurlijke sancties in geregeld in hoofdstuk 5 van de Algemene wet bestuursrecht.
-}

gevolgVan :: Sanctietraject -> Tekortkoming
PRAGMA "" " gevolgVan " 
MEANING "Elke sanctie is het gevolg van één tekortkoming."
PURPOSE RELATION gevolgVan[Sanctietraject * Tekortkoming] REF "Awb Artikel 5 lid4.2"
{+Een bestuurlijke sanctie wordt opgelegd indien de overtreding en de sanctie bij of krachtens een aan de gedraging voorafgaand wettelijk voorschrift zijn omschreven. 
Daarom moet bij elke sanctie de overtreding worden vermeld.-}

deadlineTraject :: Sanctietraject * Datum [TOT,UNI]

voorgenomen :: Beschikking * Datum [UNI] 
MEANING "De datum van het voorgenomen besluit" 

deadlineZienswijze :: Beschikking * Datum [UNI]
MEANING "De uiterste datum waarop een zienswijze bij deze beschikking moet zijn ontvangen."

initieel :: Sanctietraject * FinancieelBalang [UNI]
MEANING "Een sanctie kan een initieel financieel belang hebben." 

definitief :: Sanctietraject * FinancieelBalang [UNI]
MEANING "Een sanctie kan een definitief financieel belang hebben." 

op :: Beschikking * Zienswijze [UNI]
MEANING "Op een voorlopige beschikking kan één zienswijze zijn gegeven."

einddatum :: Sanctietraject * Datum [UNI]
MEANING "Een sanctietraject kan op een datum zijn afgesloten."

definitief :: Beschikking * Datum [UNI]
MEANING "Een besluit kan op één datum definitief zijn gemaakt."

totOpleggingVan :: Beschikking * Sanctietraject [UNI,TOT]
PRAGMA "" " tot oplegging van " 
MEANING "Elke beschikking is tot oplegging van één sanctie."
PURPOSE RELATION totOpleggingVan[Beschikking * Sanctietraject] REF "Awb Artikel 5 lid 9"
{+Een sanctie wordt opgelegd middels een beslissing, die formeel wordt vastgelegd in een beschikking tot oplegging van een sanctie.-}



type :: Sanctietraject -> SanctieType
PRAGMA "" " is van het type "
MEANING "Elke sanctie heeft een type." 



ENDPATTERN

ENDCONTEXT
