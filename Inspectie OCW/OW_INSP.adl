CONTEXT OnderwijsInspectie
INCLUDE "Stamtabellen.adl"
INCLUDE "basisonderwijs.adl" 

LATEX

META "authors" "Stefan Hersbach"
META "authors" "Han Joosten"
META "authors" "Stef Joosten"
META "authors" "Richard ter Mors"


THEMES "Toezicht"
THEMES "Sanctionering"
THEMES "Handhaving"
PURPOSE CONTEXT OnderwijsInspectie REF "www.onderwijsinspectie.nl"
{+Elk kind heeft recht op goed onderwijs. Leerlingen, studenten en ouders moeten erop kunnen vertrouwen dat het onderwijs op een school goed is. Het schoolbestuur is verantwoordelijk voor de kwaliteit van het onderwijs en moet zich verantwoorden over de resultaten. Hierbij gaat het om resultaten in brede zin; krijgen alle leerlingen onderwijs van voldoende kwaliteit, voldoen scholen aan wet- en regelgeving en hebben ze hun financiën op orde? De Inspectie van het Onderwijs houdt hierop toezicht. Daarnaast rapporteert ze gevraagd en ongevraagd over ontwikkelingen binnen het onderwijs, met als doel het onderwijs als geheel te verbeteren.
-}

PATTERN "Toezicht"
PURPOSE PATTERN Toezicht REF "www.onderwijsinspectie.nl"
{+De inspectie bewaakt de kwaliteit van het onderwijs op individuele scholen in het primair, voortgezet en speciaal onderwijs. Ze houdt ook toezicht op de kwaliteit van opleidingen in het beroepsonderwijs en de volwasseneneducatie en het stelsel van het hoger onderwijs.

Het toezicht van de inspectie is risicogericht. Dat wil zeggen: scholen met risico's, zoals minder goede leerresultaten, krijgen meer toezicht.-}

bevoegdTotControleVan :: Inspecteur * Voorschrift
PRAGMA "" " bevoegd tot controle van " 
MEANING "Een inspecteur kan bevoegd zijn tot het beoordelen van naleving van een voorschrift"
PURPOSE RELATION bevoegdTotControleVan[Inspecteur * Voorschrift] REF "WOT Artikel 3 lid 1"
{+Het toezicht op het onderwijs is opgedragen aan de inspectie van het onderwijs. Zij houdt middels haar inspecteurs toezicht op de naleving van het bepaalde wettelijke voorschriften.-}


geschrevenDoor :: Rapport * Inspecteur [UNI,TOT]
PRAGMA "" " geschreven door " 
MEANING "Elk rapport is geschreven door één inspecteur."
PURPOSE RELATION geschrevenDoor[Rapport * Inspecteur] REF "Aanname"
{+Artikel WOT 5.20 meldt: De inspectie legt haar oordeel naar aanleiding van een onderzoek als bedoeld in artikel 11, dan wel artikel 12a, vast in een inspectierapport. De aanname hierbij is dat de onder verantwoordelijkheid van een inspecteur het rapport wordt opgemaakt.-}

isVermeldIn :: Tekortkoming * Rapport [UNI,TOT]
PRAGMA "" " is vermeld in " 
MEANING "Elke tekortkoming staat vermeld in een inspectierapport"
PURPOSE RELATION isVermeldIn [Tekortkoming * Rapport] REF "WOT Artikel 25 lid 20.2"
{+Indien de inspectie oordeelt dat een bij of krachtens een onderwijswet gegeven voorschrift niet is nageleefd, is er sprake van een tekortkoming. Deze worden vermeld in het inspectierapport.-} 

betreft :: Rapport * Onderzoeksobject [UNI,TOT]
PRAGMA  " betreft " 
MEANING "Elk rapport betreft één onderzoeksobject."
PURPOSE RELATION betreft [Rapport * Onderzoeksobject]
{+Omdat een rapport de weergave is van een onderzoek naar een onderzoeksobject, gaat elk rapport over één object van onderzoek-}


--- Concepten ---
  CONCEPT Inspecteur "Een inspecteur is een opziener, toezichthouder: inspecteur van het onderwijs." "van Dale"
  PURPOSE CONCEPT Inspecteur LATEX
  {+Een van de taken van de inspectie is het houden van toezicht op individuele scholen en instellingen. Dit gebeurt door personen die daartoe zijn aangesteld.-}

  CONCEPT Voorschrift "Een voorschrift is een aanwijzing waaraan men zich moet houden." "van Dale"
  PURPOSE CONCEPT Voorschrift LATEX
  {+Het houden van toezicht impliceert dat er regels zijn die geacht worden te worden nageleefd. Deze voorschrifen zijn veelal in onderwijswetten vastgelegd.-}

  CONCEPT Onderzoeksobject "Een onderzoeksobject is een school of instelling, of een bevoegd bestuur." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT Onderzoeksobject LATEX
  {+De inspectie van het onderwijs houdt toezicht op de kwaliteit van het onderwijs in Nederland. Hieronder valt het toezicht op scholen en instellingen en haar bestuur.-}

  CONCEPT Onderzoek "Een onderzoek is de handeling van onderzoeken." "van Dale"
  PURPOSE CONCEPT Onderzoek LATEX
  {+De toezichtactiviteiten met betrekking tot het nalevingen van de geldende voorschriften bij scholen en instellingen wordt door een inspecteur gedaan. Een van de instrumenten hierbij is een kwaliteitsonderzoek. -}
  
  CONCEPT Rapport "Een rapport is een inspectierapport dat door een inspecteur opgesteld wordt na onderzoek op een school of bij een schoolbestuur." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT Rapport LATEX
  {+In het rapport wordt het oordeel van de inspectie vastgelegd naar aanleiding van het onderzoek. -}
  
  SPEC Instelling ISA Onderzoeksobject
  SPEC Bestuur ISA Onderzoeksobject

  RULE onderzoeksobject : I[Onderzoeksobject] |- I[Bestuur] \/ I[Instelling]
  MEANING "Elk onderzoeksobject is ofwel een bestuur ofwel een instelling, maar niet beide." 

ENDPATTERN

PATTERN "Handhaving"
PURPOSE PATTERN Handhaving REF "Jaarwerkplan 2012"
{+Handhaving is voor de inspectie een breed begrip. Het omvat alle activiteiten die 
erop gericht zijn dat het onderwijs voldoet aan de eisen die eraan worden gesteld. 
Daarbij vormen de kwaliteit van het onderwijs en het naleven van wettelijke 
voorschriften de belangrijkste aangrijpingspunten. 

In veruit de meeste gevallen volstaat dat de inspectie de tekortkomingen signaleert 
en het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. De school brengt de 
verbeteringen aan en de inspectie onderzoekt het resultaat ervan. Dit is het 
zogenaamde aangepaste toezicht. Dit duurt tot de tekortkomingen in voldoende 
mate zijn opgeheven.

Bij erngstigere tekortkomingen kan de inspectie 
ook gebruik maken van de wettelijke handhavingsmogelijkheden die daarmee 
samenhangen. -}

--PURPOSE CONTEXT OnderwijsInspectie REF "www.rijksoverheid.nl"
--{+De Algemene wet bestuursrecht (afgekort Awb) is een Nederlandse wet die de algemene regels bevat voor de verhouding tussen de overheid en de individuele burgers, bedrijven. 
--De Awb vermeldt hoe een besluit moet worden voorbereid, hoe het moet worden bekendgemaakt en binnen welke termijn een besluit genomen moet worden.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met Awb als referentie, wordt de Algemene wet bestuursrecht bedoeld.
--In de Wet op het onderwijstoezicht (afgekort WOT) is vastgelegd op welke de manier toezicht wordt gehouden op de kwaliteit van het onderwijs in Nederland. 
--Hierin staat dat scholen in de eerste plaats zelf verantwoordelijk zijn voor de kwaliteit van het onderwijs en dat de Inspectie van het Onderwijs hierop toezicht.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met WOT als referentie, wordt de  Wet op het onderwijstoezicht bedoeld.-}




nav :: Verbeterafspraak*Tekortkoming
PRAGMA "voor " " is afgesproken "
MEANING "Elke verbeterafspraak is gemaakt naar aanleiding van een tekortkoming."
PURPOSE RELATION nav [Verbeterafspraak*Tekortkoming] REF "Jaarplan 2012"
{+In veel gevallen volstaat het dat de inspectie het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. Omdat een afspraak tot verbetering altijd een aanleiding heeft, bestaat de volgende afspraak.-}


met :: Verbeterafspraak -> Onderzoeksobject
PRAGMA "" " gemaakt met "
MEANING "Elke verbeterafspraak is gemaakt met een bestuur of met de leiding van een instelling."
PURPOSE RELATION met[Verbeterafspraak * Onderzoeksobject]
{+Omdat het van belang is om te weten met wie een verbeterafspraak is gemaakt, leggen we dat vast.-}

opgeheven :: Tekortkoming * Tekortkoming [PROP]
MEANING "Een tekortkoming kan in voldoende mate zijn opgeheven." 
PURPOSE RELATION opgeheven REF "Jaarplan 2012" 
{+Wanneer de verbeteringen zijn aangebracht kan de inspectie vaststellen dat 
de tekortkoming in voldoende mate is opgeheven. Het moet mogelijk zijn om dat te registreren.-}

RULE afspreken : I[Tekortkoming] |- nav~;met;betreft~;isVermeldIn~
MEANING "Voor elke tekortkoming, vermeld in een rapport betreffende een instelling of bestuur, dient een verbeterafspraak te zijn gemaakt met die instelling of bestuur." 


--- CONCEPTEN ---
CONCEPT Tekortkoming "Een tekortkoming is datgene waarin iemand niet aan de eisen of verwachtingen voldoet" "van Dale"

 
ENDPATTERN
isVermeldIn :: Overtreding * Rapport
PRAGMA "" " is vermeld in " 
MEANING "Als er overtredingen worden vastgesteld tijdens de controle dan worden deze vastgelegd in een rapport."
PURPOSE RELATION isVermeldIn[Overtreding * Rapport] REF "Awb Artikel 5 lid 48.1"
{+Het bestuursorgaan en de voor de overtreding bevoegde toezichthouder kunnen van de overtreding een rapport opmaken.-}


PATTERN Sanctionering
PURPOSE PATTERN Sanctionering REF "www.onderwijsinspectie.nl"
{+Handhaving omvat een groot aantal activiteiten die erop zijn gericht te bevorderen dat scholen de regels naleven. Als wij de juiste voorwaarden hebben geschapen voor scholen om conform de regels te opereren, komt eventueel een stringentere invulling van het handhavingsbeleid aan de orde bij die scholen waar dat niet bij helpt. We praten dan over sanctioneren.

Het opleggen van bestuurlijke sancties in geregeld in hoofdstuk 5 van de Algemene wet bestuursrecht.
-}

opgelegdDoor :: Sanctie -> Bestuursorgaan
PRAGMA "" " opgelegd door " 
MEANING "Elke sanctie wordt opgelegd door één bestuursorgaan."
PURPOSE RELATION opgelegdDoor[Sanctie * Bestuursorgaan] REF "Awb Artikel 5 lid 2"
{+Een bestuurlijke sanctie wordt opgelegd door een bestuursorgaan. In deze context is dat de Inspectie van het Onderwijs.-}

type :: Sanctie -> SanctieType
PRAGMA "" " is van het type "
MEANING "Elke sanctie heeft een type." 

gevolgVan :: Sanctie -> Overtreding
PRAGMA "" " gevolgVan " 
MEANING "Elke sanctie is het gevolg van één overtreding."
PURPOSE RELATION gevolgVan[Sanctie * Overtreding] REF "Awb Artikel 5 lid4.2"
{+Een bestuurlijke sanctie wordt opgelegd indien de overtreding en de sanctie bij of krachtens een aan de gedraging voorafgaand wettelijk voorschrift zijn omschreven. 
Daarom moet bij elke sanctie de overtreding worden vermeld.-}


iseen :: Overtreding * Tekortkoming [TOT,UNI]
PRAGMA "" " is een " 
MEANING "Een overtreding is een tekortkoming, die als overtreding in de zin van de AWB als zodanig is aangewezen."
PURPOSE RELATION iseen[Overtreding * Tekortkoming] REF "Jaarplan 2012 p.5"
{+Omdat niet elke tekortkoming wordt gezien als een overtreding in de zin van de Awb, moet er onderscheid worden gemaakt tussen tekortkomingen die uit het toezichtproces komen, en tekortkomingen waarvoor een formeel sanctioneringstraject wordt gestart.-}


betalingsverplichting :: Sanctie * Geldsom [UNI]
PRAGMA "" " betalingsverplichting " 
MEANING "Een sanctie kan een betalingsverplichting inhouden."
PURPOSE RELATION betalingsverplichting[Sanctie * Geldsom] REF "Awb Artikel 5 lid 10"
{+Voor zover een bestuurlijke sanctie verplicht tot betaling van een geldsom, komt deze geldsom toe aan het bestuursorgaan dat de sanctie heeft opgelegd, tenzij bij wettelijk voorschrift anders is bepaald. Dit impliceert dat een sanctie een betalingsverplichting kan inhouden. -}

totOpleggingVan :: Beschikking -> Sanctie
PRAGMA "" " tot oplegging van " 
MEANING "Elke beschikking is tot oplegging van één sanctie."
PURPOSE RELATION totOpleggingVan[Beschikking * Sanctie] REF "Awb Artikel 5 lid 9"
{+Een sanctie wordt opgelegd middels een beslissing, die formeel wordt vastgelegd in een beschikking tot oplegging van een sanctie.-}

van ::  Overtreding -> Voorschrift
PRAGMA "" " van " 
MEANING "Elke overtreding is van één voorschrift."
PURPOSE RELATION van[Overtreding * Voorschrift] REF "Awb Artikel 5 lid 8"
{+Indien twee of meer voorschriften zijn overtreden, kan voor de overtreding van elk afzonderlijk voorschrift een bestuurlijke sanctie worden opgelegd. Dit suggereert dat elke overtreding een overtreding van precies één voorschrift is.-}

vermeldt :: Beschikking * Overtreding
PRAGMA "" " vermeldt " 
MEANING "Een beschikking vermeldt één overtreding."
PURPOSE RELATION vermeldt[Beschikking * Overtreding] REF "Awb Artikel 5 lid 9"
{+De beschikking tot oplegging van een bestuurlijke sanctie vermeldt de overtreding.-}

vermeldt :: Beschikking -> Voorschrift
PRAGMA "" " vermeldt " 
MEANING "Elke beschikking vermeldt één voorschrift."
PURPOSE RELATION vermeldt[Beschikking*Voorschrift] REF "Awb Artikel 5 lid 9"
{+De beschikking tot oplegging van een bestuurlijke sanctie vermeldt het overtreden voorschrift.-}


geconstateerdOp :: Overtreding * Tijdstip [UNI]
PRAGMA "" " geconstateerd op " 
MEANING "Een overtreding kan op een tijdstip zijn geconstateerd."
PURPOSE RELATION geconstateerdOp[Overtreding * Tijdstip] REF "Awb Artikel 5 lid 9"
{+De beschikking tot oplegging van een bestuurlijke sanctie vermeldt zo nodig een aanduiding van het tijdstip waarop de overtreding is geconstateerd. -}

geconstateerdTe :: Overtreding * Plaats [UNI]
PRAGMA "" " geconstateerd te " 
MEANING "Een overtreding kan geconstateerd zijn op een locatie."
PURPOSE RELATION geconstateerdTe[Overtreding * Plaats] REF "Awb Artikel 5 lid 9"
{+De beschikking tot oplegging van een bestuurlijke sanctie vermeldt zo nodig een aanduiding van de plaats waar de overtreding is geconstateerd.-}

verklaart :: Rechtvaardigheidsgrond * Overtreding
PRAGMA "" " verklaart " 
MEANING "Elke overtreding kan een rechtvaardigheidsgrond hebben."
PURPOSE RELATION verklaart[Rechtvaardigheidsgrond * Overtreding] REF "Awb Artikel 5 lid 5"
{+Het bestuursorgaan legt geen bestuurlijke sanctie op voor zover voor de overtreding een rechtvaardigingsgrond bestond.-}

--vastgesteldDoor :: Overtreding * Inspecteur
--PRAGMA "" " vastgesteld door " 
--MEANING "Een overtreding wordt vastgesteld door een toezichthouder."
--PURPOSE RELATION vastgesteldDoor[Overtreding * Inspecteur] REF "Aanname"
--{+De toezichthouder kan vaststellen wanneer er sprake is van een overtreding.-}

--- CONCEPTEN ---

CONCEPT Sanctie "Een sanctie is een door een bestuursorgaan wegens een overtreding opgelegde verplichting of onthouden aanspraak" "Awb Artikel 5 lid 2"
PURPOSE CONCEPT Sanctie LATEX
{+Het primaire doel van het handhavingsbeleid is een gedragsverandering bewerkstelligen bij de instelling die één of meer wettelijke voorschriften heeft overtreden. De onderwijs inspectie heeft de bevoegdheid tot het opleggen van een bestuurlijke sanctie. -}

CONCEPT Overtreding "Een overtreding is een gedraging die in strijd is met het bepaalde bij of krachtens enig wettelijk voorschrift." " Awb Artikel 5 lid 1.1"
PURPOSE CONCEPT Overtreding LATEX
{+Een overtreding ligt ten grondslag aan een sanctie. Als de inspectie van oordeel is dat een gegeven voorschrift niet is nageleefd, of één of meer wettelijke voorschriften zijn overtreden bij een instelling dan wordt dit als overtreding aangemerkt.-}

CONCEPT Bestuursorgaan "Een bestuursorgaan is een met openbaar gezag bekleed rechtspersoon of natuurlijk persoon die zich met (de uitvoering van) bestuurlijke activiteiten bezighoudt, niet zijnde wetgeving of rechtspraak." "Lycaeus Juridisch Woordenboek"
PURPOSE CONCEPT Bestuursorgaan LATEX
{+De onderwijsinspectie is als bestuursorgaan gerechtigd een sanctie op te leggen.-}

CONCEPT Beschikking "Een beschikking is een besluit dat niet van algemene strekking is, met inbegrip van de afwijzing van een aanvraag daarvan." "Awb Artikel 1 lid 3"
PURPOSE CONCEPT Beschikking LATEX
{+De opgelegde bestuurlijke sanctie, de overtreding en het overtreden voorschrift worden meegedeeld aan het schoolbestuur in een beschikking. Zonodig is in de beschikking een aanduiding van de plaats waar en het tijdstip waarop de overtreding is geconstateerd.-}

CONCEPT Rechtvaardigheidsgrond "Een rechtvaardigheidsgrond is een uitsluitingsgrond voor een onrechtmatige daad." "Lycaeus Juridisch Woordenboek"
PURPOSE CONCEPT Rechtvaardigheidsgrond LATEX
{+Geconstateerde overtredingen kunnen worden verklaard met een rechtvaardigheidsgrond. De rechtvaardigheidsgrond kan aanleiding zijn om de overtreding niet te sanctioneren.-}


ENDPATTERN


ENDCONTEXT
