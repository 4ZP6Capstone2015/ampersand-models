CONTEXT OnderwijsInspectie IN DUTCH
-- INCLUDE "Stamtabellen.adl"
INCLUDE "basisonderwijs.adl" 
INCLUDE "toezichtPopulaties.adl"
INCLUDE "Bevoegde_gezagen_basisonderwijs.adl"

LATEX

META "authors" "Stefan Hersbach"
META "authors" "Han Joosten"
META "authors" "Stef Joosten"
META "authors" "Richard ter Mors"


THEMES "Toezicht"
THEMES "Sanctionering"
THEMES "Handhaving"
PURPOSE CONTEXT OnderwijsInspectie REF "www.onderwijsinspectie.nl"
{+Elk kind heeft recht op goed onderwijs. Leerlingen, studenten en ouders moeten erop kunnen vertrouwen dat het onderwijs op een school goed is. Het schoolbestuur is verantwoordelijk voor de kwaliteit van het onderwijs en moet zich verantwoorden over de resultaten. Hierbij gaat het om resultaten in brede zin; krijgen alle leerlingen onderwijs van voldoende kwaliteit, voldoen scholen aan wet- en regelgeving en hebben ze hun financiën op orde? De Inspectie van het Onderwijs houdt hierop toezicht. Daarnaast rapporteert ze gevraagd en ongevraagd over ontwikkelingen binnen het onderwijs, met als doel het onderwijs als geheel te verbeteren.
-}

KEY Tekortkoming : Tekortkoming (TXT "\"", omschrijving,TXT "\" bij ", betreftt;ovtnaam, TXT " (",betreftt ,TXT ")")
KEY Voorschrift  : Voorschrift  (omschrijving)
KEY Verbeterafspraak: Verbeterafspraak ( omschrijving)
KEY Sanctietraject : Sanctietraject (type, TXT " n.a.v. de tekortkoming \"", gevolgVan;omschrijving, TXT "\" bij ", gevolgVan;betreftt;ovtnaam,TXT " (", gevolgVan;betreftt ,TXT ")" )
KEY Beschikking    : Beschikking    (TXT "beschikking tot: ", gevolg;type, TXT " n.a.v. de tekortkoming \"", gevolg;gevolgVan;omschrijving, TXT "\" bij ", gevolg;gevolgVan;betreftt;ovtnaam,TXT " (", gevolg;gevolgVan;betreftt ,TXT ")" )
KEY OvT          : OvT          (ovtnaam        , TXT " (",I[OvT]  ,TXT ")")
KEY Rapport : Rapport (TXT "Rapport van onderzoek ", leidtTot~)

PATTERN "Toezicht"
PURPOSE PATTERN Toezicht REF "www.onderwijsinspectie.nl"
{+Omdat in de onderhavige analyse de verbinding tussen toezicht en handhaving gemaakt moet worden,
is een deel van het toezicht betrokken in deze analyse.

De inspectie bewaakt de kwaliteit van het onderwijs op individuele scholen in het primair, voortgezet en speciaal onderwijs. Ze houdt ook toezicht op de kwaliteit van opleidingen in het beroepsonderwijs en de volwasseneneducatie en het stelsel van het hoger onderwijs. Op al deze onderwijsvormen is de vrijheid van onderwijs van toepassing, zoals vastgelegd in artikel 23 van de grondwet. Deze vrijheid komt ook met verantwoordelijkheden, vastgelegd in wet- en regelgeving. Het toezien van de naleving hiervan valt ook onder de taken van de onderwijsinspectie. In het uiterste geval kunnen hierdoor sancties worden opgelegd.

Toezicht is het verzamelen van informatie over de vraag of een handeling of zaak voldoet aan de daaraan gestelde eisen, het zich daarna vormen van een oordeel daarover en het eventueel naar aanleiding daarvan interveniëren.

Naast het instellingstoezicht, waar we het hier over hebben, houdt de inspectie zich ook bezig met stelseltoezicht. Dat laatste heeft een andere aard, en wordt in deze context buiten beschouwing gelaten.

Het toezicht van de inspectie is risicogericht. Dat wil zeggen: scholen met risico's, zoals minder goede leerresultaten, krijgen meer toezicht.-}

bevoegdTotControleVan :: Inspecteur * Voorschrift
PRAGMA "" " bevoegd tot controle van "
MEANING "In Ibis wordt geregistreerd welke inspecteur bevoegd is tot het beoordelen van naleving van een voorschrift"
PURPOSE RELATION bevoegdTotControleVan[Inspecteur * Voorschrift] REF "Artikel 3 lid 1"
{+Het toezicht op het onderwijs is opgedragen aan de inspectie van het onderwijs. Zij houdt middels haar inspecteurs toezicht op de naleving van deze voorschriften. Dit kunnen wettelijke voorschriften zijn, maar ook voorschriften die voortkomen uit het handhaven van de kwaliteit van het onderwijs en financiële continuïteit.-}

naar :: Onderzoek * OvT [TOT]
PRAGMA "In onderzoek nr. " " is " " onderzocht"
MEANING "In Ibis wordt voor elk onderzoek geregistreerd welke objecten van toezicht (OvT) worden onderzocht."
PURPOSE RELATION naar[Onderzoek * OvT] REF "Artikel 11 lid 1, Artikel 12a"
{+Voor de uitvoering van haar taken onderzoekt de inspectie jaarlijks elke instelling. Naar aanleiding van het onderzoek geeft de inspectie een oordeel over de kwaliteit van het onderwijs aan de instelling en over de naleving van wettelijke voorschriften door de instelling. Wanneer de situatie daarom vraagt, kan ook een nader onderzoek worden ingesteld.-}

geschrevenDoor :: Rapport * Inspecteur [UNI,TOT]
PRAGMA "" " is geschreven door de Inspecteur van het Onderwijs, namens deze: " 
MEANING "In Ibis wordt de auteur van elk rapport geregistreerd."
PURPOSE RELATION geschrevenDoor[Rapport * Inspecteur] REF "Artikel 20"
{+De resultaten van dergelijke onderzoeken worden vastgelegd in een rapport. Hoewel het onderzoek gedaan kan zijn door een team van inspecteurs, is er steeds één die de eindverantwoordelijkheid van het rapport heeft. Deze wordt als auteur geregistreerd.-}

leidtTot :: Onderzoek * Rapport [UNI,SUR,INJ]
PRAGMA "Onderzoek " " is gerapporteerd in "
MEANING "In Ibis wordt het rapport van elk onderzoek geregistreerd."
PURPOSE RELATION leidtTot [Onderzoek * Rapport] REF "Artikel 20 lid 1"
{+-}

isVermeldIn :: Tekortkoming * Rapport [UNI,TOT]
PRAGMA "De tekortkoming " " is vermeld in " 
MEANING "In Ibis wordt voor elke tekortkoming geregistreerd in welk inspectierapport deze tekortkoming staat vermeld."
PURPOSE RELATION isVermeldIn [Tekortkoming * Rapport] REF "Artikel 20 lid 2"
{+In het rapport worden de onderzoeksconclusies vastgelegd. Indien de inspectie oordeelt dat een bij of krachtens een onderwijswet gegeven voorschrift niet is nageleefd, is er sprake van een tekortkoming. Deze worden vermeld in het inspectierapport.-} 

van ::  Tekortkoming * Voorschrift [UNI,TOT]
PRAGMA "De bevinding " " wijkt af van het volgende voorschrift: " 
MEANING "In Ibis wordt voor elke tekortkoming geregistreerd van welk voorschrift is afgeweken."
PURPOSE RELATION van[Tekortkoming * Voorschrift] REF "Artikel 20 lid 2"
{+Van elke tekortkoming moet duidelijk zijn welk voorschrift niet (of onvoldoende) is nageleefd. Indien meerdere voorschriften zijn overtreden, wordt voor elk van die voorschriften een tekortkoming vastgesteld.-}

betreftt :: Tekortkoming * OvT [TOT,UNI]
PRAGMA "De tekortkoming " " betreft "
MEANING "In Ibis wordt voor elke tekortkoming geregistreerd op welk object van toezicht de tekortkoming van toepassing is."

omschrijving :: Tekortkoming * OmschrijvingTekortkoming [UNI]
PRAGMA "De tekortkoming " " is omschreven als \"" "\""
MEANING "In Ibis kan voor elke tekortkoming een omschrijving worden geregistreerd."
PURPOSE RELATION omschrijving [Tekortkoming * OmschrijvingTekortkoming] 
{+Tekortkomingen worden omschreven conform het van toepassing zijnde toezichtskader. Uit de daarin genoemde omschrijvingen wordt elke concrete tekortkoming geselecteerd.-}

onderdeelVan :: OvT * OvT [UNI]
PRAGMA "" " valt onder "
MEANING "In Ibis wordt geregistreerd van welk object van toezicht een ander OvT onderdeel uitmaakt, indien van toepassing."  
PURPOSE RELATION onderdeelVan [OvT * OvT] REF "IBIS documentatie"
{+Omdat een onderzoek zich over meedere objecten van toezicht kan uitstrekken, registreert Ibis de organisatorische (hiërarchische) structuur van objecten van toezicht. Alle objecten van toezicht binnen een onderzoek vallen altijd onder hetzelfde bevoegd gezag.-}

betreftr :: Rapport * OvT [TOT] -- Is eigenlijk totaal, maar voor de demo laten we dat over aan een overtreedbare regel..
PRAGMA  "" " betreft " 
MEANING "Van elk rapport wordt in Ibis geregistreerd welke objecten van toezicht zijn onderzocht."
PURPOSE RELATION betreftr [Rapport * OvT] 
{+De conclusies uit een onderzoek kunnen meerdere objecten van toezicht betreffen, die onder één bestuur vallen.
Dit kan verschillende oorzaken hebben zoals bijvoorbeeld een basisschool met meerdere locaties, waarbij het rapport over beide locaties gaat. Bij een RoC met verschillende opleidingen vallen de individuele opleidingen als object van toezicht onder de installing als object van toezicht. Ook dan is er weer die samenhang.-}

vastgesteld :: Rapport * Datum [UNI] 
PRAGMA "" " is vastgesteld op "
MEANING "In Ibis wordt van elk rapport geregistreerd op welke datum het is vastgesteld."
PURPOSE RELATION vastgesteld [Rapport * Datum] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Ieder onderzoek wordt binnen zeven werkweken afgesloten met een conceptrapport. Het bestuur krijgt vier werkweken de gelegenheid op het conceptrapport te reageren. Na hoor- en wederhoor wordt het definitieve rapport door de inspectie vastgesteld.-}

escaleren :: Tekortkoming * JaNee [UNI]
MEANING "In Ibis kan worden geregistreerd of een tekortkoming geëscaleerd moet worden."
PURPOSE RELATION escaleren [Tekortkoming * JaNee] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Een tekortkoming kan ertoe leiden dat de instelling van de inspectie een opdracht tot herstel krijgt. Als dat herstel niet of onvoldoende tijdig plaatsvindt kan deze worden geëscaleerd. De precieze inhoud van die escalatie is afhankelijk van de wettelijke regel die overtreden wordt. In praktijk komt het er op neer, dat de inspecteur in overleg gaat met de hoofdinspecteur, die vervolgens kan beslissen om een sanctietraject in te gaan.-}



--- Concepten ---
  CONCEPT Inspecteur "Een inspecteur is een opziener, toezichthouder: inspecteur van het onderwijs." "van Dale"
  PURPOSE CONCEPT Inspecteur LATEX
  {+Een van de taken van de inspectie is het houden van toezicht op individuele scholen en instellingen. Dit gebeurt door personen die daartoe zijn aangesteld.-}

  CONCEPT Voorschrift "Een voorschrift is een aanwijzing waaraan men zich moet houden." "van Dale"
  PURPOSE CONCEPT Voorschrift LATEX
  {+Het houden van toezicht gaat over het beoordelen en bevorderen van de naleving van bij of krachtens een onderwijswet gegeven voorschriften.-}

  CONCEPT OvT "Een object van toezicht is een (deel van een) school of instelling, of een bevoegd gezag." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT OvT LATEX
  {+De inspectie van het onderwijs houdt toezicht op de kwaliteit van het onderwijs in Nederland. Hieronder valt het toezicht op scholen en instellingen en haar bestuur (het bevoegd gezag). Bij instellingen voor beroeps- en volwasseneneducatie wordt vaak ook nog onderscheid gemaakt naar de verschillende onderwijsrichtingen.-}

  CONCEPT Rapport "Een rapport is een inspectierapport dat door een inspecteur opgesteld wordt na onderzoek op een object van toezicht." "Artikel 20"
  PURPOSE CONCEPT Rapport LATEX
  {+In het rapport wordt het oordeel van de inspectie vastgelegd naar aanleiding van het onderzoek. Omdat we het hier specifiek over instellingstoezicht hebben, wordt hier rapporten bedoeld naar aanleiding van regulier onderzoek of nader onderzoek.-}
  
  CONCEPT Tekortkoming "Een tekortkoming is (beschrijving van) een oordeel dat een voorschrift niet is nageleefd, vastgelegd in een rapport." "Artikel 20 lid 2"
  PURPOSE CONCEPT Tekortkoming
  {+Tijdens een onderzoek kan blijken dat een object van toezicht zich niet houdt aan een voorschrift.-}

  SPEC Instelling ISA OvT
  SPEC BevoegdGezag ISA OvT

--  RULE "object van toezicht" : I[OvT] |- I[BevoegdGezag] \/ I[Instelling]
--  MEANING "Elk object van toezicht is ofwel een bestuur ofwel een instelling, maar niet beide." 

ENDPATTERN

PROCESS Toezichthouden
  ROLE Inspecteur MAINTAINS "Aanmaken Rapport"
  RULE "Aanmaken Rapport" : I[Onderzoek] |- leidtTot;vastgesteld;vastgesteld~;leidtTot~
  MEANING "Elk onderzoek leidt tot een rapport" 
  MESSAGE "Er zijn onderzoeksresultaten die nog niet zijn vastgesteld (Artikel 20, wot)"
  VIOLATION (TXT "Voor ", SRC I[Onderzoek], TXT " bestaat nog geen vastgesteld rapport.")

  ROLE Inspecteur MAINTAINS "Objecten van toezicht"
  RULE "Objecten van toezicht" : I[Rapport] |- betreftr;betreftr~
  MEANING "Elk rapport gaat over een of meerdere objecten van toezicht"
  MESSAGE "De volgende rapporten zijn onvolledig:"
  VIOLATION ( TXT "Over welke object van toezicht gaat rapport ",SRC I[Rapport], TXT " ?")

  ROLE HoofdInspecteur MAINTAINS "Escalaties"
  RULE "Escalaties" : escaleren;'Ja' |- gevolgVan~;gevolgVan;escaleren
  MEANING "De Hoofdinspecteur beoordeelt tekortkomingen die daartoe zijn aangewezen"
  MESSAGE "Nog te beoordelen escalaties:"
  VIOLATION ( TXT " ",SRC I[Tekortkoming])

  ROLE "Juridisch medewerker" MAINTAINS "Behandelen sanctietraject"
  RULE "Behandelen sanctietraject" : I[Sanctietraject] |- gevolg~;gevolg \/ einddatum;einddatum~  --TODO: Dit klopt eigenlijk niet helemaal. Later eventueel naar kijken.
  MEANING "Voor elk sanctietraject wordt een voornemen of een beschikking opgesteld."
  VIOLATION (TXT "Voor " , SRC I , TXT " is nog geen voorlopig besluit opgesteld.")

  ROLE "Juridisch medewerker" MAINTAINS "In uitvoering geven"
  RULE "In uitvoering geven" : gevolg~;gevolg |- uitvoering;'Ja';uitvoering~
  MEANING "Elk sanctietraject met een definitief besluit wordt in uitvoering gegeven."
  MESSAGE "De volgende trajecten dienen in uitvoering te worden gegeven:"
  VIOLATION (SRC I)

  RULE afsluiten : gevolg;einddatum |- gevolg; uitvoering;'Ja';uitvoering~;einddatum
  MEANING "Een sanctietraject met een definitieve beschikking kan niet worden gesloten voordat deze in uitvoering is gegeven."
  VIOLATION (TXT "Geef het traject eerst in uitvoering.")
ENDPROCESS 


PATTERN "Handhaving"
PURPOSE PATTERN Handhaving REF "Jaarwerkplan 2012"
{+Handhaving is voor de inspectie een breed begrip. Het omvat alle activiteiten die 
erop gericht zijn dat het onderwijs voldoet aan de eisen die eraan worden gesteld. 
Daarbij vormen de kwaliteit van het onderwijs en het naleven van wettelijke 
voorschriften de belangrijkste aangrijpingspunten. 

In veruit de meeste gevallen volstaat dat de inspectie de tekortkomingen signaleert 
en het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. De school brengt de 
verbeteringen aan en de inspectie onderzoekt het resultaat ervan. Dit is het 
zogenaamde aangepaste toezicht. Dit duurt tot de tekortkomingen in voldoende 
mate zijn opgeheven.

Bij erngstigere tekortkomingen kan de inspectie 
ook gebruik maken van de wettelijke handhavingsmogelijkheden die daarmee 
samenhangen.-}

--PURPOSE CONTEXT OnderwijsInspectie REF "www.rijksoverheid.nl"
--{+De Algemene wet bestuursrecht (afgekort Awb) is een Nederlandse wet die de algemene regels bevat voor de verhouding tussen de overheid en de individuele burgers, bedrijven. 
--De Awb vermeldt hoe een besluit moet worden voorbereid, hoe het moet worden bekendgemaakt en binnen welke termijn een besluit genomen moet worden.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met Awb als referentie, wordt de Algemene wet bestuursrecht bedoeld.
--In de Wet op het onderwijstoezicht (afgekort WOT) is vastgelegd op welke de manier toezicht wordt gehouden op de kwaliteit van het onderwijs in Nederland. 
--Hierin staat dat scholen in de eerste plaats zelf verantwoordelijk zijn voor de kwaliteit van het onderwijs en dat de Inspectie van het Onderwijs hierop toezicht.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met WOT als referentie, wordt de  Wet op het onderwijstoezicht bedoeld.-}


nav :: Verbeterafspraak * Tekortkoming [UNI,TOT,INJ]
PRAGMA "" " is gemaakt naar aanleiding van "
MEANING "In Ibis wordt voor elke verbeterafspraak geregistreerd welke tekortkoming aanleiding is geweest."
PURPOSE RELATION nav [Verbeterafspraak*Tekortkoming] REF "Jaarplan 2012"
{+In veel gevallen volstaat het dat de inspectie het bevoegd gezag van de school of instelling aanspreekt op de tekortkomingen en afspraken maakt over de verbetering. Deze kunnen worden vastgelegd in een toezichtplan van de inspectie.-}

met :: Verbeterafspraak * OvT 
PRAGMA "" " gemaakt met "
MEANING "In Ibis wordt voor elke verbeterafspraak geregistreerd met welk bevoegd gezag deze afspraak is gemaakt."
PURPOSE RELATION met[Verbeterafspraak * OvT] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+De verbeterafspraken worden vastgelegd in een definitieve toezichtplan. Dit toezichtplan wordt tegelijk met het vastgestelde rapport van bevindingen aan het schoolbestuur gestuurd.-}

omschrijving :: Verbeterafspraak * Tekst [UNI,TOT]
PRAGMA "" " is omschreven als\""  "\""
MEANING "In Ibis wordt de omschrijving van elke verbeterafspraak geregistreerd."

opgeheven :: Tekortkoming * Tekortkoming [PROP]
PRAGMA "De tekortkoming " " is opgeheven"
MEANING "In Ibis wordt geregistreerd of een tekortkoming is opgeheven." 
PURPOSE RELATION opgeheven REF "Jaarplan 2012" 
{+Wanneer de verbeteringen in voldoende mate zijn aangebracht kan de inspectie vaststellen dat 
de tekortkoming is opgeheven. Het moet mogelijk zijn om dat te registreren.-}

--RULE afspreken : I[Tekortkoming]  |- nav~;met;betreftt~;isVermeldIn~
--MEANING "Voor elke tekortkoming, vermeld in een rapport betreffende een instelling of bestuur, dient een verbeterafspraak te zijn gemaakt met die instelling of bestuur." 


--- CONCEPTEN ---

ENDPATTERN
--isVermeldIn :: Overtreding * Rapport
--PRAGMA "" " is vermeld in " 
--MEANING "Als er overtredingen worden vastgesteld tijdens de controle dan worden deze vastgelegd in een rapport."
--PURPOSE RELATION isVermeldIn[Overtreding * Rapport] REF "Awb Artikel 5 lid 48.1"
--{+Het bestuursorgaan en de voor de overtreding bevoegde toezichthouder kunnen van de overtreding een rapport opmaken.-}



PATTERN Sanctionering
PURPOSE PATTERN Sanctionering REF "www.onderwijsinspectie.nl"
{+Handhaving omvat een groot aantal activiteiten die gericht zijn op het bevorderen van de kwaliteit van het aangeboden onderwijs, kwaliteit van bestuur en het bevorderen van de financiële continuïteit. Als wij de juiste voorwaarden hebben geschapen voor scholen om conform de regels te opereren, komt eventueel een stringentere invulling van het handhavingsbeleid aan de orde bij die scholen waar dat niet bij helpt. We praten dan over sanctioneren.

Hoewel het opleggen van bestuurlijke sancties in geregeld in hoofdstuk 5 van de Algemene wet bestuursrecht, gaat het hierbij ook over de definitieve vaststelling van onderwijssubsidies. De bekostiging van scholen is ingericht als subsidie, waarbij voorwaarden worden opgelegd. Wanneer wordt geconstateerd dat niet voldaan is aan die voorwaarden, kan de beschikking waarbij de subsidie is vastgesteld alsnog worden gewijzigd of zelfs worden ingetrokken. Dit wordt gezien als sanctie.
-}

gevolgVan :: Sanctietraject -> Tekortkoming
PRAGMA "Het sanctietraject " " is het gevolg van de tekortkoming " 
MEANING "In Ibis wordt voor elk sanctietraject de tekortkoming geregistreerd."
PURPOSE RELATION gevolgVan[Sanctietraject * Tekortkoming] REF "Toezichtkader PO/VO 2012"
{+Indien wordt geconstateerd dat scholen en/of besturen tekortkomingen met betrekking tot het voldoen aan wet- en regelgeving hebben, krijgen zij van de 
inspectie opdracht tot herstel. Indien dat herstel niet of onvoldoende tijdig plaatsvindt, of indien herstel niet langer mogelijk is, volgen een aantal escalatiestappen. De precieze inhoud van die escalatiestappen is afhankelijk van de wettelijke regel die overtreden wordt. De ultieme maatregel is een sanctie. Ieder sanctietraject is het gevolg van één tekortkoming.-}

deadlineTraject :: Sanctietraject * Datum [TOT,UNI]
PRAGMA "Het sanctietraject " " moet gestart zijn voor "
MEANING "In Ibis wordt de deadline van elk sanctietraject geregistreerd."
PURPOSE RELATION deadlineTraject [Sanctietraject * Datum]
{+Afhankelijk van de aard van het onderzoek, waarbij de tekortkoming is geconstateerd (bijvoorbeeld dat naar aanleiding van de jaarstukken), bestaat er een vaste periode waarin de Inspectie van het Onderwijs het sanctietraject formeel zal moeten afronden.-}

voorgenomen :: Sanctietraject * Datum [UNI] 
PRAGMA "Het sanctietraject " " heeft een voorgenomen "
MEANING "In Ibis kan de datum van dagtekening van het voornemen tot beschikking worden geregistreerd." 
PURPOSE RELATION voorgenomen [Sanctietraject * Datum]
{+Een sanctietraject is de aanloop tot een beschikking. Vaak wordt de beschikking aangekondigd, voordat deze definitief wordt. Indien dit voornemen tot de beschikking wordt verstuurd, dan wordt de datum waarop dat gebeurt vastgelegd bij het sanctietraject. Er kunnen overigens ook redenen zijn om af te zien van de aankondiging.-}

uitvoering :: Sanctietraject * JaNee [UNI]
PRAGMA "Is het sanctietraject " " in uitvoering gegeven? "
MEANING "Of een sanctietraject in uitvoering is gegeven aan DUO wordt als gegeven in Ibis geregistreerd."
PURPOSE RELATION uitvoering
{+Wanneer de beschikking definitief is vastgesteld, wordt de verdere uitvoering van het sanctietraject in uitvoering gegeven aan DUO. -}

bezwaar :: Beschikking * JaNee [UNI]
PRAGMA "Is op " " bezwaar? "
MEANING "Of er bezwaar is ingediend tegen een sanctietraject wordt als gegeven in Ibis geregistreerd."
PURPOSE RELATION bezwaar REF "Hoofdstuk 6, Awb"
{+Indien het bevoegd gezag, aan wie de sanctie is opgelegd, het niet eens is met de sanctie, kan tegen het besluit daartoe bezwaar worden gemaakt. -}

vervolgacties  :: Sanctietraject * JaNee [UNI]
PRAGMA "zijn er vervolgacties op " "? "
MEANING "Of er vervolgacties bestaan op een sanctietraject wordt als gegeven in Ibis geregistreerd."
PURPOSE RELATION vervolgacties REF "Excel sheet"
{+Om Ibis te weten of er vervolgacties gaan komen, wordt het bestaan van vervolgacties als gegeven geregistreerd.
-}
 
initieel :: Sanctietraject * FinancieelBelang [UNI,TOT]
PRAGMA "Het initieel financieel belang van sanctietraject " " bedraagt "
MEANING "In Ibis wordt voor elk sanctietraject het initieel financieel belang geregistreerd."
PURPOSE RELATION initieel [Sanctietraject * FinancieelBelang]
{+Voordat het sanctietraject is gestart wordt eerst in een berekening vastgesteld wat het mogelijke financiele belang is. Het resultaat van die berekening wordt in Ibis opgenomen-}

definitiefF :: Beschikking * FinancieelBelang [UNI,TOT]
PRAGMA "Het financieel belang van sanctietraject " " is definitief vastgesteld op "
MEANING "In Ibis wordt voor elk sanctietraject het definitieve financieel belang geregistreerd." 
PURPOSE RELATION definitiefF [Beschikking * FinancieelBelang]
{+Omdat het financieel belang gaandeweg een sanctietraject kan worden aangepast, kan het definitieve financieel belang afwijken van het initiële.-}
 
zienswijze :: Sanctietraject * JaNee [UNI]
PRAGMA "Is op het voornemen tot " " een zienswijze naar voren gebracht? "
MEANING "Of er een zienswijze naar voren is gebracht ten aanzien van een sanctietraject wordt als gegeven in Ibis geregistreerd."
PURPOSE RELATION zienswijze [Sanctietraject * JaNee] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Wanneer een voornemen tot beschikking wordt gestuurd aan een bestuur, dan krijgt dat bestuur de gelegenheid om binnen een redelijke termijn, (aangegeven bij het voornemen) om haar zienswijze te geven. Deze zienswijze kan van invloed zijn op de uiteindelijke beschikking. In het spreadsheet "sanctietraject" is een veldje opgenomen waarin geregistreerd wordt of de zienswijze is ontvangen. Dat veld wordt dus ook in Ibis opgenomen.-}

deadlineZienswijze :: Sanctietraject * Datum [UNI] 
PRAGMA "" " heeft als uiterste reactiedatum "
MEANING "In Ibis kan voor een sanctietraject worden geregistreerd de datum waarop een zienswijze uiterlijk moet zijn ontvangen."
PURPOSE RELATION deadlineZienswijze [Sanctietraject * Datum]
{+Afhankelijk van de omstandigheden van het geval wordt een redelijke termijn gesteld waarbinnen het bevoegd gezag haar zienswijze kenbaar kan maken. De redelijke termijn die doorgaans wordt gesteld is er een van vier weken.-}

startdatum :: Sanctietraject * Datum [UNI,TOT]
PRAGMA "" " is gestart op " 
MEANING "In Ibis kan worden geregistreerd de datum waarop een sanctietraject is gestart."
PURPOSE RELATION startdatum [Sanctietraject * Datum] REF "Excel sheet"
{+Het besluit om een sanctietraject te starten hoeft niet tegelijk met het vaststellen van het rapport te zijn. -}

einddatum :: Sanctietraject * Datum [UNI]
PRAGMA "" " is afgerond op " 
MEANING "In Ibis kan worden geregistreerd de datum waarop een sanctietraject is afgerond."
PURPOSE RELATION einddatum [Sanctietraject * Datum] REF "Excel sheet"
{+Zodra de beschikking definitief is vastgesteld, wordt de uitvoering hiervan overgedragen aan DUO. Wanneer in rechte wordt opgekomen tegen het besluit, dan zal DUO de behandeling van bezwaar en beroep verzorgen. Uiteraard is de onderwijsinspectie hierbij ondersteunend, indien nodig. De uiteindelijke verrekening wordt ook door DUO verzorgd.

Nadat een sanctietraject is overgedragen aan DUO blijven de trajecten actief. Pas als DUO deze sluit wordt in overleg dit meegedeeld aan de inspectie, waarop het traject als afgerond wordt beschouwd.-}

definitiefD :: Beschikking * Datum [UNI,TOT]
PRAGMA "De " " is getekend op "
MEANING "In Ibis wordt voor elke beschikking geregistreerd op welke datum hij getekend, en dus definitief geworden is."
PURPOSE RELATION definitiefD [Beschikking * Datum] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+De beslissing tot een sanctie kan, al dan niet voorafgegaan door een voornemen, op enig moment definitief worden gemaakt. De beschikking bevat dan de dagtekening van de beslissing.-}

gevolg :: Beschikking * Sanctietraject [UNI,TOT,INJ]  -- Is het denkbaar dat er één beschikking meerdere sanctietrajecten afdekt? Waaruit blijkt 
PRAGMA "De " " is het gevolg van sanctietraject " 
MEANING "In Ibis wordt van elk sanctietraject de beschikking geregistreerd."
PURPOSE RELATION gevolg [Beschikking * Sanctietraject]
{+Een beschikking tot het opleggen van een sanctie wordt niet zomaar genomen. Voorafgaand hieraan wordt een sanctietraject gestart, waarin de beslissing eerst wordt voorgelegd aan het betreffende bevoegd gezag, die de mogelijkheid krijgt om een zienswijze in te dienen. Pas nadat die mogelijkheid is gegeven, kan een formele beslissing in de vorm van een beschikking worden genomen.-}

type :: Sanctietraject -> SanctieType
PRAGMA "" " is van het type "
MEANING "In Ibis wordt het sanctietype van elk sanctietraject geregistreerd." 
PURPOSE RELATION type [Sanctietraject * SanctieType] REF "Excel sheet"
{+Omdat er verschillende soorten sanctietrajecten kunnen zijn, wordt elk individueel geval voorzien van een aanduiding van het type waarvan sprake is.-}

onderwerp :: Sanctietraject -> Onderwerp
PRAGMA "Het onderwerp van "" is "
MEANING "In Ibis wordt het onderwerp van elk sanctietraject geregistreerd."
PURPOSE RELATION onderwerp REF "Excel sheet"
{+Omdat er verschillende onderwerpen bestaan, wordt elk individueel geval voorzien van een aanduiding van het onderwerp waarvan sprake is.-}

ENDPATTERN

PATTERN "JaNee"
CONCEPT JaNee "een data type dat alleen de waarde 'Ja' of 'Nee' kan aannemen"
RULE "JaNee integriteit": I[JaNee] |- 'Ja' \/ 'Nee'
MESSAGE "U mag hier alleen 'Ja' of 'Nee' invullen"
ENDPATTERN

ENDCONTEXT
