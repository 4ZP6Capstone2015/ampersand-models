CONTEXT OnderwijsInspectie

INCLUDE "OW_INSP.adl"

INTERFACE "Overview " FOR Inspecteur : I[ONE]
BOX [ 
     "Instellingen"  : V[ONE*Instelling]
    ,"Bevoegd Gezag" : V[ONE*Instelling];bevoegd  -- HJO, 20130627: Hier niet alle bevoegde gezagen laten zien, want dat zijn er nu veel te veel...
    , Onderzoeken   : V[ONE*Onderzoek]
    , Rapporten     : V[ONE*Rapport]
    , Sanctietrajecten      : V[ONE*Sanctietraject]
    , Beschikkingen : V[ONE*Beschikking]
    ]

INTERFACE Overview FOR HoofdInspecteur : I[ONE]
BOX [ Rapporten : V[ONE*Rapport]
    , Sanctietrajecten : V[ONE*Sanctietraject]
    , "Tekortkomingen" : V[ONE*Tekortkoming]
    , Escalaties: V[ONE*JaNee];escaleren~
    ]

INTERFACE " Overview" FOR "Juridisch medewerker" : I[ONE]
BOX [ Rapporten : V[ONE*Rapport]
    , Sanctietrajecten : V[ONE*Sanctietraject]
    ]

INTERFACE Instelling FOR Inspecteur,HoofdInspecteur,"Juridisch medewerker" : I[Instelling]
BOX [ "BRIN" : I[Instelling]
    , Naam : instellingsnaam
    , "Bevoegd gezag": bevoegd
    , Adres : adres INTERFACE Adres
    , "Correspondentie adres" : correspondentie INTERFACE Adres
    , telefoonnummer : telefoonnummer
    , Denominatie : owcoderich
    , "Nodaal Gebied" : nodaal;naam
    , "RPA Gebied" : rpa;naam
    , "WGR Gebied" : wgrgebied
    , "COROP Gebied" : coropgebied
    , "Onderwijs Gebied" : onderwijsgebied
    ]
INTERFACE Adres FOR Inspecteur,HoofdInspecteur,"Juridisch medewerker" : I[Adres]
BOX [ Straat : straatnaam
    , Nr     : nummeraanduiding
    , Postcode : postcode
    , Plaats : plaatsnaam
    , Provincie : provincie
    ]
INTERFACE Onderzoek (leidtTot) FOR Inspecteur : I[Onderzoek]
BOX [ Id : I[Onderzoek]
    , "Object van Toezicht" : naar
    , "Rapport" : leidtTot
    ]
INTERFACE Rapport (isVermeldIn, omschrijving[Verbeterafspraak * Tekst],omschrijving[Tekortkoming * Tekst], betreft [Rapport * OvT],betreft [Tekortkoming * OvT],vastgesteld, van, escaleren ) FOR Inspecteur : I[Rapport]
BOX [ Id : I[Rapport]
    , "Onderzoek" : leidtTot~
    , "Object van Toezicht" : betreft [Rapport * OvT]
    , "Eindverantwoordelijke" : geschrevenDoor
    , "Bevindingen" : isVermeldIn~
      BOX [ oordeel : omschrijving 
          , voorschrift : van
          , betreft : betreft
          , afspraak : nav~;omschrijving[Verbeterafspraak * Tekst]
          , "escaleren?" : escaleren
          ]
    , "Datum vaststelling" : vastgesteld
    ]

INTERFACE SanctieTraject (gevolgVan, escaleren, deadlineTraject,type,initieel) FOR HoofdInspecteur : I[Tekortkoming]
BOX [ Tekortkoming : I[Tekortkoming]
    , "Geconstateerd door" : isVermeldIn;geschrevenDoor
    , oordeel : omschrijving
    , voorschrift : van
    , betreft : betreft
    , "Datum vaststelling" : isVermeldIn; vastgesteld
    , "Zaak (sanctie)": gevolgVan~
      BOX [ Id: I
          , "Deadline traject" : deadlineTraject
          , "Type sanctie" : type
          , "Initieel financieel belang" : initieel
          ]  
    , "maak leeg als geen escalatie": escaleren
    ] 
INTERFACE "SanctieTraject " (gevolg,voorgenomen,deadlineZienswijze) FOR "Juridisch medewerker" : I[Sanctietraject]
BOX [ Id : I[Sanctietraject]
    , "Deadline traject" : deadlineTraject
    , "Type sanctie" : type
    , "Financieel belang initieel" : initieel[Sanctietraject*FinancieelBelang]
    , Aanleiding: gevolgVan
      BOX [ "tekortkoming" : omschrijving
          , voorschrift : van
          , betreft : betreft
          , inspectierapport: isVermeldIn
          ]
    , "Datum voorgenomen besluit" : voorgenomen
    , "Deadline Zienswijze" : deadlineZienswijze
    , Zienswijze : op
    , Beslissing : gevolg~
      BOX [ Beschikkingsdatum: definitief[Beschikking*Datum]
          ]
    , "Afronding traject": einddatum
    ]

INTERFACE "Beschikking" (voorgenomen,definitief[Beschikking*FinancieelBelang], definitief[Beschikking*FinancieelBelang] ) FOR "Juridisch medewerker" : I[Beschikking]
BOX [ Id : I[Beschikking]
    , " " : gevolg
      BOX [ "Sanctie type" : type
          ]
    , "Financieel belang definitief" : definitief[Beschikking*FinancieelBelang]
    , "Datum definitief besluit" : definitief[Beschikking*Datum]
    ]

{-
INTERFACE Sanctie : I[Sanctietraject]
BOX [ Id : I
    , type : type
    , betalingsverplichting : betalingsverplichting
    , beschikking : totOpleggingVan~
    , overtreding : op 
    ]
-}


ENDCONTEXT