CONTEXT OnderwijsInspectie
-- INCLUDE "Stamtabellen.adl"
INCLUDE "basisonderwijs.adl" 
INCLUDE "toezichtPopulaties.adl"
LATEX

META "authors" "Stefan Hersbach"
META "authors" "Han Joosten"
META "authors" "Stef Joosten"
META "authors" "Richard ter Mors"


THEMES "Toezicht"
THEMES "Sanctionering"
THEMES "Handhaving"
PURPOSE CONTEXT OnderwijsInspectie REF "www.onderwijsinspectie.nl"
{+Elk kind heeft recht op goed onderwijs. Leerlingen, studenten en ouders moeten erop kunnen vertrouwen dat het onderwijs op een school goed is. Het schoolbestuur is verantwoordelijk voor de kwaliteit van het onderwijs en moet zich verantwoorden over de resultaten. Hierbij gaat het om resultaten in brede zin; krijgen alle leerlingen onderwijs van voldoende kwaliteit, voldoen scholen aan wet- en regelgeving en hebben ze hun financiën op orde? De Inspectie van het Onderwijs houdt hierop toezicht. Daarnaast rapporteert ze gevraagd en ongevraagd over ontwikkelingen binnen het onderwijs, met als doel het onderwijs als geheel te verbeteren.
-}

PATTERN "Toezicht"
PURPOSE PATTERN Toezicht REF "www.onderwijsinspectie.nl"
{+De inspectie bewaakt de kwaliteit van het onderwijs op individuele scholen in het primair, voortgezet en speciaal onderwijs. Ze houdt ook toezicht op de kwaliteit van opleidingen in het beroepsonderwijs en de volwasseneneducatie en het stelsel van het hoger onderwijs. Op al deze onderwijsvormen is de vrijheid van onderwijs van toepassing, zoals vastgelegd in artikel 23 van de grondwet. Deze vrijheid komt ook met verantwoordelijkheden, vastgelegd in wet- en regelgeving. Het toezien van de naleving hiervan valt ook onder de taken van de onderwijsinspectie. In het uiterste geval kunnen hierdoor sancties worden opgelegd.

Toezicht is het verzamelen van informatie over de vraag of een handeling of zaak voldoet aan de daaraan gestelde eisen, het zich daarna vormen van een oordeel daarover en het eventueel naar aanleiding daarvan interveniëren.

Naast het instellingstoezicht, waar we het hier over hebben, houdt de inspectie zich ook bezig met stelseltoezicht. Dat laatste heeft een andere aard, en wordt in deze context buiten beschouwing gelaten.

Het toezicht van de inspectie is risicogericht. Dat wil zeggen: scholen met risico's, zoals minder goede leerresultaten, krijgen meer toezicht.-}

bevoegdTotControleVan :: Inspecteur * Voorschrift
PRAGMA "" " bevoegd tot controle van "
MEANING "Een inspecteur kan bevoegd zijn tot het beoordelen van naleving van een voorschrift"
PURPOSE RELATION bevoegdTotControleVan[Inspecteur * Voorschrift] REF "Artikel 3 lid 1"
{+Het toezicht op het onderwijs is opgedragen aan de inspectie van het onderwijs. Zij houdt middels haar inspecteurs toezicht op de naleving van deze voorschriften. Dit kunnen wettelijke voorschriften zijn, maar ook voorschriften die voortkomen uit het handhaven van de kwaliteit van het onderwijs en financiële continuïteit.-}

naar :: Onderzoek * OvT [TOT]
PRAGMA "" " naar "
MEANING "Een onderzoek wordt altijd gedaan naar tenminste één object van toezicht (OvT)."
PURPOSE RELATION naar[Onderzoek * OvT] REF "Artikel 11 lid 1, Artikel 12a"
{+Voor de uitvoering van haar taken onderzoekt de inspectie jaarlijks elke instelling. Naar aanleiding van het onderzoek geeft de inspectie een oordeel over de kwaliteit van het onderwijs aan de instelling en over de naleving van wettelijke voorschriften door de instelling. Wanneer de situatie daarom vraagt, kan ook een nader onderzoek worden ingesteld.-}

geschrevenDoor :: Rapport * Inspecteur [UNI,TOT]
PRAGMA "" " geschreven onder eindverantwoordelijkheid van " 
MEANING "Elk rapport is geschreven onder verantwoordelijkheid van één inspecteur."
PURPOSE RELATION geschrevenDoor[Rapport * Inspecteur] REF "Artikel 20"
{+De resultaten van dergelijke onderzoeken worden vastgelegd in een rapport. Hoewel het onderzoek gedaan kan zijn door een team van inspecteurs, is er steeds één die de eindverantwoordelijkheid van het rapport heeft.-}

leidtTot :: Onderzoek * Rapport [UNI]
PRAGMA " leidt tot "
MEANING "Elk onderzoek leidt tot één rapport"
PURPOSE RELATION leidtTot [Onderzoek * Rapport] REF "Artikel 20 lid 1"
{+Het oordeel naar aanleiding van een onderzoek worden vastgesteld in een inspectierapport.-}

van ::  Tekortkoming * Voorschrift [UNI,TOT]
PRAGMA "" " van " 
MEANING "Elke tekortkoming is van één voorschrift."
PURPOSE RELATION van[Tekortkoming * Voorschrift] REF "Artikel 20 lid 2"
{+Indien naar het oordeel van de inspecite een voorschrift niet is nageleefd, dan moet dat in het rapport worden vermeld. Wanneer dit voor meerdere voorschriften geldt, dan zal voor elk van die voorschriften een vermelding worden opgenomen. Dit impliceert, dat elke tekortkoming 
over een enkel voorschrift gaat.-}

isVermeldIn :: Tekortkoming * Rapport [UNI,TOT]
PRAGMA "" " is vermeld in " 
MEANING "Elke tekortkoming staat vermeld in één inspectierapport"
PURPOSE RELATION isVermeldIn [Tekortkoming * Rapport] REF "Artikel 20 lid 2"
{+In het rapport worden de onderzoeksconclusies vastgelegd. Indien de inspectie oordeelt dat een bij of krachtens een onderwijswet gegeven voorschrift niet is nageleefd, is er sprake van een tekortkoming. Deze worden vermeld in het inspectierapport.-} 

betreft :: Tekortkoming * OvT [TOT,UNI]
PRAGMA "" " betreft "
MEANING "Elke tekortkoming heeft betrekking op één object van toezicht."
PURPOSE RELATION betreft [Tekortkoming * OvT] 
{+Wanneer tijdens een onderzoek wordt geconstateerd dat een object van toezicht een bepaald voorschrift niet wordt nageleefd, dan wordt deze tekortkoming opgenomen in het inspectierapport. Omdat duidelijk moet zijn waarop de tekortkoming betrekking heeft, wordt dit vastgelegd.-}

omschrijving :: Tekortkoming * Tekst [UNI]
PRAGMA "" " omschreven als "

onderdeelVan :: OvT * OvT [UNI]
PRAGMA "" " valt onder "
MEANING "Een object van toezicht kan onderdeel uitmaken van één ander (hogerliggend) object van toezicht"  
PURPOSE RELATION onderdeelVan [OvT * OvT] REF "IBIS documentatie"
{+Een onderzoek kan over meedere objecten van toezicht worden gedaan. Een object van toezicht is onderdeel van een organisatorische hiërarchie. In deze structuur van elementen komen vervolgens meerdere OvT's voor. Alle objecten van toezicht binnen een structuur vallen altijd onder eenzelfde bevoegd gezag.-}

betreft :: Rapport * OvT [TOT]
PRAGMA  "" " betreft " 
MEANING "Elk rapport betreft een of meerdere objecten van toezicht."
PURPOSE RELATION betreft [Rapport * OvT] 
{+De conclusies uit het onderzoek gaan over een of meerdere samenhangende objecten van toezicht.  
Indien er in het onderzoek meerdere objecten van toezicht worden meegenomen, dan vallen die objecten van toezicht allemaal onder eenzelfde object van toezicht. Dit kan verschillende oorzaken hebben zoals bijvoorbeeld een basisschool met meerdere locaties, waarbij het rapport over beide locaties gaat. Bij een RoC met verschillende opleidingen vallen de individuele opleidingen als object van toezicht onder de installing als object van toezicht. Ook dan is er weer die samenhang.-}

vastgesteld :: Rapport * Datum [UNI] 
PRAGMA "" " is vastgesteld op "
MEANING "Elk rapport kan worden vastgesteld op een datum."
PURPOSE RELATION vastgesteld [Rapport * Datum] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Ieder onderzoek wordt binnen zeven werkweken afgesloten met een conceptrapport. Het bestuur krijgt vier werkweken de gelegenheid op het conceptrapport te reageren. Na hoor- en wederhoor wordt het definitieve rapport door de inspectie vastgesteld.-}

escaleren :: Tekortkoming * JaNee [UNI]
MEANING "Een tekortkoming kan worden geescaleerd."
PURPOSE RELATION escaleren [Tekortkoming * JaNee] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Een tekortkoming kan ertoe leiden dat de instelling van de inspectie een opdracht tot herstel krijgt. Als dat herstel niet of onvoldoende tijdig plaatsvindt kan deze worden geescaleerd. De precieze inhoud van die escalatie is afhankelijk van de wettelijke regel die overtreden wordt. In praktijk komt het er op neer, dat de inspecteur in overleg gaat met de hoofdinspecteur, die vervolgens kan beslissen om een sanctietraject in te gaan.-}

KEY Tekortkoming : Tekortkoming (TXT " vermelding van ", van, TXT " betreft ", betreft )



--- Concepten ---
  CONCEPT Inspecteur "Een inspecteur is een opziener, toezichthouder: inspecteur van het onderwijs." "van Dale"
  PURPOSE CONCEPT Inspecteur LATEX
  {+Een van de taken van de inspectie is het houden van toezicht op individuele scholen en instellingen. Dit gebeurt door personen die daartoe zijn aangesteld.-}

  CONCEPT Voorschrift "Een voorschrift is een aanwijzing waaraan men zich moet houden." "van Dale"
  PURPOSE CONCEPT Voorschrift LATEX
  {+Het houden van toezicht gaat over het beoordelen en bevorderen van de naleving van bij of krachtens een onderwijswet gegeven voorschriften.-}

  CONCEPT OvT "Een object van toezicht is een (deel van een) school of instelling, of een bevoegd gezag." "www.onderwijsinspectie.nl"
  PURPOSE CONCEPT OvT LATEX
  {+De inspectie van het onderwijs houdt toezicht op de kwaliteit van het onderwijs in Nederland. Hieronder valt het toezicht op scholen en instellingen en haar bestuur (het bevoegd gezag). Bij instellingen voor beroeps- en volwasseneneducatie wordt vaak ook nog onderscheid gemaakt naar de verschillende onderwijsrichtingen.-}

  CONCEPT Rapport "Een rapport is een inspectierapport dat door een inspecteur opgesteld wordt na onderzoek op een object van toezicht." "Artikel 20"
  PURPOSE CONCEPT Rapport LATEX
  {+In het rapport wordt het oordeel van de inspectie vastgelegd naar aanleiding van het onderzoek. Omdat we het hier specifiek over instellingstoezicht hebben, wordt hier rapporten bedoeld naar aanleiding van regulier onderzoek of nader onderzoek.-}
  
  CONCEPT Tekortkoming "Een tekortkoming is (beschrijving van) een oordeel dat een voorschrift niet is nageleefd, vastgelegd in een rapport." "Artikel 20 lid 2"
  PURPOSE CONCEPT Tekortkoming
  {+Tijdens een onderzoek kan blijken dat een object van toezicht zich niet houdt aan een voorschrift.-}

  SPEC Instelling ISA OvT
  SPEC BevoegdGezag ISA OvT

--  RULE "object van toezicht" : I[OvT] |- I[BevoegdGezag] \/ I[Instelling]
--  MEANING "Elk object van toezicht is ofwel een bestuur ofwel een instelling, maar niet beide." 

ENDPATTERN

PROCESS Toezichthouden
  ROLE Inspecteur MAINTAINS "Vaststellen Rapport"
  RULE "Vaststellen Rapport" : I[Onderzoek] |- leidtTot;vastgesteld;vastgesteld~;leidtTot~
  MEANING "Elk onderzoek leidt tot een rapport dat is vastgesteld" 
  MESSAGE "Artikel 20 wot zegt dat de onderzoeksresultaten moeten worden vastgesteld"
  VIOLATION (TXT "Voor ", SRC I[Onderzoek], TXT " bestaat nog geen vastgesteld rapport.")

  ROLE HoofdInspecteur MAINTAINS "Escalaties"
  RULE "Escalaties" : escaleren |- gevolgVan~;gevolgVan;escaleren
  MEANING "De Hoofdinspecteur beoordeelt tekortkomingen die daartoe zijn aangewezen"
  MESSAGE "De volgende tekortkomingen zijn voorgedragen voor een sanctietraject"
  VIOLATION ( TXT "Er is nog geen sanctietraject gestart voor ",SRC I[Tekortkoming])

  ROLE "Juridisch medewerker" MAINTAINS "Opstellen voorgenomen besluit"
  RULE "Opstellen voorgenomen besluit" : I[Sanctietraject] |- gevolg~;gevolg
  MEANING "Voor elk sanctietraject wordt een voorlopig besluit opgesteld"
  VIOLATION (TXT "Voor " , SRC I , TXT " is nog geen voorlopig besluit opgesteld ")
ENDPROCESS 


PATTERN "Handhaving"
PURPOSE PATTERN Handhaving REF "Jaarwerkplan 2012"
{+Handhaving is voor de inspectie een breed begrip. Het omvat alle activiteiten die 
erop gericht zijn dat het onderwijs voldoet aan de eisen die eraan worden gesteld. 
Daarbij vormen de kwaliteit van het onderwijs en het naleven van wettelijke 
voorschriften de belangrijkste aangrijpingspunten. 

In veruit de meeste gevallen volstaat dat de inspectie de tekortkomingen signaleert 
en het bestuur of de leiding van de school of instelling aanspreekt op de 
tekortkomingen en afspraken maakt over de verbetering. De school brengt de 
verbeteringen aan en de inspectie onderzoekt het resultaat ervan. Dit is het 
zogenaamde aangepaste toezicht. Dit duurt tot de tekortkomingen in voldoende 
mate zijn opgeheven.

Bij erngstigere tekortkomingen kan de inspectie 
ook gebruik maken van de wettelijke handhavingsmogelijkheden die daarmee 
samenhangen.-}

--PURPOSE CONTEXT OnderwijsInspectie REF "www.rijksoverheid.nl"
--{+De Algemene wet bestuursrecht (afgekort Awb) is een Nederlandse wet die de algemene regels bevat voor de verhouding tussen de overheid en de individuele burgers, bedrijven. 
--De Awb vermeldt hoe een besluit moet worden voorbereid, hoe het moet worden bekendgemaakt en binnen welke termijn een besluit genomen moet worden.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met Awb als referentie, wordt de Algemene wet bestuursrecht bedoeld.
--In de Wet op het onderwijstoezicht (afgekort WOT) is vastgelegd op welke de manier toezicht wordt gehouden op de kwaliteit van het onderwijs in Nederland. 
--Hierin staat dat scholen in de eerste plaats zelf verantwoordelijk zijn voor de kwaliteit van het onderwijs en dat de Inspectie van het Onderwijs hierop toezicht.
--Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, met WOT als referentie, wordt de  Wet op het onderwijstoezicht bedoeld.-}


nav :: Verbeterafspraak * Tekortkoming [UNI,TOT,INJ]
PRAGMA "" " is gemaakt naar aanleiding van "
MEANING "Elke verbeterafspraak is gemaakt naar aanleiding van een tekortkoming."
PURPOSE RELATION nav [Verbeterafspraak*Tekortkoming] REF "Jaarplan 2012"
{+In veel gevallen volstaat het dat de inspectie het bevoegd gezag van de school of instelling aanspreekt op de tekortkomingen en afspraken maakt over de verbetering. Deze kunnen worden vastgelegd in een toezichtplan van de inspectie.-}


met :: Verbeterafspraak * OvT 
PRAGMA "" " gemaakt met "
MEANING "Elke verbeterafspraak is gemaakt met een bevoegd gezag."
PURPOSE RELATION met[Verbeterafspraak * OvT] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+De verbeterafspraken worden vastgelegd in een definitieve toezichtplan. Dit toezichtplan wordt tegelijk met het vastgestelde rapport van bevindingen aan het schoolbestuur gestuurd.-}

omschrijving :: Verbeterafspraak * Tekst [UNI,TOT]
KEY Verbeterafspraak: Verbeterafspraak ( omschrijving)

opgeheven :: Tekortkoming * Tekortkoming [PROP]
MEANING "Een tekortkoming kan zijn opgeheven." 
PURPOSE RELATION opgeheven REF "Jaarplan 2012" 
{+Wanneer de verbeteringen in voldoende mate zijn aangebracht kan de inspectie vaststellen dat 
de tekortkoming is opgeheven. Het moet mogelijk zijn om dat te registreren.-}

--RULE afspreken : I[Tekortkoming]  |- nav~;met;betreft~;isVermeldIn~
--MEANING "Voor elke tekortkoming, vermeld in een rapport betreffende een instelling of bestuur, dient een verbeterafspraak te zijn gemaakt met die instelling of bestuur." 


--- CONCEPTEN ---
 
ENDPATTERN
--isVermeldIn :: Overtreding * Rapport
--PRAGMA "" " is vermeld in " 
--MEANING "Als er overtredingen worden vastgesteld tijdens de controle dan worden deze vastgelegd in een rapport."
--PURPOSE RELATION isVermeldIn[Overtreding * Rapport] REF "Awb Artikel 5 lid 48.1"
--{+Het bestuursorgaan en de voor de overtreding bevoegde toezichthouder kunnen van de overtreding een rapport opmaken.-}



PATTERN Sanctionering
PURPOSE PATTERN Sanctionering REF "www.onderwijsinspectie.nl"
{+Handhaving omvat een groot aantal activiteiten die gericht zijn op het bevorderen van de kwaliteit van het aangeboden onderwijs, kwaliteit van bestuur en het bevorderen van de financiële continuïteit. Als wij de juiste voorwaarden hebben geschapen voor scholen om conform de regels te opereren, komt eventueel een stringentere invulling van het handhavingsbeleid aan de orde bij die scholen waar dat niet bij helpt. We praten dan over sanctioneren.

Hoewel het opleggen van bestuurlijke sancties in geregeld in hoofdstuk 5 van de Algemene wet bestuursrecht, gaat het hierbij ook over de definitieve vaststelling van onderwijssubsidies. De bekostiging van scholen is ingericht als subsidie, waarbij voorwaarden worden opgelegd. Wanneer wordt geconstateerd dat niet voldaan is aan die voorwaarden, kan de beschikking waarbij de subsidie is vastgesteld alsnog worden gewijzigd of zelfs worden ingetrokken. Dit wordt gezien als sanctie.
-}

gevolgVan :: Sanctietraject -> Tekortkoming
PRAGMA "Sanctietraject " " is het gevolg van " 
MEANING "Ieder sanctietraject is het gevolg van één tekortkoming."
PURPOSE RELATION gevolgVan[Sanctietraject * Tekortkoming] REF "Toezichtkader PO/VO 2012"
{+Indien wordt geconstateerd dat scholen en/of besturen tekortkomingen met betrekking tot het voldoen aan wet- en regelgeving hebben, krijgen zij van de 
inspectie opdracht tot herstel. Indien dat herstel niet of onvoldoende tijdig plaatsvindt, of indien herstel niet langer mogelijk is, volgen een aantal escalatiestappen. De precieze inhoud van die escalatiestappen is afhankelijk van de wettelijke regel die overtreden wordt. De ultieme maatregel is een sanctiemogelijkheid.-}

deadlineTraject :: Sanctietraject * Datum [TOT,UNI]
PRAGMA "" " moet gestart zijn voor "
MEANING "Een sanctietraject moet binnen een vaste periode na vaststelling zijn afgerond."
PURPOSE RELATION einddatum [Sanctietraject * Datum]
{+Afhankelijk van de aard van het onderzoek, waarbij de tekortkoming is geconstateerd (bijvoorbeeld dat naar aanleiding van de jaarstukken), bestaat er een vaste periode waarin de Inspectie van het Onderwijs het sanctietraject formeel zal moeten afronden.-}

voorgenomen :: Sanctietraject * Datum [UNI] 
PRAGMA "" " heeft een voorgenomen "
MEANING "Een sanctietraject kan één dagtekening van het voornemen bevatten." 
PURPOSE RELATION voorgenomen [Sanctietraject * Datum]
{+Een sanctietraject is de aanloop tot een beschikking. Vaak wordt de beschikking aangekondigd, voordat deze definitief wordt. Indien dit voornemen tot de beschikking wordt verstuurd, dan wordt de datum waarop dat gebeurt vastgelegd bij het sanctietraject. Er kunnen overigens ook redenen zijn om af te zien van de aankondiging.-}

deadlineZienswijze :: Sanctietraject * Datum [UNI] 
PRAGMA "" " heeft als uiterste reactiedatum "
MEANING "Een sanctietraject kan één datum bevatten waarop een zienswijze uiterlijk moet zijn ontvangen."
PURPOSE RELATION deadlineZienswijze [Sanctietraject * Datum]
{+Afhankelijk van de omstandigheden van het geval wordt een redelijke termijn gesteld waarbinnen het bevoegd gezag haar zienswijze kenbaar kan maken. De redelijke termijn die doorgaans wordt gesteld is er een van vier weken.-}

initieel :: Sanctietraject * FinancieelBelang [UNI,TOT]
PRAGMA "Het initieel financieel belang van sanctietraject " " bedraagt "
MEANING "Voor elk sanctietraject is één initieel financieel belang bepaald." 
PURPOSE RELATION initieel [Sanctietraject * FinancieelBelang]
{+Voordat het sanctietraject is gestart wordt eerst in een berekening vastgesteld wat het mogelijke financiele belang is.-}

definitief :: Beschikking * FinancieelBelang [UNI,TOT]
PRAGMA "Het financieel belang van sanctietraject " " is definitief vastgesteld op "
MEANING "Elke beschikking kan één definitief financieel belang hebben." 
PURPOSE RELATION definitief [Beschikking * FinancieelBelang]
{+Het financiële belang kan echter gaandeweg het sanctietraject worden aangepast, bijvoorbeeld op basis van argumentatie in de zienswijze.-}
 
op :: Sanctietraject * Zienswijze [UNI]
PRAGMA "" " kan opgevolgd worden door "
MEANING "Op een voornemen kan één zienswijze zijn gegeven."
PURPOSE RELATION op [Sanctietraject * Zienswijze] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+Wanneer een voornemen tot beschikking wordt gestuurd aan een bestuur, dan krijgt dat bestuur de gelegenheid om binnen een redelijke termijn, (aangegeven bij het voornemen) om haar zienswijze te geven. Deze zienswijze kan van invloed zijn op de uiteindelijke beschikking.-}

einddatum :: Sanctietraject * Datum [UNI]
PRAGMA "" " is afgerond op " 
MEANING "Elk sanctietraject kan op één datum zijn afgerond."
PURPOSE RELATION einddatum [Sanctietraject * Datum] REF "Excel sheet"
{+Zodra de beschikking definitief is vastgesteld, wordt de uitvoering hiervan overgedragen aan DUO. Wanneer in rechte wordt opgekomen tegen het besluit, dan zal DUO de behandeling van bezwaar en beroep verzorgen. Uiteraard is de onderwijsinspectie hierbij ondersteunend, indien nodig. De uiteindelijke verrekening wordt ook door DUO verzorgd.

Nadat een sanctietraject is overgedragen aan DUO blijven de trajecten actief. Pas als DUO deze sluit wordt in overleg dit meegedeeld aan de inspectie, waarop het traject als afgerond wordt beschouwd.-}

definitief :: Beschikking * Datum [UNI,TOT]
PRAGMA "" " besloten op "
MEANING "Elke beschikking is voorzien van een dagtekening."
PURPOSE RELATION definitief [Beschikking * Datum] REF "Artikel 13 - Toezichtkader PO/VO 2012"
{+De beslissing tot een sanctie kan, al dan niet voorafgegaan door een voornemen, op enig moment definitief worden gemaakt. De beschikking bevat dan de dagtekening van de beslissing.-}

gevolg :: Beschikking * Sanctietraject [UNI,TOT,INJ]
PRAGMA "" " gevolg van sanctietraject " 
MEANING "Elke beschikking is gevolg van één sanctietraject."
PURPOSE RELATION gevolg [Beschikking * Sanctietraject]
{+Een beschikking tot het opleggen van een sanctie wordt niet zomaar genomen. Voorafgaand hieraan wordt een sanctietraject gestart, waarin de beslissing eerst wordt voorgelegd aan het betreffende bevoegd gezag, die de mogelijkheid krijgt om een zienswijze in te dienen. Pas nadat die mogelijkheid is gegeven, kan een formele beslissing in de vorm van een beschikking worden genomen.-}

type :: Sanctietraject -> SanctieType
PRAGMA "" " is van het type "
MEANING "Elk sanctietraject heeft een type." 
PURPOSE RELATION type [Sanctietraject * SanctieType] REF "Excel sheet"
{+Omdat er verschillende soorten sanctietrajecten kunnen zijn, wordt elk individueel geval voorzien van een aanduiding van het type waarvan sprake is.-}

ENDPATTERN

ENDCONTEXT
