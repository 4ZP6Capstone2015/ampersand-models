CONTEXT RWS IN DUTCH
{- Dit bestand bevat functioneel precies hetzelfde als RWS.adl (afgezien van dit kommentaar) -}

PROCESS Kabels

kabelNummer :: Kabel -> KabelNummer
KEY "Kabel nummers": Kabel(kabelNummer)

kabelSoort :: Kabel * KabelSoort

kabelVanInstallatie :: Kabel * Installatie
ROLE USER MAINTAINS "Kabel beginpunt [uni]"
RULE "Kabel beginpunt [uni]": I /\ kabelVanInstallatie;-I;kabelVanInstallatie~ |- -V
MEANING "Elke kabel kan van hoogstens 1 installatie uit gaan" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere BEGINpunten, waaronder ", SRC kabelVanInstallatie)

kabelNaarInstallatie:: Kabel * Installatie
ROLE USER MAINTAINS "Kabel eindpunt [uni]"
RULE "Kabel eindpunt [uni]": I /\ kabelNaarInstallatie;-I;kabelNaarInstallatie~ |- -V
MEANING "Elke kabel kan naar maximaal 1 installatie lopen" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere EINDpunten, waaronder ", SRC kabelNaarInstallatie)

instSoort :: Installatie * InstallatieSoort [UNI]

instAansluitpunt :: Installatie * Aansluitpunt

ENDPROCESS
-----------------------------------------------------------
--[Overzicht]-----------------------------------------------
INTERFACE "Overzicht" FOR USER : '_SESSION'[SESSION]
TABS[ "Kabels"         : V[SESSION*Kabel] INTERFACE "Kabels"
    , "Installaties"   : V[SESSION*Installatie] INTERFACE "Installaties"
    , "Aansluitpunten" : V[SESSION*Aansluitpunt]
    ]

INTERFACE "Issues" FOR USER : '_SESSION'[SESSION]
TABS[ "Kabel issues" : V[SESSION*Kabel];
    (  (I /\ kabelVanInstallatie;-I;kabelVanInstallatie~) -- meerdere beginpunten
    \/ (I /\ kabelNaarInstallatie;-I;kabelNaarInstallatie~) -- meerdere eindpunten
    \/ (I /\  kabelNaarInstallatie;kabelNaarInstallatie~ /\ -(kabelVanInstallatie;kabelVanInstallatie~)) -- alleen agsl op eindpunt
    \/ (I /\ -(kabelNaarInstallatie;kabelNaarInstallatie~) /\ kabelVanInstallatie;kabelVanInstallatie~) -- alleen agsl op beginpunt
    ) INTERFACE "Kabels"

    , "Installatie issues" : V[SESSION*Installatie];
    (  (I /\ kabelVanInstallatie~;(I /\ -(kabelNaarInstallatie;kabelNaarInstallatie~));kabelVanInstallatie) -- kabel gaat nergens heen
    \/ (I /\ kabelNaarInstallatie~;(I /\ -(kabelVanInstallatie;kabelVanInstallatie~));kabelNaarInstallatie) -- kabel komt nergens vandaan
    ) INTERFACE "Installaties"
    ]

INTERFACE "Kabels" FOR USER : I[Kabel]
COLS[ "Kabelnr" : I
    , "Soort " : kabelSoort
    , "Van (installatie)" : kabelVanInstallatie
    , "Naar (installatie)" : kabelNaarInstallatie
    , "Problemen" : (I /\ kabelVanInstallatie;-I;kabelVanInstallatie~
                    );V;'Kabel heeft meerdere installaties als beginpunt'[TEXT]
                 \/ (I /\ kabelNaarInstallatie;-I;kabelNaarInstallatie~
                    );V;'Kabel heeft meerdere installaties als eindpunt'[TEXT]
                 \/ (I /\  kabelNaarInstallatie;kabelNaarInstallatie~
                       /\ -(kabelVanInstallatie;kabelVanInstallatie~)
                    );V;'Kabel is eenzijdig op eindpunt aangesloten'[TEXT]
                 \/ (I /\ -(kabelNaarInstallatie;kabelNaarInstallatie~)
                       /\ kabelVanInstallatie;kabelVanInstallatie~
                    );V;'Kabel is eenzijdig op beginpunt aangesloten'[TEXT]
    ]

INTERFACE "Installaties" : I[Installatie]
COLS[ "Installatienummer " : I
    , "Soort installatie" : instSoort
    , "Kabel problemen" : kabelVanInstallatie~;(I /\ -(kabelNaarInstallatie;kabelNaarInstallatie~))
                       \/ kabelNaarInstallatie~;(I /\ -(kabelVanInstallatie;kabelVanInstallatie~)) 
    COLS[ "Kabel" : I
        , "Probleem" : (I /\ -(kabelNaarInstallatie;kabelNaarInstallatie~)
                       );V;'gaat nergens heen.'[TEXT]
                    \/ (I /\ -(kabelVanInstallatie;kabelVanInstallatie~)
                       );V;'komt nergens vandaan.'[TEXT]
        ]
    ]

PROCESS DefRoleUser -- Dummy
ROLE USER MAINTAINS "Define User Role"
RULE "Define User Role": I[Kabel] = I[Kabel]
ENDPROCESS

-----------------------------------------------------------
ENDCONTEXT