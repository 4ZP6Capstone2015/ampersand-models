CONTEXT RWS IN DUTCH
{- RWS is een proof-of-principle  voor het opschonen van gegevensbestanden.
CONTEXT: 
Installaties kunnen onderling zijn verbonden via kabels.
Dit moet worden geadministreerd.
Een dergelijke administratie kan fouten bevatten - zeker als hij groot is.

DEMO:
Voorbereiden:
  - Vertaal dit ADL bestand naar een prototype, start hem op en reset 
    de database (die is dan leeg).
  - Als er al een bestand "Bron.xlsx" bestaat, gooi hem dan weg.
  - Kopieer het bestand "Kabeladministratie (fout).xlsx" naar "Bron.xlsx"
Stap 1: 
  - Importeer "Bron.xlsx".
  - Klik op 'Overzicht' in de menubalk, en laat zien dat je een overzicht 
    hebt van de kabels en installaties, en dat de fouten daar zijn opgemerkt.
  - Klik op 'Issues' in de menubalk, en 
    laat zien dat je hier alleen die dingen ziet waar problemen mee zijn.
Stap 2:
  - Herstel alle fouten in "Bron.xlsx"
    hiermee simuleer je het op orde brengen van de administratie.
    (ter info: het bestand "Kabeladministratie (goed).xlsx" bevat geen fouten
               en zou je dus ook kunnen gebruiken)
  - Reset de database (dan is-ie weer leeg).
  - Importeer "Bron.xlsx" en kijk (als in stap 1) of er geen issues meer zijn.
Herhaal stap 2 tot er geen issues meer zijn.

N.B.: het bestand "Kabeladministratie (goed).xlsx" bevat originele gegevens
      op 'Blad1'. 'Sheet1' bevat dezelfde gegevens, maar nu in een formaat
      dat voor ons importeerbaar is (van XLSX bestanden wordt alleen Sheet1
      ingelezen). Je kunt dit zelf proberen (wel eerst de DB resetten!).
      Het levert natuurlijk geen Issues op.
-}
PROCESS Kabels

kabelNummer :: Kabel -> KabelNummer
KEY "Kabel nummers": Kabel(kabelNummer)

kabelSoort :: Kabel * KabelSoort [UNI]
MEANING "Elke kabel is van een zeker soort, die bekend kan zijn."

vanInst :: Kabel * Installatie
ROLE USER MAINTAINS "Kabel beginpunt [uni]"
RULE "Kabel beginpunt [uni]": I /\ vanInst;-I;vanInst~ |- -V
MEANING "Elke kabel kan van hoogstens 1 installatie uit gaan" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere BEGINpunten, waaronder ", SRC vanInst)

naarInst:: Kabel * Installatie
ROLE USER MAINTAINS "Kabel eindpunt [uni]"
RULE "Kabel eindpunt [uni]": I /\ naarInst;-I;naarInst~ |- -V
MEANING "Elke kabel kan naar maximaal 1 installatie lopen" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere EINDpunten, waaronder ", SRC naarInst)

instSoort :: Installatie * InstallatieSoort [UNI]
MEANING "Elke installatie is van een zeker type, die bekend kan zijn."

ENDPROCESS
--[Interfaces]---------------------------------------------
INTERFACE "Overzicht" FOR USER : '_SESSION'[SESSION]
TABS[ "Kabels"         : V[SESSION*Kabel] INTERFACE "[Kabels]"
    , "Installaties"   : V[SESSION*Installatie] INTERFACE "[Installaties]"
    , "Aansluitpunten" : V[SESSION*Aansluitpunt]
    ]

INTERFACE "Issues" FOR USER : '_SESSION'[SESSION]
TABS[ "Kabel issues" : V[SESSION*Kabel];
    (  (I /\ vanInst;-I;vanInst~) -- meerdere beginpunten
    \/ (I /\ naarInst;-I;naarInst~) -- meerdere eindpunten
    \/ (I /\  naarInst;naarInst~ /\ -(vanInst;vanInst~)) -- alleen agsl op eindpunt
    \/ (I /\ -(naarInst;naarInst~) /\ vanInst;vanInst~) -- alleen agsl op beginpunt
    ) INTERFACE "[Kabels]"

    , "Installatie issues" : V[SESSION*Installatie];
    (  (I /\ vanInst~;(I /\ -(naarInst;naarInst~));vanInst) -- kabel gaat nergens heen
    \/ (I /\ naarInst~;(I /\ -(vanInst;vanInst~));naarInst) -- kabel komt nergens vandaan
    ) INTERFACE "[Installaties]"
    ]

INTERFACE "[Kabels]" FOR USER : I[Kabel]
COLS[ "Kabelnr" : I
    , "Soort " : kabelSoort
    , "Van (installatie)" : vanInst
    , "Naar (installatie)" : naarInst
    , "Problemen" : (I /\ vanInst;-I;vanInst~);V;'Kabel heeft meerdere installaties als beginpunt'[TEXT]
                 \/ (I /\ naarInst;-I;naarInst~);V;'Kabel heeft meerdere installaties als eindpunt'[TEXT]
                 \/ (I /\  naarInst;naarInst~ /\ -(vanInst;vanInst~));V;'Kabel is eenzijdig op eindpunt aangesloten'[TEXT]
                 \/ (I /\ -(naarInst;naarInst~) /\ vanInst;vanInst~);V;'Kabel is eenzijdig op beginpunt aangesloten'[TEXT]
    ]

INTERFACE "[Installaties]" : I[Installatie]
COLS[ "Installatienummer " : I
    , "Soort installatie" : instSoort
    , "Kabel problemen" : vanInst~;(I /\ -(naarInst;naarInst~))
                       \/ naarInst~;(I /\ -(vanInst;vanInst~)) 
    COLS[ "Kabel" : I
        , "Probleem" : (I /\ -(naarInst;naarInst~));V;'gaat nergens heen.'[TEXT]
                    \/ (I /\ -(vanInst;vanInst~));V;'komt nergens vandaan.'[TEXT]
        ]
    ]
-----------------------------------------------------------
ENDCONTEXT