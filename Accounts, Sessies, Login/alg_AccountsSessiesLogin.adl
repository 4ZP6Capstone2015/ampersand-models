CONTEXT "Accounts, Sessies en Login" IN DUTCH LATEX
{- PURPOSE CONTEXT "Accounts, Sessies en Login" {+

Dit script is generiek bruikbaar - althans zo vindt de auteur dat. Het is dan ook niet de bedoeling om hier flink aan te gaan zitten editen. Je kunt het bestand gewoon INCLUDEn in een van je eigen scripts, en dan in die bestanden weer additionele sessie variabelen en/of interfaces e.d. maken. In het bijzonder kun je er dus ook zelf je eigen persoonsadministratie aan hangen - daarvoor hoef je niet in dit bestand te zijn.

Dit script kun je vertalen en dan werkt het. Er is dan een account administratie waarin alleen het 'admin' account is gepopuleerd. In de interface 'Accounts' kan deze admin dan nieuwe accounts aanmaken. Als je dat doet, dan kunnen die nieuwe gebruikers hun eigen account wijzigen (username, password, maar niet hun persoons-id). Alleen als een dergelijke gebruiker de rol 'Accountbeheerder' heeft kan hij ook andere accounts wijzigen. Merk op dat een Accountbeheerder zijn eigen rollen niet kan wijzigen - dat moet een andere Accountbeheerder doen... Door dit voorbeeld kun je zien hoe je met rollen e.d. om kunt gaan binnen INTERFACEs.
-}

--[Sessies]------------------------------------------------
PROCESS "Sessies"
PURPOSE PROCESS "Sessies"
{+Dit hoofdstuk beschrijft de termen die nodig zijn om personen (gebruikers) te laten communiceren met het systeem, en op een gepersonaliseerde manier toegang te kunnen krijgen tot de gegevens uit het systeem. Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit proces wordt gebruikt.

Het personaliseren van de manier van toegang gebeurt op basis van attributen die, direct of indirect, aan de gebruiker zijn toegekend. Het kan bijvoorbeeld gaan over diens functie in de organisatie, de OrgUnit waar hij is geplaatst of de Bedrijfsfuncties die hij in beheer heeft. Het kan echter ook gaan over rollen die aan het Account van de gebruiker zijn toegekend. Op basis van zulke gegevens wordt een doorsnijding uit de datawolk van het systeem gemaakt, waar de gebruiker in zijn sessie mee kan werken. Dit hoofdstuk definieert niet alleen deze additionele sessie variabelen, maar ook de regels die de datawolk bepalen voor een zekere sessie. 
-}
--?Afpopuleren is alleen nodig als de sessie variabele gedurende de sessie van waarde kan veranderen, omdat bij het uitloggen de hele sessie verdwijnt, inclusief de bijbehorende sessie variabelen. Merk op dat een van de mogelijkheden dat sessie variabelen veranderen, is dat er in een andere sessie (ook) met de datawolk wordt gewerkt. Dat geldt bijvoorbeeld als een actor met gegevens werkt (in een sessie) en dat gedurende die sessie, bijvoorbeeld door een beheersactiviteit in een andere sessie, deze gegevens niet meer bij mij horen. Vooralsnog is het een goed gebruik om het afpopuleren gewoon mee te modelleren en niet te gaan voor de 'winst' van het niet hoeven opschrijven/evalueren van die regels (dat laatste temeer omdat we later voorzien dat zulke regels aan INTERFACEs gehangen kunnen worden; dan worden deze regels aan de login/logout interfaces gehangen en is een evt. performance probleem opgelost).

sessionUser:: SESSION * Persoon [UNI]
MEANING "In een sessie kan de (responsible) actor/persoon bekend zijn."

PURPOSE RULE "Sessie context opzetten"
{+Als een gebruiker is ingelogd, d.w.z. het sessieAccount is vastgesteld, dan kunnen ook andere sessie variabelen worden gepopuleerd. Dat geldt bijvoorbeeld voor de sessie variabelen die persoons- en organisatiegegevens bevatten.-}
ROLE ExecEngine MAINTAINS "Sessie context opzetten"
RULE "Sessie context opzetten": sessionAccount |- sessionUser;accPersoon~
MEANING "De gebruiker van een sessie kan bekend zijn."
VIOLATION (TXT "{EX} InsPair;sessionUser;SESSION;", SRC I, TXT ";Persoon;", TGT accPersoon)

sessionRoles :: SESSION * Rol
MEANING "In een sessie kunnen rollen (autorisaties) zijn geactiveerd."

ROLE ExecEngine MAINTAINS "Activeren van rollen"
RULE "Activeren van rollen": sessionAccount;accRoles |- sessionRoles
MEANING "In een sessie moeten de rollen van de gebruiker zijn geactiveerd."
VIOLATION (TXT "{EX} InsPair;sessionRoles;SESSION;", SRC I, TXT ";Rol;", TGT I)

ROLE ExecEngine MAINTAINS "Deactiveren van rollen"
RULE "Deactiveren van rollen": sessionRoles |- sessionAccount;accRoles
MEANING "Alleen de rollen van de gebruiker mogen zijn geactiveerd in een sessie."
VIOLATION (TXT "{EX} DelPair;sessionRoles;SESSION;", SRC I, TXT ";Rol;", TGT I)

ENDPROCESS

--[In- en Uitloggen]---------------------------------------

PROCESS "In- en Uitloggen"
PURPOSE PROCESS "In- en Uitloggen"
{+Het 'inloggen' (van een gebruiker in een sessie) bestaat uit het identificeren en authenticeren van die gebruiker en het vaststellen welk Account bij die gebruiker hoort, zodat dit Account met de sessie kan worden geassocieerd. Van daaruit kan de sessie context verder worden opgebouwd. Het 'uitloggen' (van een gebruiker uit een sessie) bestaat uit het verwijderen van de sessie, om te voorkomen dat een andere gebruiker later in die sessie zal gaan inloggen; dat is wenselijk omdat hiermee altijd ondubbelzinnig duidelijk is wie voor (de handelingen in) welke sessie verantwoordelijk is. Dit hoofdstuk beschrijft de betreffende functionaliteit, die (op het inloggen na) volledig geautomatiseerd verloopt. De afspraken zijn zodanig opgesteld dat het vrij eenvoudig is om een faciliteit te bouwen die het tijdelijk opschorten van een sessie mogelijk maakt, zoals dat wel gebeurt bij screensavers. Dat is echter niet verder gespecificeerd.-}

sessionAccount :: SESSION * Account [UNI]
MEANING "In een sessie kan het account van de gebruiker bekend zijn."

PURPOSE RELATION loginUID
{+Om te kunnen garanderen dat in elke sessie hoogstens één gebruiker kan inloggen, moet het systeem onderscheid kunnen maken tussen de gebruikersnaam die tijdens het inloggen wordt ingetypt, en een gebruikersnaam die al eerder is ingetypt.-}
loginUID :: SESSION * UserID [UNI]
MEANING "Bij het inloggen wordt gevraagd om een user ID."

PURPOSE RELATION loginWW
{+Om te kunnen controleren dat de gebruiker is die hij zegt dat hij is, moet het wachtwoord ingetypt kunnen worden.-}
loginWW :: SESSION * Wachtwoord [UNI]
MEANING "Bij het inloggen wordt gevraagd om een wachtwoord."

PURPOSE RULE "Inloggegevens moeten compleet zijn"
{+Als een gebruiker in eens sessie inlogt, moet hij doen met zowel een UserID als een Wachtwoord. Anders kan niet worden ingelogd omdat het bijbehorende Account niet kan worden bepaald en ook niet kan worden geauthenticeerd.-}
RULE "Inloggegevens moeten compleet zijn":
   '_SESSION'[SESSION] /\ loginUID;loginUID~ = '_SESSION'[SESSION] /\ loginWW;loginWW~
MEANING "Inloggegevens zijn niet compleet."
VIOLATION (TXT "U moet zowel een UserID als een Wachtwoord opgeven. Probeer het opnieuw.")

PURPOSE RULE "Gebruikersauthenticatie"
{+Als een gebruiker in eens sessie inlogt, moet hij daar goede credentials voor gebruiken, d.w.z. een UserID en Wachtwoord die bij een geregistreerd Account horen.-}
RULE "Gebruikersauthenticatie":
   loginUID~;loginWW |- accUserid~;accPassword
MEANING "U mag alleen met geldige credentials inloggen."
VIOLATION (TXT "De UserID en/of het Wachtwoord klopt niet. Probeer het opnieuw.")

PURPOSE RULE "Inloggen"
{+Een gebruiker moet in eens sessie kunnen inloggen, zodat zijn persoonlijke context kan worden opgezet (sessie variabelen van waarden kunnen worden voorzien). Daartoe moet hij de gebruikersnaam (UserID) en het wachtwoord intypen van het Account dat aan hem persoonlijk is toegekend. Inloggen in een sessie kan slechts een keer - zodra een gebruiker is ingelogd, blijft het aan de sessie toegekende Account hetzelfde.-}
ROLE ExecEngine MAINTAINS "Inloggen"
RULE "Inloggen": (loginUID;accUserid~ /\ loginWW;accPassword~) |- sessionAccount
MEANING "De gebruiker in een (nieuwe) sessie moet zijn geauthenticeerd."
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;loginUID;SESSION;", SRC I, TXT ";UserID;", SRC loginUID
          ,TXT "{EX} DelPair;loginWW;SESSION;", SRC I, TXT ";Wachtwoord;", SRC loginWW -- Passwords moet je niet willen onthouden.
          )

PURPOSE RELATION firstSessionAccount
{+Om het (later) mogelijk te maken een sessie op te schorten moet van de sessie het eerste sessieaccount onthouden worden, omdat aan het eind van deze opschorting de oorspronkelijke gebruiker weer moet kunnen inloggen, en niet iemand anders.-}
firstSessionAccount :: SESSION * Account [UNI]
MEANING "Het Account dat in een sessie als eerste is geauthenticeerd, moet geregistreerd worden."

ROLE ExecEngine MAINTAINS "Het eerst geauthenticeerde sessie account registreren"
RULE "Het eerst geauthenticeerde sessie account registreren": 
   ('_SESSION'[SESSION] /\ -(firstSessionAccount;firstSessionAccount~));sessionAccount
|- firstSessionAccount
MEANING "Voor de eerste keer inloggen mag met elk (geauthenticeerd) account."
VIOLATION (TXT "{EX} InsPair;firstSessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I)

PURPOSE RULE "Integriteit van sessie accounts"
{+Er mag hooguit één gebruiker inloggen in een sessie, zodat kan worden gegarandeerd dat het ondubbelzinnig duidelijk is welke persoon verantwoordelijk is voor wat er in een sessie gebeurt. Dat moet dus ook worden gecontroleerd.-}
RULE "Integriteit van sessie accounts" : 
   ('_SESSION'[SESSION] /\ firstSessionAccount;firstSessionAccount~);sessionAccount
|- firstSessionAccount 
MEANING "In een sessie mag hoogstens één account worden gebruikt."
VIOLATION (TXT "U moet met uw eerder ingevoerde credentials inloggen.")

PURPOSE RULE "Uitloggen"
{+Een ingelogde gebruiker moet ook uit kunnen loggen, zodat zijn persoonlijke context wordt opgeruimd. Dat betekent concreet dat de sessie wordt verwijderd, waardoor ook alle sessie variabelen verdwijnen. Een gebruiker logt uit door zij Account van de sessie te ontkoppelen.-}
ROLE ExecEngine MAINTAINS "Uitloggen"
RULE "Uitloggen": 
   ('_SESSION'[SESSION] /\ -(sessionAccount;sessionAccount~))
|- -(firstSessionAccount;firstSessionAccount~) 
MEANING "De sessie is beëindigd."
VIOLATION (TXT "{EX} DelAtom;SESSION;", SRC I)

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Login/Logout" (loginUID, loginWW, sessionAccount): V[ONE*SESSION];'_SESSION'
BOX[ "Inloggen" : I  /\ -(sessionAccount;sessionAccount~)
   BOX[ "UserID" : loginUID
      , "Wachtwoord" : loginWW
      ]   
   , "Uitloggen" : I  /\ sessionAccount;sessionAccount~
   BOX[ "UserID" : sessionAccount
      , "Rollen" : sessionRoles --? Mag dit een menu worden (van te gebruiken interfaces)?
      ]
   ]

--[Accounts]-----------------------------------------------
PATTERN Accounts
PURPOSE PATTERN Accounts
{+Elk systeem wordt door meerder personen gebruikt. Die mogen echter niet allemaal dezelfde dingen doen met het systeem. Het proces 'Sessies' berekent welke activitieten een gebruiker mag uitvoeren en de gegevens die hij daarbij mag betrekken - de sessie-datawolk. Dit hoofdstuk specificeert de (persoonsgebonden) gegevens op basis waarvan zulke berekeningen kunnen plaatsvinden.-}

RELATION accPersoon[Account*Persoon] [UNI,TOT] PRAGMA "" " is toegekend aan "
MEANING "Elk Account is toegekend aan één Persoon - de gebruiker van het Account."

RULE "Account uniciteit": accUserid;accUserid~ /\ accPassword;accPassword~ |- I[Account]
MEANING "De gebuiker van een Account kan worden geïdentificeerd middels het UserID en Wachtwoord."

--!RJ/20141206: Onderstaande relatie zou ook nog 'INJ' moeten (kunnen) zijn. Daar heeft de prototypegenerator nog problemen mee.
RELATION accUserid[Account*UserID] [UNI,TOT] PRAGMA "" " heeft als gebruikersnaam "
MEANING "Het UserId van de gebruiker van een Account moet zijn geregistreerd en is uniek voor dat Account."

RELATION accPassword[Account*Wachtwoord] [UNI,TOT] PRAGMA "" " heeft als wachtwoord "
MEANING "Het Wachtwoord van de gebruiker van een Account moet zijn geregistreerd."

RELATION accRoles[Account*Rol] PRAGMA "" " heeft Rol " " toegewezen gekregen "
MEANING "Alle Rol(len) die aan de gebruiker van een Account zijn toegekend, moeten zijn geregistreerd."

ENDPATTERN
-----------------------------------------------------------
PROCESS AccountBeheerInterface
PURPOSE PROCESS AccountBeheerInterface
{+Om accounts te beheren is een beheer interface ingericht. Zoals met alle interfaces dient ook deze ertoe om de samenhang te waarborgen tussen het (consistente) 'wereldbeeld' binnen in het systeem, en dat van de gebruiker. Dat kan inhouden dat er af en toe transformaties moeten plaatsvinden. Dit proces beschrijft zulke transformaties voor de Account beheer interface.-}

accMgtAccount :: SESSION * Account [UNI]
MEANING "In een sessie kan een nieuw account worden aangemaakt"

ROLE ExecEngine MAINTAINS "Nieuw account is aangemaakt"
RULE "Nieuw account is aangemaakt": '_SESSION'[SESSION] |- -(accMgtAccount;accMgtAccount~)
MEANING "Nadat een nieuw account is aangemaakt in de beheersinterface moet 'accMgtAccount' weer leeg worden."
VIOLATION (TXT "{EX} DelPair;accMgtAccount;SESSION;", SRC I, TXT ";Account;", TGT accMgtAccount)

accMgtUserid :: Account * UserID [UNI]
MEANING "Het UserID van een Account die wordt gebruikt in de beheersinterface."

ROLE ExecEngine MAINTAINS "UserID kan worden gewijzigd"
RULE "UserID kan worden gewijzigd": (I /\ -(accMgtUserid;accMgtUserid~));accUserid |- accMgtUserid
MEANING "Het UserID van een Account die wordt gebruikt in de beheersinterface moet initieel gelijk worden gesteld aan het UserID van het account zelf."
VIOLATION (TXT "{EX} InsPair;accMgtUserid;Account;", SRC I, TXT ";UserID;", TGT I)

ROLE ExecEngine MAINTAINS "UserID is gewijzigd"
RULE "UserID is gewijzigd": accMgtUserid |- accUserid
MEANING "Het UserID van een Account moet gelijk zijn aan de (mogelijk veranderde) waarde uit de beheersinterface."
VIOLATION (TXT "{EX} InsPair;accUserid;Account;", SRC I, TXT ";UserID;", TGT I)

accMgtPassword :: Account * Wachtwoord [UNI]
MEANING "Het Wachtwoord van een Account die wordt gebruikt in de beheersinterface."

ROLE ExecEngine MAINTAINS "Wachtwoord kan worden gewijzigd"
RULE "Wachtwoord kan worden gewijzigd": (I /\ -(accMgtPassword;accMgtPassword~));accPassword |- accMgtPassword
MEANING "Het Wachtwoord van een Account die wordt gebruikt in de beheersinterface moet initieel gelijk worden gesteld aan het Wachtwoord van het account zelf."
VIOLATION (TXT "{EX} InsPair;accMgtPassword;Account;", SRC I, TXT ";Wachtwoord;", TGT I)

ROLE ExecEngine MAINTAINS "Wachtwoord is gewijzigd"
RULE "Wachtwoord is gewijzigd": accMgtPassword |- accPassword
MEANING "Het Wachtwoord van een Account moet gelijk zijn aan de (mogelijk veranderde) waarde uit de beheersinterface."
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Wachtwoord;", TGT I)

ENDPROCESS
-----------------------------------------------------------
VIEW Account : Account(accUserid)

INTERFACE "Accounts" 
   (accPersoon, accUserid, accPassword, accRoles 
   ,accMgtAccount, accMgtUserid, accMgtPassword
   ) : V[ONE*SESSION];'_SESSION'
BOX[ "Mijn Account" : sessionAccount 
   BOX[ "Gebruiker" : I;accPersoon --$ de 'I;' voorkomt dat hier ge-edit kan worden.
      , "User ID"   : accMgtUserid
      , "Wachtwoord": accMgtPassword
      ]
   , "Andere accounts" : I /\ sessionRoles;'Accountbeheerder';sessionRoles~
   BOX[ "Nieuw account"  : accMgtAccount
      BOX[ "Gebruiker"   : accPersoon
         , "User ID"     : accUserid
         , "Wachtwoord"  : accPassword
         , "Rol(len)"    : accRoles
         ]
      , "Bestaande accounts" : V[SESSION*Account] /\ -sessionAccount
      BOX[ "Gebruiker"   : accPersoon
         , "User ID"     : accUserid
         , "Wachtwoord"  : accPassword
         , "Rol(len)"    : accRoles
         ]
      ]
   ]

-----------------------------------------------------------

POPULATION accPersoon  CONTAINS [ ("admin", "admin") ]
POPULATION accUserid   CONTAINS [ ("admin", "admin") ]
POPULATION accPassword CONTAINS [ ("admin", "admin") ]
POPULATION accRoles    CONTAINS [ ("admin", "Accountbeheerder") ]

--[Glossary]-----------------------------------------------

PURPOSE CONCEPT SESSION
{+Een persoon gebruikt een gegevensruimte (en heet dan 'gebruiker') door met een browser (bijv. Chrome of Firefox) het systeem te benaderen dat de gegegensruimte beheert. Als meerdere personen een gegevensruimte delen, moet het systeem de context van elk van hen kunnen onderscheiden, bijvoorbeeld om:
\begin{itemize}
\item de interactie 'klein' te houden, d.w.z. alleen gegevens te laten zien die voor hem/haar relevant zijn;
\item ervoor te zorgen dat een gebruiker niet ziet wat hij niet mag zien;
\item te kunnen bijhouden welke persoon, of welk(e) organisatie(onderdeel) verantwoordelijk is voor een zekere transactie;
\item automatisch gegevens betreffende de gebruiker of zijn context aan transacties toe te kunnen voegen
\end{itemize}
We gebruiken de term 'SESSION' of 'sessie' om de verzameling van gegevens betreffende één (actieve) gebruiker mee aan te geven. Deze term correspondeert met de gelijknamige term browsers gebruiken, nl. 'een verbinding (door de browser) met een webservice (die een URL heeft)'. Het systeem houdt één sessie bij voor elke actieve gebruiker, d.w.z. voor elke browser die het systeem benadert. Merk op dat dit in het bijzonder geldt als de gebruiker in verschillende tabbladen van dezelfde browser het systeem benadert - er is dan toch maar één sessie (en één gebruiker).
-}
CONCEPT SESSION "een verzameling van gegevens die de context beschrijven waarin één persoon het systeem gebruikt"

PURPOSE CONCEPT UserID
{+De User ID (gebruikersnaam) van een account identificeert dat account en daarmee ook diens eigenaar (d.w.z. de persoon die als enige geacht wordt van dat account gebruik te maken. Een goed gebruik is om hiervoor een e-mailadres te gebruiken waarop de gebruiker van het account bereikbaar is.-}
CONCEPT UserID "een rij karakters (bijvoorbeeld het e-mailadres van de gebruiker), die een account identificeert binnen het systeem"

PURPOSE CONCEPT Wachtwoord
{+Om het moeilijk te maken dat het Account van een zeker persoon door een ander wordt gebruikt, registreert het systeem wachtwoorden.
Door een wachtwoord geheim te houden ontstaat enige mate van zekerheid dat het systeem gebruikt word door (dan wel met medeweten van) de persoon op wiens naam het wachtwoord is geregistreerd.-}
CONCEPT Wachtwoord "een rij karakters, die geheim gehouden kan worden door een persoon, en door die persoon gebruikt moet worden om toegang te krijgen tot het systeem"

PURPOSE CONCEPT Rol
{+Niet iedereen mag alle gegevens uit een systeem inzien en/of wijzigen. Om de beheerslast te beperken die ontstaat als dit soort rechten per persoon wordt uitgegeven, gebruiken we het concept 'Rol'. Een Rol wordt enerzijds toegekend aan Accounts (en daarmee aan Personen) en anderzijds wordt hij gebruikt om gegevens in te zien en/of te wijzigen. Als een gebruiker inlogt worden de Rollen die aan hem zijn toegekend in de sessie geactiveerd (sessierollen). Interfaces gebruiken deze sessierollen om al dan niet gegevens te tonen en/of te editen.-}
CONCEPT Rol "de naam voor het mogen inzien en/of wijzigen van zekere gegevens, die kan worden toegekend aan accounts en in sessies kan worden geactiveerd" 

PURPOSE CONCEPT Account
{+Als iemand inlogt in het systeem moet diens 'context' worden geactiveerd, d.w.z. de gegevens over de persoon die het systeem nodig heeft om te kunnen berekenen wat hij/zij wel en niet mag doen, en welke gegevens van het systeem daarbij mogen worden gebruikt. Om zulke berekeningen te kunnen maken wordt een aantal zaken geregistreerd en aan één persoon gekoppeld.-}
CONCEPT Account "een verzameling gegevens die (een deel van) de gebruikerscontext van één persoon binnen het systeem beschrijft"

-----------------------------------------------------------
ENDCONTEXT