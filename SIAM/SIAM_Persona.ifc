CONTEXT "Persona Interfaces" IN ENGLISH
-----------------------------------------------------------

INCLUDE "../SIAM/SIAM_Persona.adl"

-----------------------------------------------------------
VIEW Persona: Persona(pActor;personFirstName, TXT " ", pActor;personLastName, TXT "(", pParty;orgAbbrName, TXT ")")

POPULATION Role CONTAINS [ "OrganizationMgr" ]

INTERFACE "New Organization" 
   (orgName
   ) FOR "OrganizationMgr" : I[Organization] Crud COLS
   [ "Name"   : orgName
   ]

INTERFACE "Update Organization Details" 
   (orgName
   ) FOR "OrganizationMgr" : I[Organization] cRUD COLS
   [ "Name"   : orgName
   ]

-----------------------------------------------------------

IDENT Persona: Persona(pActor,pParty,pRelation)

pActor :: Persona * Actor [UNI,TOT] MEANING "A Persona is associated with one Actor that performs all of its actions"
pParty :: Persona * Party [UNI,TOT] MEANING "A Persona is associated with one Party that is accountable for all of its actions"
pRelation :: Persona * Relationship [UNI] MEANING "A Persona is associated with a particular relationship (e.g. that of 'employee') that its Actor and Party have with one another."

CONCEPT Relationship "a (set of) purpose(s) that an Actor works to achieve and for which a Party is accountable"
REPRESENT Relationship TYPE ALPHANUMERIC

actorPartyRelationship :: Actor * Party
MEANING "For (at least) one purpose, an Actor may act for a Party"

ROLE ExecEngine MAINTAINS "Actor-Party relationship integrity (1)"
RULE "Actor-Party relationship integrity (1)": pActor~;pParty |- actorPartyRelationship
VIOLATION (TXT "{EX} InsPair;actorPartyRelationship;Actor;", SRC I, TXT ";Party;", TGT I)
ROLE ExecEngine MAINTAINS "Actor-Party relationship integrity (2)"
RULE "Actor-Party relationship integrity (2)": actorPartyRelationship |- pActor~;pParty
VIOLATION (TXT "{EX} DelPair;actorPartyRelationship;Actor;", SRC I, TXT ";Party;", TGT I)

--[Glossary]--
CONCEPT Actor "an entity that is capable of acting, such as a person, or a web-service"
CONCEPT Party "an entity that is capable of being accountable, such as an organization, or an individual person"

-----------------------------------------------------------
ENDCONTEXT