CONTEXT "Person and Contacts Registration" IN ENGLISH
{- This is a stand-alone file that implements a person registration is a minimalistic fashion.
This means that a Person has a First- and a Last name, and
so-called 'communication end points' can be defined where the Person can be reached.
-}

--[Persons (minimalistic)]--

IDENT Persons: Person(personFirstName,personLastName)

REPRESENT FirstName TYPE ALPHANUMERIC
RELATION personFirstName[Person*FirstName] [UNI,TOT]
MEANING "The first name of a Person can be registered."

REPRESENT LastName TYPE ALPHANUMERIC
RELATION personLastName[Person*LastName] [UNI,TOT]
MEANING "The last name of a Person can be registered."

-----------------------------------------------------------
VIEW Persons : Person(personFirstName, TXT " ", personLastName)

POPULATION Role CONTAINS [ "Persons Manager" ]

INTERFACE "New Person" (personFirstName, personLastName) FOR "Persons Manager": I[Person] Crud COLS
   [ "First name"   : personFirstName
   , "Last name"    : personLastName
   ]

INTERFACE "Update Person Details"
   (personFirstName, personLastName
   ): I[Person]  cRUD COLS
   [ "First name": personFirstName
   , "Last name" : personLastName
   ]

-----------------------------------------------------------
ENDCONTEXT