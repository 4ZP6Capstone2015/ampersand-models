CONTEXT "Roles" IN ENGLISH
PURPOSE CONTEXT "Roles"
{+This context defines Accounts, i.e. registrations (of attributes) of Persona from a (web)Service Provider's perspective. 
Examples of such attributes are usernames, passwords, roles.
Also, it defines ways to use such attributes in SESSIONs, by defining (and (de)populating) appropriate 'session-variables'.
Examples of session variables are: `sessionPerson`, `sessionAccount`.
Session-variables may be used to personalize the way that a user is given access to the system.

Personalizing the way access is being given is determined by attributes that have been assigned to the user, either directory or indirectly. Such attributes may be the function of the user in the organization, or the business unit he belongs to, the business function(s) that he has some doing with, etc. However, such attributes may also be roles that have been assigned to the users Axccount. Based on all such attributes, a cross-section of the data-cloud in the system is made with which the user may work in his session. This section defines not only the session variables that are specific to this system, but also the rules that determine the data-cloud for specific sessions.

Dit hoofdstuk beschrijft de termen die nodig zijn om personen (users) te laten communiceren met het systeem, en op een gepersonaliseerde manier toegang te kunnen krijgen tot de gegevens uit het systeem. Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit proces wordt gebruikt.

Het personaliseren van de manier van toegang gebeurt op basis van attributen die, direct of indirect, aan de user zijn toegekend. Het kan bijvoorbeeld gaan over diens functie in de organisatie, de OrgUnit waar hij is geplaatst of de Bedrijfsfuncties die hij in beheer heeft. Het kan echter ook gaan over rollen die aan het Account van de user zijn toegekend. Op basis van zulke gegevens wordt een doorsnijding uit de datawolk van het systeem gemaakt, waar de user in zijn session mee kan werken. Dit hoofdstuk definieert niet alleen deze additionele session variabelen, maar ook de regels die de datawolk bepalen voor een zekere session. 
-}

--[Account support for Roles/access control]--
accAllowedRoles :: Account * Role
MEANING "An Account registers the Roles that MAY be activated in a SESSION to which the Account is assigned"
accDefaultRoles :: Account * Role
MEANING "An Account registers the Roles that ARE activated in a SESSION once the Account is assigned"

ROLE ExecEngine MAINTAINS "Default roles must be allowed roles"
RULE "Default roles must be allowed roles": accDefaultRoles |- accAllowedRoles
VIOLATION (TXT "{EX} DelPair;accDefaultRoles;Account;", SRC I, TXT ";Role;", TGT I)

PURPOSE RELATION accIsGodAccount
{+There may be a need to have one (or a few) Account(s) that are allowed to activate all roles. We call an Account that has this property a 'god account'.-}
accIsGodAccount :: Account * Account [PROP] -- An account may have the property of being a God-account

--[Account-related Session Variables]--

sessionAllowedRoles :: SESSION * Role
MEANING "Within a SESSION, a Role may be activated"
ROLE ExecEngine MAINTAINS "Set allowed session roles"
RULE "Set allowed session roles": sessionAccount;(accAllowedRoles \/ accIsGodAccount;V) |- sessionAllowedRoles
VIOLATION (TXT "{EX} InsPair;sessionAllowedRoles;SESSION;", SRC I, TXT ";Role;", TGT I)
ROLE ExecEngine MAINTAINS "Clear allowed session roles"
RULE "Clear allowed session roles": sessionAllowedRoles |- sessionAccount;(accAllowedRoles \/ accIsGodAccount;V)
VIOLATION (TXT "{EX} DelPair;sessionAllowedRoles;SESSION;", SRC I, TXT ";Role;", TGT I)

sessionActiveRoles :: SESSION * Role
MEANING "A SESSION has activated a Role"
ROLE ExecEngine MAINTAINS "Activate roles in a session"
RULE "Activate roles in a session": 
   (I-(sessionActiveRoles;sessionActiveRoles~));sessionAccount;(accDefaultRoles \/ accIsGodAccount;V) |- sessionActiveRoles
VIOLATION (TXT "{EX} InsPair;sessionActiveRoles;SESSION;", SRC I, TXT ";Role;", TGT I)
ROLE ExecEngine MAINTAINS "Deactivate roles in a session"
RULE "Deactivate roles in a session": sessionActiveRoles |- sessionAccount;V
VIOLATION (TXT "{EX} DelPair;sessionActiveRoles;SESSION;", SRC I, TXT ";Role;", TGT I)

--[Glossary]-----------------------------------------------

PURPOSE CONCEPT Account
{+Als iemand inlogt in het systeem moet diens 'context' worden geactiveerd, d.w.z. de gegevens over de persoon die het systeem nodig heeft om te kunnen berekenen wat hij/zij wel en niet mag doen, en welke gegevens van het systeem daarbij mogen worden gebruikt. Om zulke computations te kunnen maken wordt een aantal zaken geregistreerd en aan één persoon gekoppeld.-}
CONCEPT Account "een verzameling gegevens die (een deel van) de gebruikerscontext van één gebruiker binnen het systeem beschrijft"

PURPOSE CONCEPT UserID
{+Het UserID (gebruikersnaam) van een account identificeert dat account en daarmee ook diens eigenaar (d.w.z. de persoon die als enige geacht wordt van dat account gebruik te maken. Een goed gebruik is om hiervoor een e-mailadres te gebruiken waarop de user van het account bereikbaar is.-}
CONCEPT UserID "een rij karakters (bijvoorbeeld het e-mailadres van de user), die een account identificeert binnen het systeem"
REPRESENT UserID TYPE ALPHANUMERIC

PURPOSE CONCEPT Password
{+Om het moeilijk te maken dat het Account van een zeker persoon door een ander wordt gebruikt, registreert het systeem wachtwoorden.
Door een wachtwoord geheim te houden ontstaat enige mate van zekerheid dat het systeem gebruikt word door (dan wel met medeweten van) de persoon op wiens naam het wachtwoord is geregistreerd.-}
CONCEPT Password "een rij karakters, die geheim gehouden kan worden door een persoon, en door die persoon gebruikt moet worden om toegang te krijgen tot het systeem"
REPRESENT Password TYPE PASSWORD

PURPOSE CONCEPT Role
{+Niet iedereen mag alle gegevens uit een systeem inzien en/of wijzigen. Om de beheerslast te beperken die ontstaat als dit soort rechten per persoon wordt uitgegeven, gebruiken we het concept 'Role'. Een Role wordt enerzijds toegekend aan Accounts (en daarmee aan Personen) en anderzijds wordt hij gebruikt om gegevens in te zien en/of te wijzigen. Als een user inlogt worden de Rollen die aan hem zijn toegekend in de session geactiveerd (sessionrollen). Interfaces gebruiken deze sessionrollen om al dan niet gegevens te tonen en/of te editen.-}
CONCEPT Role "de naam voor het mogen inzien en/of wijzigen van zekere gegevens, die kan worden toegekend aan accounts en in sessions kan worden geactiveerd" 

PURPOSE CONCEPT SESSION
{+Een persoon gebruikt een gegevensruimte (en heet dan 'user') door met een browser (bijv. Chrome of Firefox) het systeem te benaderen dat de gegegensruimte beheert. Als meerdere personen een gegevensruimte delen, moet het systeem de context van elk van hen kunnen onderscheiden, bijvoorbeeld om:
\begin{itemize}
\item de interactie 'klein' te houden, d.w.z. alleen gegevens te laten zien die voor hem/haar relevant zijn;
\item ervoor te zorgen dat een user niet ziet wat hij niet mag zien;
\item te kunnen bijhouden welke persoon, of welk(e) organisatie(onderdeel) verantwoordelijk is voor een zekere transactie;
\item automatisch gegevens betreffende de user of zijn context aan transacties toe te kunnen voegen
\end{itemize}
We gebruiken de term 'SESSION' of 'session' om de verzameling van gegevens betreffende één (actieve) user mee aan te geven. Deze term correspondeert met de gelijknamige term browsers gebruiken, nl. 'een verbinding (door de browser) met een webservice (die een URL heeft)'. Het systeem houdt één session bij voor elke actieve user, d.w.z. voor elke browser die het systeem benadert. Merk op dat dit in het bijzonder geldt als de user in verschillende tabbladen van dezelfde browser het systeem benadert - er is dan toch maar één session (en één user).
-}
CONCEPT SESSION "een verzameling van gegevens die de context beschrijven waarin één persoon het systeem gebruikt"

-- Onderstaande VIEW vervangt vieze SESSION identifiers door de tekst 'My Session' (of iets anders)
VIEW SESSIONs: SESSION('_SESSION'[SESSION];V;'My Session'[Message] \/ (I[SESSION]-'_SESSION');V;'Some other Session'[Message])

-----------------------------------------------------------
ENDCONTEXT