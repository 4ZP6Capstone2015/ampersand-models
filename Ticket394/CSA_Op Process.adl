CONTEXT CSA_Op_Process IN ENGLISH
-----------------------------------------------------------
PROCESS "Process Work"
{-
ROLE Developer MAINTAINS "Activity integrity"
RULE "Activity integrity": actResult |- actTask;taskExpectsResult
MESSAGE "Activity results need to be linked to task results"
MEANING "An activity exists to realize a result that is required by the task by which it is planned"
-}

ROLE Developer MAINTAINS "SUR[taskExpectsResult]"
RULE "SUR[taskExpectsResult]": I[Result] |- taskExpectsResult~;taskExpectsResult
MEANING "Every Result must be expected by (at least) one Task"
VIOLATION (TXT "Result ", SRC I, TXT " must be expected by some task")

{-
ROLE Developer MAINTAINS "TOT[taskPurpose]"
RULE "TOT[taskPurpose]": I[Task] |- taskPurpose;taskPurpose~
MEANING "Every Task must have (at least) one Purpose"
VIOLATION (TXT "Assign Purpose to Task ", SRC I)
-}

{-
ROLE Developer MAINTAINS "Task or Activity creation"
RULE "Task or Activity creation": taskExpectsResult |- actTask~;actResult \/ isSubtaskOf~;taskExpectsResult
MEANING "If a Task expects a Result, then there must be an Activity that realizes this Result and/or a subtask (of thisTask) that requires this result"
VIOLATION (TXT "Within ", SRC I, TXT ", define subtask of or activity for Result ", TGT I)
-}

{-
ROLE ExecEngine MAINTAINS "Initial activity planning"
RULE "Initial activity planning": I[Activity] |- actPlannedEndTime;actPlannedEndTime~
VIOLATION (TXT "{EX} Inspair;actEndTime;Activity;", SRC I, TXT ";Timestamp;", TGT actResult;resultDeadline)
-}

ROLE Developer MAINTAINS "Connect computer to network router"
RULE "Connect computer to network router": I /\ compReqCommWith;compReqCommWith~ |- compRouter;(I /\ routerNW;routerNW~);compRouter~
MEANING "A computer that needs to communicate with another computer must be connected to a network-router"
VIOLATION (SRC I, TXT " must be connected to a network-router.")

connectComputerToNetworkRouter :: Computer * Computer [PROP]
ROLE ExecEngine MAINTAINS "insconnectComputerToNetworkRouter"
RULE "insconnectComputerToNetworkRouter": (I /\ compReqCommWith;compReqCommWith~) /\ -(compRouter;(I /\ routerNW;routerNW~);compRouter~) |- connectComputerToNetworkRouter
VIOLATION (TXT "{EX} InsPair;connectComputerToNetworkRouter;Computer;", SRC I, TXT ";Computer;", TGT I)
ROLE ExecEngine MAINTAINS "delconnectComputerToNetworkRouter"
RULE "delconnectComputerToNetworkRouter": connectComputerToNetworkRouter |- (I /\ compReqCommWith;compReqCommWith~) /\ -(compRouter;(I /\ routerNW;routerNW~);compRouter~)
VIOLATION (TXT "{EX} DelPair;connectComputerToNetworkRouter;Computer;", SRC I, TXT ";Computer;", TGT I)

ROLE Developer MAINTAINS "Create router paths"
RULE "Create router paths": routerNeedsPathTo |- -V
MEANING "A router must have a path to another router if a computer is connected to it that has an unsatisfied communication requirement with another computer that is connected to the other router."
VIOLATION (TXT "A path must exist from router ", SRC I, TXT " to ", TGT I)

ROLE Developer MAINTAINS "Routes to be discarded"
RULE "Routes to be discarded": rrtDirect |- routerNW;V;routerNW~
MEANING "Routing can only be done by routers that are part of a network."
VIOLATION (TXT "Route from ", SRC I, TXT " to ", TGT I, TXT " should be removed.")
routesToBeDiscarded :: Router * Router
ROLE ExecEngine MAINTAINS "insroutesToBeDiscarded"
RULE "insroutesToBeDiscarded": rrtDirect /\ -(routerNW;V;routerNW~) |- routesToBeDiscarded
VIOLATION (TXT "{EX} InsPair;routesToBeDiscarded;Router;", SRC I, TXT ";Router;", TGT I)
ROLE ExecEngine MAINTAINS "delroutesToBeDiscarded"
RULE "delroutesToBeDiscarded": routesToBeDiscarded |- rrtDirect /\ -(routerNW;V;routerNW~)
VIOLATION (TXT "{EX} DelPair;routesToBeDiscarded;Router;", SRC I, TXT ";Router;", TGT I)

ROLE Developer MAINTAINS "Computers to be disconnected"
RULE "Computers to be disconnected": compRouter |- compRouter;(I /\ routerNW;routerNW~)
MEANING "A router to which a computer is connected must be part of a network."
VIOLATION (TXT "Computer ", SRC I, TXT " cannot be connected to router ", TGT I, TXT " as the latter is not connected to a network.")
computerToBeDisconnected :: Computer * Router
ROLE ExecEngine MAINTAINS "inscomputerToBeDisconnected"
RULE "inscomputerToBeDisconnected":  compRouter /\ -(compRouter;(I /\ routerNW;routerNW~)) |- computerToBeDisconnected
VIOLATION (TXT "{EX} InsPair;computerToBeDisconnected;Computer;", SRC I, TXT ";Router;", TGT I)
ROLE ExecEngine MAINTAINS "delcomputerToBeDisconnected"
RULE "delcomputerToBeDisconnected": computerToBeDisconnected |- compRouter /\ -(compRouter;(I /\ routerNW;routerNW~)) 
VIOLATION (TXT "{EX} DelPair;computerToBeDisconnected;Computer;", SRC I, TXT ";Router;", TGT I)

ROLE ExecEngine MAINTAINS "Activity termination"
RULE "Activity termination": (I /\ -(actEndTime;actEndTime~));actResult;actResult~;(I /\ actEndTime;actEndTime~) |- -I
VIOLATION (TXT "{EX} Inspair;actEndTime;Activity;", SRC I, TXT ";Timestamp;", TGT actEndTime)

RULE "Task completion integrity": I /\ taskEndTime;taskEndTime~ |- -taskExpectsResult!(actResult~;(I /\ actEndTime;actEndTime~);actTask) /\ -isSubtaskOf~!(I /\ taskEndTime;taskEndTime~)
MEANING "A task can only be marked as complete when every result that it, or one of its subtasks, expects, has been realized"

ROLE Developer MAINTAINS "Deploying ITService"
RULE "Deploying ITService": actExecutingOU~;actUsesITSvc  |- compOwner~;compITService
MEANING "If an OrgUnit executes an activity that uses an ITService, then the OrgUnit must have at least one computer on which this ITService is available."
MESSAGE "ITService needs to be made available:"
VIOLATION (TXT "OrgUnit ", SRC I, TXT " requires ", TGT I)

{- A Computer is capable of running an ITService if
   - either the computer runs a SvcComponent that implements the ITService (as a whole),
   - or the computer runs a client for the ITService and is connected to a Computer that runs a server for that ITService
-}

ROLE ExecEngine MAINTAINS inscompITService
RULE inscompITService:
     (scDeployedOn~;scIsClientFor /\ (I \/ compRouter;rrtStar;compRouter~);scDeployedOn~;scIsServerFor)
     \/ scDeployedOn~;scImplements  |- compITService
VIOLATION (TXT "{EX} InsPair;compITService;Computer;", SRC I, TXT ";ITService;", TGT I)
ROLE ExecEngine MAINTAINS delcompITService
RULE delcompITService:
     compITService |- scDeployedOn~;scImplements \/
     (scDeployedOn~;scIsClientFor /\ (I \/ compRouter;rrtStar;compRouter~);scDeployedOn~;scIsServerFor)
VIOLATION (TXT "{EX} DelPair;compITService;Computer;", SRC I, TXT ";ITService;", TGT I)

--[Non-trivial (business) rules]--

ROLE ExecEngine MAINTAINS "Router network integrity" -- "Only routers that are part of a network may have routes"

ROLE ExecEngine MAINTAINS "Create path within network"
RULE "Create path within network": routerNeedsPathTo /\ routerNW;routerNW~ /\ -I |- rrtStar
MEANING "Any routes within a network, for which there is a need, must exist."
VIOLATION (TXT "{EX} InsPair;rrtDirect;Router;", SRC I, TXT ";Router;", TGT I)

ROLE ExecEngine MAINTAINS "Remove routes from non-network routers"
RULE "Remove routes from non-network routers": rrtDirect |- routerNW;(I \/ -I);routerNW~
MEANING "Routes from or to a router that is not part of a network cannot exist."
-- Note: the pair should be deleted from 'routerRoutesTo' in order to propagate to 'rrtDirect' and 'rrtStar'
VIOLATION (TXT "{EX} DelPair;routerRoutesTo;Router;", SRC I, TXT ";Router;", TGT I)

ROLE ExecEngine MAINTAINS "Remove computers from non-network routers"
RULE "Remove computers from non-network routers": compRouter |- compRouter;(I /\ routerNW;routerNW~)
MEANING "Computers may not be connected to routers that are not part of a network."
VIOLATION (TXT "{EX} DelPair;compRouter;Computer;", SRC I, TXT ";Router;", TGT I)

--[Computer computation rules]--

ROLE ExecEngine MAINTAINS "InscompReqCommWith"
RULE "InscompReqCommWith":
-I /\ (compOwner;actExecutingOU~;actUsesITSvc /\ scDeployedOn~;scIsClientFor);scIsServerFor~;scDeployedOn |- compReqCommWith
VIOLATION (TXT "{EX} InsPair;compReqCommWith;Computer;", SRC I, TXT ";Computer;", TGT I
          ,TXT "{EX} InsPair;compReqCommWith;Computer;", TGT I, TXT ";Computer;", SRC I
          )
ROLE ExecEngine MAINTAINS "DelcompReqCommWith"
RULE "DelcompReqCommWith":
compReqCommWith |-    ((compOwner;actExecutingOU~;actUsesITSvc /\ scDeployedOn~;scIsClientFor);scIsServerFor~;scDeployedOn)
                   \/ ((compOwner;actExecutingOU~;actUsesITSvc /\ scDeployedOn~;scIsClientFor);scIsServerFor~;scDeployedOn)~
VIOLATION (TXT "{EX} DelPair;compReqCommWith;Computer;", SRC I, TXT ";Computer;", TGT I
          ,TXT "{EX} DelPair;compReqCommWith;Computer;", TGT I, TXT ";Computer;", SRC I
          )

compRequiresComm :: Computer * Computer [PROP] -- Computer requires communication (with some other computer)
ROLE ExecEngine MAINTAINS "InscompRequiresComm"
RULE "InscompRequiresComm": I /\ (compReqCommWith;compReqCommWith~) |- compRequiresComm
VIOLATION (TXT "{EX} InsPair;compRequiresComm;Computer;", SRC I, TXT ";Computer;", TGT I)
ROLE ExecEngine MAINTAINS "DelcompRequiresComm"
RULE "DelcompRequiresComm": compRequiresComm |- I /\ (compReqCommWith;compReqCommWith~)
VIOLATION (TXT "{EX} DelPair;compRequiresComm;Computer;", SRC I, TXT ";Computer;", TGT I)

compHasCommWith :: Computer * Computer -- can actually send messages to  (and this is required)
ROLE ExecEngine MAINTAINS "InscompHasCommWith"
RULE "InscompHasCommWith": compReqCommWith /\ compRouter;rrtStar;compRouter~ /\ -I |- compHasCommWith
VIOLATION (TXT "{EX} InsPair;compHasCommWith;Computer;", SRC I, TXT ";Computer;", TGT I)
ROLE ExecEngine MAINTAINS "DelcompHasCommWith"
RULE "DelcompHasCommWith": compHasCommWith |- compReqCommWith /\ compRouter;rrtStar;compRouter~ /\ -I
VIOLATION (TXT "{EX} DelPair;compHasCommWith;Computer;", SRC I, TXT ";Computer;", TGT I)

compAllReqCommSatisfied :: Computer * Computer [PROP] -- All required communication is available
ROLE ExecEngine MAINTAINS "InscompAllReqCommSatisfied"
RULE "InscompAllReqCommSatisfied": I /\ compReqCommWith;compReqCommWith~ /\ -compReqCommWith!(compRouter;rrtStar;compRouter~)~ |- compAllReqCommSatisfied
VIOLATION (TXT "{EX} InsPair;compAllReqCommSatisfied;Computer;", SRC I, TXT ";Computer;", TGT I)
ROLE ExecEngine MAINTAINS "DelCompAllReqCommSatisfied"
RULE "DelCompAllReqCommSatisfied": compAllReqCommSatisfied |- I /\ compReqCommWith;compReqCommWith~ /\ -compReqCommWith!(compRouter;rrtStar;compRouter~)~
VIOLATION (TXT "{EX} DelPair;compAllReqCommSatisfied;Computer;", SRC I, TXT ";Computer;", TGT I)

compNoReqCommSatisfied :: Computer * Computer [PROP] -- None of the require communication is available
ROLE ExecEngine MAINTAINS "InscompNoReqCommSatisfied"
RULE "InscompNoReqCommSatisfied": I /\ -(compReqCommWith; (compRouter;rrtStar;compRouter~)~) |- compNoReqCommSatisfied
VIOLATION (TXT "{EX} InsPair;compNoReqCommSatisfied;Computer;", SRC I, TXT ";Computer;", TGT I)
ROLE ExecEngine MAINTAINS "DelCompNoReqCommSatisfied"
RULE "DelCompNoReqCommSatisfied": compNoReqCommSatisfied |- I /\ -(compReqCommWith; (compRouter;rrtStar;compRouter~)~)
VIOLATION (TXT "{EX} DelPair;compNoReqCommSatisfied;Computer;", SRC I, TXT ";Computer;", TGT I)

--[Router computation rules]--

routerRoutesTo :: Router * Router -- relation to edit in INTERFACES for specifying rrtDirect; needed because we need the transitive closure of such a relation.
rrtDirect :: Router * Router -- is capable of routing (any) incoming traffic directly through to. **Do NOT edit this relation** in INTERFACES!!!
rrtStar   :: Router * Router -- [ASY,TRN] (smallest) transitive closure of 'rrtDirect'

ROLE ExecEngine MAINTAINS "rrtCompTransitiveClosure"
RULE "rrtCompTransitiveClosure": routerRoutesTo = rrtDirect
VIOLATION (TXT "{EX} TransitiveClosure;routerRoutesTo;Router;rrtDirect;rrtStar")

--!below rule is violated when the expansion of the transitive closure that is used rule above is insufficient for this demonstration.
-- ROLE ExecEngine MAINTAINS "rrtStar Transitive closure integrity"
RULE "rrtStar Transitive closure integrity": 
rrtStar;rrtStar |- rrtStar
MEANING "Violation of this rule means that the demo is becoming more complex than was expected, and therefore may produce signals that are not correct. Please read the documentation (in the source code)"

routerNeedsPathTo :: Router * Router -- would fulfill a communications need if it can route traffic to 
ROLE ExecEngine MAINTAINS "InsrouterNeedsPathTo"
RULE "InsrouterNeedsPathTo": compRouter~;(compReqCommWith /\ -(compRouter;rrtStar;compRouter~));compRouter /\ -I |- routerNeedsPathTo
MEANING "A router needs a path to another router if both routers are connected to computers that need to communicate but do not have a communications path to do this."
VIOLATION (TXT "{EX} InsPair;routerNeedsPathTo;Router;", SRC I, TXT ";Router;", TGT I)
ROLE ExecEngine MAINTAINS "DelrouterNeedsPathTo"
RULE "DelrouterNeedsPathTo": routerNeedsPathTo |- compRouter~;( compReqCommWith /\ -(compRouter;rrtStar;compRouter~));compRouter /\ -I
VIOLATION (TXT "{EX} DelPair;routerNeedsPathTo;Router;", SRC I, TXT ";Router;", TGT I)

routerAllCommReqsSatisfied :: Router * Router [PROP] -- Comm. Req's of all connected computers are satisfied 
ROLE ExecEngine MAINTAINS "InsrouterAllCommReqsSatisfied"
RULE "InsrouterAllCommReqsSatisfied": I[Router] /\ -(routerNeedsPathTo;routerNeedsPathTo~) |- routerAllCommReqsSatisfied
VIOLATION (TXT "{EX} InsPair;routerAllCommReqsSatisfied;Router;", SRC I, TXT ";Router;", TGT I)
ROLE ExecEngine MAINTAINS "DelrouterAllCommReqsSatisfied"
RULE "DelrouterAllCommReqsSatisfied": routerAllCommReqsSatisfied |- I[Router] /\ -(routerNeedsPathTo;routerNeedsPathTo~)
VIOLATION (TXT "{EX} DelPair;routerAllCommReqsSatisfied;Router;", SRC I, TXT ";Router;", TGT I)

routerNoCommReqsSatisfied :: Router * Router [PROP] -- At least one computer connected to the router cannot communicate
ROLE ExecEngine MAINTAINS "InsrouterNoCommReqsSatisfied"
RULE "InsrouterNoCommReqsSatisfied": I[Router] /\ compRouter~;(I[Computer] /\ compReqCommWith /\ -compHasCommWith);compRouter |- routerNoCommReqsSatisfied
VIOLATION (TXT "{EX} InsPair;routerNoCommReqsSatisfied;Router;", SRC I, TXT ";Router;", TGT I)
ROLE ExecEngine MAINTAINS "DelrouterNoCommReqsSatisfied"
RULE "DelrouterNoCommReqsSatisfied": routerNoCommReqsSatisfied |- I[Router] /\ compRouter~;(I[Computer] /\ compReqCommWith /\ -compHasCommWith);compRouter
VIOLATION (TXT "{EX} DelPair;routerNoCommReqsSatisfied;Router;", SRC I, TXT ";Router;", TGT I)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT