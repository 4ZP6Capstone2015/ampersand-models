CONTEXT TaskList
PURPOSE CONTEXT TaskList IN ENGLISH
{+-}
-----------------------------------------------------------
#define useTestPopulations 1
-----------------------------------------------------------
PATTERN Tasks
PURPOSE PATTERN Tasks IN ENGLISH
{+In order to keep track of the work to be done, we need an overview of tasks-}
 
CONCEPT Task "a piece of work"
PURPOSE CONCEPT Task IN ENGLISH
{+-}

PURPOSE RELATION complete
{+We need to be able to distinguish tasks that still need work to be done from those
that do not. We say that tasks that do not need any (additional) work are 'complete'.-}
complete :: Task * Task [PROP]
PRAGMA "" " does not need any (additional) work."
#if useTestPopulations
POPULATION complete[Task*Task] CONTAINS
  [ ("Task1", "Task1")
  ; ("Task3", "Task3")
  ]
#endif

PURPOSE RELATION goal
{+-}
goal :: Task * Text [UNI]
PRAGMA "" " serves the fulfillment of "
#if useTestPopulations
POPULATION goal CONTAINS
  [ ("Task1", "Goal of task 1")
  ; ("Task2", "Be happy")
  ; ("Task3", "Remain alive")
  ]
#endif

ENDPATTERN
-----------------------------------------------------------
PROCESS TaskManagement
PURPOSE PROCESS TaskManagement IN ENGLISH
{+-}

RULE "Specify the goal": I[Task] |- goal;V
MEANING "Every task should have its goal specified"
MESSAGE "This task has no goal specified"
ROLE TaskOwner MAINTAINS "Specify the goal"

RULE "All work is done": I[Task] |- complete
MEANING "All work is done means that all tasks are complete"
MESSAGE "This task is incomplete"
ROLE TaskOwner MAINTAINS "All work is done"

ENDPROCESS
-----------------------------------------------------------
INTERFACE Overview : I[ONE]
BOX[ "Tasks" : V[ONE*Task]
   ]

INTERFACE Task : I[Task]
BOX[ "TaskID"    : I
   , "goal"      : goal
   , "completed" : complete
   ]

INTERFACE "My Tasks" (goal,complete) FOR TaskOwner : I[Task];-complete
BOX[ "TaskID"    : I
   , "goal"      : goal
   , "completed" : complete
   ]
-----------------------------------------------------------
ENDCONTEXT
