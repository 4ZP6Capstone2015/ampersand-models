CONTEXT ProjectAdministration IN ENGLISH
INCLUDE "ProjectAdministration.xlsx"

  PROCESS ProjectAdministration
  
    CONCEPT SESSION ""
    
  	CONCEPT Project "Planned set of interrelated tasks to be executed over a fixed period and within certain cost and other limitations."
  	VIEW  "Project name"   : Project(projectName) 
  	REPRESENT ProjectName, ProjectStatus TYPE ALPHANUMERIC 
    projectName           :: Project * ProjectName [UNI]  
  	projectStatus         :: Project * ProjectStatus [UNI]

  	REPRESENT Description TYPE BIGALPHANUMERIC
    projectDescription    :: Project * Description [UNI] 

  	REPRESENT Date TYPE DATE
    projectStartDate      :: Project * Date [UNI] 
  	projectStarted        :: Project * Project [PROP]
  	pl                    :: Project * Person
  	member                :: Project * Person
  
  	CONCEPT Person "Person"
  	VIEW "Person name"     : Person(personName)
  	REPRESENT PersonName, PersonStatus, Email TYPE ALPHANUMERIC 
  	personName            :: Person * PersonName [UNI]
  	personStatus          :: Person * PersonStatus [UNI]
  	personEmail           :: Person * Email [UNI]
  	workswith             :: Person * Person
  
    CONCEPT PPL "Objectified relation of pl (Projectleader)"
  	pplProject            :: PPL * Project [UNI,TOT]
  	pplPerson             :: PPL * Person [UNI,TOT]
  	pplStartDate          :: PPL * Date [UNI]
  	pplStarted            :: PPL * PPL [PROP]
  
  -- Invariant rules
  	RULE "Every project must have a name" : I[Project] |- projectName;projectName~
  
  -- Process rules for role Planner
  	ROLE "Planner" MAINTAINS "Every project must have a projectleader"
  	RULE "Every project must have a projectleader" : I[Project] |- pl;pl~
  	MESSAGE "Every project must have a projectleader"
  	VIOLATION ( TXT "Project '", SRC projectName, TXT "' does not have a projectleader")
  
  -- Automated rules
  	ROLE "ExecEngine" MAINTAINS "Projectleaders are not members of a team"
  	RULE "Projectleaders are not members of a team": pl |- -member
  	VIOLATION ( TXT "DelPair;member;Project;", SRC I, TXT ";Person;", TGT I)
  
  	ROLE "ExecEngine" MAINTAINS "Works with"
  	RULE "Works with": (pl~;member \/ member~;pl \/ member~;member) /\ -I |- workswith
  	VIOLATION ( TXT "InsPair;workswith;Person;", SRC I, TXT ";Person;", TGT I )
  
  	ROLE "ExecEngine" MAINTAINS "Works with_del"
  	RULE "Works with_del": workswith |- (pl~;member \/ member~;pl \/ member~;member) /\ -I
  	VIOLATION ( TXT "DelPair;workswith;Person;", SRC I, TXT ";Person;", TGT I)
  
  	ROLE "ExecEngine" MAINTAINS "Create PPL"
  	RULE "Create PPL" : pl |- pplProject~;pplPerson
  	VIOLATION ( TXT "NewStruct;PPL"
              , TXT ";pplProject;PPL;_NEW;Project;", SRC I
              , TXT ";pplPerson;PPL;_NEW;Person;", TGT I
              , TXT ";pplStartDate;PPL;_NEW;Date;{php}date(DATE_ISO8601)" -- Current date in ISO 8601 date format 
              )

   ROLE "ExecEngine" MAINTAINS "Delete PPL"
   RULE "Delete PPL" :  pplProject~;pplPerson |- pl
   VIOLATION ( TXT "DelAtom;PPL;", SRC I)
    
  ENDPROCESS
  
  -- Top level interfaces
  INTERFACE "Started projects" : V[SESSION*Project];projectStarted
    BOX <SCOLS> [ "Projects"      : I[Project]
		        , "Name"          : projectName
		        , "Description"   : projectDescription
		        , "Projectleider" : pl                   
		        , " "             : pl                   <PersonEmail>
		        , "Start"         : projectStartDate
		        , "Started"       : projectStarted
		        ]
  
  INTERFACE "Unstarted projects" (projectStarted) : V[SESSION*Project];(-(projectStarted) /\ I)
    BOX <SCOLS> [ "Name"            : projectName
                , "Description"     : projectDescription
                , "Projectleider"   : pl                  
                , " "               : pl                  <PersonEmail>
                , "Started"         : projectStarted
                ]
  
  INTERFACE "People" (member) : V[SESSION*Person] cRUD
  	COLS  [ "Person"    : I[Person]
          , "Name"      : personName
          , "Email"     : personEmail
          , "Projects"  : member~
          ]
  
  -- Other interfaces
  INTERFACE "Project" (projectName, projectDescription, pl, projectStartDate, projectStarted, member, pplStartDate, pplProject) : I[Project] CRUD
    ROWS [ "Project"     : I[Project]
         , "Name"        : projectName
         , "Description" : projectDescription
         , "Start date"  : projectStartDate
         , "Current PL"  : pl
         , "Administration" : I[Project]
            TABS [ "Project leaders"   : pplProject~ 
                	COLS [ "Name"      : pplPerson;personName
                         , "Status"    : pplPerson;personStatus
                         , "Email"     : pplPerson;personEmail
                         , "Pl from"   : pplStartDate
                         ]
                 , "Project members" : member
                 ]
         ]
    
  INTERFACE "Person" (personName, personStatus, personEmail) : I[Person] CRUD
  	ROWS [ "Name"      : personName
         , "Status"     : personStatus
         , "Email"      : personEmail
         , "Works with" : workswith 
         ]
  
  -- Specific views	
  VIEW PersonEmail : Person { email : personEmail } HTML TEMPLATE "PersonEmail.html" ENDVIEW
  
ENDCONTEXT