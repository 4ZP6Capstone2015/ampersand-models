CONTEXT RisicoRegister
-- (C) 2010, TNO/RJ
-- 2DO: MyDomains en MyScopes en meer van dat MyDingesGedoe
--------------------------------------------------------

PATTERN "Scores of waardes" -- WIJZIGER: rieks.joosten@tno.nl

lmhIsL :: LMH * LMH [SYM,ASY] PRAGMA "" "heeft als waarde Laag (of: Groen)".
lmhIsM :: LMH * LMH [SYM,ASY] PRAGMA "" "heeft als waarde Midden (of: Geel, of Oranje)".
lmhIsH :: LMH * LMH [SYM,ASY] PRAGMA "" "heeft als waarde Hoog (of: Rood)".

{-lmhIsL = 'L' \/ 'Laag' \/ 'Groen'
lmhIsM = 'M' \/ 'Medium' \/ 'Geel' \/ 'Oranje'
lmhIsH = 'H' \/ 'Hoog' \/ 'Rood'
-}
lmhIsL |- -lmhIsM /\ -lmhIsH EXPLANATION "Als LMH Laag  is, dan is hij geen andere score"
lmhIsM |- -lmhIsH /\ -lmhIsL EXPLANATION "Als LMH Midden is, dan is hij geen andere score"
lmhIsH |- -lmhIsL /\ -lmhIsM EXPLANATION "Als LMH Hoog  is, dan is hij geen andere score"


V[LMH] = lmhIsL \/ lmhIsM \/ lmhIsH \/ I- EXPLANATION "Naast Laag, Midden, of Hoog zijn er geen andere scores mogelijk."

ENDPATTERN

POPULATION lmhIsL[LMH*LMH] CONTAINS
  [ ("L",      "L") -- de database is niet case-sensitive
  ; ("Laag",   "Laag")
  ; ("Groen",  "Groen")
  ]

POPULATION lmhIsM[LMH*LMH] CONTAINS
  [ ("M",      "M") -- de database is niet case-sensitive
  ; ("Medium", "Medium")
  ; ("Geel",   "Geel")
  ; ("Oranje", "Oranje")
  ]

POPULATION lmhIsH[LMH*LMH] CONTAINS
  [ ("H",      "H") -- de database is niet case-sensitive
  ; ("Hoog",   "Hoog")
  ; ("Rood",   "Rood")
  ]

---------------------------------------------------------
-- Accounts (en rollen) hebben we nodig om de SERVICEs zoals die verderop staan de populaties te kunnen uitdunnen tot hetgeen voor de persoon die is ingelogd, relevant is.

PATTERN "Gebruikersaccounts" -- WIJZIGER: rieks.joosten@tno.nl

gaPersoon    :: Gebruikersaccount * Persoon [UNI] PRAGMA "" " has been assigned to ".
gaWachtwoord :: Gebruikersaccount * Wachtwoord [UNI] PRAGMA "" " mag alleen worden geactiveerd in een sessie als " " aan de andere kant van de sessie bekend is".
gaDomein     :: Gebruikersaccount * Domein [UNI,TOT] PRAGMA "" " heeft betekenis en wordt beheerd binnen ".
gaBeheerder  :: Gebruikersaccount * Rol [TOT] PRAGMA "" " mag worden beheerd door personen die verantwoordelijk zijn voor een gebruikersaccount die is gekoppeld aan ".
gaRol        :: Gebruikersaccount * Rol [] PRAGMA "Aan " " is " " toegekend".

rolIsBeheerder :: Rol * Rol [SYM,ASY] = [ ("ADMIN", "ADMIN" ) ].
rolIsMedewerker:: Rol * Rol [SYM,ASY] = [ ("medewerker","medewerker") ].
rolIsManager   :: Rol * Rol [SYM,ASY] = [ ("manager",   "manager"   ) ].

ENDPATTERN

{- ADLvraag:
hoe zouden we kunnen modelleren dat bij het aanmaken van een beheerde
gebruiker de relatie 'gaBeheerder' wordt gepopuleerd met de
'sGebruiker' van de sessie waarin de beheerde gebruiker wordt
aangemaakt?
-}

SERVICE "BeheerdeGebruiker" : I[Sessie];sGebruiker;(I \/ gaRol;gaBeheerder~)
= [ "Accountverantwoordelijke (persoon)": gaPersoon
  , "Wachtwoord"  : gaWachtwoord
  , "Domein"      : gaDomein
  , "Rol(len)"    : gaRol
  ]

SERVICE "BeheerdeGebruikers" : I[Sessie];sGebruiker;(I \/ gaRol;gaBeheerder~)
= [ "Accounts"    : I[Gebruikersaccount]
  = [ "Eigenaar"  : gaPersoon
  ] ]

SERVICE "Gebruikersaccounts" : I[ONE]
= [ "Accounts"    : V[ONE*Sessie];sGebruiker;(I \/ gaRol;gaBeheerder~)
  = [ "Eigenaar"   : gaPersoon
  ] ]

-- De huidige populatie is een subset van personen die in de praktijk zullen kunnen inloggen
-- Daarvoor is t.z.t. dan wel een koppeling nodig met de KPN inloggegevens.
-- De registratie voorziet wel in (verzonnen) wachtwoorden (initialen van de betreffende persoon), maar dat wordt nog niet gebruikt.

POPULATION gaPersoon[Gebruikersaccount*Persoon]       CONTAINS [ ("mnevels", "Martin Nevels") ]
POPULATION gaWachtwoord[Gebruikersaccount*Wachtwoord] CONTAINS [ ("mnevels", "mn") ]
POPULATION gaDomein[Gebruikersaccount*Domein]         CONTAINS [ ("mnevels", "IO") ]
POPULATION gaBeheerder[Gebruikersaccount*Rol]         CONTAINS [ ("mnevels", "ADMIN") ]
POPULATION gaRol[Gebruikersaccount*Rol]               CONTAINS [ ("mnevels", "medewerker") ]

POPULATION gaPersoon[Gebruikersaccount*Persoon]       CONTAINS [ ("kvdgaast", "Kees van der Gaast") ]
POPULATION gaWachtwoord[Gebruikersaccount*Wachtwoord] CONTAINS [ ("kvdgaast", "jg") ]
POPULATION gaDomein[Gebruikersaccount*Domein]         CONTAINS [ ("kvdgaast", "IO") ]
POPULATION gaBeheerder[Gebruikersaccount*Rol]         CONTAINS [ ("kvdgaast", "ADMIN") ]
POPULATION gaRol[Gebruikersaccount*Rol]               CONTAINS [ ("kvdgaast", "medewerker") ]
POPULATION gaRol[Gebruikersaccount*Rol]               CONTAINS [ ("kvdgaast", "manager") ]

POPULATION gaPersoon[Gebruikersaccount*Persoon]       CONTAINS [ ("ppuk", "Piet J. Puk") ]
POPULATION gaWachtwoord[Gebruikersaccount*Wachtwoord] CONTAINS [ ("ppuk", "pp") ]
POPULATION gaDomein[Gebruikersaccount*Domein]         CONTAINS [ ("ppuk", "IO") ]
POPULATION gaBeheerder[Gebruikersaccount*Rol]         CONTAINS [ ("ppuk", "ADMIN") ]
POPULATION gaRol[Gebruikersaccount*Rol]               CONTAINS [ ("ppuk", "ADMIN") ]
POPULATION gaRol[Gebruikersaccount*Rol]               CONTAINS [ ("ppuk", "medewerker") ]

---------------------------------------------------------

PATTERN "Sessies en inloggen" -- WIJZIGER: rieks.joosten@tno.nl

sGebruiker :: Sessie * Gebruikersaccount [UNI] PRAGMA "" " draait onder "
= [ ("s1", "ppuk") ].

ENDPATTERN

SERVICE "Login" : I[Sessie]
= [ "Gebruiker" : sGebruiker
  ]

SERVICE "Sessies" : I[ONE]
= [ "Sessies"     : V[ONE*Sessie]
  = [ "Gebruiker" : sGebruiker
    , "Persoon"   : sGebruiker; gaPersoon
  ] ]

---------------------------------------------------------

PATTERN "Domeinen" -- WIJZIGER: rieks.joosten@tno.nl

domNaam     :: Domein -> Text [] PRAGMA "De volledige naam van " " is  ".
domManager  :: Domein -> Persoon [] PRAGMA "Voor alle besluitvorming binnen " " is " " verantwoordelijk".
heeftSubdom :: Domein * Domein [INJ,ASY] PRAGMA "" " heeft " " als subdomein, d.w.z. dat het tweede domein rapporteert aan het eerste domein".

ENDPATTERN

SERVICE "Domein" : I[Sessie];sGebruiker;V[Gebruikersaccount*Domein] -- moet zijn ingelogd
= [ "Naam org.eenheid (voluit)" : domNaam
  , "Manager"      : domManager
  , "Subdomein van": heeftSubdom~
  , "Subdomein(en)": heeftSubdom
  ]

SERVICE "Domeinen" : I[ONE]
= [ "Domeinen"     : V[ONE*Sessie];sGebruiker;V[Gebruikersaccount*Domein] -- moet zijn ingelogd
  = [ "Voluit"     : domNaam
    , "Subdomein van" : heeftSubdom~
  ] ]

SERVICE "MijnDomein" : I[Sessie];sGebruiker;gaPersoon;domManager~
= [ "Naam org.eenheid (voluit)" : domNaam
  , "Manager"    : domManager
  , "Subdomein van": heeftSubdom~
  , "Subdomein(en)": heeftSubdom
  ]

POPULATION domNaam[Domein*Text] CONTAINS
  [ ("TNO",    "Instituut voor Technisch en Natuurwetenschappelijk Onderzoek B.V.")
  ; ("KVL",    "Kwaliteit van Leven")
  ; ("D&V",    "Defensie en Veiligheid")
  ; ("I&T",    "Industrie en Techniek")
  ; ("B&O",    "Bouw en Ondergrond")
  ; ("ICT",    "Informatie- en Communicatie Technologie")
  ; ("BEDR",   "Bedrijven")
  ; ("DELTARES", "DELTARES")
-- Binnen TNO zijn de volgende stafafdelingen (selectie):
  ; ("IO",     "Information Office")
-- Binnen ICT zijn de volgende business units:
  ; ("CT",     "Communicatietechnologie")
  ; ("IT",     "Informatietechnologie")
  ; ("IM",     "Innovatiemanagement")
-- Ook stafafdelingen tellen we vandaag mee bij de subdomeinen
  ; ("P&O",     "Personeelszaken")
  ; ("COM",    "Communicatie")
  ; ("F&O",    "Finance & Ondersteuning (Control)")
-- Binnen CT zijn de volgende afdelingen:
  ; ("PPQ",    "Planning, Performance & Quality")
  ; ("NSC",    "Network & Service Control")
  ; ("CON",    "Connectivity")
  ; ("ANT",    "Access Network Technologies")
-- Binnen IT zijn de volgende afdelingen:
  ; ("MMT",    "Multimedia Technologie")
  ; ("SEC",    "Security")
  ; ("EIT",    "E-business & IT")
  ; ("SEM",    "Service Enabeling & Management")
-- Binnen IM zijn de volgende afdelingen:
  ; ("SIS",    "Strategies for the Information Society")
  ; ("BIC",    "Business Innovation & Customer Behaviour")
  ; ("SBA",    "Strategic Business Analysis")
  ]

POPULATION domManager[Domein*Persoon] CONTAINS
  [ ("TNO",    "Jan Mengelers")
-- Binnen TNO zijn de volgende kerngebieden:
  ; ("KVL",    "Niek Snoeij")
  ; ("D&V",    "Victor Elsendoorn")
  ; ("I&T",    "Paul Korting")
  ; ("B&O",    "Dick Schmidt")
  ; ("ICT",    "Gerlof Bosveld")
  ; ("BEDR",   "Sonja Vos-Poppelaars")
  ; ("DELTARES", "Jan Ebbing")
-- Binnen TNO zijn de volgende stafafdelingen (selectie):
  ; ("IO",     "Kees van der Gaast")
-- Binnen ICT zijn de volgende business units:
  ; ("CT",     "Erik Peeters")
  ; ("IT",     "Frank van Aken")
  ; ("IM",     "Suzanne van Kooten")
-- Ook stafafdelingen tellen we vandaag mee bij de subdomeinen
  ; ("P&O",    "Jindra Kessener")
  ; ("COM",    "Josée Verbeek")
  ; ("F&O",    "Markus Mandemaker")
-- Binnen CT zijn de volgende afdelingen:
  ; ("PPQ",    "Esther Vos")
  ; ("NSC",    "Rixte Thomas")
  ; ("CON",    "Marc van den Homberg")
  ; ("ANT",    "Ellen Harreveld")
-- Binnen IT zijn de volgende afdelingen:
  ; ("MMT",    "Dick van Smirren")
  ; ("SEC",    "Paul de Jager")
  ; ("EIT",    "Richard Beekhuis")
  ; ("SEM",    "Jurjen Veldhuizen")
-- Binnen IM zijn de volgende afdelingen:
  ; ("SIS",    "Jeroen Heres")
  ; ("BIC",    "Natascha Agricola")
  ; ("SBA",    "Eric Veldkamp")
  ]

POPULATION heeftSubdom[Domein*Domein] CONTAINS
  [ ("TNO",    "KVL")
  ; ("TNO",    "D&V")
  ; ("TNO",    "I&T")
  ; ("TNO",    "B&O")
  ; ("TNO",    "ICT")
  ; ("TNO",    "BEDR")
  ; ("TNO",    "DELTARES")
-- Binnen TNO zijn de volgende stafafdelingen (selectie):
  ; ("TNO",    "IO")
-- Binnen ICT zijn de volgende business units:
  ; ("ICT",    "CT")
  ; ("ICT",    "IT")
  ; ("ICT",    "IM")
-- Ook stafafdelingen tellen we vandaag mee bij de subdomeinen
  ; ("ICT",    "P&O")
  ; ("ICT",    "COM")
  ; ("ICT",    "F&O")
-- Binnen CT zijn de volgende afdelingen:
  ; ("CT",     "PPQ")
  ; ("CT",     "NSC")
  ; ("CT",     "CON")
  ; ("CT",     "ANT")
-- Binnen IT zijn de volgende afdelingen:
  ; ("IT",     "MMT")
  ; ("IT",     "SEC")
  ; ("IT",     "EIT")
  ; ("IT",     "SEM")
-- Binnen IM zijn de volgende afdelingen:
  ; ("IM",     "SIS")
  ; ("IM",     "BIC")
  ; ("IM",     "SBA")
  ]

---------------------------------------------------------
-- Asset Register (wat voor KPN Assets zijn, noemen wij scopes)

PATTERN "Scopes" -- WIJZIGER: rieks.joosten@tno.nl
-- BIA's voor individuele assets zijn nog niet geimplementeerd.

scopeBeschrijving :: Scope -> Text PRAGMA "" " houdt in: ".
scopeType         :: Scope -> Scopetype PRAGMA "" " is van het type ".
scopeEigenaar     :: Scope -> Persoon [UNI] PRAGMA "De verantwoordelijkheid (RACI: responsible) voor "  " wordt door  " " ingevuld".
scopeDomein       :: Scope -> Domein PRAGMA "De eindverantwoordelijkheid (accountability) van " " ligt bij ".
heeftSubscope     :: Scope * Scope [ASY] PRAGMA "" " heeft " " als subscope, d.w.z. dat de eerste scope bouwt op al hetgeen er in de tweede scope gebeurt".

ENDPATTERN

SERVICE "Scope"   : I[Scope]
= [ "Naam"        : scopeBeschrijving
  , "Type"        : scopeType
  , "Eigenaar"    : scopeEigenaar
  , "Domein"      : scopeDomein
  , "Subscope van": heeftSubscope~
  , "Subscope(s)" : heeftSubscope
  , "Risicos"   : rScope~
  ]

SERVICE "Scopes" : I[ONE]
= [ "Scopes"     : V[ONE*Sessie];sGebruiker;V[Gebruikersaccount*Scope] -- moet zijn ingelogd
  = [ "Naam"     : I[Scope]
    , "Beschrijving" : scopeBeschrijving
    , "Type"     : scopeType
    , "Eigenaar" : scopeEigenaar
    , "Domein"   : scopeDomein
    , "Subscope van": heeftSubscope~
    , "Subscope(s)" : heeftSubscope
  ] ]

SERVICE "AssetRegister"  : I[ONE]
= [ "Scopetype" : V[ONE*Scopetype]
{-  = [ "Assettype"      : I[Scopetype]
    = [ "Asset"           : scopeType~
    = [ "Beschrijving"    : scopeBeschrijving
      , "Eigenaar"        : scopeEigenaar
      , "Domein"          : scopeDomein
      , "Subscope van"    : heeftSubscope~
      , "Subscopes"       : heeftSubscope
  ] ] ]
-} ]

SERVICE "X" : I[ONE]
= [ "X"     : V[ONE*Scope]
  ]

SERVICE "Y" : I[ONE]
= [ "Y"     : V[ONE*Scopetype]
  ]

---------------------------------------------------------
PATTERN "Risicos" -- WIJZIGER: rieks.joosten@tno.nl

rScope        :: Risico -> Scope PRAGMA "" " is geidentificeerd als risico voor ".
rResponsible  :: Risico * Persoon [UNI] PRAGMA "De verantwoordelijkheid (RACI: responsibility) voor " " ligt bij ".
rAccountable  :: Risico -> Persoon PRAGMA "De eindverantwoordelijkheid (RACI: accountability) voor " " ligt bij ".

rGeaccepteerd :: Risico * Risico [SYM,ASY] PRAGMA "" " is geaccepteerd door het domein dat eindverantwoordelijk is voor ".

rResponsible = rScope; scopeEigenaar EXPLANATION "Elk risico dat in een zekere scope is geidentificeerd wordt beheerd door de manager van die scope."
rAccountable = rScope; scopeDomein; domManager EXPLANATION "De eindverantwoordelijkheid (accountability) voor een zeker risico ligt bij het domein dat verantwoordelijk is voor de scope waartoe dat risico behoort."

-- SIGNAL srResponsible ON I[Risico] |- rResponsible; rResponsible~ EXPLANATION "Aan elk risico moet iemand zijn gekoppeld die de Responsible rol vervult."
srResponsible :: Risico * Risico [SYM,ASY].
srResponsible = I[Risico] /\ -(rResponsible;rResponsible~) EXPLANATION "Aan elk risico moet iemand zijn gekoppeld die de Responsible rol vervult."

ENDPATTERN

SERVICE "Risico" : I[Risico]
= [ "ID" : I[Risico]
  , "Scope" : rScope -- De scope waarbinnen het risico ligt impliceert de 'responsability' en 'accountability' van het risico
  , "Omschrijving" : rOmschrijving
  , "Risico (LMH)" : rWaarde
  , "Dreiging" : rDreiging
    = [ "Kans (LMH)" : drgWaarde
      , "Dreiging" : drgOmschrijving
      ]
  , "Impact" : rImpact
    = [ "Impact (LMH)" : impWaarde
      , "Omschrijving" : impOmschrijving
      ]
  , "Omgang" : rOmgang
  , "Toelichting" : rToelichting
  ]

SERVICE "riskrule" : I[Risico]
= [ "ID" : I[Risico]
  , "Description" : rOmschrijving \/ (-rOmschrijving /\ rDreiging;drgOmschrijving)
  , "R" : rResponsible
  , "A" : rAccountable
  , "Impact" : rImpact; impWaarde
  , "Kans"   : rDreiging; drgWaarde
  , "Risk"   : rWaarde
  , "Treatment" : rOmgang
  ]

SERVICE "riskRegister" : I[ONE]
= [ "Scopes" : V[ONE*Scope]
  = [ "Asset"     : I[Scope]
--  . "Naam"      : scopeBeschrijving
--  , "Type"      : scopeType
--  , "Eigenaar"  : scopeEigenaar
    , "Risicos"   : rScope~
    = [ "ID" : I[Risico]
--    , "Description" : rOmschrijving \/ (-rOmschrijving /\ rDreiging;drgOmschrijving)
--    , "R" : rResponsible
--    , "A" : rAccountable
--    , "Impact" : rImpact; impWaarde
--    , "Kans"   : rDreiging; drgWaarde
      , "Risk"   : rWaarde
--    , "Treatment" : rOmgang
  ] ] ]

SERVICE "Risicos" : I[ONE]
= [ "Risicos"     : V[ONE*Sessie];sGebruiker;(gaRol;rolIsBeheerder;gaRol~ /\ I);V[Gebruikersaccount*Risico]
  = [ "ID"     : I[Risico]
    , "Scope" : rScope -- De scope waarbinnen het risico ligt impliceert de 'responsability' en 'accountability' van het risico
    , "Omschrijving" : rOmschrijving
    , "Risico (LMH)" : rWaarde
--  , "Kans (LMH)"   : rDreiging; drgWaarde
--  , "Impact (LMH)" : rImpact; impWaarde
  ] ]

SERVICE "srRisicos" : I[ONE]
= [ "Risicos waarvoor ik RESPONSIBLE ben" : V[ONE*Sessie];sGebruiker;gaPersoon;rResponsible~
  = [ "ID"     : I[Risico]
    , "Scope" : rScope -- De scope waarbinnen het risico ligt impliceert de 'responsability' en 'accountability' van het risico
    , "Omschrijving" : rOmschrijving
    , "Risico (LMH)" : rWaarde
--  , "Kans (LMH)"   : rDreiging; drgWaarde
--  , "Impact (LMH)" : rImpact; impWaarde
  ] ]

SERVICE "saRisicos" : I[ONE]
= [ "Risicos waarvoor ik ACCOUNTABLE ben" : V[ONE*Sessie];sGebruiker;gaPersoon;rAccountable~
  = [ "ID"     : I[Risico]
    , "Scope" : rScope -- De scope waarbinnen het risico ligt impliceert de 'responsability' en 'accountability' van het risico
    , "Omschrijving" : rOmschrijving
    , "Risico (LMH)" : rWaarde
--  , "Kans (LMH)"   : rDreiging; drgWaarde
--  , "Impact (LMH)" : rImpact; impWaarde
  ] ]

---------------------------------------------------------
PATTERN "Risico inventarisatie" -- WIJZIGER: rieks.joosten@tno.nl
-- Hier zit nog niet de automatische berekening van de risicowaarde in als gevolg van de waardes van kans (dreigingswaarde) en impact(waarde).

rOmschrijving   :: Risico * Text [UNI] PRAGMA "" " is als volgt omschreven ".
rWaarde         :: Risico * LMH [UNI] PRAGMA "De waarde (hoogte) van " " is ".
rDreiging       :: Risico -> Dreiging [INJ,SUR] PRAGMA "" " kan materialiseren als " " optreedt".
rImpact         :: Risico * Impact [INJ,SUR,UNI] PRAGMA "Als de dreiging van " " optreedt, is de maximaal te verwachten impact ".

drgScope        :: Dreiging -> Scope [] PRAGMA "" " heeft betekenis binnen ".
drgOmschrijving :: Dreiging -> Text [] PRAGMA "" " kan worden beschreven als ".
drgWaarde       :: Dreiging * LMH [UNI] PRAGMA "De waarde (hoogte) van " " is ".

drgScope = rDreiging~;rScope EXPLANATION "De scopes van een dreiging en het daaraan gekoppelde risico zijn altijd dezelfde."
I = drgScope;drgScope~ /\ drgOmschrijving;drgOmschrijving~ EXPLANATION "Elke dreiging wordt uniek gekarakteriseerd door de omschrijving en de scope waarbinnen die betekenis heeft."

impScope        :: Impact -> Scope [] PRAGMA "" " heeft betekenis binnen ".
impOmschrijving :: Impact * Text [UNI] PRAGMA "" " kan worden beschreven als ".
impWaarde       :: Impact * LMH [UNI] PRAGMA "De waarde (hoogte) van " " is ".

impScope = rImpact~;rScope EXPLANATION "De scopes van een impact en het bijbehorende risico zijn altijd dezelfde."

rOmgang         :: Risico * Text [UNI] PRAGMA "Met " " wordt omgegaan volgens ".
rToelichting    :: Risico * Text [UNI] PRAGMA "Toelichting op " " is als volgt ".

ENDPATTERN

{-================================================================

  INVULLING VAN HET ASSET REGISTER EN RISICO REGISTER vsn 0.01

  De risico-populaties zijn gefingeerd

================================================================-}

--======== Scope TNOASE Netwerk =======--

POPULATION scopeBeschrijving[Scope*Text] CONTAINS [ ("TNOASE", "TNOASE netwerk") ]
POPULATION scopeType[Scope*Scopetype]    CONTAINS [ ("TNOASE", "Netwerk") ]
POPULATION scopeEigenaar[Scope*Persoon]  CONTAINS [ ("TNOASE", "Martin Nevels") ]
POPULATION scopeDomein[Scope*Domein]     CONTAINS [ ("TNOASE", "IO") ]
POPULATION heeftSubscope[Scope*Scope]    CONTAINS [ ]

-----------------------------------------

-- Risico 1
POPULATION rScope[Risico*Scope]           CONTAINS [ ("R1", "TNOASE") ]
POPULATION rResponsible[Risico*Persoon]   CONTAINS [ ("R1", "Martin Nevels") ]
POPULATION rAccountable[Risico*Persoon]   CONTAINS [ ("R1", "Kees van der Gaast") ]
--POPULATION rOmschrijving[Risico*Text]     CONTAINS [ ("R1", "") ]
POPULATION rWaarde[Risico*LMH]            CONTAINS [ ("R1", "H") ]
POPULATION rDreiging[Risico*Dreiging]     CONTAINS [ ("R1", "D1") ]
 POPULATION drgScope[Dreiging*Scope]       CONTAINS [ ("D1", "TNOASE") ]
 POPULATION drgOmschrijving[Dreiging*Text] CONTAINS [ ("D1", "SW-EHT-1 valt uit, gebeurt enkele keren per jaar") ]
 POPULATION drgWaarde[Dreiging*LMH]        CONTAINS [ ("D1", "H") ]
POPULATION rImpact[Risico*Impact]         CONTAINS [ ("R1", "I1") ]
 POPULATION impScope[Impact*Scope]         CONTAINS [ ("I1", "TNOASE") ]
 POPULATION impOmschrijving[Impact*Text]   CONTAINS [ ("I1", "Locaties Gn en Dft niet meer gekoppeld") ]
 POPULATION impWaarde[Impact*LMH]          CONTAINS [ ("I1", "M") ]
--POPULATION rOmgang[Risico*Text]           CONTAINS [ ("R1", "") ]
--POPULATION rToelichting[Risico*Text]      CONTAINS [ ("R1", "") ]

-- Risico 2
POPULATION rScope[Risico*Scope]           CONTAINS [ ("R2", "TNOASE") ]
POPULATION rResponsible[Risico*Persoon]   CONTAINS [ ("R2", "Martin Nevels") ]
POPULATION rAccountable[Risico*Persoon]   CONTAINS [ ("R2", "Kees van der Gaast") ]
--POPULATION rOmschrijving[Risico*Text]     CONTAINS [ ("R2", "") ]
POPULATION rWaarde[Risico*LMH]            CONTAINS [ ("R2", "M") ]
POPULATION rDreiging[Risico*Dreiging]     CONTAINS [ ("R2", "D2") ]
 POPULATION drgScope[Dreiging*Scope]       CONTAINS [ ("D2", "TNOASE") ]
 POPULATION drgOmschrijving[Dreiging*Text] CONTAINS [ ("D2", "SW-EHT-2 valt uit, gebeurt enkele keren per jaar") ]
 POPULATION drgWaarde[Dreiging*LMH]        CONTAINS [ ("D2", "H") ]
POPULATION rImpact[Risico*Impact]         CONTAINS [ ("R2", "I2") ]
 POPULATION impScope[Impact*Scope]         CONTAINS [ ("I2", "TNOASE") ]
 POPULATION impOmschrijving[Impact*Text]   CONTAINS [ ("I2", "Locaties En en Dft niet meer gekoppeld") ]
 POPULATION impWaarde[Impact*LMH]          CONTAINS [ ("I2", "L") ]
--POPULATION rOmgang[Risico*Text]           CONTAINS [ ("R2", "") ]
--POPULATION rToelichting[Risico*Text]      CONTAINS [ ("R2", "") ]

--======== Research Netwerk =======--

POPULATION scopeBeschrijving[Scope*Text] CONTAINS [ ("RES-NW", "Research Netwerk") ]
POPULATION scopeType[Scope*Scopetype]    CONTAINS [ ("RES-NW", "Netwerk") ]
POPULATION scopeEigenaar[Scope*Persoon]  CONTAINS [ ("RES-NW", "Martin Nevels") ]
POPULATION scopeDomein[Scope*Domein]     CONTAINS [ ("RES-NW", "IO") ]
POPULATION heeftSubscope[Scope*Scope]    CONTAINS [ ]

-- Risico 6
POPULATION rScope[Risico*Scope]           CONTAINS [ ("R6", "RES-NW") ]
POPULATION rResponsible[Risico*Persoon]   CONTAINS [ ("R6", "Martin Nevels") ]
POPULATION rAccountable[Risico*Persoon]   CONTAINS [ ("R6", "Kees van der Gaast") ]
--POPULATION rOmschrijving[Risico*Text]     CONTAINS [ ("R6", "") ]
POPULATION rWaarde[Risico*LMH]            CONTAINS [ ("R6", "M") ]
POPULATION rDreiging[Risico*Dreiging]     CONTAINS [ ("R6", "D6") ]
 POPULATION drgScope[Dreiging*Scope]       CONTAINS [ ("D6", "RES-NW") ]
 POPULATION drgOmschrijving[Dreiging*Text] CONTAINS [ ("D6", "Één persoon is verantwoordelijk voor het netwerk beheer") ]
 POPULATION drgWaarde[Dreiging*LMH]        CONTAINS [ ("D6", "H") ]
POPULATION rImpact[Risico*Impact]         CONTAINS [ ("R6", "I6") ]
 POPULATION impScope[Impact*Scope]         CONTAINS [ ("I6", "RES-NW") ]
--POPULATION impOmschrijving[Impact*Text]   CONTAINS [ ("I6", "") ]
 POPULATION impWaarde[Impact*LMH]          CONTAINS [ ("I6", "L") ]
--POPULATION rOmgang[Risico*Text]           CONTAINS [ ("R6", "") ]
--POPULATION rToelichting[Risico*Text]      CONTAINS [ ("R6", "") ]

-- Risico 7
POPULATION rScope[Risico*Scope]           CONTAINS [ ("R7", "RES-NW") ]
POPULATION rResponsible[Risico*Persoon]   CONTAINS [ ("R7", "Martin Nevels") ]
POPULATION rAccountable[Risico*Persoon]   CONTAINS [ ("R7", "Kees van der Gaast") ]
--POPULATION rOmschrijving[Risico*Text]     CONTAINS [ ("R7", "") ]
POPULATION rWaarde[Risico*LMH]            CONTAINS [ ("R7", "M") ]
POPULATION rDreiging[Risico*Dreiging]     CONTAINS [ ("R7", "D7") ]
 POPULATION drgScope[Dreiging*Scope]       CONTAINS [ ("D7", "RES-NW") ]
 POPULATION drgOmschrijving[Dreiging*Text] CONTAINS [ ("D7", "Brand op modem-voorraadlocatie") ]
 POPULATION drgWaarde[Dreiging*LMH]        CONTAINS [ ("D7", "L") ]
POPULATION rImpact[Risico*Impact]         CONTAINS [ ("R7", "I7") ]
 POPULATION impScope[Impact*Scope]         CONTAINS [ ("I7", "RES-NW") ]
--POPULATION impOmschrijving[Impact*Text]   CONTAINS [ ("I7", "") ]
 POPULATION impWaarde[Impact*LMH]          CONTAINS [ ("I7", "H") ]
--POPULATION rOmgang[Risico*Text]           CONTAINS [ ("R7", "") ]
--POPULATION rToelichting[Risico*Text]      CONTAINS [ ("R7", "") ]

-- Risico 8
POPULATION rScope[Risico*Scope]           CONTAINS [ ("R8", "RES-NW") ]
POPULATION rResponsible[Risico*Persoon]   CONTAINS [ ("R8", "Martin Nevels") ]
POPULATION rAccountable[Risico*Persoon]   CONTAINS [ ("R8", "Kees van der Gaast") ]
--POPULATION rOmschrijving[Risico*Text]     CONTAINS [ ("R8", "") ]
POPULATION rWaarde[Risico*LMH]            CONTAINS [ ("R8", "M") ]
POPULATION rDreiging[Risico*Dreiging]     CONTAINS [ ("R8", "D8") ]
 POPULATION drgScope[Dreiging*Scope]       CONTAINS [ ("D8", "RES-NW") ]
 POPULATION drgOmschrijving[Dreiging*Text] CONTAINS [ ("D8", "Stroomstoring in Gn") ]
 POPULATION drgWaarde[Dreiging*LMH]        CONTAINS [ ("D8", "L") ]
POPULATION rImpact[Risico*Impact]         CONTAINS [ ("R8", "I8") ]
 POPULATION impScope[Impact*Scope]         CONTAINS [ ("I8", "RES-NW") ]
--POPULATION impOmschrijving[Impact*Text]   CONTAINS [ ("I8", "") ]
 POPULATION impWaarde[Impact*LMH]          CONTAINS [ ("I8", "H") ]
--POPULATION rOmgang[Risico*Text]           CONTAINS [ ("R8", "") ]
--POPULATION rToelichting[Risico*Text]      CONTAINS [ ("R8", "") ]

---------------------------------------------------------

SERVICE "Maatregel" : I[Maatregel]
= [ "ID" : I[Maatregel]
  , "Omschrijving" : mOmschrijving
  , "Scope" : mScope
  , "Voorstel voor risico" : mrVoorstel
  , "Besloten voor risico" : mrBesloten
  ]

 SERVICE "Maatregelen" : I[ONE]
= [ "Maatregelen"      : V[ONE*Sessie];sGebruiker;gaPersoon;scopeEigenaar~;mScope~
  = [ "Naam"     : I[Maatregel]
    , "Beschrijving" : mOmschrijving
    , "Scope" : mScope
    , "Voorstel voor risico" : mrVoorstel
    , "Besloten voor risico" : mrBesloten
  ] ]

PATTERN "Risico maatregelen voorstellen en besluiten" -- WIJZIGER: rieks.joosten@tno.nl

mOmschrijving :: Maatregel -> Text PRAGMA "" " is als volgt omschreven ".
mScope        :: Maatregel -> Scope PRAGMA "" " zit in ".
mrVoorstel    :: Maatregel * Risico PRAGMA "" " is voorgesteld teneinde " " te reduceren".
mrBesloten    :: Maatregel * Risico PRAGMA "Er ligt een besluit van het bevoegd management om " " te implementeren met het oogmerk om " " te reduceren".

mrBesloten~;mScope |- rScope EXPLANATION "Een maatregel die dient ter reductie van een zeker risico moet in dezelfde scope zitten als dat risico."
mrBesloten |- mrVoorstel EXPLANATION "Er kan alleen op grond van een daartoe strekkend voorstel besloten worden tot het implementeren van een maatregel."

-- SIGNAL sTeBesluitenMaatregelen ON
sTeBesluitenMaatregelen :: Maatregel * Maatregel.
sTeBesluitenMaatregelen = I /\ mrVoorstel;mrVoorstel~ /\ -(mrBesloten;mrBesloten~)
EXPLANATION "Maatregelen waarover besloten moet worden zijn voorstellen waarover nog geen besluit is genomen."

ENDPATTERN

POPULATION mOmschrijving[Maatregel*Text] CONTAINS [ ("M1", "Redundante switch bijplaatsen") ]
POPULATION mScope[Maatregel*Scope] CONTAINS [ ("M1", "TNOASE") ]
POPULATION mrVoorstel[Maatregel*Risico] CONTAINS [ ("M1", "R1") ]
POPULATION sTeBesluitenMaatregelen[Maatregel*Maatregel] CONTAINS [ ("M1", "M1") ]

POPULATION rGeaccepteerd CONTAINS [ ("R2", "R2") ]
---------------------------------------------------------
PATTERN "Risico maatregelen implementeren" -- WIJZIGER: rieks.joosten@tno.nl

mimplStatus :: Maatregel * Text [UNI] PRAGMA "De implementatiestatus van " " wordt beschreven door ".
mrGeimplementeerd :: Maatregel * Risico PRAGMA "Er is vastgesteld dat " " is geimplementeerd ter beheersing van ".

mrGeimplementeerd |- mrBesloten EXPLANATION "Een maatregel mag alleen worden gekenschetst als zijnde geimplementeerd als daartoe ook besloten is."

--SIGNAL sTeImplementerenMaatregelen ON mrBesloten = mrGeimplementeerd EXPLANATION "Elke maatregel waarvoor een implementatiebesluit ligt, moet zijn geimplementeerd."
sTeImplementerenMaatregelen :: Maatregel * Risico.
sTeImplementerenMaatregelen = mrBesloten /\ - mrGeimplementeerd EXPLANATION "Maatregelen waartoe is besloten maar die nog niet zijn geimplementeerd, dienen te worden geimplementeerd."

ENDPATTERN
---------------------------------------------------------
PATTERN "Risico maatregelen audit" -- WIJZIGER: rieks.joosten@tno.nl

mauditDatum  :: Maatregel * Datum [UNI] PRAGMA "De laatste audit naar " " is gehouden op ".
mauditWaarde :: Maatregel * LMH [UNI] PRAGMA "N.a.v. de laatst gehouden audit naar " " is de waarde van de bevindingen samengevat als ".
mauditStatus :: Maatregel * Text [UNI] PRAGMA "In de laatst gehouden audit naar " " zijn de volgende bevindingen geconstateerd: ".

mauditDatum; mauditDatum~ |- mauditWaarde; mauditWaarde~ EXPLANATION "Aan elke maatregel die is geauditeerd, is een waarde toegekend die de bevindingen samenvat."

mauditWaarde; -lmhIsL; mauditWaarde~ |- mauditStatus; mauditStatus~ EXPLANATION "Bij elke geauditeerde maatregel die niet Laag scoort, moet beschreven zijn aan welke bevindingen de score is ontleend."

SIGNAL sTeAuditeren ON
I[Maatregel] |- mauditDatum; mauditDatum~
EXPLANATION "Elke maatregel moet (recentelijk) zijn geauditeerd."

ENDPATTERN
---------------------------------------------------------
ENDCONTEXT