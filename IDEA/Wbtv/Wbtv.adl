CONTEXT Wbtv
INCLUDE "Persoonsgegevens.adl"
THEMES Rbtv,Aanvragen,Inschrijfingseisen,Beëdiging

META "authors" "Han Joosten, Ordina"
PURPOSE CONTEXT Wbtv  REF "https://zoek.officielebekendmakingen.nl/stcrt-2008-2502.html"
{+
De Wet beëdigde tolken en vertalers (hierna: Wet) strekt onder meer tot het instellen van een register
voor beëdigde tolken en beëdigd vertalers (hierna: register). In dit register worden de vertalers en de
tolken die aan kwaliteits- en integriteitseisen voldoen op hun verzoek opgenomen. Via dit register
kunnen partijen die behoefte hebben aan bijstand van een beëdigd tolk of vertaler kennis nemen van
het beschikbare aanbod. Tolken en vertalers die willen worden opgenomen in het register als
beëdigde tolk of vertaler, moeten voldoen aan de in artikel 3 van de Wet gestelde (opleidings)eisen.
Daarnaast wordt een lijst bijgehouden van tolken en vertalers die wegens het ontbreken van opleidingen
of het ontbreken van onafhankelijke deskundigen die de kennis kunnen toetsen, niet kunnen
aantonen te beschikken over een aantal vereiste competenties, maar wel een verklaring omtrent
gedrag kunnen overleggen.

Op deze wet is een conceptuele analyse gedaan, om inzicht te krijgen in de benodigde processen om deze 
regeling uit te voeren. 

Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, zonder de naam van de wet erbij te noemen,
wordt de Wet beëdigde tolken en vertalers bedoeld.
-}



----------------------
-- Register beëdigde tolken en vertalers --
----------------------

PATTERN Rbtv
PURPOSE PATTERN "Rbtv"
{+
Het Rbtv is een register waarin tolken en vertalers staan ingeschreven die bevoegd zijn om in Nederland als beëdigd tolk en/of beëdigd vertaler werkzaam te zijn. 
Sinds 1 januari 2009 zijn bepaalde afnemers uit het justitiedomein verplicht om, in het kader van het straf- en vreemdelingenrecht, in beginsel een tolk of vertaler uit het Rbtv in te zetten. Gewaarmerkte vertalingen mogen zelfs alleen nog maar gemaakt worden door vertalers die in het Rbtv staan ingeschreven en beëdigd zijn. 
De tolken en vertalers uit het Rbtv hebben aangetoond in voldoende mate te beschikken over de in artikel 3 Wbtv vereiste competenties. De afnemer kan dus bepaalde garanties ten aanzien van kwaliteit en integriteit ontlenen aan een inschrijving in het Rbtv. 
Zodra een tolk/vertaler is ingeschreven in het Rbtv kunnen afnemers zijn gegevens vinden via het raadplegen van het Rbtv. 
Een inschrijving in het Rbtv is vijf jaar geldig en kan daarna onder bepaalde voorwaarden worden verlengd. Er moeten in die vijf jaar onder andere 80 punten zijn behaald in het kader van bijscholing.  (bron:http://www.bureaubtv.nl/faq/)
-}

--------------
-- Relaties --
--------------

vaardigheid :: Taalervaring * Vaardigheid [UNI,TOT] PRAGMA "De taalervaring `" "' betreft de vaardigheid als " 
MEANING "Elke taalervaring vermeldt precies één vaardigheid."
PURPOSE RELATION vaardigheid [Taalervaring * Vaardigheid] 
{+Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal.-}
POPULATION vaardigheid CONTAINS
 [ ("tolkvaardigheid tussen Engels en Duits" , "Tolk")
 ]
brontaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De brontaal van `" "' is " 
MEANING "Elke taalervaring heeft precies één brontaal."
PURPOSE RELATION brontaal [Taalervaring * Taal] 
{+Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal. 
-}
POPULATION brontaal CONTAINS
 [ ("tolkvaardigheid tussen Engels en Duits" , "Engels")
 ]

doeltaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De doeltaal van `" "' is " 
MEANING "Elke taalervaring heeft precies één doeltaal."
PURPOSE RELATION doeltaal [Taalervaring * Taal] 
{+Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal.-}
POPULATION doeltaal CONTAINS
 [ ("vertaalvaardigheid van Engels naar Duits" , "Duits")
 ]

ingeschrevene :: Wbtvnummer * NatuurlijkPersoon [UNI,TOT,INJ] PRAGMA "Wbtvnummer " "is toegekend aan" ""
MEANING "Elke ingeschreve heeft precies één uniek Wbtv-nummer."
PURPOSE RELATION ingeschrevene[Wbtvnummer*NatuurlijkPersoon] IN DUTCH REF ""
{+Tolken en vertalers die daarvoor in aanmerking komen, worden door de Raad opgenomen in het register. 

De Raad wijst aan ingeschrevenen een uniek nummer toe, waarmee koppelingen 
gelegd kunnen worden met externe applicaties om beëdigd tolken of vertalers 
uniek te identificeren.-}

wbtvnummer :: Inschrijving -> Wbtvnummer PRAGMA "het Wbtv-nummer van "" is "
MEANING "Elke inschrijving bevat het Wbtv-nummer van de ingeschreven beëdigd tolk of vertaler."

bevat :: Inschrijving * Taalervaring [TOT] PRAGMA "Inschrijving " " is als " ""
MEANING "Elke inschrijving bevat tenminste één taalervaring."
PURPOSE RELATION bevat[Inschrijving * Taalervaring] IN DUTCH REF "Art. 2 lid 1 Rbtv"
{+ Elke ingeschreven tolk of vertaler dient het register minimaal de bron- en doeltaal (talen) te
bevatten, en een aanduiding of betrokkene tolk of vertaler is. Door nu te 
eisen dat elke inschrijving tenminste een taalervaring bevat, wordt 
recht gedaan aan de desbetreffende wetsartikelen. 
-}
inschrijfdatum :: Inschrijving * Datum [TOT,UNI] PRAGMA "De datum van inschrijving van " " is "
MEANING "Elke inschrijving is gedaan op één datum."
PURPOSE RELATION inschrijfdatum [Inschrijving * Datum] REF "Art. 8 lid 1 Wbtv"
{+Een inschrijving geschiedt voor de periode van vijf jaar. Om dit te kunnen 
vaststellen, is het noodzakelijk om de datum van inschrijving te registreren.-}


-----------------
-- Invarianten --
-----------------


ENDPATTERN

PATTERN Inschrijfingseisen
PURPOSE PATTERN Inschrijfingseisen REST REF "Art. 3 Wbtr"
{+Om voor inschrijving in het register in aanmerking te komen dient 
de tolk dan wel de vertaler te voldoen aan de bij of krachtens 
algemene maatregel van bestuur te stellen eisen ten aanzien van 
in dit artikel specifiek benoemde competenties. 
Afhankelijk van de competenties moet er daarom bepaalde informatie geregistreerd kunnen worden in het systeem. 
Bij de volgende eisen worden de competenties genoemd, gevolgd door de concrete afspraak ten aanzien van het systeem:-}
attitudeAls :: NatuurlijkPersoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde attitude-eisen voor een "
MEANING "Geregistreerd moet kunnen worden dat de attitude van een persoon in de vaardigheid als tolk dan wel als vertaler voldoet aan de aan die vaardigheid gestelde eisen."
PURPOSE RELATION attitudeAls REF "Art. 3 Wbtv"
{+* attitude van een tolk voor de tolk;
* attitude van een vertaler voor de vertaler;
 -}

isInteger :: NatuurlijkPersoon*NatuurlijkPersoon [PROP] PRAGMA "De integriteit van " " is vastgesteld "
MEANING "Geregistreerd moet kunnen worden dat de integriteit van een peroon is vastgesteld."
PURPOSE RELATION isInteger REF "Art. 3 Wbtv"
{+* integriteit;
 -}

taalvaardig :: NatuurlijkPersoon*Taal PRAGMA "De taalvaardigheid van " " in de taal " " voldoet aan de daaraan gestelde eisen."
MEANING "Geregistreerd moet kunnen worden dat de taalvaardigheid van een persoon in een specifieke taal voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION taalvaardig REF "Art. 3 Wbtv"
{+* taalvaardigheid in de brontaal;
* taalvaardigheid in de doeltaal;
 -}

cultuurKennisVan :: NatuurlijkPersoon*Taal PRAGMA "" "voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van de taal ""."
MEANING "Geregistreerd moet kunnen worden dat een persoon voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van een taal."
PURPOSE RELATION cultuurKennisVan REF "Art. 3 Wbtv"
{+* kennis van de cultuur van het land of gebied van de brontaal;
* kennis van de cultuur van het land of gebied van de doeltaal;
 -}

vaardigAls :: NatuurlijkPersoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde vaardigheidseisen voor een "
MEANING "Geregistreerd moet kunnen worden dat de tolk- dan wel vertaalvaardigheid van een persoon voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION vaardigAls REF "Art. 3 Wbtv"
{+* tolkvaardigheid voor de tolk;
* vertaalvaardigheid voor de vertaler.
 -}

-----------------
-- Invarianten --
-----------------
RULE attitudeEis: ingeschrevene~;wbtvnummer~;bevat;vaardigheid |- attitudeAls
MEANING "Van iedere persoon die is ingeschreven met een taalervaring die een vaardigheid vermeldt, is vastgesteld dat die persoon voldoet aan aan die vaardigheid gestelde attitude-eisen."
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde attitude-eisen als tolk dan wel als vertaler.
-}
RULE integriteit: ingeschrevene~;wbtvnummer~;wbtvnummer;ingeschrevene |- isInteger
MEANING "Van iedere persoon die is ingeschreven is vastgesteld dat die persoon voldoet aan de gestelde integriteits eisen"
PURPOSE RULE integriteit REF "Art 3 Wbtv"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde eisen ten aanzien van integriteit.
-}
RULE taalvaardigheid: ingeschrevene~;wbtvnummer~;bevat;(brontaal \/ doeltaal) |- taalvaardig
MEANING "Van iedere persoon die is ingeschreven met een taalervaring met een bepaalde taal als bron- of doeltaal, is vastgesteld dat die persoon voldoet aan de voor die taal gestelde taalvaardigheid-eisen."
PURPOSE RULE taalvaardigheid REF "Art 3 Wbtv"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde taalvaardigheids-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE cultuurkennis: ingeschrevene~;wbtvnummer~;bevat;(brontaal \/ doeltaal) |- cultuurKennisVan
MEANING "Van iedere persoon die is ingeschreven met een taalervaring met een bepaalde taal als bron- of doeltaal, is vastgesteld dat die persoon voldoet aan de voor die taal gestelde eisen met betrekking tot kennis van cultuur van het land of gebied voor die taal."
PURPOSE RULE cultuurkennis REF "Art 3 Wbtv"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde kennis-van-cultuur-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE vaardigheidsEis: ingeschrevene~;wbtvnummer~;bevat;vaardigheid |- vaardigAls
MEANING "Van iedere persoon die is ingeschreven met een taalervaring die een vaardigheid vermeldt, is vastgesteld dat die persoon voldoet aan aan die vaardigheid gestelde vaardigheids-eisen."
PURPOSE RULE vaardigheidsEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde vaardigheids-eisen als tolk dan wel als vertaler.
-}

ENDPATTERN

PATTERN Aanvragen
PURPOSE PATTERN Aanvragen
{+Teneinde het register actueel te houden, kunnen aanvragen worden ingediend, 
die door Bureau btv, als beheerder van het register, conform de Wbtv worden behandeld. 
Dit thema beschrijft het taalmodel van dergelijke verzoeken.
-}
aanvrager :: Aanvraag * NatuurlijkPersoon [TOT,UNI] PRAGMA "" "is het verzoek van" ""
MEANING "Elke aanvraag wordt gedaan door een natuurlijk persoon."
PURPOSE RELATION aanvrager[Aanvraag*NatuurlijkPersoon] IN DUTCH REF "Art. 4:2 lid 1a en Art. 4 Wbtv"
{+ Elke aanvraag heeft een aanvrager. In geval van een verzoek tot inschrijving is dit de tolk of vertaler wiens inschrijving het betreft. -}

dagtekening :: Aanvraag * Datum [TOT,UNI] PRAGMA "" " heeft als dagtekening " 
MEANING "Elke aanvraag bevat de dagtekening" 
PURPOSE RELATION dagtekening REF "Art 4:2 lid 1b awb"
{+ Om te weten waneer een aanvraag is gedaan, is de aanvraag gedagtekend -}

SPEC InschrijfVerzoek ISA Aanvraag

woonadres :: InschrijfVerzoek * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een verzoek tot inschrijving kan een woonadres bevatten."
PURPOSE RELATION woonadres[InschrijfVerzoek * Adres] REF "Inschrijfformulier RBTV"
{+ Het aavraagformulier bevat ruimte voor de opgave van een adres als woonadres.
-}
postadres :: InschrijfVerzoek * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een verzoek tot inschrijving kan een woonadres bevatten."
PURPOSE RELATION postadres REF "Inschrijfformulier Rbtv"
{+ Het aavraagformulier bevat ruimte voor de opgave van een adres als postadres.
-}

bevat :: InschrijfVerzoek * Taalervaring [] PRAGMA "In " " wordt verzocht tot inschrijving als" ""
MEANING "Een verzoek tot inschrijving kan taalervaringen bevatten, waarvoor inschrijving wordt verzocht."
PURPOSE RELATION bevat[InschrijfVerzoek * Taalervaring] IN DUTCH REF "Inschrijfformulier Rbtv"
{+ Het aanvraagforumier bevat ruimte voor taalcombinaties, waarin de aanvrager kan opgeven voor welke 
 (als tolk of vertaler, bron- en doeltaal) een inschijving wordt verzocht. 
-}


ENDPATTERN


PATTERN Beëdiging
beëdigde :: Beëdiging -> NatuurlijkPersoon [UNI] PRAGMA "" "is beëdiging van" ""
MEANING "Elke beëdiging is van één persoon."
PURPOSE RELATION beëdigde[Beëdiging*NatuurlijkPersoon] IN DUTCH REF "Art. 12 Wbtv."
{+ Een persoon die als tolk of als vertaler is ingeschreven, dient binnen twee maanden na inschrijving als tolk of als vertaler te worden beëdigd. 
-}
als :: Beëdiging * Vaardigheid [TOT] PRAGMA "" " is een beëdiging als "
MEANING "Elke beëdiging gaat over de eed of belofte in tenminste één rol als tolk of als vertaler."
PURPOSE RELATION als [Beëdiging*Vaardigheid] REF "Art. 13 en 14 Wbtv."
{+Elke beëdiging is als tolk of als vertaler. De wet is onduidelijk over de gang van zaken waneer een ingeschrevene als vertaler na verloop van tijd ook als tolk wil worden ingeschreven.
-}
datumBeëdiging :: Beëdiging -> Datum [UNI] PRAGMA "" "vond plaats op" ""
MEANING "Een beëdiging vindt plaats op een datum."
PURPOSE RELATION datumBeëdiging[Beëdiging*Datum] IN DUTCH REF "Art. 13 en 14 Wbtv."
{+ Een beëdiging vindt plaats op één datum.
-}

--RULE TODO--alleen inggeschrevenen kunnen worden beedigd.

ENDPATTERN



---------------
-- Concepten --
---------------
CONCEPT Vaardigheid "Een generieke term voor de vaardigheid als tolk of vaardigheid als vertaler."
PURPOSE CONCEPT Vaardigheid
{+De wet maakt onderschrijd naar de vaardigheden van een persoon als tolk of als vertaler. Om over dit verschil 
te kunnen spreken, introduceren we een term, die tweewaardig is, `tolk` of `vertaler`.
-}
CONCEPT Taal "Een taal of dialect die als zodanig is opgenomen in de taallijst."
PURPOSE CONCEPT Taal 
{+Er bestaat een lijst van talen, waarvoor tolken en vertalers in het Rbtv kunnen 
worden ingeschreven of op de Uitwijklijst worden geplaatst. De adviescommissie Talen 
is formeel aangewezen als de beheerder van deze lijst. 
-} 
 
CONCEPT Taalervaring "De combinatie van een vaardigheid, een bron- en een doeltaal." 
PURPOSE CONCEPT Taalervaring
{+De Wet schrijft in artikel 2, lid 1.b en c, minimale eisen voor 
waaraan de geregistreerde gegevens van een ingeschreve moeten voldoen. 
Deze minimale gegevens zijn echter op zichzelf niet toereikend, omdat 
het om verschillende combinaties kan gaan van bron- en doeltaal, 
en de of het de vaardigheid als tolk of die van vertaler betreft. 
Hiertoe is het concept `Taalervaring` geïntroduceerd.
Bij vertalingservaring is ook de richting van belang. Dan wordt ook wel gesproken van 
vertaalrichting, maar in deze analyse wordt daarin geen onderscheid 
gemaakt. Bij tolkervaring is de richting niet van belang.
-}

CONCEPT InschrijfVerzoek "een verzoek tot inschrijving in het Rbtv" "art. 2 besluit inschrijving Rbtv"
PURPOSE CONCEPT InschrijfVerzoek REF "Brochure `Wbtf voor tolken en vertalers`"  -- http://www.bureaubtv.nl/pdf/Wbtv%20voor%20tolken%20en%20vertalers.pdf
{+Voor het indienen van een verzoek tot inschrijving in het Rbtv is op de 
website van Bureau btv (www.bureaubtv.nl) een formulier beschikbaar. 
Dit formulier kan online worden ingevuld en uitgeprint. Vervolgens moet 
de aanvrager het formulier te ondertekenen en met de benodigde bijlagen 
naar het Bureau btv te sturen. Alleen volledige verzoeken tot inschrijving 
worden in behandeling genomen. Indien het verzoek volledig is, krijgt de 
aanvrager een nota ad € 125,-. Pas na betaling van deze wettelijke leges 
neemt Bureau btv de aanvraag in behandeling. 
Voor een aanvulling op de inschrijving, bijvoorbeeld een extra bron- of 
doeltaal of een nieuwe vaardigheid, bestaat een apart formulier, welke ook 
te vinden is op de website van het Bureau btv. In deze gevallen zijn geen 
leges verschuldigd.
-}

PATTERN Overige
RULE vaardigheden : I[Vaardigheid] = 'Tolk' \/ 'Vertaler'
MEANING "Vaardigheden die wettelijk geregistreerd worden, zijn 'Tolk' en 'Vertaler'."
PURPOSE RULE "vaardigheden"
{+Om invoerfouten te voorkomen worden de invoertypen voor vaardigheid beperkt tot de waarden 'tolk' en 'vertaler'. 
-}ENDPATTERN
ENDCONTEXT