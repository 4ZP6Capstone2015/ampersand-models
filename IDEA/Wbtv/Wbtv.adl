CONTEXT Wbtv
META "authors" "Han Joosten, Ordina"
PURPOSE CONTEXT Wbtv  REF "https://zoek.officielebekendmakingen.nl/stcrt-2008-2502.html"
{+
De Wet beëdigde tolken en vertalers (hierna: Wet) strekt onder meer tot het instellen van een register
voor beëdigde tolken en beëdigd vertalers (hierna: register). In dit register worden de vertalers en de
tolken die aan kwaliteits- en integriteitseisen voldoen op hun verzoek opgenomen. Via dit register
kunnen partijen die behoefte hebben aan bijstand van een beëdigd tolk of vertaler kennis nemen van
het beschikbare aanbod. Tolken en vertalers die willen worden opgenomen in het register als
beëdigde tolk of vertaler, moeten voldoen aan de in artikel 3 van de Wet gestelde (opleidings)eisen.
Daarnaast wordt een lijst bijgehouden van tolken en vertalers die wegens het ontbreken van opleidingen
of het ontbreken van onafhankelijke deskundigen die de kennis kunnen toetsen, niet kunnen
aantonen te beschikken over een aantal vereiste competenties, maar wel een verklaring omtrent
gedrag kunnen overleggen.

Op deze wet is een conceptuele analyse gedaan, om inzicht te krijgen in de benodigde processen om deze 
regeling uit te voeren. 

Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, zonder de naam van de wet erbij te noemen,
wordt de Wet beëdigde tolken en vertalers bedoeld.
-}

--INCLUDE "Persoonsgegevens.pop"

--THEMES Advocatentableau

----------------------
-- Register beëdigde tolken en vertalers --
----------------------

PATTERN Rbtv
PURPOSE PATTERN "Rbtv"
{+
Het Rbtv is een register waarin tolken en vertalers staan ingeschreven die bevoegd zijn om in Nederland als beëdigd tolk en/of beëdigd vertaler werkzaam te zijn. 
Sinds 1 januari 2009 zijn bepaalde afnemers uit het justitiedomein verplicht om, in het kader van het straf- en vreemdelingenrecht, in beginsel een tolk of vertaler uit het Rbtv in te zetten. Gewaarmerkte vertalingen mogen zelfs alleen nog maar gemaakt worden door vertalers die in het Rbtv staan ingeschreven en beëdigd zijn. 
De tolken en vertalers uit het Rbtv hebben aangetoond in voldoende mate te beschikken over de in artikel 3 Wbtv vereiste competenties. De afnemer kan dus bepaalde garanties ten aanzien van kwaliteit en integriteit ontlenen aan een inschrijving in het Rbtv. 
Zodra een tolk/vertaler is ingeschreven in het Rbtv kunnen afnemers zijn gegevens vinden via het raadplegen van het Rbtv. 
Een inschrijving in het Rbtv is vijf jaar geldig en kan daarna onder bepaalde voorwaarden worden verlengd. Er moeten in die vijf jaar onder andere 80 punten zijn behaald in het kader van bijscholing.  (bron:http://www.bureaubtv.nl/faq/)
-}

----------
-- Keys --
----------


---------------
-- Concepten --
---------------
CONCEPT Vaardigheid "Een generieke term voor de vaardigheid als tolk of vaardigheid als vertaler"
PURPOSE CONCEPT Vaardigheid
{+Er is een significant verschil tussen een tolk of een vertaler, hoewel er ook overeenkomsten zijn. 
Het is nodig om dit onderscheid te kunnen maken. 
-}
CONCEPT Taalervaring "De registratie van een ingeschrevene als tolk tussen of als vertaler van en naar een specifieke brontaal en een specifieke doeltaal." 
PURPOSE CONCEPT Taalervaring
{+Een tolk of vertaler kan werkzaamheden verrichten in de rol van tolk 
en/of in de rol van vertaler. Bovendien kunnen hierbij verschillende 
bron- en doeltalen mogelijk zijn.
Een registratie bevat de specifieke combinatie hiervan. Bij vertaling 
is ook de richting van belang. Dan wordt ook wel gesproken van 
vertaalrichting, maar in deze analyse wordt daarin geen onderscheid 
gemaakt. Bij tolken is de richting echter niet van belang.
-}

CONCEPT Aanvraag "een verzoek tot inschrijving in het Rbtv" "art. 2 besluit inschrijving Rbtv"
PURPOSE CONCEPT Aanvraag REF "Brochure `Wbtf voor tolken en vertalers`"  -- http://www.bureaubtv.nl/pdf/Wbtv%20voor%20tolken%20en%20vertalers.pdf
{+Voor het indienen van een verzoek tot inschrijving in het Rbtv is op de 
website van Bureau btv (www.bureaubtv.nl) een formulier beschikbaar. 
Dit formulier kan online worden ingevuld en uitgeprint. Vervolgens moet 
de aanvrager het formulier te ondertekenen en met de benodigde bijlagen 
naar het Bureau btv te sturen. Alleen volledige verzoeken tot inschrijving 
worden in behandeling genomen. Indien het verzoek volledig is, krijgt de 
aanvrager een nota ad € 125,-. Pas na betaling van deze wettelijke leges 
neemt Bureau btv de aanvraag in behandeling. 
Voor een aanvulling op de inschrijving, bijvoorbeeld een extra bron- of 
doeltaal of een nieuwe vaardigheid, bestaat een apart formulier, welke ook 
te vinden is op de website van het Bureau btv. In deze gevallen zijn geen 
leges verschuldigd.
-}


--------------
-- Relaties --
--------------

aanvrager :: Aanvraag * Persoon [TOT,UNI] PRAGMA "" "is het verzoek tot inschrijving als tolk of vertaler van" ""
MEANING "Voor elke aanvraag wordt de tolk of vertaler geregistreerd."
PURPOSE RELATION aanvrager[Aanvraag*Persoon] IN DUTCH REF "Art. 4 Wbtv"
{+ Elk verzoek van inschrijving dient altijd ingediend te zijn door de tolk of vertaler wiens inschrijving het betreft. -}


woonadres :: Aanvraag * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een aanvraag kan een woonadres bevatten."
PURPOSE RELATION woonadres REF "Inschrijfformulier RBTV"
{+ Het aavraagformulier bevat ruimte voor de opgave van een adres als woonadres.
-}
postadres :: Aanvraag * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een aanvraag kan een woonadres bevatten."
PURPOSE RELATION postadres REF "Inschrijfformulier Rbtv"
{+ Het aavraagformulier bevat ruimte voor de opgave van een adres als postadres.
-}

bevat :: Aanvraag * Taalervaring [] PRAGMA "In " " wordt verzocht tot inschrijving als" ""
MEANING "Een aanvraag kan taalervaringen bevatten, waarvoor inschrijving wordt verzocht."
PURPOSE RELATION bevat[Aanvraag * Taalervaring] IN DUTCH REF "Inschrijfformulier Rbtv"
{+ Het aanvraagforumier bevat ruimte voor taalcombinaties, waarin de aanvrager kan opgeven voor welke 
 (als tolk of vertaler, bron- en doeltaal) een inschijving wordt verzocht. 
-}

vaardigheid :: Taalervaring * Vaardigheid [UNI,TOT] PRAGMA "" " in de rol van " 
MEANING "Elke taalervaring betreft precies één rol als tolk of als vertaler."
PURPOSE RELATION vaardigheid [Taalervaring * Vaardigheid] 
{+ Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal. 
-}

brontaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De brontaal van " " is " 
MEANING "Elke taalervaring heeft precies één brontaal."
PURPOSE RELATION brontaal [Taalervaring * Taal] 
{+ Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal. 
-}
doeltaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De doeltaal van " " is " 
MEANING "Elke taalervaring heeft precies één doeltaal."
PURPOSE RELATION doeltaal [Taalervaring * Taal] 
{+ Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal. 
-}



beëdigde :: Beëdiging -> Persoon [UNI] PRAGMA "" "is beëdiging van" ""
MEANING "Elke beëdiging is van één persoon."
PURPOSE RELATION beëdigde[Beëdiging*Persoon] IN DUTCH REF "Art. 12 Wbtv."
{+ Een persoon die als tolk of als vertaler is ingeschreven, dient binnen twee maanden na inschrijving als tolk of als vertaler te worden beëdigd. 
-}
als :: Beëdiging * Vaardigheid [TOT] PRAGMA "" " is een beëdiging als "
MEANING "Elke beëdiging gaat over de eed of belofte in tenminste één rol als tolk of als vertaler."
PURPOSE RELATION als [Beëdiging*Vaardigheid] REF "Art. 13 en 14 Wbtv."
{+Elke beëdiging is als tolk of als vertaler. De wet is onduidelijk over de gang van zaken waneer een ingeschrevene als vertaler na verloop van tijd ook als tolk wil worden ingeschreven.
-}
datumBeëdiging :: Beëdiging -> Datum [UNI] PRAGMA "" "vond plaats op" ""
MEANING "Een beëdiging vindt plaats op een datum."
PURPOSE RELATION datumBeëdiging[Beëdiging*Datum] IN DUTCH REF "Art. 13 en 14 Wbtv."
{+ Een beëdiging vindt plaats op één datum.
-}



ingeschrevene :: Wbtvnummer * Persoon [UNI,TOT,INJ] PRAGMA "Wbtvnummer " "is toegekend aan" ""
MEANING "Elke ingeschreve heeft een uniek Wbtv-nummer."
PURPOSE RELATION ingeschrevene[Wbtvnummer*Persoon] IN DUTCH REF ""
{+ Ingeschrevenen hebben een uniek nummer waarmee koppelingen gelegd kunnen worden met externe applicaties om beëdigd tolken of vertalers uniek te identificeren.
-}

wbtvnummer :: Inschrijving -> Wbtvnummer PRAGMA "het Wbtv-nummer van "" is "
MEANING "Elke inschrijving bevat het Wbtv-nummer van de ingeschreven beëdigd tolk of vertaler."

bevat :: Inschrijving * Taalervaring [TOT] PRAGMA "Inschrijving " " is als " ""
MEANING "Elke inschrijving bevat tenminste één taalervaring."
PURPOSE RELATION bevat[Inschrijving * Taalervaring] IN DUTCH REF "Art. 2 lid 1 Rbtv"
{+ De wet schrijft in artikel 2, lid 1.b en c, minimale eisen voor 
waaraan de geregistreerde gegevens van een ingeschreve moeten voldoen. 
Deze minimale gegevens zijn echter op zichzelf niet toereikend, omdat 
het om verschillende combinaties kan gaan van bron- en doeltaal, 
en de of het de vaardigheid als tolk of die van vertaler betreft. 
Hiertoe is het concept `Taalervaring` geïntroduceerd. Door nu te 
eisen dat elke inschrijving tenminste een taalervaring bevat, wordt 
recht gedaan aan de desbetreffende wetsartikelen. 
-}


--------------------
-- Specialisaties --
--------------------
--SPEC Beëdiging ISA Gebeurtenis




ENDPATTERN

PATTERN Inschrijfingseisen
PURPOSE PATTERN Inschrijfingseisen REF "Art. 3 Wbtr"
{+
Om voor inschrijving in het register in aanmerking te komen dient 
de tolk dan wel de vertaler te voldoen aan de bij of krachtens 
algemene maatregel van bestuur te stellen eisen ten aanzien van 
de volgende competenties:
 – attitude van een tolk voor de tolk;
 – attitude van een vertaler voor de vertaler;
 – integriteit;
 – taalvaardigheid in de brontaal;
 – taalvaardigheid in de doeltaal;
 – kennis van de cultuur van het land of gebied van de brontaal;
 – kennis van de cultuur van het land of gebied van de doeltaal;
 – tolkvaardigheid voor de tolk;
 – vertaalvaardigheid voor de vertaler.

Er zal derhalve geregistreerd moeten kunnen worden, of aan deze eisen voldoende voldaan is.
-}
isInteger :: Persoon*Persoon [PROP]
MEANING "Geregistreerd moet kunnen worden dat de integriteit van een peroon is vastgesteld."
PURPOSE RELATION isInteger REF "Art. 3 Wbtv"
{+-}

attitudeAls :: Persoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde attitude-eisen voor een "
MEANING "Geregistreerd moet kunnen worden dat de attitude van een persoon in de rol als tolk dan wel in de rol van vertaler voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION attitudeAls REF "Art. 3 Wbtv"
{+-}

vaardigAls :: Persoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde vaardigheidseisen voor een "
MEANING "Geregistreerd moet kunnen worden dat de tolk- dan wel vertaalvaardigheid van een persoon voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION vaardigAls REF "Art. 3 Wbtv"
{+-}

taalvaardig :: Persoon*Taal PRAGMA "De taalvaardigheid van " " in de taal " " voldoet aan de daaraan gestelde eisen."
MEANING "Geregistreerd moet kunnen worden dat de taalvaardigheid van een persoon in een specifieke taal voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION taalvaardig REF "Art. 3 Wbtv"
{+-}

cultuurKennisVan :: Persoon*Taal PRAGMA "" "voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van de taal ""."
MEANING "Geregistreerd moet kunnen worden een persoon voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van een taal."
PURPOSE RELATION cultuurKennisVan REF "Art. 3 Wbtv"
{+-}

-----------------
-- Invarianten --
-----------------
RULE attitudeEis: ingeschrevene~;wbtvnummer~;bevat;vaardigheid |- attitudeAls
MEANING "Elke ingeschrevene moet voldoen aan de gestelde attitude-eisen voor de vaardigheid vaarvoor hij of zij staat ingeschreven"
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde attitude-eisen als tolk dan wel vertaler.
-}
RULE integriteit: ingeschrevene~;wbtvnummer~;wbtvnummer;ingeschrevene |- isInteger
MEANING "Elke ingeschrevene moet voldoen aan de gestelde integriteits eisen"
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde eisen ten aanzien van integriteit.
-}
RULE taalvaardigheid: ingeschrevene~;wbtvnummer~;bevat;(brontaal \/ doeltaal) |- taalvaardig
MEANING "Elke ingeschrevene moet voldoen aan de gestelde taalvaardigheid-eisen voor de bron- en doeltalen vaarvoor hij of zij staat ingeschreven"
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde taalvaardigheids-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE cultuurkennis: ingeschrevene~;wbtvnummer~;bevat;(brontaal \/ doeltaal) |- cultuurKennisVan
MEANING "Elke ingeschrevene moet voldoen aan de gestelde eisen met betrekking tot kennis van cultuur van het land of gebied voor de bron- en doeltalen vaarvoor hij of zij staat ingeschreven"
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde kennis-van-cultuur-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE vaardigheidsEis: ingeschrevene~;wbtvnummer~;bevat;vaardigheid |- vaardigAls
MEANING "Elke ingeschrevene moet voldoen aan de gestelde vaardigheids-eisen voor de vaardigheid vaarvoor hij of zij staat ingeschreven"
PURPOSE RULE attitudeEis REF "Art 3 Wbtv"
{+ Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde vaardigheids-eisen als tolk dan wel vertaler.
-}

ENDPATTERN

ENDCONTEXT