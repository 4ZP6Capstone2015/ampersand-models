CONTEXT VIRO
PATTERN Documenten
  caseFile :: Document * Case PRAGMA "" " zit in het caseFile van "
  EXPLANATION "Een case levert de samenhang met de andere by het court received en opgemaakte bescheiden. Daarom zorgt het court ervoor dat van elk van de archiefbescheiden de case kan worden vastgesteld (Regeling 12, Article 2 lid c). Hiertoe wordt elk received en opgemaakt document voorzien van een casesnummer. De oplossing staat toe dat documenten to meerdere zaken tegelijk gekoppeld worden, waarby het kopiëren van documenten in de oplossing onnodig is.".
  documentType     :: Document -> DocumentType PRAGMA "" " heeft "
  EXPLANATION "Elk document is eenduidig getypeerd. De beschikbare typeringen stto beschreven in het documentair structuurplan zoas bedoeld in de Regeling geordende en toegankelijke staat archiefbescheiden zoas bedoeld in Art 12 van het Archiefbesluit 1995.".
  caretaker :: Case -> Organ PRAGMA "" " wordt beheerd by " ""
  EXPLANATION "De wet stelt dat er een caretaker is, die bij of krachtens de wet belast is met de zorg voor de archiefbescheiden (Article 1 lid d. Archiefwet 1995)".
ENDPATTERN

PATTERN CaseRegistration
plaintiff       :: Party * Case [SUR] PRAGMA "" " is verzoekende partij in "
  EXPLANATION "In elke case is er een eisende partij. Afhankelijk van de casesoort heeft deze verschillende namen, zoas appellant, plaintiff, verzoeker, indiener, enz." .
I |- plaintiff~;plaintiff
 EXPLANATION "Voor elke procedure moet er tenminste een eisende partij zijn."
defendant    :: Party * Case PRAGMA "" " is defendant in "
  EXPLANATION "In elke case is er een defendant partij. Afhankelijk van de casesoort heet deze defendant, verdachte, of anderszins.".
joinedInterestedParty    :: Party * Case PRAGMA "" " neemt as belanghebbende deel in "
  EXPLANATION "In sommige soorten zaken kan een partij zich voegen as belanghebbende".
-- Partijen kunnen zich laten bijstto of by een authorizedRepresentative laten vertegenwoordigen. (Art 8:24, lid 1 Awb)
authorizedRepresentative :: Party * Authorization [SUR] PRAGMA "" " is gemachtigd op base van "
  EXPLANATION "Een persoon kan zich laten vertegenwoordigen by een authorizedRepresentative; veelal een advocaat. De rechtbank kan van een authorizedRepresentative een schriftelijke authorization verlangen. (Art 8:24, lid 2 Awb)".
-- by~;for |- plaintiff \/ defendant \/ joinedInterestedParty
-- EXPLANATION "Alle authorizationen in een case zijn verleend by iemand die partij is in die case"
for      :: Authorization * Case    PRAGMA "" " betreft "
  EXPLANATION "Een authorization kan een case betreffen".
by        :: Authorization -> Party  PRAGMA "" " is verleend by "
  EXPLANATION "Er is altijd één partij die een gegeven authorization heeft verleend".
-- De rechtbank kan van een authorizedRepresentative een schriftelijke authorization verlangen. (Art 8:24, lid 2 Awb)
-- Daarom biedt dit kennismodel de mogelijkheid om de authorization te koppelen to het betreffende bewijsmiddel.
authorization  :: Document * Authorization PRAGMA "" " bewijst "
  EXPLANATION "De authorization is schriftelijk wanneer er een document bestaat dat de authorization bewijst.".

areaOfLaw :: Case -> AreaOfLaw
  EXPLANATION "Alle zaken hebben een areaOfLaw (bestuursrecht, civiel, straf, familie, enz.)".
caseType :: Case -> CaseType
  EXPLANATION "Alle zaken zijn in te delen naar caseType (eerste toleg, beroep, hoger beroep, enz.)".
cluster :: Case * Cluster PRAGMA "" " behoort tot "
  EXPLANATION "Een total zaken kan worden geclusterd om de inhoudelijke samenhang te bewaken. Vroeger ging daar een elastiekje omheen, zodat ze bijvoorbeeld gezamenlijk kunnen worden ingepland.".
base :: Cluster * Text [TOT].
name :: Cluster -> Text.
ENDPATTERN

PATTERN Sessions
-- Guus Harten: De vastlegging van casesinformatie volgt de locationgebonden organisatie van de bestuursrechtspraak.
--              Data zijn eigendom van de location.
-- dient       :: Case * Session PRAGMA "" " dient ter "
--   EXPLANATION "Een case wordt in een session behandeld.".
-- dient = case~;session
KEY session: I[Session][panel:panel,city:city ,date:scheduled]
session     :: Process -> Session PRAGMA "" " vindt city op "
  EXPLANATION "De process vindt dus city op een session".
case        :: Process -> Case PRAGMA "" " behandelt "
  EXPLANATION "Elke process betreft één case. Er kunnen dus meerdere zaken in één session worden behandeld".
-- Art 6 RO: Tenzij in deze 	wet anders is bepaald, bestto de meervoudige panels uit drie judgelijke ambtenaren met rechtspraak belast, van wie een as voorzitter optreedt. Indien ook anderen dan judgelijke ambtenaren deel uitmaken van een meervoudige panel, treedt een judgelijk ambtenaar met rechtspraak belast op as voorzitter.
judge     :: Session * Party [TOT] PRAGMA "" " heeft " " as judge"
  EXPLANATION "Aan een session worden judges toegekend (Art 6 RO)".
clerk    :: Session -> Party [TOT] PRAGMA "" " heeft " " as clerk"
  EXPLANATION "Op elke session is één clerk towezig".
clerk    :: Court * Party  PRAGMA "" " heeft " " as clerk"
  EXPLANATION "Op elke rechtbank zijn clerks benoemd".
clerk |- location;clerk
  EXPLANATION "De clerk in een case moet benoemd zijn bij de rechtbank waar deze case dient."
scheduled :: Session -> Date PRAGMA "" " is scheduled op "
  EXPLANATION "Een session wordt scheduled op een bepaalde datum.".
occured :: Session * Date [UNI] PRAGMA "" " heeft occured op ".

occured |- scheduled
EXPLANATION "Alle sessionen worden scheduled"

{-
Het bestuur van de rechtbank hoort de sectorvergadering van de sector kanton over het houden van sessionen by de sector kanton in localCitiessessionscityen (Article 47 lid 3b, RO)
De sector kanton verricht zijn taken in de arrondissementsmainCity asmede in de localCitiesvestigingscityen die zijn vermeld in de schedule, bedoeld in artikel 41, tweede lid, en in de localCitiessessionscityen die krachtens artikel 41, tweede lid, zijn togewezen. (Article 47 lid 2 RO)
-}
city      :: Session -> City PRAGMA "" " vindt city in "
  EXPLANATION "De session vindt city in een city waar een court gevestigd is.".
location     :: Session -> Court PRAGMA "" " vindt city bij "
  EXPLANATION "De session vindt city bij een court".
city~;location |- mainCity~ \/ localCities
EXPLANATION "Elke session vindt city in de hoofdvestigingscity van een court of een van de localCitiesvestigingscityen (tekst checken, Article 47 lid 2 RO)"

authorized :: Case * Court
 EXPLANATION "Een case wordt behandeld in een court dat daartoe authorized is.".
case~;session;location |- authorized
  EXPLANATION "An appeal lodged against a decision of an administrative authority of a province or municipality, or a water management board, or a region as referred to in article 21 of the 1993 Police Act, or of a joint body or public body established under the Joint Arrangements Act, falls within the jurisdiction of the district court within whose district the administrative authority has its seat. (art. 8:7 par.1 Awb.)"

-- GEN Rechtbank ISA Court
-- GEN Courtshof ISA Court (TODO: ivm bug nog niet tozetten)
ENDPATTERN

PATTERN Panels
members :: Party * Panel -- Article 48 lid 1 RO
  EXPLANATION "Het bestuur van een rechtbank bepaalt de members van een panel (tekst checken, Article 48 lid 1 RO)"
  = [ ("mr. T.M.A. van Löben Sels", "Amsterdam district court fourth single-judge panel") -- TODO: bug in diacriet!
    ; ("mr. G.M.P. Brouns",         "Roermond district court single-judge panel" )
    ; ("mr. N.M. van Waterschoot", "Amsterdam district court single-judge panel for cases of administrative law") -- "Session RbAms 1094", 
    ; ("mr. J.H.B. van der Meer", "Panel 2 department administrative administration of justice of the Council of State") -- "Session RvS 83", 
    ; ("mr. Ph.Q. van Otterloo-Pannerden", "Panel 2 department administrative administration of justice of the Council of State") -- "Session RvS 84", 
    ; ("mr. H.P. Kijlstra", "Amsterdam district court single-judge panel for cases of administrative law")
    ; ("M. Vtodrager", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("mr. J. Sap", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. M. ter Brugge", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. L.E. Verschoor-Bergsma", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. J. Ebbens", "Utrecht district court single-judge panel for cases of administrative law")
    ; ("mr. B.J. van Ettekoven", "Utrecht district court three-judge panel for cases of administrative law")
    ; ("mr. G.J. van Binsbergen", "Utrecht district court three-judge panel for cases of administrative law")
    ; ("mr. J. Struiksma", "Utrecht district court three-judge panel for cases of administrative law")
    ].
court :: Panel -> Court -- Article 6 lid 1 RO
  EXPLANATION "Voor het behandelen en beslissen van zaken vormt het bestuur van de rechtbank enkelvoudige en meervoudige panels. (Art 50-56 RO, Article 6 lid 1 RO)"
  = [ ("Amsterdam district court fourth single-judge panel", "Amsterdam district court")
    ; ("Roermond district court single-judge panel", "Roermond district court")
    ; ("Amsterdam district court single-judge panel for cases of administrative law", "Amsterdam district court")
    ; ("Panel 2 department administrative administration of justice of the Council of State", "Council of State")
    ; ("Three-judge panel to process judicial disqualifications of Utrecht district court", "Utrecht district court")
    ; ("Utrecht district court three-judge panel for cases of administrative law", "Utrecht district court")
    ; ("Utrecht district court single-judge panel for cases of administrative law", "Utrecht district court")
    ].
sector :: Panel -> Sector -- Article 20 lid 2 RO
  EXPLANATION "Het bestuur van een court bepaalt in welke sector een panel functioneert (tekst checken, Article 20 lid 2 RO)"
  = [ ("Amsterdam district court fourth single-judge panel", "District")
    ; ("Roermond district court single-judge panel", "District")
    ; ("Amsterdam district court single-judge panel for cases of administrative law", "Administrative law")
    ; ("Panel 2 department administrative administration of justice of the Council of State", "Administrative law")
    ; ("Three-judge panel to process judicial disqualifications of Utrecht district court", "Administrative law")
    ; ("Utrecht district court single-judge panel for cases of administrative law", "Administrative law")
    ; ("Utrecht district court three-judge panel for cases of administrative law", "Administrative law")
    ].

panel         :: Session -> Panel PRAGMA "" " from "
  EXPLANATION "Panels houden session".

judge |- panel;members~
EXPLANATION "De judge ter session maakt deel uit from de members from de panel die de session houdt"
 district :: Court -> CourtOfAppeal PRAGMA "" " districteert onder "
  EXPLANATION "Elke rechtbank districteert onder een courtshof."
  = [ ("Alkmaar district court", "Amsterdam court of appeal")
    ; ("Amsterdam district court", "Amsterdam court of appeal")
    ; ("Haarlem district court", "Amsterdam court of appeal")
    ; ("Utrecht district court", "Amsterdam court of appeal")
    ; ("Almelo district court", "Arnhem court of appeal")
    ; ("Arnhem district court", "Arnhem court of appeal")
    ; ("Zutphen district court", "Arnhem court of appeal")
    ; ("Zwolle-Lelystad district court", "Arnhem court of appeal")
    ; ("Dordrecht district court", "'s-Gravenhage court of appeal")
    ; ("'s-Gravenhage district court", "'s-Gravenhage court of appeal")
    ; ("Middelburg district court", "'s-Gravenhage court of appeal")
    ; ("Rotterdam district court", "'s-Gravenhage court of appeal")
    ; ("Breda district court", "'s-Hertogenbosch court of appeal")
    ; ("'s-Hertogenbosch district court", "'s-Hertogenbosch court of appeal")
    ; ("Maastricht district court", "'s-Hertogenbosch court of appeal")
    ; ("Roermond district court", "'s-Hertogenbosch court of appeal")
    ; ("Assen district court", "Leeuwarden court of appeal")
    ; ("Groningen district court", "Leeuwarden court of appeal")
    ; ("Leeuwarden district court", "Leeuwarden court of appeal")
    ; ("Council of State", "Council of State")
    ].
 mainCity :: Court -> City PRAGMA "" " heeft " " as arrondissementsmainCity " -- Art 47 RO
  EXPLANATION "Elk court heeft een hoofdvestigingscity (Art 47 RO)"
  = [ ("Alkmaar district court",          "Alkmaar")
    ; ("Amsterdam district court",        "Amsterdam")
    ; ("Haarlem district court",          "Haarlem")
    ; ("Utrecht district court",          "Utrecht")
    ; ("Almelo district court",           "Almelo")
    ; ("Arnhem district court",           "Arnhem")
    ; ("Zutphen district court",          "Zutphen")
    ; ("Zwolle-Lelystad district court",  "Zwolle")
    ; ("Dordrecht district court",        "Dordrecht")
    ; ("'s-Gravenhage district court",    "Den Haag")
    ; ("Middelburg district court",       "Middelburg")
    ; ("Rotterdam district court",        "Rotterdam")
    ; ("Breda district court",            "Breda")
    ; ("'s-Hertogenbosch district court", "Den Bosch")
    ; ("Maastricht district court",       "Maastricht")
    ; ("Roermond district court",         "Roermond")
    ; ("Assen district court",            "Assen")
    ; ("Groningen district court",        "Groningen")
    ; ("Leeuwarden district court",       "Leeuwarden")
    ; ("Council of State",             "Den Haag")
    ].
 localCities :: City * Court  [UNI] PRAGMA "" " is localCitiesvestigingscity from "
  EXPLANATION "Elk court kan beschikken over localCitiesvestigingscityen. Deze stto vermeld in de Bijlage bij de wet RO, as bedoeld in artikel 41, tweede lid RO"
  = [ ("Den Helder", "Alkmaar district court")
    ; ("Hoorn", "Alkmaar district court")
    ; ("Hilversum", "Amsterdam district court")
    ; ("Ztostad", "Haarlem district court")
    ; ("Amersfoort", "Utrecht district court")
    ; ("Enschede", "Almelo district court")
    ; ("Nijmegen", "Arnhem district court")
    ; ("Tiel", "Arnhem district court")
    ; ("Wageningen", "Arnhem district court")
    ; ("Apelbyn", "Zutphen district court")
    ; ("Groenlo", "Zutphen district court")
    ; ("Harderwijk", "Zutphen district court")
    ; ("Terborg", "Zutphen district court")
    ; ("Deventer", "Zwolle-Lelystad district court")
    ; ("Lelystad", "Zwolle-Lelystad district court")
    ; ("Gorinchem", "Dordrecht district court")
    ; ("Oud-Beijerland", "Dordrecht district court")
    ; ("Alphen to de Rijn", "'s-Gravenhage district court" )
    ; ("Delft", "'s-Gravenhage district court" )
    ; ("Gouda", "'s-Gravenhage district court" )
    ; ("Leiden", "'s-Gravenhage district court" )
    ; ("Schouwen-Duiveland", "Middelburg district court")
    ; ("Terneuzen", "Middelburg district court")
    ; ("Brielle", "Rotterdam district court")
    ; ("Middelharnis", "Rotterdam district court")
    ; ("Schiedam", "Rotterdam district court")
    ; ("Bergen op Zoom", "Breda district court")
    ; ("Tilburg", "Breda district court")
    ; ("Boxmeer", "'s-Hertogenbosch district court")
    ; ("Eindhoven", "'s-Hertogenbosch district court")
    ; ("Helmond", "'s-Hertogenbosch district court")
    ; ("Heerlen", "Maastricht district court")
    ; ("Sittard-Geleen", "Maastricht district court")
    ; ("Venlo", "Roermond district court")
    ; ("Emmen", "Assen district court")
    ; ("Meppel", "Assen district court")
    ; ("Winschoten", "Groningen district court")
    ; ("Heerenveen", "Leeuwarden district court")
    ; ("Sneek", "Leeuwarden district court")
    ].
-- Elke gebeurtenis heeft een "persoonsvorm", zoas "De gemeente Schiedam heeft het bezwaar from Jansen niet-ontfromkelijk verklaard.".
-- Zulke zinnen moeten voldoen to een subjecttype, wat vervolgens gerelateerd is to de wet.
-- (in tobouw)
  lawText :: Article*Text PRAGMA "" " luidt "
  EXPLANATION "Elk artikel heeft een tekst, zoas die letterlijk in de wet staat.".
  act :: Article * Act PRAGMA "" " definieert "
  EXPLANATION "Acten worden uitgevoerd op base from een wetsartikel. Bijvoorbeeld: 'Het vormen from een enkelvoudige panel (een act) op base from artikel 6 RO.'".
  organ :: Article * Organ PRAGMA "" " heeft " " as subject"
  EXPLANATION "Een artikel beschrijft welk organ een act uitvoert. Bijvoorbeeld: 'Het bestuur from een court vormt een enkelvoudige panel (een act) op base from artikel 6 RO.'".
  object :: Act * ObjectType PRAGMA "" " heeft " " as object"
  EXPLANATION "Acten worden uitgevoerd op objecten. Bijvoorbeeld: 'Het registreren from een poststuk (een act) heeft een poststuk as object.'".
  object :: Usecase * ObjectType PRAGMA "" " heeft " " as object"
  EXPLANATION "Usecases worden uitgevoerd op objecten. Bijvoorbeeld: 'Het registreren from een poststuk (een act) heeft een poststuk as object.'".
  verb  :: Article*Verb
  EXPLANATION "Een wetsartikel kan een verb definieren, die bepalend kan zijn voor acten.".
  objectType :: Article*ObjectType
  EXPLANATION "Een wetsartikel kan een objecttype definieren, die lijdend voorwerp kan zijn voor acten.".
  verb :: Usecase * Verb PRAGMA "" " heeft " " as verb"
  EXPLANATION "Een usecase is gebaseerd op een verb. Bijvoorbeeld: 'Het registreren from een poststuk (een act) heeft registreren as verb.'".
  verb :: Act * Verb PRAGMA "" " heeft " " as verb"
  EXPLANATION "Een act is gebaseerd op een verb. Bijvoorbeeld: 'Het registreren from een poststuk (een act) heeft registreren as verb.'".
  subject :: Action -> Party PRAGMA "" " wordt uitgevoerd by "
  EXPLANATION "Actions worden uitgevoerd by personen. Bijvoorbeeld: 'Piet de Vries is een poststuk to het registreren.'".
  object :: Action * Object PRAGMA "" " heeft " " as object"
  EXPLANATION "Actions worden uitgevoerd op objecten. Bijvoorbeeld: 'Piet de Vries heeft poststuk 2009/87832 geregistreerd.'".
  as :: Action * Organ PRAGMA "" " wordt namens " " uitgevoerd"
  EXPLANATION "Voor elke actie is er een juridische entiteit, die in juridische zin de actie uitvoert (los from de persoon). Bijvoorbeeld: 'Piet de Vries heeft namens de rechtbank Utrecht heeft poststuk 2009/87832 geregistreerd.'".
  actionType :: Action -> Act PRAGMA "" " is een instantie from type "
  EXPLANATION "Bijvoorbeeld: 'Piet de Vries is een poststuk to het registreren.' is een actie from het type 'registreren from een poststuk'".
  objectType :: Object * ObjectType PRAGMA "" " is een object from type "
  EXPLANATION "Bijvoorbeeld: poststuk 2009/87832 is een object from type Exploot".
  actsas :: Party * Role PRAGMA "" " actsas "
  EXPLANATION "Bijvoorbeeld: Eida de Vries heeft de rol from beroepschriftbeoordelaar".

actionType;act~;organ |- as
  EXPLANATION "De persoon die een actie uitvoert doet dat as vertegenwoordiger from het organ dat de act uitvoert"

  prio :: Usecase*Moscow [UNI].
  use_case :: Usecase*Act.
  may :: Act*Role PRAGMA "" " may worden uitgevoerd by "
  EXPLANATION "Om een act te mogen uitvoeren, is een rolpermissie benodigd. Bijvoorbeeld: Een secretaresse may binnengekomen poststukken openen.".
ENDPATTERN

PATTERN Berichten -- zou "Bericht" moeten zijn ipv document, maar ISA werkt nog niet naar behoren.
  from        :: Document -> Party        PRAGMA "" " is afkomstig from ".
  to        :: Document *  Party  [TOT] PRAGMA "" " is gericht to ".
  sent  :: Document -> TimeStamp       PRAGMA "" " is sent op ".
  received  :: Document *  TimeStamp [UNI] PRAGMA "" " is received op ".
  propertyOf :: Document -> Text          PRAGMA "" " is afkomstig from ".
  yourProperty  :: Document *  Text          PRAGMA "" " is gericht to ".
ENDPATTERN

POPULATION actsas[Party*Role] CONTAINS
    [ ("de Stichting Katholiek Onderwijs Staphorsteradeel, gevestigd te Staphorst", "Plaintiff")
    ; ("de Staatssecretaris van Onderwijs, Cultuur en Wetenschappen", "Defendant")
    ; ("het dagelijks bestuur van het stadsdeel Zeeburg van de gemeente Amsterdam", "Defendant")
    ; ("de besloten vennootschap Fountainhead Enterprise B.V., gevestigd te Amsterdam", "Joined")
    ; ("Jan met de Vilten Hoed", "Nobody")
    ; ("mr. M.R.A. Dekker", "Representative")
    ; ("drs. D. de Rooij", "Representative")
    ; ("mr. S.M. Klein", "Representative")
    ; ("mr. G.L.M. Teeuwen", "Representative")
    ; ("mr. J.H.A. van der Grinten", "Representative")
    ; ("mr. M.L.M. Lohman", "Representative")
    ; ("Mevr. El Amrani", "Plaintiff")
    ; ("Dhr. Klaas Vreugdenhil", "Defendant")
    ; ("Mw. Annemarie Stegeman", "Local official")
    ; ("mr. F. H. Goossens", "Lawyer")
    ; ("Emilio Garcia", "InterestedParty")
    ; ("Frits Ticherus", "Secretary")
    ; ("mr. K.L.M. Lenaerts", "Judge")
    ; ("mw.mr. Chr.Ph. Tetrode", "Clerk")
    ; ("mr. P. van der Vossen", "Judge")
    ; ("mr. M.M. Mijwaard", "Clerk")
    ; ("mr. V.M. Behrens", "Clerk")
    ; ("mr. J.J. Schuurman", "Clerk")
    ; ("mr. K.F. van Dam", "Clerk")
    ; ("mr. Ch. Dequaistenit", "Clerk")
    ; ("mr. N.M. van Waterschoot", "Judge")
    ; ("mr. J.H.B. van der Meer", "Judge")
    ; ("mr. Ph.Q. van Otterloo-Pannerden", "Judge")
    ]

  SERVICE Letter : I[Document]
   = [ case : caseFile            -- Case
     , type : documentType
     , from : from
     , to : to
     , mark : propertyOf
     , "sent at" : sent
     , "received at" : received
     ]

  SERVICE Document : I[Document]
   = [ type : documentType                            -- DocumentType
     , case : caseFile            -- Casee
        = [ "caretaker of case file" : caretaker,
            "area of law" : areaOfLaw,
            "type of case" : caseType  ]
     ]

  SERVICE Documenten : I[ONE]
   = [ Documenten : V[ONE*Document]
        = [ nr : I[Document]
          , type : documentType
          , "caretaker of case file" : caseFile;caretaker
          , "area of law" : caseFile;areaOfLaw
          , "type of case" : caseFile;caseType
          ]
     ]

  SERVICE DocumentType : I[DocumentType]
   = [ "type Document" : documentType~
        = [ nr : I[Document]
          ]
     ]


  SERVICE Organ : I[Organ]
   = [ "current acts" : organ~;act -- (act~;organ)~
        = [ act : I[Act]
          , prio : use_case~;prio
          , usecase : use_case~
          , rol : may
          ]
     , "actions" : as~
        = [ actie : I[Action]
          , subject : subject
          , type : actionType
          ]
     , "case files" : caretaker~
        = [ case : I[Case]
          , "area of law" : areaOfLaw
          , "type of case" : caseType
          ]
     ]


POPULATION session[Process*Session] CONTAINS
  [ ("199902238/1","Session RvS 83")
  ; ("199902238/2","Session RvS 84")
  ; ("AWB 07/2481 WRO/1","Session RbAms 1094")
  ]
POPULATION case[Process*Case] CONTAINS
  [ ("199902238/1","199902238")
  ; ("199902238/2","199902238")
  ; ("AWB 07/2481 WRO/1","AWB 07/2481 WRO")
  ]

POPULATION organ[Article*Organ] CONTAINS
    [ ("Article 1:3 par. 3 Awb", "authorities")              	
    ; ("Article 4:1 Awb", "authorities")
    ; ("Article 1:5 par. 1 Awb", "citizen")                         		
    ; ("Article 1:5 par. 2 Awb", "citizen")     	 	
    ; ("Article 1:5 par. 3 Awb", "citizen")                     		
    ; ("Article 6:6 Awb", "authorities")        	
    ; ("Article 6:6 Awb; 8:70 par. b Awb", "judge")        	
    ; ("Article 6:6 Awb", "authorities") 	
    ; ("Article 6:6 Awb; 8:70 par. b Awb", "judge") 	
    ; ("Article 8:55 par. 5 sub a Awb", "judge") 	
    ; ("Article 8:70 sub a Awb", "")              		
    ; ("Article 8:70 sub c Awb", "")                 		
    ; ("Article 8:70 sub d Awb", "")                   		
    ; ("Article 8:82 par. 3 Awb", "clerk")
    ; ("Article 8:84 par. 2 sub a Awb", "")   		
    ; ("Article 8:84 par. 2 sub b Awb", "")        		
    ; ("Article 8:84 par. 2 sub c Awb", "")                           		
    ; ("Article 8:84 par. 2 sub d Awb", "")   		
    ; ("Article 8:87 par. 3 Awb", "clerk")
    ; ("Article 48 par. 1 RO", "district authorities")
    ; ("Article 6 par. 1 RO", "district authorities")
    ; ("Article 7:1a par. 6 Awb", "clerk")
    ]


POPULATION plaintiff[Party*Case] CONTAINS
    [ ("de Stichting Katholiek Onderwijs Staphorsteradeel, gevestigd te Staphorst", "199902238")
    ; ("Jan met de Vilten Hoed", "AWB 07/2481 WRO")
    ; ("Mevr. El Amrani", "SBR 02/74331")
    ]
POPULATION authorizedRepresentative[Party*Authorization] CONTAINS
    [ ("mr. M.R.A. Dekker", "letter 2000/864821a")
    ; ("drs. D. de Rooij", "letter 2000/864821a")
    ; ("mr. S.M. Klein", "letter 2000/860338e")
    ; ("mr. G.L.M. Teeuwen", "letter 2007/33-9887")
    ; ("mr. J.H.A. van der Grinten", "letter 2007/33-9854")
    ; ("mr. M.L.M. Lohman", "letter 2007/33-9910")
    ]
POPULATION for[Authorization*Case] CONTAINS
    [ ("letter 2000/864821a", "199902238")
    ; ("letter 2000/860338e", "199902238")
    ; ("letter 2007/33-9887", "AWB 07/2481 WRO")
    ; ("letter 2007/33-9854", "AWB 07/2481 WRO")
    ; ("letter 2007/33-9910", "AWB 07/2481 WRO")
    ]
POPULATION by[Authorization*Party] CONTAINS
    [ ("letter 2000/864821a", "de Stichting Katholiek Onderwijs Staphorsteradeel, gevestigd te Staphorst")
    ; ("letter 2000/860338e", "de Staatssecretaris van Onderwijs, Cultuur en Wetenschappen")
    ; ("letter 2007/33-9887", "Jan met de Vilten Hoed")
    ; ("letter 2007/33-9854", "de besloten vennootschap Fountainhead Enterprise B.V., gevestigd te Amsterdam")
    ; ("letter 2007/33-9910", "het dagelijks bestuur van het stadsdeel Zeeburg van de gemeente Amsterdam")
    ]
POPULATION defendant[Party*Case] CONTAINS
    [ ("de Staatssecretaris van Onderwijs, Cultuur en Wetenschappen", "199902238")
    ; ("het dagelijks bestuur van het stadsdeel Zeeburg van de gemeente Amsterdam", "AWB 07/2481 WRO")
    ; ("Gemeente Utrecht", "SBR 02/74331")
    ]
POPULATION joinedInterestedParty[Party*Case] CONTAINS
    [ ("de besloten vennootschap Fountainhead Enterprise B.V., gevestigd te Amsterdam", "AWB 07/2481 WRO")
    ; ("Dhr. Klaas Vreugdenhil", "SBR 02/74331")
    ]

POPULATION location[Session*Court] CONTAINS
    [ ("Session RbAms 1094", "Amsterdam district court")
    ; ("Session RvS 83", "Council of State")
    ; ("Session RvS 84", "Council of State")
    ; ("SBR 2009/05/02", "Utrecht district court")
    ]
POPULATION scheduled[Session*Date] CONTAINS
    [ ("Session RbAms 1094", "23 januari 2009")
    ; ("Session RvS 83", "15 september 2000")
    ; ("Session RvS 84", "16 november 2000")
    ; ("SBR 2009/05/02", "2 mei 2009")
    ]
POPULATION clerk[Session*Party] CONTAINS
    [ ("Session RbAms 1094", "mr. V.M. Behrens")
    ; ("Session RvS 83", "mr. J.J. Schuurman")
    ; ("Session RvS 84", "mr. J.J. Schuurman")
    ; ("SBR 2009/05/02", "mr. Ch. Dequaistenit")
    ]
POPULATION clerk[Court*Party] CONTAINS
    [ ("Amsterdam district court", "mr. V.M. Behrens")
    ; ("Council of State", "mr. J.J. Schuurman")
    ; ("Utrecht district court", "mr. K.F. van Dam")
    ; ("Utrecht district court", "mr. Ch. Dequaistenit")
    ]
POPULATION judge[Session*Party] CONTAINS
    [ ("Session RbAms 1094", "mr. N.M. van Waterschoot")
    ; ("Session RvS 83", "mr. J.H.B. van der Meer")
    ; ("Session RvS 84", "mr. Ph.Q. van Otterloo-Pannerden")
    ; ("SBR 2009/05/02", "mr. J. Ebbens")
    ]
POPULATION city[Session*City] CONTAINS
    [ ("Session RbAms 1094", "Amsterdam")
    ; ("Session RvS 83", "Den Haag")
    ; ("Session RvS 84", "Den Haag")
    ; ("SBR 2009/05/02", "Utrecht")
    ]
POPULATION panel[Session*Panel] CONTAINS
    [ ("Session RbAms 1094", "Amsterdam district court single-judge panel for cases of administrative law")
    ; ("Session RvS 83", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("Session RvS 84", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("SBR 2009/05/02", "Utrecht district court single-judge panel for cases of administrative law")
    ]

POPULATION areaOfLaw[Case*AreaOfLaw] CONTAINS
    [ ("199902238", "Administrative law")
    ; ("AWB 07/2481 WRO", "Administrative law")
    ; ("SBR 02/74331", "Administrative law")
    ]

POPULATION caseType[Case*CaseType] CONTAINS
    [ ("199902238", "Appeal to a higher court")
    ; ("AWB 07/2481 WRO", "Appeal")
    ; ("SBR 02/74331", "Appeal")
    ]

POPULATION caseFile[Document * Case] CONTAINS
    [ ("doc987384","199902238")
    ; ("doc763820", "AWB 07/2481 WRO")
    ; ("letter 2009/87743", "SBR 02/74331")
    ; ("letter 2009/87743a", "SBR 02/74331")
    ; ("schedule 2009/87743.1", "SBR 02/74331")
--    ; ("betaling1", "SBR 02/74331")
    ]

POPULATION from[Document * Party] CONTAINS
    [ ("doc987384","Council of State")
    ; ("letter 2000/864821a", "de Stichting Katholiek Onderwijs Staphorsteradeel, gevestigd te Staphorst")
    ; ("letter 2000/860338e", "de Staatssecretaris van Onderwijs, Cultuur en Wetenschappen")
    ; ("doc763820", "AWB 07/2481 WRO")
    ; ("letter 2009/87743", "Mevr. El Amrani")
    ; ("letter 2009/87743a", "Utrecht district court")
    ; ("schedule 2009/87743.1", "Gemeente Utrecht")
--    ; ("betaling1", "Mevr. El Amrani")
    ]

POPULATION propertyOf[Document * Text] CONTAINS
    [ ("doc987384","doc987384")
    ; ("letter 2000/864821a", "2000/864821a")
    ; ("letter 2000/860338e", "2000/860338e")
    ; ("doc763820", "doc763820")
    ; ("letter 2009/87743", "Vreugdenhil")
    ; ("letter 2009/87743a", "2009/87743a")
    ; ("schedule 2009/87743.1", "B. en W.-besluit van 27 februari 2009, Gemeenteblad van Utrecht 2009 Nr. 8")
--    ; ("betaling1", "SBR 02/74331")
    ]

POPULATION yourProperty[Document * Text] CONTAINS
    [ --    ; ("betaling1", "159")
    ]

POPULATION sent[Document * TimeStamp] CONTAINS
    [ ("doc987384","10-10-2000")
    ; ("letter 2000/864821a", "2-3-2000")
    ; ("letter 2000/860338e", "5-3-2000")
    ; ("doc763820", "???")
    ; ("letter 2009/87743", "10 april 2009")
    ; ("letter 2009/87743a", "16 april 2009")
    ; ("schedule 2009/87743.1", "27 februari 2009")
    ]

POPULATION received[Document * TimeStamp] CONTAINS
    [ ("letter 2009/87743", "14 april 2009")
    ; ("schedule 2009/87743.1", "14 april 2009")
    ]

POPULATION to[Document * Party] CONTAINS
    [ ("doc987384","de Stichting Katholiek Onderwijs Staphorsteradeel, gevestigd te Staphorst")
    ; ("doc987384","de Staatssecretaris van Onderwijs, Cultuur en Wetenschappen")
    ; ("doc987384","mr. M.R.A. Dekker")
    ; ("doc987384","drs. D. de Rooij")
    ; ("doc987384","mr. S.M. Klein")
    ; ("letter 2000/864821a", "Council of State")
    ; ("letter 2000/860338e", "Council of State")
    ; ("doc763820", "???")
    ; ("letter 2009/87743", "Utrecht district court")
    ; ("letter 2009/87743a", "Mevr. El Amrani")
    ; ("schedule 2009/87743.1", "Dhr. Klaas Vreugdenhil")
--    ; ("betaling1", "Utrecht district court")
    ]

POPULATION documentType[Document * DocumentType] CONTAINS
    [ ("doc987384", "Verdict")
    ; ("doc763820", "Evidence")
    ; ("letter 2000/864821a", "Authorization")
    ; ("letter 2000/860338e", "Authorization")
    ; ("letter 2009/87743", "Correspondence")
    ; ("letter 2009/87743a", "Appeal")
    ; ("schedule 2009/87743.1", "Evidence")
--    ; ("betaling1", "Betaling")
    ]

POPULATION caretaker[Case*Organ] CONTAINS
    [ ("199902238", "Council of State")
    ; ("AWB 07/2481 WRO", "Amsterdam district court")
    ; ("SBR 02/74331", "Utrecht district court")
    ]

POPULATION authorized[Case*Court] CONTAINS
    [ ("199902238", "Council of State")
    ; ("AWB 07/2481 WRO", "Amsterdam district court")
    ; ("SBR 02/74331", "Utrecht district court")
    ]


SERVICE CoreDataUC001 : I[Case] -- UC001 Registreren basisgegevens procedure
{- Het tomaken from een nieuwe procedure middels het registreren from de basisgegevens from een centraal binnengekomen beroepschrift of verzoekschrift om voorlopige voorziening en het automatisch (geen samenhangende procedure togegeven) of handmatig (wel samenhangende procedure togegeven) voorlopig/definitief toewijzen from de nieuwe procedure to een behandelend court.
-}
   = [ plaintiff : plaintiff~;by~/\for~
        = [ party : by
          , representative : authorizedRepresentative~
          ]
     , defendant : defendant~;by~/\for~
        = [ party : by
          , representative : authorizedRepresentative~
          ]
     , "joined party" : joinedInterestedParty~;by~/\for~
        = [ party : by
          , representative : authorizedRepresentative~
          ]
     , "area of law" : areaOfLaw
     , "type of case" : caseType
     , authorized : authorized
     , "caretaker of case file" : caretaker             -- Organ
     , "case file" : caseFile~                     -- [Document]
        = [ Document : I[Document]
          , type : documentType
          ]
     , cluster : cluster
        = [ name  : I[Cluster]
          , base : base ]
     , "authorization documents" :  for~
        = [ document : I[Authorization]
          , represented : by
          , representative : authorizedRepresentative~
          ]
     ]

SERVICE LegalCase : I[Case]
   = [ plaintiff : plaintiff~          -- Party
     , "representative of plaintiff" : (plaintiff~;by~/\for~);authorizedRepresentative~
     , defendant : defendant~          -- Party
     , "representative of defendant" : (defendant~;by~/\for~);authorizedRepresentative~
     , "joined party" : joinedInterestedParty~          -- Party
     , "representative of joined party" : (joinedInterestedParty~;by~/\for~);authorizedRepresentative~
     , "area of law" :  areaOfLaw
     , "type of case" : caseType
     , process : case~
        = [ nr : I[Process]
          , session : session
          , panel : session;panel
          , scheduled : session;scheduled
          , judge : session;judge
          , clerk : session;clerk
          ]
     , authorized : authorized
     , "caretaker of case file" : caretaker           -- Organ
     , "case file" : caseFile~                     -- [Document]
        = [ Document : I[Document]
          , type : documentType
          ]
     , "authorization documents" :  for~
        = [ authorization : I[Authorization]
          , party : by
          , representative : authorizedRepresentative~
          ]
     ]

SERVICE Cluster : I[Cluster]
   = [ name : name
     , base : base
     , "case" : cluster~
         = [ "caretaker of case file": caretaker
           , "area of law" :  areaOfLaw
           , "type of case" : caseType]]

SERVICE Clusters : I[ONE]
   = [ Clusters : V[ONE*Cluster]
         = [ nr : I[Cluster]
           , name : name
           , cases : cluster~
               = [ "caretaker of case file": caretaker
                 , "area of law" :  areaOfLaw
                 , "type of case" : caseType
                 ]
           ]
     ]

  SERVICE Cases : I[ONE]
   = [ Cases : V[ONE*Case]
        = [ nr : I[Case]
          , session : case~;session
          , "area of law" :  areaOfLaw
          , "type of case" : caseType
          , "caretaker voor dossier" : caretaker
          , court : case~;session;location
          , clusters : cluster
             = [ cases : cluster~ ]
          ]
     ]

SERVICE newCase : I[Case]
   = [ "caretaker voor dossier" : caretaker
     , "area of law" :  areaOfLaw
     , "type of case" : caseType]

  SERVICE Session : I[Session]
   = [ court : location
     , panel : panel
     , city : city
     , judge : judge
     , clerk : clerk
     , scheduled : scheduled
     , "date of occurence" : occured
     , cases : (case~;session)~
        = [ nr : I[Case]
          , plaintiff : plaintiff~
          , defendant : defendant~
          ]
     ]

  SERVICE Sessions : I[ONE]
   = [ Sessions : V[ONE*Session]
        = [ nr : I[Session]
          , panel : panel
          , court : location
          , city : city
          , judge : judge
          , clerk : clerk
          , scheduled : scheduled
          , "date of occurence" : occured
          ]
     ]

  SERVICE City : I[City]
   = [ "courts" : localCities
     , district : localCities;district
     , sessions : city~
        = [ Session : I[Session]
          , judge : judge
          , scheduled : scheduled
          , panel : panel
          ]
     , "main city" : mainCity~
     ]

  SERVICE CaseParties : I[ONE]
   = [ Plaintiffs : V[ONE*Party];plaintiff;plaintiff~
     , Defendants : V[ONE*Party];defendant;defendant~
     , "Joined Parties" : V[ONE*Party];joinedInterestedParty;joinedInterestedParty~
     ]

  SERVICE Schedule : I[ONE]
   = [ Process : V[ONE*Process]
        = [ nr : I
          , session : session
          , case : case
          , scheduled : session;scheduled
          ]
     ]

  SERVICE Process : I[Process]
   = [ session : session
        = [ court : location
          , judge : judge
          , clerk : clerk
          , scheduled : scheduled
          , "date of occurence" : occured
          ]
     , case : case
        = [ "area of law" :  areaOfLaw
          , "type of case" : caseType
          , session : case~;session
          ]
     ]

  SERVICE ScheduleProcess : I[Process]
   = [ session : session
     , case : case
     ]

  SERVICE Panels : I[ONE]
   = [ Panels : V[ONE*Panel]
         = [ panel : I
           , court : court
           , sector : sector
           , members : members~
           ]
     ]

  SERVICE Panel : I[Panel]
   = [ court : court
     , sector : sector
     , members : members~
     , sessions : panel~
     ]

  SERVICE Sector : I[Sector]
   = [ "sector Panel" : sector~
        = [ Panel : I[Panel]
          , court : court
          ]
     ]

  SERVICE Court : I[Court]
   = [ Sessions : location~
        = [ nr : I[Session]
          , panel : panel
          , judge : judge
          , clerk : clerk
          , scheduled : scheduled
          , schedule : session~
             = [ nr : I[Process]
               , case : case
               , "type of case" : case;caseType
               ]
          ]
     , panels : court~
     , members : clerk \/ court~;members~
     , cases : authorized~
     , "main city" : mainCity
     , "local cities" : localCities~
     , "o schedule" : authorized~ /\ -(location~;session~;case) -- de rechtbank heeft kennis genomen van de case en de case staat nog niet op de rol...
             = [ casenr   : I[Case]
               , defendant : defendant~
               , plaintiff    : plaintiff~
               , "joined party" : joinedInterestedParty~
               ]
     ]

  SERVICE Article : I[Article]
   = [ text : lawText
     , "act" : act
     , "organ" : organ
     , "verb" : verb
     , "object type" : objectType
     ]

  SERVICE Articles : I[ONE]
   = [ Articles : V[ONE*Article]
        = [ "article" : I[Article]
           , "act" : act
           , "organ" : organ
           , "verb" : verb
           , "object type" : objectType
           ]
     ]


SERVICE Magistrate : I[Party]
   = [ court : clerk~ \/ members;court
     , panel : members
         = [ panel : I[Panel]
           , sector : sector
           ]
     , role : actsas
     , Sessions : judge~\/clerk[Session*Party]~
         = [ judge : judge
           , clerk : clerk[Session*Party]
           , scheduled : scheduled
           , city : city
           , location : location
           , panel : panel
               = [ court : court
                 , sector : sector]]
     , received : from~
         = [ message : I
           , from : from
           , sent : sent -- TimeStamp
           , received : sent] -- TimeStamp
     , sent : to~
         = [ message : I
           , from : from
           , sent : sent]] -- TimeStamp

  SERVICE Parties : I[ONE]
   = [ "Interested parties" : V[ONE*Party];(plaintiff;plaintiff~\/defendant;defendant~\/joinedInterestedParty;joinedInterestedParty~)
        = [ name : I
          , rol : actsas ]
     , "Representatives" : V[ONE*Party];authorizedRepresentative;authorizedRepresentative~
        = [ name : I
          , rol : actsas ]
     , Judges : V[ONE*Party];judge~;judge
        = [ name : I
          , rol : actsas ]
     , Clerks : V[ONE*Party];clerk~;I[Court];clerk
        = [ name : I
          , rol : actsas ]
     ]

  SERVICE InterestedParties : I[ONE]
-- Bug:   = [ InterestedPartyn : V[ONE*Party];(plaintiff\/defendant\/joinedInterestedParty);(plaintiff\/defendant\/joinedInterestedParty)~
   = [ "Interested parties" : V[ONE*Party];(plaintiff;plaintiff~\/defendant;defendant~\/joinedInterestedParty;joinedInterestedParty~)
        = [ name : I
          , role : actsas
          , representative : by~;authorizedRepresentative~
          ]
     ]

  SERVICE Representatives : I[ONE]
   = [ Representatives : V[ONE*Party];authorizedRepresentative;authorizedRepresentative~
        = [ name : I , rol : actsas
          , represents :authorizedRepresentative;by
          , cases : authorizedRepresentative;for
          ]
     ]

  SERVICE Magistrates : I[ONE]
   = [ Judges : V[ONE*Party];members;members~
        = [ name : I
          , court : members;court
          , role : actsas ]
     , Clerks : V[ONE*Party];clerk~;I[Court];clerk
        = [ name : I
          , court : clerk[Court*Party]~
          , role : actsas ]
     ]

  SERVICE Party : I[Party]
   = [ "cases" : plaintiff\/defendant\/joinedInterestedParty
         = [ nr : I[Case]
           , "caretaker of case file" : caretaker
           , "area of law" :  areaOfLaw
           , "type of case" : caseType]
     , role : actsas
     , "authorized" : by~
         = [ representative : authorizedRepresentative~]
     , correspondence : from~ \/ to~
         = [ from : from
           , to : to
           , "sent at" : sent]
     ]

SERVICE InterestedParty : I[Party]
   = [ "cases" : plaintiff\/defendant\/joinedInterestedParty
         = [ nr : I[Case]
           , "caretaker of case file" : caretaker
           , "area of law" :  areaOfLaw
           , "type of case" : caseType]
     , correspondence : from~ \/ to~
         = [ from : from
           , to : to
           , mark : propertyOf
           , "sent at" : sent]
     ]

  SERVICE Authorization : I[Authorization]
   = [ authorization : I[Authorization]
     , for : for
     , by : by
     , representative : authorizedRepresentative~
     , "legal authorization" : authorization~
        = [ Document : I[Document]
          , type : documentType
          ]
     ]

  SERVICE Authorizations : I[ONE]
   = [ Authorizations : V[ONE*Authorization]
        = [ document : I[Authorization]
          , for : for
          , by : by
          , representative : authorizedRepresentative~
          ]
     ]

  SERVICE AreaOfLaw : I[AreaOfLaw]
   = [ "Cases" : areaOfLaw~
        = [ nr : I[Case]
          , "type of case" : caseType
          , "caretaker of case file": caretaker
          ]
     ]

  SERVICE CaseType : I[CaseType]
   = [ "Cases" : caseType~
        = [ nr : I[Case]
          , "area of law" :  areaOfLaw
          , "caretaker of case file": caretaker
          ]
     ]

ENDCONTEXT
