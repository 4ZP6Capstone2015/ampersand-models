--replace: 
--iNTERFACE <-> sERVICE
--rULE <-> {-rULE-}
--pHRASE IN ENGLISH <-> eXPLANATION
--pURPOSE <-> --pURPOSE
CONTEXT VIRO

PURPOSE PATTERN CaseRegistration IN ENGLISH 
{+
The registration of administrative cases is based on three articles from the Dutch administrative law, 'Algemene wet bestuursrecht (Awb)'.
An administrative case is a legal case against a decision of an administrative authority.
Within the 'VIRO' context the terms 'case' and 'legal case' will always refer to an administrative case.

Article 1:1 Awb

1. 'Administrative authority' means:

  a. an organ of a juristic person governed by public law, or

  b. any other person or body vested with public authority.

Article 6:4 Awb

1. An objection is lodged by filing a notice of objection with the administrative authority which took the decision.

2. An administrative appeal is lodged by filing a notice of appeal with an appellate authority.

3. An appeal to an administrative court is lodged by filing a notice of appeal with that court.

Article 8:24 Awb

1. A party may be assisted or represented by an authorized representative.

2. The district court may require written authorization of a representative.

3. Paragraph 2 does not apply to members of the Dutch Bar.

Notices as referred to in art.6:4 Awb are filed in the case file.
Written authorizations as referred to in 8:24 par.3 Awb are filed in the case file.
-}
PURPOSE PATTERN Sessions IN ENGLISH 
{+
When administrative cases are brought before the district court, they are assigned to a scheduled session with a certain panel of judges.
The registration of sessions is based on article 8:10 Awb.

Article 8:10 Awb

1. Cases brought before the district court shall first be considered by a single-judge panel.

2. If the single-judge panel deems a case unsuitable for consideration by a single judge, it will refer it to a three-judge panel. A single-judge panel may also refer a case to a three-judge panel for other reasons.

3. If a three-judge panel thinks that a case is suitable for further consideration by a single judge, it may refer it to a single-judge panel.

4. A case may be referred at any stage of the proceedings. The case shall then be resumed at the point at which it was referred.

The organization of panels is defined in the Dutch law for the administration of justice, 'Wet op de rechterlijke organisatie (RO)'.
For example article 6:1 RO describes that there are single-judge and three-judge panels, of which the members are recognized judges.
Article 41 par.1 RO defines that the district court is seated in the main city of the jurisdiction.
Article 41 par.2 RO allows for local offices of the district court.

For illustration purposes, article 8:7 par.1 Awb will be maintained within the 'VIRO' context.
-}
PURPOSE PATTERN Correspondence IN ENGLISH 
{+
Communication data of documents in case files may be registered. 
-}
PATTERN CaseRegistration
  --parties
  plaintiff      :: Party * LegalCase [SUR]       PRAGMA "party " " is the plaintiff in case ".
  RULE plaintiff;plaintiff~ /\ I |- person \/ organization
    PHRASE IN ENGLISH "The plaintiff in an administrative case is a juristic person"
  defendant      :: Party * LegalCase             PRAGMA "party " " is the defendant in case ".
  RULE defendant;defendant~ /\ I |- administrativeAuthority
    PHRASE IN ENGLISH "The defendant in an administrative case is an administrative authority as referred to in art.1:1 Awb."
  domicile       :: Party * City. --when I make this UNI only the Partys with a domicile are put in the Party table.
  --authorization of representatives
  writtenAuthOf :: Document * Party PRAGMA "document " " is the written authorization of representative ".
  authFor       :: Document * LegalCase     PRAGMA "authorization " " for case ".
  authBy        :: Document * Party   PRAGMA "" " given by ".

  areaOfLaw      :: LegalCase -> AreaOfLaw.
  caseFile     :: Document * LegalCase          PRAGMA "document " " in case file of case ".
  documentType :: Document -> DocumentType PRAGMA "document " " of type ".
  RULE authFor |- -caseFile
    PHRASE IN ENGLISH "Written authorizations for representatives of a case are not put in the case file"

  --types of administrative cases
    caseType       :: LegalCase -> CaseType.
    appeal :: LegalCase * LegalCase[SYM,ASY] = [("AWB 07/2481 WRO","AWB 07/2481 WRO");("SBR 02/74331", "SBR 02/74331")].
    appealToAdminCourt :: LegalCase * LegalCase[SYM,ASY] = [("199902238", "199902238")].
    objection :: LegalCase * LegalCase[SYM,ASY].
    RULE I |- (appeal \/ appealToAdminCourt \/ objection) /\ -(appeal /\ appealToAdminCourt /\ objection)
      PHRASE IN ENGLISH "Every administrative case is either an appeal or an objection or an appeal to an administrative court. (Art.6:4 Awb)"
  --types of parties
    person :: Party * Party[SYM,ASY].
    organization :: Party * Party[SYM,ASY]
      = [("Fountainhead Enterprise B.V., Amsterdam", "Fountainhead Enterprise B.V., Amsterdam")
        ;("the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst", "the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst")]. 
    administrativeAuthority :: Party * Party[SYM,ASY] 
      = [("the council of borough Zeeburg of the municipality of Amsterdam", "the council of borough Zeeburg of the municipality of Amsterdam")
        ;("the council of the municipality of Utrecht", "the council of the municipality of Utrecht")
        ;("the Secretary of State for Education, Culture and Science", "the Secretary of State for Education, Culture and Science")].
    RULE I |- (person \/ organization \/ administrativeAuthority) /\ -(person /\ organization /\ administrativeAuthority)
      PHRASE IN ENGLISH "Every party is either a person or an organization or an administrative authority."
    memberOfGovernment :: Party * Party[SYM,ASY]
      = [("the Secretary of State for Education, Culture and Science", "the Secretary of State for Education, Culture and Science")].
    RULE memberOfGovernment |- administrativeAuthority
      PHRASE IN ENGLISH "Members of the government, i.e., Ministers and Secretaries of State, are administrative authorities according to the constitution."
ENDPATTERN

PROCESS CaseFiles
RULE "file documents":  I |- caseFile;caseFile~ \/ authFor;authFor~
    PHRASE IN ENGLISH "Documents are put in a case file except for written authorizations for representatives."
ROLE Archivist MAINTAINS "file documents"
ENDPROCESS

PATTERN Sessions
  --processes
  session :: Process -> Session     PRAGMA "process " " is held in session ".
  legalCase    :: Process -> LegalCase        PRAGMA "process " " treats case ".
  --sessions
  --KEY session: Session(panel:panel,city:location ,date:scheduled)
  RULE panel;panel~ /\ location;location~ /\ scheduled;scheduled~ |- I
    PHRASE IN ENGLISH "a session can be identified by its panel, its city and its date."
 
  panel   :: Session -> Panel PRAGMA "session " " is run by panel ".
  court   :: Panel -> Court. 
  members :: Party * Panel.
  --magistrates
  judge   :: Session * Party [TOT]  PRAGMA "session " " with " " as judge".
  clerk   :: Session -> Party       PRAGMA "session " " with " " as clerk".
  clerk   :: Court * Party          PRAGMA "" " has " " as clerk".
  actsas  :: Party -> Role           PRAGMA "" " actsas ".

  RULE judge |- panel;members~
    PHRASE IN ENGLISH "A judge at a session is a member of the panel that runs the session."
  RULE clerk |- location;clerk
    PHRASE IN ENGLISH "The clerk of a session must be the clerk of the court where the session is held."
  --schedule
  broughtBefore :: LegalCase * Court.
  scheduled :: Session -> Date      PRAGMA "" " is scheduled at ".
  occured   :: Session * Date [UNI] PRAGMA "" " has occured at ".
  RULE occured |- scheduled
    PHRASE IN ENGLISH "All sessions are scheduled"

  administrativeAuthorityAwb87 :: Party * Party[SYM,ASY]
    = [("the council of borough Zeeburg of the municipality of Amsterdam", "the council of borough Zeeburg of the municipality of Amsterdam")
      ;("the council of the municipality of Utrecht", "the council of the municipality of Utrecht")].
  RULE administrativeAuthorityAwb87 |- administrativeAuthority
    PHRASE IN ENGLISH "Administrative authorities as referred to in art.8:7 par.1 Awb are administrative authorities as referred to in art.1:1 Awb."
  RULE appeal;defendant~;administrativeAuthorityAwb87;domicile;jurisdiction |- broughtBefore
    PHRASE IN ENGLISH "An appeal lodged against a decision of an administrative authority of a province or municipality, or a water management board, or a region as referred to in article 21 of the 1993 Police Act, or of a joint body or public body established under the Joint Arrangements Act, falls within the jurisdiction of the district court within whose district the administrative authority has its seat. (art. 8:7 par.1 Awb.)"

  seatedIn :: Court -> City.
  seatedIn :: CourtOfAppeal -> City.
  location  :: Session -> Court PRAGMA "session " " at ".
  district    :: Court -> CourtOfAppeal PRAGMA "" " is served by ".
  localOffices :: City * Court  [UNI]    PRAGMA "" " has a local office of ".
  jurisdiction:: City -> Court.
ENDPATTERN

PROCESS Scheduling
RULE "assign cases":  I |- legalCase~;legalCase
    PHRASE IN ENGLISH "Cases must be assigned to a process in a scheduled session."
ROLE Scheduler MAINTAINS "assign cases"
ENDPROCESS

PATTERN Correspondence
  sent     :: Document * TimeStamp [UNI]  PRAGMA "" " is sent at ".
  received :: Document *  TimeStamp [UNI] PRAGMA "" " is received at ".
  remark   :: Document * Text             PRAGMA "document " " with remark ".
ENDPATTERN
INTERFACE Correspondence : I[Document]
 = [ case : caseFile            -- Case
   , type : documentType
   , remark: remark
   , "sent at" : sent
   , "received at" : received
   ]

INTERFACE Document : I[Document]
 = [ type : documentType                            -- DocumentType
   , case : caseFile            -- Casee
      = [ "area of law" : areaOfLaw,
          "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType  ]
   ]

INTERFACE Documents : I[ONE]
 = [ Documents : V[ONE*Document];(-(writtenAuthOf;writtenAuthOf~) /\ I[Document])
      = [ nr : I[Document]
        , type : documentType
        , "area of law" : caseFile;areaOfLaw
        , "type of case" : caseFile;(appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType)
        ]
   ]

INTERFACE DocumentType : I[DocumentType]
 = [ "type Document" : documentType~
      = [ nr : I[Document]
        ]
   ]

INTERFACE LegalCase : I[LegalCase]
   = [ plaintiff : plaintiff~          -- Party
     , "representative of plaintiff" : (plaintiff~;authBy~/\authFor~);writtenAuthOf
     , defendant : defendant~          -- Party
     , "representative of defendant" : (defendant~;authBy~/\authFor~);writtenAuthOf
     , "area of law" :  areaOfLaw
     , "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType
     , process : legalCase~
        = [ nr : I[Process]
          , session : session
          , panel : session;panel
          , scheduled : session;scheduled
          , judge : session;judge
          , clerk : session;clerk
          ]
     , court : broughtBefore
     , "case file" : caseFile~                     -- [Document]
        = [ Document : I[Document]
          , type : documentType
          ]
     , "authorization documents" :  authFor~
        = [ authorization : I[Document]
          , party : authBy
          , representative : writtenAuthOf
          ]
     ]

INTERFACE Cases : I[ONE]
 = [ Cases : V[ONE*LegalCase]
      = [ nr : I[LegalCase]
        , session : legalCase~;session
        , "area of law" :  areaOfLaw
        , "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType
        , court : broughtBefore
        ]
   ]

INTERFACE Session : I[Session]
 = [ court : location
   , panel : panel
   , city : location;seatedIn
   , judge : judge
   , clerk : clerk
   , scheduled : scheduled
   , "date of occurence" : occured
   , cases : (legalCase~;session)~
      = [ nr : I[LegalCase]
        , plaintiff : plaintiff~
        , defendant : defendant~
        ]
   ]

INTERFACE Sessions : I[ONE]
 = [ Sessions : V[ONE*Session]
      = [ nr : I[Session]
        , panel : panel
        , court : location
        , city : location;seatedIn
        , judge : judge
        , clerk : clerk
        , scheduled : scheduled
        , "date of occurence" : occured
        ]
   ]

INTERFACE City : I[City]
 = [ "court office" : seatedIn[Court*City]~
   , "jurisdiction" : jurisdiction
   , "court of appeal" : jurisdiction;district
   , sessions : (location;seatedIn)~
      = [ Session : I[Session]
        , judge : judge
        , scheduled : scheduled
        , panel : panel
        ]
   ]

INTERFACE CaseParties : I[ONE]
 = [ Plaintiffs : V[ONE*Party];plaintiff;plaintiff~
   , Defendants : V[ONE*Party];defendant;defendant~
   ]

INTERFACE Schedule : I[ONE]
 = [ Process : V[ONE*Process]
      = [ nr : I
        , session : session
        , case : legalCase
        , scheduled : session;scheduled
        ]
   ]

INTERFACE Process : I[Process]
 = [ session : session
      = [ court : location
        , judge : judge
        , clerk : clerk
        , scheduled : scheduled
        , "date of occurence" : occured
        ]
   , case : legalCase
      = [ "area of law" :  areaOfLaw
        , "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType
        , session : legalCase~;session
        ]
   ]

INTERFACE ScheduleProcess : I[Process]
 = [ session : session
   , case : legalCase
   ]

INTERFACE Panels : I[ONE]
 = [ Panels : V[ONE*Panel]
       = [ panel : I
         , court : court
         , members : members~
         ]
   ]

INTERFACE Panel : I[Panel]
 = [ court : court
   , members : members~
   , sessions : panel~
   ]

INTERFACE Court : I[Court]
 = [ Sessions : location~
      = [ nr : I[Session]
        , panel : panel
        , judge : judge
        , clerk : clerk
        , scheduled : scheduled
        , schedule : session~
           = [ nr : I[Process]
             , case : legalCase
             , "type of case" : legalCase;(appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType)
             ]
        ]
   , panels : court~
   , members : clerk \/ court~;members~
   , cases : broughtBefore~
   , "main office" : seatedIn
   , "local offices" : localOffices~
   , "court of appeal" : district
   , "schedule" : broughtBefore~ /\ -(location~;session~;legalCase) 
           = [ casenr   : I[LegalCase]
             , defendant : defendant~
             , plaintiff    : plaintiff~
             ]
   ]

INTERFACE Magistrate : I[Party]
   = [ court : clerk~ \/ members;court
     , panel : members
     , role : actsas
     , Sessions : judge~\/clerk[Session*Party]~
         = [ judge : judge
           , clerk : clerk[Session*Party]
           , scheduled : scheduled
           , city : location;seatedIn
           , location : location
           , panel : panel
               = [ court : court]]
      ]

INTERFACE Parties : I[ONE]
 = [ "Parties" : V[ONE*Party];(plaintiff;plaintiff~\/defendant;defendant~)
      = [ name : I
        , role : actsas ]
   , "Representatives" : V[ONE*Party];writtenAuthOf~;writtenAuthOf
      = [ name : I
        , role : actsas ]
   , Judges : V[ONE*Party];judge~;judge
      = [ name : I
        , role : actsas ]
   , Clerks : V[ONE*Party];clerk~;I[Court];clerk
      = [ name : I
        , role : actsas ]
   ]

INTERFACE InterestedParties : I[ONE]
 = [ "Interested parties" : V[ONE*Party];(plaintiff;plaintiff~\/defendant;defendant~)
      = [ name : I
        , role : actsas
        , representative : authBy~;writtenAuthOf
        ]
   ]

INTERFACE Representatives : I[ONE]
 = [ Representatives : V[ONE*Party];writtenAuthOf~;writtenAuthOf
      = [ name : I , rol : actsas
        , represents :writtenAuthOf~;authBy
        , cases : writtenAuthOf~;authFor
        ]
   ]

INTERFACE Magistrates : I[ONE]
 = [ Judges : V[ONE*Party];members;members~
      = [ name : I
        , court : members;court
        , role : actsas ]
   , Clerks : V[ONE*Party];clerk~;I[Court];clerk
      = [ name : I
        , court : clerk[Court*Party]~
        , role : actsas ]
   ]

INTERFACE Party : I[Party]
 = [ "cases" : plaintiff\/defendant
       = [ nr : I[LegalCase]
         , "area of law" :  areaOfLaw
         , "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType]
   , role : actsas
   , "authorized" : authBy~
       = [ representative : writtenAuthOf]
   ]

INTERFACE Authorizations : I[ONE]
 = [ Authorizations : V[ONE*Document];writtenAuthOf;writtenAuthOf~
      = [ document : I[Document]
        , for : authFor
        , by : authBy
        , representative : writtenAuthOf
        ]
   ]

INTERFACE AreaOfLaw : I[AreaOfLaw]
 = [ "Cases" : areaOfLaw~
      = [ nr : I[LegalCase]
        , "type of case" : appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType
        ]
   ]

INTERFACE CaseType : I[CaseType]
 = [ "Cases" : (appeal;caseType \/ appealToAdminCourt;caseType \/ objection;caseType)~
      = [ nr : I[LegalCase]
        , "area of law" :  areaOfLaw
        ]
   ]

POPULATION members[Party * Panel] CONTAINS 
    [ ("mr. T.M.A. van Loben Sels", "Amsterdam district court fourth single-judge panel") 
    ; ("mr. G.M.P. Brouns",         "Roermond district court single-judge panel" )
    ; ("mr. N.M. van Waterschoot", "Amsterdam district court single-judge panel for cases of administrative law") 
    ; ("mr. J.H.B. van der Meer", "Panel 2 department administrative administration of justice of the Council of State") 
    ; ("mr. Ph.Q. van Otterloo-Pannerden", "Panel 2 department administrative administration of justice of the Council of State") 
    ; ("mr. H.P. Kijlstra", "Amsterdam district court single-judge panel for cases of administrative law")
    ; ("mr. M. Vtodrager", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("mr. J. Sap", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. M. ter Brugge", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. L.E. Verschoor-Bergsma", "Three-judge panel to process judicial disqualifications of Utrecht district court")
    ; ("mr. J. Ebbens", "Utrecht district court single-judge panel for cases of administrative law")
    ; ("mr. B.J. van Ettekoven", "Utrecht district court three-judge panel for cases of administrative law")
    ; ("mr. G.J. van Binsbergen", "Utrecht district court three-judge panel for cases of administrative law")
    ; ("mr. J. Struiksma", "Utrecht district court three-judge panel for cases of administrative law")
    ]
POPULATION court[Panel * Court] CONTAINS 
    [ ("Amsterdam district court fourth single-judge panel", "Amsterdam district court")
    ; ("Roermond district court single-judge panel", "Roermond district court")
    ; ("Amsterdam district court single-judge panel for cases of administrative law", "Amsterdam district court")
    ; ("Panel 2 department administrative administration of justice of the Council of State", "Council of State")
    ; ("Three-judge panel to process judicial disqualifications of Utrecht district court", "Utrecht district court")
    ; ("Utrecht district court three-judge panel for cases of administrative law", "Utrecht district court")
    ; ("Utrecht district court single-judge panel for cases of administrative law", "Utrecht district court")
    ]
POPULATION district[Court * CourtOfAppeal] CONTAINS
    [ ("Alkmaar district court", "Amsterdam court of appeal")
    ; ("Amsterdam district court", "Amsterdam court of appeal")
    ; ("Haarlem district court", "Amsterdam court of appeal")
    ; ("Utrecht district court", "Amsterdam court of appeal")
    ; ("Almelo district court", "Arnhem court of appeal")
    ; ("Arnhem district court", "Arnhem court of appeal")
    ; ("Zutphen district court", "Arnhem court of appeal")
    ; ("Zwolle-Lelystad district court", "Arnhem court of appeal")
    ; ("Dordrecht district court", "'s-Gravenhage court of appeal")
    ; ("'s-Gravenhage district court", "'s-Gravenhage court of appeal")
    ; ("Middelburg district court", "'s-Gravenhage court of appeal")
    ; ("Rotterdam district court", "'s-Gravenhage court of appeal")
    ; ("Breda district court", "'s-Hertogenbosch court of appeal")
    ; ("'s-Hertogenbosch district court", "'s-Hertogenbosch court of appeal")
    ; ("Maastricht district court", "'s-Hertogenbosch court of appeal")
    ; ("Roermond district court", "'s-Hertogenbosch court of appeal")
    ; ("Assen district court", "Leeuwarden court of appeal")
    ; ("Groningen district court", "Leeuwarden court of appeal")
    ; ("Leeuwarden district court", "Leeuwarden court of appeal")
    ; ("Council of State", "Council of State")
    ]
POPULATION localOffices[City * Court]  CONTAINS
    [ ("Den Helder", "Alkmaar district court")
    ; ("Hoorn", "Alkmaar district court")
    ; ("Hilversum", "Amsterdam district court")
    ; ("Zaanstad", "Haarlem district court")
    ; ("Amersfoort", "Utrecht district court")
    ; ("Enschede", "Almelo district court")
    ; ("Nijmegen", "Arnhem district court")
    ; ("Tiel", "Arnhem district court")
    ; ("Wageningen", "Arnhem district court")
    ; ("Apeldoorn", "Zutphen district court")
    ; ("Groenlo", "Zutphen district court")
    ; ("Harderwijk", "Zutphen district court")
    ; ("Terborg", "Zutphen district court")
    ; ("Deventer", "Zwolle-Lelystad district court")
    ; ("Lelystad", "Zwolle-Lelystad district court")
    ; ("Gorinchem", "Dordrecht district court")
    ; ("Oud-Beijerland", "Dordrecht district court")
    ; ("Alphen to de Rijn", "'s-Gravenhage district court" )
    ; ("Delft", "'s-Gravenhage district court" )
    ; ("Gouda", "'s-Gravenhage district court" )
    ; ("Leiden", "'s-Gravenhage district court" )
    ; ("Schouwen-Duiveland", "Middelburg district court")
    ; ("Terneuzen", "Middelburg district court")
    ; ("Brielle", "Rotterdam district court")
    ; ("Middelharnis", "Rotterdam district court")
    ; ("Schiedam", "Rotterdam district court")
    ; ("Bergen op Zoom", "Breda district court")
    ; ("Tilburg", "Breda district court")
    ; ("Boxmeer", "'s-Hertogenbosch district court")
    ; ("Eindhoven", "'s-Hertogenbosch district court")
    ; ("Helmond", "'s-Hertogenbosch district court")
    ; ("Heerlen", "Maastricht district court")
    ; ("Sittard-Geleen", "Maastricht district court")
    ; ("Venlo", "Roermond district court")
    ; ("Emmen", "Assen district court")
    ; ("Meppel", "Assen district court")
    ; ("Winschoten", "Groningen district court")
    ; ("Heerenveen", "Leeuwarden district court")
    ; ("Sneek", "Leeuwarden district court")
    ]
POPULATION actsas[Party*Role] CONTAINS
    [ ("the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst", "plaintiff")
    ; ("the Secretary of State for Education, Culture and Science", "defendant")
    ; ("the council of borough Zeeburg of the municipality of Amsterdam", "defendant")
    ; ("Fountainhead Enterprise B.V., Amsterdam", "joined")
    ; ("the council of the municipality of Utrecht","defendant")
    ; ("dhr. J. de Vries", "plaintiff")
    ; ("mr. M.R.A. Dekker", "representative")
    ; ("drs. D. de Rooij", "representative")
    ; ("mr. S.M. Klein", "representative")
    ; ("mr. G.L.M. Teeuwen", "representative")
    ; ("mr. J.H.A. van der Grinten", "representative")
    ; ("mr. M.L.M. Lohman", "representative")
    ; ("mw. El Amrani", "plaintiff")
    ; ("dhr. Klaas Vreugdenhil", "defendant")
    ; ("mr. F. H. Goossens", "representative")
    ; ("mw. E. Garcia", "InterestedParty")
    ; ("mr. K.L.M. Lenaerts", "judge")
    ; ("mw.mr. Chr.Ph. Tetrode", "clerk")
    ; ("mr. P. van der Vossen", "judge")
    ; ("mr. M.M. Mijwaard", "clerk")
    ; ("mr. V.M. Behrens", "clerk")
    ; ("mr. J.J. Schuurman", "clerk")
    ; ("mr. K.F. van Dam", "clerk")
    ; ("mr. Ch. Dequaistenit", "clerk")
    ; ("mr. N.M. van Waterschoot", "judge")
    ; ("mr. J.H.B. van der Meer", "judge")
    ; ("mr. Ph.Q. van Otterloo-Pannerden", "judge")
    ; ("mr. T.M.A. van Loben Sels", "judge")
    ; ("mr. G.M.P. Brouns", "judge")
    ; ("mr. H.P. Kijlstra", "judge")
    ; ("mr. M. Vtodrager", "judge")
    ; ("mr. J. Sap", "judge")
    ; ("mr. M. ter Brugge", "judge")
    ; ("mr. L.E. Verschoor-Bergsma", "judge")
    ; ("mr. J. Ebbens", "judge")
    ; ("mr. B.J. van Ettekoven", "judge")
    ; ("mr. G.J. van Binsbergen", "judge")
    ; ("mr. J. Struiksma", "judge")
    ]
POPULATION session[Process*Session] CONTAINS
  [ ("199902238/1","Session RvS 83")
  ; ("199902238/2","Session RvS 84")
  ; ("AWB 07/2481 WRO/1","Session RbAms 1094")
  ]
POPULATION legalCase[Process*LegalCase] CONTAINS
  [ ("199902238/1","199902238")
  ; ("199902238/2","199902238")
  ; ("AWB 07/2481 WRO/1","AWB 07/2481 WRO")
  ]
POPULATION domicile [Party * City] CONTAINS
    [ ("the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst", "Staphorst")
    ; ("the council of borough Zeeburg of the municipality of Amsterdam", "Amsterdam")
    ; ("Fountainhead Enterprise B.V., Amsterdam", "Amsterdam")
    ; ("the council of the municipality of Utrecht", "Utrecht")
    ]
POPULATION seatedIn [Court * City] CONTAINS
    [ ("Council of State",                "Den Haag")
    ; ("Utrecht district court",          "Utrecht")
    ; ("Alkmaar district court",          "Alkmaar")
    ; ("Amsterdam district court",        "Amsterdam")
    ; ("Haarlem district court",          "Haarlem")
    ; ("Almelo district court",           "Almelo")
    ; ("Arnhem district court",           "Arnhem")
    ; ("Zutphen district court",          "Zutphen")
    ; ("Zwolle-Lelystad district court",  "Zwolle")
    ; ("Dordrecht district court",        "Dordrecht")
    ; ("'s-Gravenhage district court",    "Den Haag")
    ; ("Middelburg district court",       "Middelburg")
    ; ("Rotterdam district court",        "Rotterdam")
    ; ("Breda district court",            "Breda")
    ; ("'s-Hertogenbosch district court", "Den Bosch")
    ; ("Maastricht district court",       "Maastricht")
    ; ("Roermond district court",         "Roermond")
    ; ("Assen district court",            "Assen")
    ; ("Groningen district court",        "Groningen")
    ; ("Leeuwarden district court",       "Leeuwarden")
    ]
POPULATION seatedIn [CourtOfAppeal * City] CONTAINS
    [ ("Council of State",                "Den Haag")
    ; ("Amsterdam court of appeal",       "Amsterdam")
    ; ("Arnhem court of appeal",          "Arnhem")
    ; ("'s-Gravenhage court of appeal",   "Den Haag")
    ; ("'s-Hertogenbosch court of appeal","Den Bosch")
    ; ("Leeuwarden court of appeal",      "Leeuwarden")
    ]
POPULATION jurisdiction [City * Court] CONTAINS
    [ ("Utrecht", "Utrecht district court")
    ; ("Staphorst", "Zwolle-Lelystad district court")
    ; ("Amsterdam", "Amsterdam district court")
    ; ("Den Haag","'s-Gravenhage district court")
    ; ("Alkmaar","Alkmaar district court")
    ; ("Haarlem","Haarlem district court")
    ; ("Almelo","Almelo district court")
    ; ("Arnhem","Arnhem district court")
    ; ("Zutphen","Zutphen district court")
    ; ("Zwolle","Zwolle-Lelystad district court")
    ; ("Dordrecht","Dordrecht district court")
    ; ("Middelburg","Middelburg district court")
    ; ("Rotterdam","Rotterdam district court")
    ; ("Breda","Breda district court")
    ; ("Den Bosch","'s-Hertogenbosch district court")
    ; ("Maastricht","Maastricht district court")
    ; ("Roermond","Roermond district court")
    ; ("Assen","Assen district court")
    ; ("Groningen","Groningen district court")
    ; ("Leeuwarden","Leeuwarden district court")
    ; ("Den Helder", "Alkmaar district court")
    ; ("Hoorn", "Alkmaar district court")
    ; ("Hilversum", "Amsterdam district court")
    ; ("Zaanstad", "Haarlem district court")
    ; ("Amersfoort", "Utrecht district court")
    ; ("Enschede", "Almelo district court")
    ; ("Nijmegen", "Arnhem district court")
    ; ("Tiel", "Arnhem district court")
    ; ("Wageningen", "Arnhem district court")
    ; ("Apeldoorn", "Zutphen district court")
    ; ("Groenlo", "Zutphen district court")
    ; ("Harderwijk", "Zutphen district court")
    ; ("Terborg", "Zutphen district court")
    ; ("Deventer", "Zwolle-Lelystad district court")
    ; ("Lelystad", "Zwolle-Lelystad district court")
    ; ("Gorinchem", "Dordrecht district court")
    ; ("Oud-Beijerland", "Dordrecht district court")
    ; ("Alphen to de Rijn", "'s-Gravenhage district court" )
    ; ("Delft", "'s-Gravenhage district court" )
    ; ("Gouda", "'s-Gravenhage district court" )
    ; ("Leiden", "'s-Gravenhage district court" )
    ; ("Schouwen-Duiveland", "Middelburg district court")
    ; ("Terneuzen", "Middelburg district court")
    ; ("Brielle", "Rotterdam district court")
    ; ("Middelharnis", "Rotterdam district court")
    ; ("Schiedam", "Rotterdam district court")
    ; ("Bergen op Zoom", "Breda district court")
    ; ("Tilburg", "Breda district court")
    ; ("Boxmeer", "'s-Hertogenbosch district court")
    ; ("Eindhoven", "'s-Hertogenbosch district court")
    ; ("Helmond", "'s-Hertogenbosch district court")
    ; ("Heerlen", "Maastricht district court")
    ; ("Sittard-Geleen", "Maastricht district court")
    ; ("Venlo", "Roermond district court")
    ; ("Emmen", "Assen district court")
    ; ("Meppel", "Assen district court")
    ; ("Winschoten", "Groningen district court")
    ; ("Heerenveen", "Leeuwarden district court")
    ; ("Sneek", "Leeuwarden district court")
    ]
    
POPULATION person[Party*Party] CONTAINS
    [ ("dhr. J. de Vries", "dhr. J. de Vries")
    ; ("mr. M.R.A. Dekker", "mr. M.R.A. Dekker")
    ; ("drs. D. de Rooij", "drs. D. de Rooij")
    ; ("mr. S.M. Klein", "mr. S.M. Klein")
    ; ("mr. G.L.M. Teeuwen", "mr. G.L.M. Teeuwen")
    ; ("mr. J.H.A. van der Grinten", "mr. J.H.A. van der Grinten")
    ; ("mr. M.L.M. Lohman", "mr. M.L.M. Lohman")
    ; ("mw. El Amrani", "mw. El Amrani")
    ; ("dhr. Klaas Vreugdenhil", "dhr. Klaas Vreugdenhil")
    ; ("mr. F. H. Goossens", "mr. F. H. Goossens")
    ; ("mw. E. Garcia", "mw. E. Garcia")
    ; ("mr. K.L.M. Lenaerts", "mr. K.L.M. Lenaerts")
    ; ("mw.mr. Chr.Ph. Tetrode", "mw.mr. Chr.Ph. Tetrode")
    ; ("mr. P. van der Vossen", "mr. P. van der Vossen")
    ; ("mr. M.M. Mijwaard", "mr. M.M. Mijwaard")
    ; ("mr. V.M. Behrens", "mr. V.M. Behrens")
    ; ("mr. J.J. Schuurman", "mr. J.J. Schuurman")
    ; ("mr. K.F. van Dam", "mr. K.F. van Dam")
    ; ("mr. Ch. Dequaistenit", "mr. Ch. Dequaistenit")
    ; ("mr. N.M. van Waterschoot", "mr. N.M. van Waterschoot")
    ; ("mr. J.H.B. van der Meer", "mr. J.H.B. van der Meer")
    ; ("mr. Ph.Q. van Otterloo-Pannerden", "mr. Ph.Q. van Otterloo-Pannerden")
    ; ("mr. T.M.A. van Loben Sels", "mr. T.M.A. van Loben Sels")
    ; ("mr. G.M.P. Brouns", "mr. G.M.P. Brouns")
    ; ("mr. H.P. Kijlstra", "mr. H.P. Kijlstra")
    ; ("mr. M. Vtodrager", "mr. M. Vtodrager")
    ; ("mr. J. Sap", "mr. J. Sap")
    ; ("mr. M. ter Brugge", "mr. M. ter Brugge")
    ; ("mr. L.E. Verschoor-Bergsma", "mr. L.E. Verschoor-Bergsma")
    ; ("mr. J. Ebbens", "mr. J. Ebbens")
    ; ("mr. B.J. van Ettekoven", "mr. B.J. van Ettekoven")
    ; ("mr. G.J. van Binsbergen", "mr. G.J. van Binsbergen")
    ; ("mr. J. Struiksma", "mr. J. Struiksma")
    ]
POPULATION plaintiff[Party*LegalCase] CONTAINS
    [ ("the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst", "199902238")
    ; ("dhr. J. de Vries", "AWB 07/2481 WRO")
    ; ("mw. El Amrani", "SBR 02/74331")
    ]
POPULATION writtenAuthOf[Document*Party] CONTAINS
    [ ("letter 2000/864821a","mr. M.R.A. Dekker")
    ; ("letter 2000/864821a","drs. D. de Rooij")
    ; ("letter 2000/860338e","mr. S.M. Klein")
    ; ("letter 2007/33-9887","mr. G.L.M. Teeuwen")
    ; ("letter 2007/33-9910","mr. M.L.M. Lohman")
    ]
POPULATION authFor[Document*LegalCase] CONTAINS
    [ ("letter 2000/864821a", "199902238")
    ; ("letter 2000/860338e", "199902238")
    ; ("letter 2007/33-9887", "AWB 07/2481 WRO")
    ; ("letter 2007/33-9910", "AWB 07/2481 WRO")
    ]
POPULATION authBy[Document*Party] CONTAINS
    [ ("letter 2000/864821a", "the Stichting Katholiek Onderwijs Staphorsteradeel, Staphorst")
    ; ("letter 2000/860338e", "the Secretary of State for Education, Culture and Science")
    ; ("letter 2007/33-9887", "dhr. J. de Vries")
    ; ("letter 2007/33-9910", "the council of borough Zeeburg of the municipality of Amsterdam")
    ]
POPULATION defendant[Party*LegalCase] CONTAINS
    [ ("the Secretary of State for Education, Culture and Science", "199902238")
    ; ("the council of borough Zeeburg of the municipality of Amsterdam", "AWB 07/2481 WRO")
    ; ("the council of the municipality of Utrecht", "SBR 02/74331")
    ]
POPULATION location[Session*Court] CONTAINS
    [ ("Session RbAms 1094", "Amsterdam district court")
    ; ("Session RvS 83", "Council of State")
    ; ("Session RvS 84", "Council of State")
    ; ("SBR 2009/05/02", "Utrecht district court")
    ]
POPULATION scheduled[Session*Date] CONTAINS
    [ ("Session RbAms 1094", "23-4-2009")
    ; ("Session RvS 83", "15-9-2000")
    ; ("Session RvS 84", "16-11-2000")
    ; ("SBR 2009/05/02", "2-5-2009")
    ]
POPULATION occured[Session*Date] CONTAINS
    [ ("Session RvS 83", "15-9-2000")
    ; ("Session RvS 84", "16-11-2000")
    ]
POPULATION clerk[Session*Party] CONTAINS
    [ ("Session RbAms 1094", "mr. V.M. Behrens")
    ; ("Session RvS 83", "mr. J.J. Schuurman")
    ; ("Session RvS 84", "mr. J.J. Schuurman")
    ; ("SBR 2009/05/02", "mr. Ch. Dequaistenit")
    ]
POPULATION clerk[Court*Party] CONTAINS
    [ ("Amsterdam district court", "mr. V.M. Behrens")
    ; ("Council of State", "mr. J.J. Schuurman")
    ; ("Utrecht district court", "mr. K.F. van Dam")
    ; ("Utrecht district court", "mr. Ch. Dequaistenit")
    ]
POPULATION judge[Session*Party] CONTAINS
    [ ("Session RbAms 1094", "mr. N.M. van Waterschoot")
    ; ("Session RvS 83", "mr. J.H.B. van der Meer")
    ; ("Session RvS 84", "mr. Ph.Q. van Otterloo-Pannerden")
    ; ("SBR 2009/05/02", "mr. J. Ebbens")
    ]
POPULATION panel[Session*Panel] CONTAINS
    [ ("Session RbAms 1094", "Amsterdam district court single-judge panel for cases of administrative law")
    ; ("Session RvS 83", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("Session RvS 84", "Panel 2 department administrative administration of justice of the Council of State")
    ; ("SBR 2009/05/02", "Utrecht district court single-judge panel for cases of administrative law")
    ]

POPULATION areaOfLaw[LegalCase*AreaOfLaw] CONTAINS
    [ ("199902238", "Administrative law")
    ; ("AWB 07/2481 WRO", "Administrative law")
    ; ("SBR 02/74331", "Administrative law")
    ]

POPULATION caseType[LegalCase*CaseType] CONTAINS
    [ ("199902238", "Appeal to administrative court")
    ; ("AWB 07/2481 WRO", "Appeal")
    ; ("SBR 02/74331", "Appeal")
    ]

POPULATION caseFile[Document * LegalCase] CONTAINS
    [ ("doc987384","199902238")
    ; ("doc763820", "AWB 07/2481 WRO")
    ; ("letter 2009/87743", "SBR 02/74331")
    ; ("letter 2009/87743a", "SBR 02/74331")
    ; ("schedule 2009/87743.1", "SBR 02/74331")
    ]
POPULATION remark[Document * Text] CONTAINS
    [ ("doc987384","doc987384")
    ; ("letter 2000/864821a", "2000/864821a")
    ; ("letter 2000/860338e", "2000/860338e")
    ; ("doc763820", "doc763820")
    ; ("letter 2009/87743", "Vreugdenhil")
    ; ("letter 2009/87743a", "2009/87743a")
    ; ("schedule 2009/87743.1", "Decision of the city council of Utrecht, Utrechtsblad 2009 No. 8")
    ; ("letter 2007/33-9887","2007/33-9887")
    ; ("letter 2009/33-9854","2009/33-9854")
    ; ("letter 2007/33-9910","2007/33-9910")
    ]

POPULATION sent[Document * TimeStamp] CONTAINS
    [ ("doc987384","10-10-2000")
    ; ("letter 2000/864821a", "2-3-2000")
    ; ("letter 2000/860338e", "5-3-2000")
    ; ("doc763820", "10-4-2009")
    ; ("letter 2009/87743", "10-4-2009")
    ; ("letter 2009/87743a", "16-4-2009")
    ; ("schedule 2009/87743.1", "27-2-2009")
    ; ("letter 2007/33-9887","12-9-2007")
    ; ("letter 2009/33-9854","20-4-2009")
    ; ("letter 2007/33-9910","28-10-2007")
    ]

POPULATION received[Document * TimeStamp] CONTAINS
    [ ("doc987384","12-10-2000")
    ; ("letter 2000/864821a", "3-3-2000")
    ; ("letter 2000/860338e", "8-3-2000")
    ; ("doc763820", "11-4-2009")
    ; ("letter 2009/87743", "12-4-2009")
    ; ("letter 2009/87743a", "17-4-2009")
    ; ("schedule 2009/87743.1", "27-2-2009")
    ; ("letter 2007/33-9887","14-9-2007")
    ; ("letter 2009/33-9854","22-4-2009")
    ; ("letter 2007/33-9910","29-10-2007")
    ]
POPULATION documentType[Document * DocumentType] CONTAINS
    [ ("doc987384", "Verdict")
    ; ("doc763820", "Evidence")
    ; ("letter 2000/864821a", "Authorization")
    ; ("letter 2000/860338e", "Authorization")
    ; ("letter 2009/87743", "Correspondence")
    ; ("letter 2009/87743a", "Appeal")
    ; ("schedule 2009/87743.1", "Evidence")
    ; ("letter 2007/33-9887","Authorization")
    ; ("letter 2009/33-9854","Correspondence")
    ; ("letter 2007/33-9910","Authorization")
    ]

POPULATION broughtBefore[LegalCase*Court] CONTAINS
    [ ("199902238", "Council of State")
    ; ("AWB 07/2481 WRO", "Amsterdam district court")
    ; ("SBR 02/74331", "Utrecht district court")
    ]

ENDCONTEXT
