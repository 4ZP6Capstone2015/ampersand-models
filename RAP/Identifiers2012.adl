CONTEXT Identifiers
PATTERN Identifiers
-- Dit pattern is bediscussieerd op het Techneutenweekend van feb. 2012
PURPOSE CONTEXT Identifiers
{+Dit pattern is een beetje speciaal in de zin dat het niet per se bedoeld is om integraal deel uit te maken van een systeem (d.w.z.: meegecompileerd worden). Het belang hiervan is dat het een conceptueel kader (denkmodel) biedt over het aanwijzen van Atomen binnen verschillende contexten, waarbij een context hier mogelijk semantisch wat anders is dan het Context concept uit de RAP patterns.-}

CONCEPT "Entiteit" 
--	"An entity is a something that has separate and distinct existence and objective or conceptual reality. See: http://www.m-w.com/dictionary/entity" ""
	 "iets dat wezenlijk bestaat. (Zie: http://www.vandale.nl/opzoeken/woordenboek/?zoekwoord=entiteit)" ""
   PURPOSE CONCEPT "Entiteit"
   {+Om te kunnen praten over communicatie tussen contexten is het nodig dat er dingen zijn om over te kunnen praten. In wezen kunnen we over alles praten dat bestaat. Daarom moeten we daar een naam voor hebben.-}

CONCEPT "Identifier"
	 "een verwijzing (binnen een zekere Context) door middel van een Symbool naar een Atoom (dat in diezelfde Context bestaat), welke een representatie is (binnen die Context) van een entiteit." ""
   PURPOSE CONCEPT "Identifier"
   {+In communicatie binnen een, of tussen meerdere contexten, moet het mogelijk zijn om te kunnen verwijzen naar entiteiten (waarvan binnen de context(en) bekend is dat ze bestaan (hebben)). Daarom is het nodig om de representaties voor die entiteiten binnen de verschillende contexten te kunnen identificeren.-}

CONCEPT "Symbol"
   "een middel om binnen Contexten te kunnen verwijzen naar Atomen" ""
   PURPOSE CONCEPT "Symbol"
   {+Binnen een context worden atomen identificeerbaar door er een symbool (label, naam) aan te hangen. Dit symbool (label, naam) heeft dan betekenis binnen die context, en is binnen die context een verwijzing naar dat atoom.-}

CONCEPT "Context"
   "een afbakening waarbinnen precies 1 'wereldbeeld' bestaat, d.w.z. een verzameling van (middels relaties en regels) onderling samenhangende Atomen die elk verwijzen naar een entiteit." "" 
   PURPOSE CONCEPT "Context"
   {+We moeten toegeven dat iedereen zijn eigen wereldbeeld heeft, met bijbehorende ideeen, namen en wat dies meer zijn. Deze erkenning maakt dat we secuur, en consequent, deze 'werelden' moeten onderscheiden. Dit is een zeer fundamenteel principe waarvoor we dan ook een concept willen hebben-}

CONCEPT "Atom"
   "een representatie, binnen een zekere Context, voor een entiteit." ""
   PURPOSE CONCEPT "Atom"
   {+Een wereldbeeld bestaat uit representaties van entiteiten die zijn waargenomen. Dat kan een visuele, tekstuele, chemische of andere representatie zijn. Dit noemen we atomen.-}
--------------------------------------------------------------------

SPEC Identifier ISA Atom

VIEW "Identifiers": Identifier(TXT "(", context, TXT ")", identifier)

identifier :: Identifier -> Symbol
PRAGMA "Door " " wordt naar een zeker atoom verwezen door middel van "

context :: Identifier -> Context
PRAGMA "" " identificeert een atoom binnen "

identifies :: Identifier -> Atom
PRAGMA "" " identificeert (onderscheidt) " " (van alle andere atomen binnen de context waarin de identifier bestaat"

entity :: Atom * Entity [INJ,UNI]
PRAGMA "" " is een representatie van " " (die ooit in de werkelijke wereld heeft bestaan, maar misschien nu niet meer)" 

exists :: Atom -> Context
PRAGMA "" " bestaat binnen "

RULE "Identifier integrity": identifies~; context |- exists
MEANING "Elk atoom dat identificeerbaar is in een gegeven context, moet binnen die context bestaan."
PURPOSE RULE "Identifier integrity"
{+De definitie van een identifier is dat deze iets - een atoom - identificeert. Deze moet dan ook bestaan en wel binnen dezelfde context.-}

RULE "Identifier integrity (2)": context |- exists
MEANING "Elke identifier die tot een context behoort is zelf een atoom binnen die context"
ENDPATTERN
----------------------------------------------------------
PATTERN Communication

isCommunicatableTo :: Identifier * Context
PRAGMA "Het symbool van " " is communiceerbaar met " " omdat het in beide contexten verwijst naar atomen die dezelfde entiteit representeren"

RULE "Symbol communication integrity": isCommunicatableTo = (identifies;entity;entity~;identifies~ /\ identifier;identifier~); context
MEANING "Om vanuit de ene context naar de andere context een verwijzing te communiceren naar een (al dan niet meer) bestaande entiteit, mag alleen gebruik gemaakt worden van een symbool dat in beide contexten  bij een identifier hoort die een atoom identificeert die op zijn beurt de betreffende entiteit representeert."

ENDPATTERN
----------------------------------------------------------
ENDCONTEXT
