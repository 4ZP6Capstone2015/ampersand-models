CONTEXT Identifiers
PATTERN Identifiers
PURPOSE CONTEXT Identifiers
{+Dit pattern is een beetje speciaal in de zin dat het niet per se bedoeld is om integraal deel uit te maken van een systeem (d.w.z.: meegecompileerd worden). Het belang hiervan is dat het een conceptueel kader (denkmodel) biedt over het aanwijzen van Atomen binnen verschillende contexten, waarbij een context hier mogelijk semantisch wat anders is dan het Context concept uit de RAP patterns. 
Dit pattern vindt zijn oorsprong in de discussies van  het Techneutenweekend van feb. 2012. Daarna is verder denkwerk verricht, waarvan de resultaten te vinden zijn op de Wiki: http://wiki.tarski.nl/index.php/Identifiers_and_KEYs Daar wordt ingegaan op de verschillende soorten identifers, te weten: "internal identifiers" (of "vieze identifiers"), "business identifiers", "query identifiers" en "link identifiers".-}

CONCEPT "Entiteit" 
--	"An entity is a something that has separate and distinct existence and objective or conceptual reality. See: http://www.m-w.com/dictionary/entity" ""
	 "iets dat wezenlijk bestaat. (Zie: http://www.vandale.nl/opzoeken/woordenboek/?zoekwoord=entiteit)" ""
   PURPOSE CONCEPT "Entiteit"
   {+Om zelf (d.w.z. binnen de 'eigen Context') te kunnen redeneren en (daarna) met anderen (c.q. andere Contexten) te kunnen communiceren, is het nodig dat er dingen zijn om over te kunnen redeneren en communiceren. In wezen kunnen we over alles redeneren en communiceren dat bestaat (d.w.z. waarvan binnen het eigen wereldbeeld een representatie bestaat zodat het gekend wordt en kan worden onderscheiden van andere dingen die (voor ons) bestaan). Daarom moeten we een naam hebben voor de dingen die bestaan.-}

CONCEPT "Identifier" -- dit is een "interne identifier" of "vieze identifier"
	 "een symbool die binnen een zekere Context een zekere Entiteit representeert (en geen enkele andere Entiteit representeerd in diezelfde Context)." ""
   PURPOSE CONCEPT "Interne Identifier"
   {+Om zelf (d.w.z. binnen de 'eigen Context') te kunnen redeneren over Entiteiten, is het nodig dat elke Entiteiten in het wereldbeeld van de eigen Context kan worden geidentificeerd (d.w.z. kan worden onderscheiden van alle andere Entiteiten in dat wereldbeeld). Daarom is het nodig om dat elke Context beschikt over (interne) representaties voor de Entiteiten die in hun wereldbeeld zitten (= die ze kennen) die elk een dergelijke Entiteit identificeren. Merk op dat het hier gaat over *interne* representaties. Deze hoeven dus niet gecommuniceerd (gedeeld) te worden met (naar) een andere Context. Daar zijn ze ook niet voor bedoeld. Een eenvoudig voorbeeld van een interne identifier is een ordernummer (bijv: "Order:12345"). Aan dit voorbeeld is overigens al te zien dat een nuttige specialisatie van 'interne identifier' de constructie is waarbij de interne identifier bestaat uit de combinatie van een symbool dat een objectklasse identificeert en een symbool dat een atom binnen die klasse identificeert.-}

CONCEPT "Symbol"
   "een middel (bitrij, character string, ...) dat  onder meer gebuikt wordt om binnen Contexten te kunnen verwijzen naar Atomen" ""
   PURPOSE CONCEPT "Symbol"
   {+Binnen een context worden atomen (en daarmee de entiteiten die ze representeren) identificeerbaar door er een symbool aan te hangen. Dit symbool wordt binnen de context vaak met de term 'naam' of 'label' aangeduid. Dit symbool heeft dan betekenis binnen die context, en is binnen die context een verwijzing naar dat atoom (en impliciet dus ook naar de Entiteit die door het atoom binnen de context wordt gerepresenteerd).-}

CONCEPT "Context"
   "een afbakening waarbinnen precies 1 'wereldbeeld' bestaat, d.w.z. een verzameling van (middels relaties en regels) onderling samenhangende Atomen die elk verwijzen naar een entiteit." "" 
   PURPOSE CONCEPT "Context"
   {+We moeten toegeven dat iedereen zijn eigen wereldbeeld heeft, met bijbehorende ideeen, namen en wat dies meer zijn. Deze erkenning maakt dat we secuur, en consequent, deze 'werelden' moeten onderscheiden. Dit is een zeer fundamenteel principe waarvoor we dan ook een concept willen hebben.-}

CONCEPT "Atom"
   "een representatie, binnen een zekere Context, voor een entiteit." ""
   PURPOSE CONCEPT "Atom"
   {+Een wereldbeeld bestaat uit representaties van entiteiten die zijn waargenomen. Dat kan een visuele, tekstuele, chemische of andere representatie zijn. Dit noemen we atomen.-}
--------------------------------------------------------------------

SPEC Identifier ISA Atom

VIEW "Identifiers": Identifier(TXT "(", context, TXT ")", identifier)

identifier :: Identifier -> Symbol
PRAGMA "Door " " wordt naar een zeker atoom verwezen door middel van "

context :: Identifier -> Context
PRAGMA "" " identificeert een atoom binnen "

identifies :: Identifier -> Atom
PRAGMA "" " identificeert (onderscheidt) " " (van alle andere atomen binnen de context waarin de identifier bestaat"

entity :: Atom * Entity [INJ,UNI]
PRAGMA "" " is een representatie van " " (die ooit in de werkelijke wereld heeft bestaan, maar misschien nu niet meer)" 

exists :: Atom -> Context
PRAGMA "" " bestaat binnen "

RULE "Identifier integrity": identifies~; context |- exists
MEANING "Elk atoom dat identificeerbaar is in een gegeven context, moet binnen die context bestaan."
PURPOSE RULE "Identifier integrity"
{+De definitie van een identifier is dat deze iets - een atoom - identificeert. Deze moet dan ook bestaan en wel binnen dezelfde context.-}

RULE "Identifier integrity (2)": context |- exists
MEANING "Elke identifier die tot een context behoort is zelf een atoom binnen die context"
ENDPATTERN
----------------------------------------------------------
PATTERN Communication

isCommunicatableTo :: Identifier * Context
PRAGMA "Het symbool van " " is communiceerbaar met " " omdat het in beide contexten verwijst naar atomen die dezelfde entiteit representeren"

RULE "Symbol communication integrity": isCommunicatableTo = (identifies;entity;entity~;identifies~ /\ identifier;identifier~); context
MEANING "Om vanuit de ene context naar de andere context een verwijzing te communiceren naar een (al dan niet meer) bestaande entiteit, mag alleen gebruik gemaakt worden van een symbool dat in beide contexten  bij een identifier hoort die een atoom identificeert die op zijn beurt de betreffende entiteit representeert."

ENDPATTERN
----------------------------------------------------------
ENDCONTEXT
