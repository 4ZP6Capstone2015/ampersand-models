CONTEXT Identificatiemiddelen
--!Paspoorten, rijbewijzen en wat dies meer zij. Moet nog 'gewettigd' worden....
PATTERN "Authenticatie middelen"
--!Toetsen aan de Wet op de identificatieplicht is nog niet gebeurd.
CONCEPT Identificatiemiddel "een bewijs dat iemand is die hij/zij zegt dat hij/zij is"

KEY "Identificatiemiddelen": Identificatiemiddel(idMiddelSoort, TXT ", ", idMiddelNummer)

idMiddelSoort :: Identificatiemiddel -> IdentificatiemiddelSoort
idMiddelNummer :: Identificatiemiddel -> IdentificatiemiddelNummer
idMiddelHouder :: Identificatiemiddel * BurgerServiceNummer [UNI]

RULE "Toegelaten identificatiemiddelen": I[IdentificatiemiddelSoort] = 'Paspoort' \/ 'ID-kaart' \/ 'Rijbewijs' \/ 'DigiD'
MEANING "De volgende soorten identificatiemiddelen zijn toegestaan: Paspoort, ID-kaart, Rijbewijs, DigiD"

RULE "Geldige identificatiemiddelen": I[Identificatiemiddel] |- idMiddelHouder;idMiddelHouder~
MEANING "Elk geldig identificatiemiddel is uitgereikt aan een houder"
MESSAGE "Het identificiatiemiddel is ongeldig, d.w.z. niet uitgereikt aan een houder."

ENDPATTERN
-----------------------------------------------------------
PATTERN DigiD
--!Dit kan allemaal veel makkelijker als ISA goed werkt.

idMiddelUsername :: Identificatiemiddel * Gebruikersnaam [UNI]
idMiddelPassword :: Identificatiemiddel * Wachtwoord [UNI]

--!Onderstaande regel kan weer als ticket #306 is opgelost
{-
RULE "DigiD U/PWs": I /\ idMiddelSoort;'DigiD';idMiddelSoort~ |- idMiddelUsername;idMiddelUsername~ /\ idMiddelPassword;idMiddelPassword~
MEANING "Elk DigiD moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze DigiD mist een gebruikersnaam en/of wachtwoord"
-}
RULE "Identificatie dmv DigiD": idMiddelUsername;idMiddelUsername~ /\ idMiddelPassword;idMiddelPassword~ /\ idMiddelSoort;'DigiD';idMiddelSoort~ |- I
MEANING "De combinatie van gebruikersnaam en wachtwoord moet voor DigiD's uniek identificerend zijn"
MESSAGE "De gebruikersnaam en wachtwoord van dit DigiD moet uniek identificerend zijn"

ENDPATTERN
------------------------------------------------------------
ENDCONTEXT
