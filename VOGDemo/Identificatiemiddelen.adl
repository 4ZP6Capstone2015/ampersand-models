CONTEXT Identificatiemiddelen
--!Paspoorten, rijbewijzen en wat dies meer zij. Moet nog 'gewettigd' worden....
PATTERN "Authenticatie middelen"
--!Toetsen aan de Wet op de identificatieplicht is nog niet gebeurd.
CONCEPT Identificatiemiddel "een bewijs dat iemand is die hij/zij zegt dat hij/zij is"

--KEY "Identificatiemiddelen": Identificatiemiddel(idmSoort, TXT ", ", idmNummer)

idmDisplayName :: Identificatiemiddel -> IdentificatiemiddelID -- wordt gebruikt om namen van mensen netjes op het scherm te zetten.

idmSoort  :: Identificatiemiddel -> IdentificatiemiddelSoort
idmNummer :: Identificatiemiddel -> IdentificatiemiddelNummer

RULE "Identificatie dmv idmDisplayNamedelen": (idmSoort;idmSoort~ /\ idmNummer;idmNummer~) |- I
MEANING "De combinatie van identificatiemiddel soort en serienummer moet per soort identificatiemiddel uniek identificerend zijn" -- voor fysieke idmDisplayNamedelen
MESSAGE "De combinatie van identificatiemiddel soort en serienummer moet uniek identificerend zijn" -- voor fysieke idmDisplayNamedelen

idmHouder :: Identificatiemiddel * BurgerServiceNummer [UNI] -- identificeert een natuurlijk persoon in de GBA

--**Onderstaande regels kunnen weer als ticket #306 is opgelost**
RULE "Persoonlijke identificatiemiddelen": I /\ idmSoort;('Paspoort' \/ 'ID-kaart' \/ 'Rijbewijs' \/ 'DigiD');idmSoort~ |- idmHouder;idmHouder~
MEANING "DigiD identificatiemiddelen identificeren personen"

RULE "DigiD U/PWs": I /\ idmSoort;'DigiD';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~
MEANING "Elk DigiD moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze DigiD mist een gebruikersnaam en/of wachtwoord"


idmOrganisatie :: Identificatiemiddel * Organisatie [UNI] -- identificeert een organisatie

--**Onderstaande regels kunnen weer als ticket #306 is opgelost**
RULE "Organisatie identificatiemiddelen": I /\ idmSoort;('EHvB');idmSoort~ |- idmOrganisatie;idmOrganisatie~
MEANING "EHvB identificatiemiddelen identificeren organisaties"

RULE "EHvB U/PWs": I /\ idmSoort;'EHvB';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~
MEANING "Elk EHvB moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze EHvB mist een gebruikersnaam en/of wachtwoord"


idmGemeente :: Identificatiemiddel * Gemeente [UNI] -- identificeert een gemeenteambtenaar
idmGemeentePlaats :: Identificatiemiddel * Plaats [UNI] -- Plaats waar het gemeentehuis is gevestigd

--**Onderstaande regels kunnen weer als ticket #306 is opgelost**
RULE "Gemeentelijke identificatiemiddelen": I /\ idmSoort;('GemeenteID');idmSoort~ |- idmGemeente;idmGemeente~
MEANING "GemeenteID identificatiemiddelen identificeren medewerkers van gemeentes"

RULE "GemeenteID U/PWs": I /\ idmSoort;'GemeenteID';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~
MEANING "Elk GemeenteID moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze GemeenteID mist een gebruikersnaam en/of wachtwoord"


RULE "Geldige identificatiemiddelen": I[Identificatiemiddel] |- idmHouder;idmHouder~ \/ idmOrganisatie;idmOrganisatie~ \/ idmGemeente;idmGemeente~
MEANING "Elk geldig identificatiemiddel is uitgereikt aan een houder, een organisatie of een gemeente"
MESSAGE "Het identificiatiemiddel is ongeldig, d.w.z. niet uitgereikt aan een houder, organisatie of gemeente"

idmUserid :: Identificatiemiddel * Userid [UNI]
idmPassword :: Identificatiemiddel * Password [UNI]

RULE "Identificatie dmv U/PWs": idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmSoort;idmSoort~ |- I
MEANING "De combinatie van gebruikersnaam en wachtwoord moet per soort identificatiemiddel identificerend zijn"
MESSAGE "De gebruikersnaam en het wachtwoord van dit soort identificatiemiddel moet uniek identificerend zijn"

idmDisplayHandtekening :: Identificatiemiddel * Handtekening [UNI] PRAGMA "Als met " " een handtekening is gezet, ziet dat er zo uit: "
idmDigitaleHandtekening :: Identificatiemiddel * DigitaleHandtekening [UNI] PRAGMA "Degene die " " heeft, zet digitale handtekeningen met de volgende code: "
idmPasswordHandtekening :: Identificatiemiddel * PasswordHandtekening [UNI] PRAGMA "Om met " " een digitale handtekening te kunnen zetten, moet " " worden ingevoerd"

idmEmailAddress :: Identificatiemiddel * EmailAddress

--**Onderstaande regels kunnen weer als ticket #306 is opgelost**
--[Paspoorten]--

RULE "Paspoorten": I /\ idmSoort;'Paspoort';idmSoort~ |- idmNummer;idmNummer~ /\ idmHouder;idmHouder~
MEANING "Elk paspoort heeft een nummer en een houder, en identificeert een natuurlijk persoon"

RULE "Paspoorten identificeren natuurlijke personen": (idmSoort;'Paspoort';idmSoort~ /\ idmNummer;idmNummer~) |- I
MEANING "Elk paspoort heeft een uniek nummer"

--[Rijbewijzen]--

RULE "Rijbewijzen": I /\ idmSoort;'Rijbewijs';idmSoort~ |- idmNummer;idmNummer~ /\ idmHouder;idmHouder~
MEANING "Elk rijbewijs heeft een nummer en een houder, en identificeert een natuurlijk persoon"

RULE "Rijbewijzen identificeren natuurlijke personen": (idmSoort;'Rijbewijs';idmSoort~ /\ idmNummer;idmNummer~) |- I
MEANING "Elk rijbewijs heeft een uniek nummer"

--[DigiDs]--
RULE "DigiDs": I /\ idmSoort;'DigiD';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmHouder;idmHouder~
MEANING "Elk DigiD heeft een gebruikersnaam en wachtwoord, en identificeert een natuurlijk persoon"

RULE "DigiDs identificeren natuurlijke personen": (idmSoort;'DigiD';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I
MEANING "Elk DigiD heeft een unieke combinatie van gebruikersnaam en wachtwoord"

--[E-Herkenning voor Bedrijven]--
RULE "EHvBs": I /\ idmSoort;'EHvB';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmOrganisatie;idmOrganisatie~
MEANING "Elk EHvB heeft een gebruikersnaam en wachtwoord, en identificeert een organisatie"

RULE "EHvBs identificeren ortanisaties": (idmSoort;'EHvB';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I
MEANING "Elk EHvB heeft een unieke combinatie van gebruikersnaam en wachtwoord"

--[GemeenteIDs]--

RULE "GemeenteIDs": I /\ idmSoort;('GemeenteID');idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmGemeente;idmGemeente~
MEANING "Elk GemeenteID heeft een gebruikersnaam en wachtwoord, en identificeert een gemeente"

RULE "GemeenteIDs identificeren ortanisaties": (idmSoort;'GemeenteID';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I
MEANING "Elk GemeenteID heeft een unieke combinatie van gebruikersnaam en wachtwoord"

ENDPATTERN
-----------------------------------------------------------
PATTERN SessionLogin

CONCEPT SESSION "een communicatiekanaal tussen een applicatie en een gebruiker."
PURPOSE CONCEPT SESSION IN DUTCH
{+Als meerdere gebruikers tegelijkertijd van eenzelfde applicatie gebruik willen maken, moet de communicatie die deze gebruikers (webclients) met de applicatie hebben, netjes uitelkaar worden gehouden. Daartoe gebruiken we de singleton '_SESSION', die door de prototype generator is geimplementeerd.-}

sessionUserid :: SESSION * Userid [UNI]
PRAGMA "De gebruiker van " " is ingelogd onder "
MEANING "Onder de 'sessionUserid' van een sessie verstaan we (de business identifier van) het account waarvan de eigenaar verantwoordelijk gehouden wordt voor al hetgeen er in die sessie gebeurt."

sessionPassword :: SESSION * Password [UNI]
PRAGMA "De gebruiker van " " heeft zich geauthenticeerd met behulp van "

RULE "Login": '_SESSION'[SESSION];sessionUserid;idmUserid~ |- '_SESSION';sessionPassword;idmPassword~ --Dit is een '|-' ipv '=' omdat een wachtwoord niet uniek hoeft te zijn
MEANING "Bij inloggen moet een geldige (Userid,Password) combinatie worden opgegeven."
MESSAGE "Incorrect password"

RULE "Session login integrity": I /\ sessionUserid;sessionUserid~ /\ sessionPassword;sessionPassword~
|- (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~);V
MEANING "Je mag alleen inloggen met een geldig account"
MESSAGE "Je mag alleen inloggen met een geldig account"

ENDPATTERN
-----------------------------------------------------------
--[Interfaces voor alle users]------------------------------

-- INTERFACE die je moet gebruiken om nieuwe Sessions mee te maken:
INTERFACE Login (sessionUserid,sessionPassword): V[ONE*SESSION];'_SESSION'[SESSION]
BOX [ userid       : sessionUserid
    , password     : sessionPassword
    ]
-----------------------------------------------------------
ENDCONTEXT
