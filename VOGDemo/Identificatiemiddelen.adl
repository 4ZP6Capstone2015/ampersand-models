CONTEXT Identificatiemiddelen
--!Paspoorten, rijbewijzen en wat dies meer zij. Moet nog 'gewettigd' worden....
PATTERN "Authenticatie middelen"
--!Toetsen aan de Wet op de identificatieplicht is nog niet gebeurd.
CONCEPT Identificatiemiddel "een bewijs dat iemand is die hij/zij zegt dat hij/zij is"

--KEY "Identificatiemiddelen": Identificatiemiddel(idmSoort, TXT ", ", idmNummer)

idmSoort  :: Identificatiemiddel -> IdentificatiemiddelSoort
idmNummer :: Identificatiemiddel -> IdentificatiemiddelNummer

RULE "Identificatie dmv Idmiddelen": (idmSoort;idmSoort~ /\ idmNummer;idmNummer~) |- I
MEANING "De combinatie van identificatiemiddel soort en serienummer moet uniek identificerend zijn"
MESSAGE "De combinatie van identificatiemiddel soort en serienummer moet uniek identificerend zijn"

RULE "Toegelaten identificatiemiddelen": I[IdentificatiemiddelSoort] = 'Paspoort' \/ 'ID-kaart' \/ 'Rijbewijs' \/ 'DigiD' \/ 'EHvB' --EHvB: E-Herkenning voor Bedrijven
MEANING "De volgende soorten identificatiemiddelen zijn toegestaan: Paspoort, ID-kaart, Rijbewijs, DigiD"

idmHouder :: Identificatiemiddel * BurgerServiceNummer [UNI]
idmOrganisatie :: Identificatiemiddel * Organisatie [UNI]

RULE "Geldige identificatiemiddelen": I[Identificatiemiddel] |- idmHouder;idmHouder~ \/ idmOrganisatie;idmOrganisatie~
MEANING "Elk geldig identificatiemiddel is uitgereikt aan een houder of een organisatie"
MESSAGE "Het identificiatiemiddel is ongeldig, d.w.z. niet uitgereikt aan een houder of organisatie."

idmUsername :: Identificatiemiddel * Gebruikersnaam [UNI]
idmPassword :: Identificatiemiddel * Wachtwoord [UNI]

RULE "Identificatie dmv U/PWs": idmUsername;idmUsername~ /\ idmPassword;idmPassword~ /\ idmSoort;idmSoort~ |- I
MEANING "De combinatie van gebruikersnaam en wachtwoord moet per soort identificatiemiddel identificerend zijn"
MESSAGE "De gebruikersnaam en het wachtwoord van dit soort identificatiemiddel moet uniek identificerend zijn"

--!Onderstaande regels kunnen weer als ticket #306 is opgelost
{-
RULE "DigiD U/PWs": I /\ idmSoort;'DigiD';idmSoort~ |- idmUsername;idmUsername~ /\ idmPassword;idmPassword~
MEANING "Elk DigiD moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze DigiD mist een gebruikersnaam en/of wachtwoord"

RULE "EHvB U/PWs": I /\ idmSoort;'EHvB';idmSoort~ |- idmUsername;idmUsername~ /\ idmPassword;idmPassword~
MEANING "Elk EHvB moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze EHvB mist een gebruikersnaam en/of wachtwoord"
-}

ENDPATTERN
-----------------------------------------------------------
ENDCONTEXT
