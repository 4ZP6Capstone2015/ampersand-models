CONTEXT Identificatiemiddelen
--!Paspoorten, rijbewijzen en wat dies meer zij. Moet nog 'gewettigd' worden....
PATTERN "Authenticatie middelen"
--!Toetsen aan de Wet op de identificatieplicht is nog niet gebeurd.
CONCEPT Identificatiemiddel "een bewijs dat iemand is die hij/zij zegt dat hij/zij is"

KEY "Identificatiemiddelen": Identificatiemiddel(idMiddelSoort, TXT ", ", idMiddelNummer)

idMiddelSoort :: Identificatiemiddel -> IdentificatiemiddelSoort
idMiddelNummer :: Identificatiemiddel -> IdentificatiemiddelNummer
idMiddelHouder :: Identificatiemiddel * BurgerServiceNummer [UNI]
idMiddelOrganisatie :: Identificatiemiddel * Organisatie [UNI]

RULE "Toegelaten identificatiemiddelen": I[IdentificatiemiddelSoort] = 'Paspoort' \/ 'ID-kaart' \/ 'Rijbewijs' \/ 'DigiD' \/ 'EHvB' --EHvB: E-Herkenning voor Bedrijven
MEANING "De volgende soorten identificatiemiddelen zijn toegestaan: Paspoort, ID-kaart, Rijbewijs, DigiD"

RULE "Geldige identificatiemiddelen": I[Identificatiemiddel] |- idMiddelHouder;idMiddelHouder~ \/ idMiddelOrganisatie;idMiddelOrganisatie~
MEANING "Elk geldig identificatiemiddel is uitgereikt aan een houder of een organisatie"
MESSAGE "Het identificiatiemiddel is ongeldig, d.w.z. niet uitgereikt aan een houder of organisatie."

idMiddelUsername :: Identificatiemiddel * Gebruikersnaam [UNI]
idMiddelPassword :: Identificatiemiddel * Wachtwoord [UNI]

RULE "Identificatie dmv U/PWs": idMiddelUsername;idMiddelUsername~ /\ idMiddelPassword;idMiddelPassword~ /\ idMiddelSoort;idMiddelSoort~ |- I
MEANING "De combinatie van gebruikersnaam en wachtwoord moet per soort identificatiemiddel identificerend zijn"
MESSAGE "De gebruikersnaam en het wachtwoord van dit soort identificatiemiddel moet uniek identificerend zijn"

--!Onderstaande regels kunnen weer als ticket #306 is opgelost
{-
RULE "DigiD U/PWs": I /\ idMiddelSoort;'DigiD';idMiddelSoort~ |- idMiddelUsername;idMiddelUsername~ /\ idMiddelPassword;idMiddelPassword~
MEANING "Elk DigiD moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze DigiD mist een gebruikersnaam en/of wachtwoord"

RULE "EHvB U/PWs": I /\ idMiddelSoort;'EHvB';idMiddelSoort~ |- idMiddelUsername;idMiddelUsername~ /\ idMiddelPassword;idMiddelPassword~
MEANING "Elk EHvB moet een gebruikersnaam en wachtwoord hebben"
MESSAGE "Deze EHvB mist een gebruikersnaam en/of wachtwoord"
-}

ENDPATTERN
-----------------------------------------------------------
ENDCONTEXT
