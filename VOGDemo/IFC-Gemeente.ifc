CONTEXT VOGAanvraagGemeentebalie
-----------------------------------------------------------
INTERFACE "G: Overzicht" FOR Gemeente : I[ONE]
BOX[ "Uitstaande aanvragen" : V[ONE*GemeentelijkeVOGZaak];(I /\ gemVOGAanvraag;-(vogAfgifteBesluit;vogAfgifteBesluit~);gemVOGAanvraag~)
   , "Ontvangen VOGs"       : V[ONE*GemeentelijkeVOGZaak];(I /\ gemVOGAanvraag;vogAfgifteBesluit;vogAfgifteBesluit~;gemVOGAanvraag~)
   ]
-----------------------------------------------------------
PROCESS "VOG Aanvraag (Gemeente)"

RULE "Gemeentegegevens invullen": -- dus exclusief het gedeelte voor controle op volledigheid en ondertekening
I |- {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ 
     /\ gemVOGAanvraag;(I /\ vogAanvraagOrganisatieTemplate;vogAanvraagOrganisatieTemplate~);gemVOGAanvraag~
     /\ bijzonderhedenGeconstateerd;bijzonderhedenGeconstateerd~
     /\ persisterenInDeAanvraag;persisterenInDeAanvraag~
     /\ covogOmAdviesGevraagd;covogOmAdviesGevraagd~)
MEANING "Alle gegevens op de aanvraag zijn volledig ingevuld"
MESSAGE "Te completeren aanvragen"
ROLE Gemeente MAINTAINS "Gemeentegegevens invullen"

ENDPROCESS
-----------------------------------------------------------
INTERFACE "G1: VOG Aanvraag"
( gemeenteAanvraagnummer
, gemVOGAanvraag
  , vogAanvragerIDMSoort, vogAanvragerIDMNummer, vogAanvragerIDVastgesteld
  , vogAanvragerUserid, vogAanvragerWachtwoord
  , vogAanvraagOrganisatieTemplate
, bijzonderhedenGeconstateerd, bijzonderhedenGeconstateerdToelichting
, persisterenInDeAanvraag, persisterenInDeAanvraagToelichting
, covogOmAdviesGevraagd, covogOmAdviesGevraagdToelichting
, gemAmbtenaarUserid, gemAmbtenaarWachtwoord, gemOndertekeningDatum
) FOR Gemeente : I[GemeentelijkeVOGZaak] /\ -((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~);(gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~)~)
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "VOG Aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Soort identiteitsbewijs (paspoort, rijbewijs)" : vogAanvragerIDMSoort
      , "Serienummer" : vogAanvragerIDMNummer
      , "Komen deze gegevens overeen met de persoon die u voor zich heeft?" : vogAanvragerIDVastgesteld
      , "DigID" : vogAanvragerUserid
      , "wachtwoord" : vogAanvragerWachtwoord
      , "GBA gegevens van aanvrager" : 
             {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUsername~ /\ vogAanvragerWachtwoord;idmPassword~);(I /\ idmSoort;'DigID';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen"     : gbaVoornamen
         , "Geboortedatum" : gbaGeboortedatum
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "ID gemeenteambtenaar" : gemAmbtenaarUserid
      , "Wachtwoord" : gemAmbtenaarWachtwoord
      , "Datum" : gemOndertekeningDatum
      , "Ondertekening" : (gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~)
      BOX[ "Naam gemeenteambtenaar" : idmID
         , "Gemeente" : idmGemeente
         , "Plaats" : idmGemeentePlaats
      ]  ]
   ]
-----------------------------------------------------------
INTERFACE "G2: VOG Aanvraag (door gemeente ondertekend)" 
FOR Gemeente : I[GemeentelijkeVOGZaak] /\ ((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~);(gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~)~)
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "VOG Aanvraag" : gemVOGAanvraag
   BOX[ "Gegevens van organisatie / instelling die de VOG verlangt" : vogAanvraagOrganisatieTemplate
      , "Gegevens van aanvrager" : 
             {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUsername~ /\ vogAanvragerWachtwoord;idmPassword~);(I /\ idmSoort;'DigID';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen"     : gbaVoornamen
         , "Geboortedatum" : gbaGeboortedatum
         ]
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Plaats" : 
         ((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~))
         ;idmGemeentePlaats
      , "Naam gemeenteambtenaar" : 
         ((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~))
         ;idmID
      , "Gemeente" : 
         ((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~))
         ;idmGemeente
      ]
   ]
-----------------------------------------------------------
INTERFACE "G3: Doel van de aanvraag"
FOR Gemeente : I[VOGAanvraagOrganisatieTemplate];(I /\ (orgOndertekeningHandtekening;orgOndertekeningHandtekening~))
BOX[ "Aanvraagnummer": vogAanvraagOrganisatieTemplate~;gemVOGAanvraag~
-- , "Template ID" : orgVOGTemplateID
   , "1 - Gegevens organisatie-instelling" : I
   BOX[ "Naam organisatie/instelling" : orgNaam
      , "Naam vertegenwoordiger" : orgVertegenwoordiger
      , "Vestigingsadres" : I -- adresHaakje~
      BOX[ "Straat" : orgStraatnaam
         , "Huisnummer" : orgHuisnummer
         , "Postcode" : orgPostcode
         , "Plaats" : orgPlaats
         , "Land" : orgLand
         ]
      , "Telefoonnummer" : orgTelefoonnummer
      ]
   , "2 - Doel van de aanvraag" : I
   BOX[ "Werkrelatie (Ja/Nee)" : werkrelatie
      , "Functie (waarop de aanvraag betrekking heeft)" : functie
      , "Taakomschrijving (bij voorkeur functiebeschrijving bijvoegen)" : taakomschrijving
      , "Overig (Ja/Nee)" : overig
      , "Omschrijving (bijv. Chaufferskaart, Visum en emigratie, Exploitatievergunning)" : overigOmschrijving
      ]
   , "3 - Specifiek screeningsprofiel" : I
   BOX[ "Heeft de aanvraag betrekking op één van de hieronder genoemde functies of doelen?" : specifiekScreeningsProfielRelevant
      , "Ja, namelijk op: (u kunt slechts één vakje aankruisen!)" : specifiekScreeningsProfiel
      ]
   , "4 - Algemeen screeningsprofiel" : I
   BOX[ " ": algemeenScreeningsProfiel
      ]
   , "5 - Bijzondere omstandigheden m.b.t. de aanvraag" : I
   BOX[ "Zijn er bijzondere omstandigheden (Ja/Nee)?" : orgErZijnBijzondereOmstandigheden
      , "Ja, toelichting" : orgErZijnBijzondereOmstandighedenToelichting
      ]
   , "6 - Ondertekening door organisatie/instelling" : I
   BOX[ "Plaats" : orgOndertekeningPlaats
      , "Datum" : orgOndertekeningDatum
      , "Handtekening" : orgOndertekeningHandtekening
   ]  ]
-----------------------------------------------------------
ENDCONTEXT
