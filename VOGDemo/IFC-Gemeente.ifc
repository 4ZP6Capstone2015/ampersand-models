CONTEXT VOGAanvraagGemeentebalie
-----------------------------------------------------------
INTERFACE "G: Overzicht" FOR Gemeente : I[ONE]
BOX[ "Gemeente ambtenaar" : V[ONE*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   , "In te vullen aanvragen" :V[ONE*GemeentelijkeVOGZaak];
   (I /\ -{-sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld-}
          (I /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~)
   )
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      ]
   , "Uitstaande aanvragen" : V[ONE*GemeentelijkeVOGZaak];
   (I /\ {-sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld-}
          (I /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~)
      /\ gemVOGAanvraag;-(vogAfgifteBesluit;vogAfgifteBesluit~);gemVOGAanvraag~
   )
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Voor" : gemVOGAanvraag;vogAanvraagOrganisatieTemplate
      , "Betrokkene" : gemVOGAanvraag;{-vogBSNAanvrager-}
              (((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
          ;gbaDisplayNaam
      , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      ]
   , "Ontvangen VOGs"       : V[ONE*GemeentelijkeVOGZaak];
   (I /\ gemVOGAanvraag;vogAfgifteBesluit;vogAfgifteBesluit~;gemVOGAanvraag~)
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Voor" : gemVOGAanvraag;vogAanvraagOrganisatieTemplate
      , "Betrokkene" : gemVOGAanvraag;{-vogBSNAanvrager-}
              (((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
          ;gbaDisplayNaam
      , "Behandelend door" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      ]
   ]
-----------------------------------------------------------
PROCESS "VOG Aanvraag (Gemeente)"

RULE "Gemeentegegevens invullen": -- dus exclusief het gedeelte voor controle op volledigheid en ondertekening
(I[GemeentelijkeVOGZaak]
  /\ gemeenteAanvraagnummer;gemeenteAanvraagnummer~
  /\ gemLegesBetaald;'Ja';gemLegesBetaald~
  /\ gemAmbtenaarUserid;gemAmbtenaarUserid~
  /\ gemAmbtenaarPassword;gemAmbtenaarPassword~
)
|- {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ 
     /\ gemVOGAanvraag;(I /\ vogAanvraagOrganisatieTemplate;vogAanvraagOrganisatieTemplate~);gemVOGAanvraag~
     /\ bijzonderhedenGeconstateerd;bijzonderhedenGeconstateerd~
     /\ persisterenInDeAanvraag;persisterenInDeAanvraag~
     /\ covogOmAdviesGevraagd;covogOmAdviesGevraagd~)
MEANING "Alle gegevens op de aanvraag zijn volledig ingevuld"
MESSAGE "Gemeentelijke aanvraag invullen en ondertekenen"
ROLE Gemeente MAINTAINS "Gemeentegegevens invullen"

RULE "Gemeentegegevens ondertekenen":
I /\ {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ 
     /\ gemVOGAanvraag;(I /\ vogAanvraagOrganisatieTemplate;vogAanvraagOrganisatieTemplate~);gemVOGAanvraag~
     /\ bijzonderhedenGeconstateerd;bijzonderhedenGeconstateerd~
     /\ persisterenInDeAanvraag;persisterenInDeAanvraag~
     /\ covogOmAdviesGevraagd;covogOmAdviesGevraagd~)
  |- gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~
MEANING "Elke aanvraag die volledig is ingevuld en waarvan de identiteit van de aanvrager is vastgesteld, moet worden ondertekend (art xxx)"
MESSAGE "Te ondertekenen aanvragen"
ROLE Gemeente MAINTAINS "Gemeentegegevens ondertekenen"

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Nieuwe VOG Aanvraag"
( gemeenteAanvraagnummer, gemLegesBetaald
, gemAmbtenaarUserid, gemAmbtenaarPassword
) FOR Gemeente : I[GemeentelijkeVOGZaak] 
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Zijn de leges betaald?" : gemLegesBetaald
   , "Behandelend ambtenaar" : I
   BOX[ "GemeenteID" : gemAmbtenaarUserid
      , "Wachtwoord" : gemAmbtenaarPassword
      ]
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag invullen en ondertekenen"
( gemAmbtenaarUserid, gemAmbtenaarPassword
, vogAanvraagOrganisatieTemplate
, gemVOGAanvraag
  , vogAanvragerIDMSoort, vogAanvragerIDMNummer, vogAanvragerIDVastgesteld
  , vogAanvragerUserid, vogAanvragerPassword
, bijzonderhedenGeconstateerd, bijzonderhedenGeconstateerdToelichting
, persisterenInDeAanvraag, persisterenInDeAanvraagToelichting
, covogOmAdviesGevraagd, covogOmAdviesGevraagdToelichting
, gemOndertekeningDatum, gemAmbtenaarHandtekening
) FOR Gemeente : (I[GemeentelijkeVOGZaak] 
/\ (  gemVOGAanvraag;zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~;gemVOGAanvraag~
   \/ gemLegesBetaald;'Ja';gemLegesBetaald~
   )
/\ gemeenteAanvraagnummer;gemeenteAanvraagnummer~)
/\ {-userIsBehandelendGemeenteambtenaar-}(I /\ V;'_SESSION'[SESSION];sessionUserid;gemAmbtenaarUserid~)
/\ -{-sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld-}(I /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~)
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\gemAmbtenaarPassword;idmPassword~);idmDisplayName
   , "VOG Aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Soort identiteitsbewijs (paspoort, rijbewijs)" : vogAanvragerIDMSoort
      , "Serienummer" : vogAanvragerIDMNummer
      , "Komen deze gegevens overeen met de persoon die u voor zich heeft?" : vogAanvragerIDVastgesteld
      , "DigiD" : vogAanvragerUserid
      , "Wachtwoord" : vogAanvragerPassword
      , "GBA gegevens van aanvrager" : 
             {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen"     : gbaVoornamen
         , "Geboortedatum" : gbaGeboortedatum
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Handtekening" : gemAmbtenaarHandtekening
      , "Ondertekening" : (I /\ gemOndertekeningDatum;gemOndertekeningDatum~); (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~ /\ gemAmbtenaarHandtekening;idmHandtekening~);(I /\ idmSoort;'GemeenteID';idmSoort~)
      BOX[ "Naam gemeenteambtenaar" : idmDisplayName
         , "Gemeente" : idmGemeente
         , "Plaats" : idmGemeentePlaats
      ]  ]
   ]
-----------------------------------------------------------
INTERFACE "Opgestuurde VOG aanvragen" 
FOR Gemeente : I[GemeentelijkeVOGZaak]
;{-userIsBehandelendGemeenteambtenaar-}(I /\ V;'_SESSION'[SESSION];sessionUserid;gemAmbtenaarUserid~)
;{-sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld-}(I /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~)
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\gemAmbtenaarPassword;idmPassword~);idmDisplayName
   , "VOG Aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Aanvrager" : 
             {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
      BOX[ "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen"     : gbaVoornamen
         , "Geboortedatum" : gbaGeboortedatum
         , "BSN" : I
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      , "Aanvrager is geauthenticeerd m.b.v." : ((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~) \/ (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmSoort
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Ondertekening" : (I /\ gemOndertekeningDatum;gemOndertekeningDatum~); (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~ /\ gemAmbtenaarHandtekening;idmHandtekening~);(I /\ idmSoort;'GemeenteID';idmSoort~)
      BOX[ "Naam gemeenteambtenaar" : idmDisplayName
         , "Gemeente" : idmGemeente
         , "Plaats" : idmGemeentePlaats
      ]  ]
   ]
-----------------------------------------------------------
ENDCONTEXT
