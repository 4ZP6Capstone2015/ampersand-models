CONTEXT VOGAanvraag
------------------------------------------------------------
PATTERN "VOG Aanvraag"

--[Informatiebronnen ontsluiting]--

vogAanvragerDigiDUserid     :: VOGAanvraag * Gebruikersnaam [UNI]
vogAanvragerDigiDWachtwoord :: VOGAanvraag * Wachtwoord [UNI]

vogAanvragerIDMSoort        :: VOGAanvraag * IdentificatiemiddelSoort [UNI]
vogAanvragerIDMNummer       :: VOGAanvraag * IdentificatiemiddelNummer [UNI]
vogAanvragerIDVastgesteld   :: VOGAanvraag * JaNee [UNI]

--[1. Invullen door Aanvrager]--
vogAanvraagAanvragerDeel :: VOGAanvraag * VOGAanvraagAanvragerDeel [INJ,UNI]
--De aanvrager heeft geen KEY naar zijn VOGAanvragen

gaTelefoonnummer :: VOGAanvraagAanvragerDeel * Telefoonnummer [UNI] -- Meerdere telefoonnummers zouden mogelijk kunnen zijn
gaEmailadres :: VOGAanvraagAanvragerDeel * EmailAddr [UNI] -- Meerdere emailadressen zouden mogelijk kunnen zijn

-- 1.1 - Ondertekening door aanvrager
gaOndertekeningPlaats :: VOGAanvraagAanvragerDeel * Plaats [UNI]
gaOndertekeningDatum :: VOGAanvraagAanvragerDeel * Datum [UNI]
gaOndertekeningHandtekening :: VOGAanvraagAanvragerDeel * Handtekening [UNI]

--[2. Invullen door organisatie / instelling die de VOG verlangt]--
vogAanvraagOrganisatieDeel :: VOGAanvraag * VOGAanvraagOrganisatieDeel [UNI]
-- 2.1 - Gegevens organisatie-instelling"
orgVOGTemplateID :: VOGAanvraagOrganisatieDeel * OrgVOGTemplateID
KEY "Organisatie VOG templates": VOGAanvraagOrganisatieDeel(orgVOGTemplateID)

organisatieNaam :: VOGAanvraagOrganisatieDeel * VOGAanvraagOrganisatieDeelNaam [UNI]
organisatieVertegenwoordiger :: VOGAanvraagOrganisatieDeel * Geslachtsnaam [UNI]
orgStraatnaam :: VOGAanvraagOrganisatieDeel * Straatnaam [UNI]
orgHuisnummer :: VOGAanvraagOrganisatieDeel * Huisnummer [UNI]
orgPostcode :: VOGAanvraagOrganisatieDeel * Postcode [UNI]
orgPlaats :: VOGAanvraagOrganisatieDeel * Plaats [UNI]
orgLand :: VOGAanvraagOrganisatieDeel * Land [UNI]
organisatieTelefoonnummer :: VOGAanvraagOrganisatieDeel * Telefoonnummer [UNI]
-- 2.2 Doel van de aanvraag
werkrelatie :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
functie :: VOGAanvraagOrganisatieDeel * Functie [UNI]
taakomschrijving :: VOGAanvraagOrganisatieDeel * Taakomschrijving [UNI]
overig :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
overigOmschrijving :: VOGAanvraagOrganisatieDeel * OverigOmschrijving [UNI]
-- 2.3 Specifiek screeningsprofiel
specifiekScreeningsProfielRelevant :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
specifiekScreeningsProfiel :: VOGAanvraagOrganisatieDeel * SpecifiekScreeningsProfiel [UNI]
-- 2.4 Algemeen screeningsprofiel
algemeenScreeningsProfiel :: VOGAanvraagOrganisatieDeel * AlgemeenScreeningsProfiel -- hier mogen meerdere van worden gekozen
-- 2.5 Bijzondere omstandigheden m.b.t. de aanvraag
orgErZijnBijzondereOmstandigheden :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
orgErZijnBijzondereOmstandighedenToelichting :: VOGAanvraagOrganisatieDeel * Toelichting [UNI]
-- 2.6 - Ondertekening door organisatie/instelling
orgOndertekeningPlaats :: VOGAanvraagOrganisatieDeel * Plaats [UNI]
orgOndertekeningDatum :: VOGAanvraagOrganisatieDeel * Datum [UNI]
orgOndertekeningHandtekening :: VOGAanvraagOrganisatieDeel * Handtekening [UNI]

--[3. Invullen door gemeente]--
vogAanvraagGemeenteDeel :: VOGAanvraag * VOGAanvraagGemeenteDeel [INJ,UNI]
KEY "VOG aanvragen door gemeentes": VOGAanvraagGemeenteDeel(gemeenteAanvraagnummer)
gemeenteAanvraagnummer :: VOGAanvraagGemeenteDeel -> GemeenteAanvraagnummer

bijzonderhedenGeconstateerd :: VOGAanvraagGemeenteDeel * JaNee [UNI]
bijzonderhedenGeconstateerdToelichting :: VOGAanvraagGemeenteDeel * BijzonderhedenGeconstateerdToelichting [UNI]
persisterenInDeAanvraag :: VOGAanvraagGemeenteDeel * JaNee [UNI]
persisterenInDeAanvraagToelichting :: VOGAanvraagGemeenteDeel * PersisterenInDeAanvraagToelichting [UNI]
covogOmAdviesGevraagd :: VOGAanvraagGemeenteDeel * JaNee [UNI]
covogOmAdviesGevraagdToelichting :: VOGAanvraagGemeenteDeel * COVOGOmAdviesGevraagdToelichting [UNI]

naamGemeenteambtenaar :: VOGAanvraagGemeenteDeel * NaamGemeenteambtenaar [UNI]
gemGemeente :: VOGAanvraagGemeenteDeel * Gemeente [UNI]
gemOndertekeningPlaats :: VOGAanvraagGemeenteDeel * Plaats [UNI]
gemOndertekeningDatum :: VOGAanvraagGemeenteDeel * Datum [UNI]
gemOndertekeningHandtekening :: VOGAanvraagGemeenteDeel * Handtekening [UNI]

RULE "Screeningsprofielen uitsluiten": specifiekScreeningsProfiel;specifiekScreeningsProfiel~ /\ algemeenScreeningsProfiel;algemeenScreeningsProfiel~ |- -V
MEANING "Alleen als u bij vraag 2.3 geen screeningsprofiel heeft aangekruist, kruist u bij vraag 2.4 de aspecten aan bij de functie/taak of ander doel" -- Aanvraagformulier, Toelichting bij 2.4

RULE "Integriteit functie-taakomschrijving": I /\ werkrelatie;'Ja'[JaNee];werkrelatie~ |- functie;functie~ /\ taakomschrijving;taakomschrijving~
MEANING "Als de aanvraag een werkrelatie betreft, moet de functie en taakomschrijving zijn opgegeven"
MESSAGE "U moet de functie en/of taakomschrijving opgeven"

RULE "Integriteit overig": I /\ overig;'Ja'[JaNee];overig~ |- overigOmschrijving;overigOmschrijving~
MEANING "Als de aanvraag geen werkrelatie (dus: overig) betreft, moet het doel zijn omschreven"
MESSAGE "U moet het doel nog invullen (bij 'Omschrijving') "

RULE "Werkrelatie is niet 'overig'": werkrelatie;'Ja';overig~ |- -I
MEANING "De aanvraag mag niet zowel een werkrelatie als 'overig' betreffen"
MESSAGE "U mag niet zowel 'werkrelatie' als 'overig' aankruisen (met 'Ja' beantwoorden)"

RULE "Beedigd tolk/vertaler aanvraag integriteit": I /\ specifiekScreeningsProfiel;'80 Beëdigd tolken/vertalers';specifiekScreeningsProfiel~ |- organisatieNaam;'Raad voor Rechtsbijstand';organisatieNaam~ --Bron: VOG Aanvraag formulier
MEANING "Het profiel 'Beëdigd tolken/vertalers' mag alleen worden gebruikt door de 'Raad voor Rechtsbijstand'"

RULE "Integriteit van specifieke screeningsprofielen invullen": specifiekScreeningsProfielRelevant;'Ja';specifiekScreeningsProfielRelevant~ |- specifiekScreeningsProfiel;specifiekScreeningsProfiel~
MEANING "Als de aanvraag heeft betrekking heeft op één van de specifieke screeningsprofielen, dan moet dit ook zijn gespecificeerd"
MESSAGE "U moet een specifiek screeningsprofiel kiezen"

RULE "Specifieke screeningsprofielen": I[SpecifiekScreeningsProfiel] = 
    '01 Politieke ambtsdragers'
 \/ '06 Visum en emigratie'
 \/ '25 (Buitengewoon) opsporingsambtenaar'
 \/ '40 Vakantiegezinnen en adoptie'
 \/ '45 Gezondheidszorg en welzijn van mens en dier'
 \/ '50 Exploitatievergunning'
 \/ '55 Juridische dienstverlening'
 \/ '60 Onderwijs'
 \/ '65 Taxibranche; chauffeurskaart'
 \/ '70 Taxibranche; ondernemersvergunning'
 \/ '75 (Gezins)voogd bij voogdijinstellingen, reclasseringswerker, raadsonderzoeker en maatschappelijk werker'
 \/ '80 Beëdigd tolken/vertalers' --Dit screeningsprofiel kan alleen worden gebruikt als de Raad voor Rechtsbijstand de organisatie is die de VOG verlangt
 \/ '85 Lidmaatschap schietvereniging'
 \/ '95 Financiële dienstverlening'
MEANING "Een specifiek screeningsprofiel moet worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

RULE "Algemene screeningsprofielen": I[AlgemeenScreeningsProfiel] = 
-- Informatie
    '11 Bevoegdheid hebben tot het raadplegen en/of bewerken van systemen'
 \/ '12 Met gevoelige/vertrouwelijke informatie omgaan'
 \/ '13 Kennis dragen van veiligheidssystemen, controlemechanismen en verificatieprocessen'
-- Geld
 \/ '21 Met contante en/of girale gelden en/of (digitale) waardepapieren omgaan'
 \/ '22 Budgetbevoegdheid hebben'
-- Goederen
 \/ '36 Het bewaken van productieprocessen'
 \/ '37 Het beschikken over goederen'
 \/ '38 Het voorhanden hebben van stoffen, objecten en voorwerpen e.d., die bij oneigenlijk of onjuist gebruik een risico vormen voor mens (en dier)'
-- Diensten
 \/ '41 Het verlenen van diensten (advies, beveiliging, schoonmaak, catering, onderhoud, etc)'
 \/ '43 Het verlenen van diensten in de persoonlijke leefomgeving'
-- Zakelijke transacties
 \/ '53 Beslissen over offertes (het voeren van onderhandelingen en het afsluiten van contracten) en het doen van aanbestedingen'
-- Proces
 \/ '61 Het onderhouden/ombouwen/bedienen van (productie)machines en/of apparaten, voertuigen en/of luchtvaartuigen'
 \/ '62 (Rijdend) vervoer waarbij goederen, producten, post en pakketten worden getransporteerd en/of bezorgd, anders dan het intern transport binnen een bedrijf'
 \/ '63 (Rijdend) vervoer waarbij personen worden vervoerd'
-- Aansturen organisatie
 \/ '71 Personen die vanuit hun functie mensen en/of een organisatie (of een deel daarvan) aansturen'
-- Personen
 \/ '84 Belast zijn met de zorg voor minderjarigen'
 \/ '85 Belast zijn met de zorg voor (hulpbehoevende) personen, zoals ouderen en gehandicapten'
MEANING "Een algemeen screeningsprofiel mag alleen worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

RULE "Integriteit toelichting bijzonderheden geconstateerd": I /\ bijzonderhedenGeconstateerd;'Ja'[JaNee];bijzonderhedenGeconstateerd~ = I /\ bijzonderhedenGeconstateerdToelichting;bijzonderhedenGeconstateerdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

RULE "Integriteit toelichting persisteren in de aanvraag": I /\ persisterenInDeAanvraag;'Ja'[JaNee];persisterenInDeAanvraag~ = I /\ persisterenInDeAanvraagToelichting;persisterenInDeAanvraagToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

RULE "Integriteit toelichting COVOG om advies gevraagd": I /\ covogOmAdviesGevraagd;'Ja'[JaNee];covogOmAdviesGevraagd~ = I /\  covogOmAdviesGevraagdToelichting;covogOmAdviesGevraagdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

ENDPATTERN
------------------------------------------------------------
PROCESS "VOG Aanvraag (Organisatie)"

RULE "Organisatiegegevens invullen": I |- 
  vogAanvraagOrganisatieDeel;{-sigOrganisatiegegevensZijnIngevuld-}(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
MEANING "Alle gegevens betreffende de organisatie zijn volledig ingevuld."
MESSAGE "Er ontbreken nog gegevens betreffende de organisatie."
ROLE Organisatie MAINTAINS "Organisatiegegevens invullen"

RULE "Organisatiegegevens ondertekenen": I |- vogAanvraagOrganisatieDeel;{-sigOrganisatieHeeftGetekend-}(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
MEANING "De (vertegenwoordiger van de) organisatie heeft voor de gegevens betreffende de organisatie getekend."
MESSAGE "De (vertegenwoordiger van de) organisatie moet nog tekenen."
ROLE Organisatie MAINTAINS "Organisatiegegevens ondertekenen"

ENDPROCESS
------------------------------------------------------------
PROCESS "VOG Aanvraag (Aanvrager)"

RULE "Aanvragergegevens invullen": I[VOGAanvraag] |- 
  {-vogBSNAanvrager-}(((vogAanvragerDigiDUserid;idmUsername~ /\ vogAanvragerDigiDWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~);V
MEANING "Alle gegevens betreffende de aanvrager zijn volledig bekend."
MESSAGE "Er ontbreken nog gegevens betreffende de aanvrager."
ROLE Aanvrager MAINTAINS "Aanvragergegevens invullen"

RULE "Aanvragergegevens ondertekenen": I |- vogAanvraagAanvragerDeel;{-sigAanvragerHeeftGetekend-}(gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~);vogAanvraagAanvragerDeel~
MEANING "De aanvrager heeft voor de gegevens betreffende hemzelf getekend."
MESSAGE "De aanvrager moet nog tekenen."
ROLE Aanvrager MAINTAINS "Aanvragergegevens ondertekenen"

ENDPROCESS
------------------------------------------------------------
PROCESS "VOG Aanvraag (Gemeente)"

RULE "Integriteit van identificatimiddel specificatie": 
(  (vogAanvragerDigiDUserid;idmUsername~ /\ vogAanvragerDigiDWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~)
   ;idmHouder;gbaBSN~
)~ ;I[VOGAanvraag];
(
   (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
   ;(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~)
   ;idmHouder;gbaBSN~
) |- I
MEANING "Wanneer de identiteit van de aanvrager door zowel de gemeente als via DigiD is vastgesteld, dan moeten die met elkaar overeenstemmen."

RULE "Integriteit van het vaststellen van de aanvrager identiteit": vogAanvragerIDVastgesteld;'Ja' |- (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~);idmHouder;gbaBSN~;V;'Ja'[JaNee]
MEANING "De identiteit van een aanvrager kan alleen zijn vastgesteld als het middel op basis waarvan die identiteit is vastgesteld, is geregistreerd"
MESSAGE "U moet het identificatiemiddel eerst registreren"

RULE "Identiteit van aanvrager vaststellen": I /\ vogAanvraagGemeenteDeel;vogAanvraagGemeenteDeel~ |- vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~
MEANING "Als de gemeente betrokken is bij de VOG aanvraag, dan moet zij de identiteit van de aanvrager met voldoende zekerheid hebben vastgesteld (art XXX)"
MESSAGE "De identiteit van de aanvrager moet nog worden vastgesteld"
ROLE Gemeente MAINTAINS "Identiteit van aanvrager vaststellen"

RULE "Gemeentegegevens invullen": I |- {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~)
MEANING "Alle gegevens betreffende de gemeente zijn volledig ingevuld."
MESSAGE "Er ontbreken nog gegevens betreffende de gemeente."
ROLE Gemeente MAINTAINS "Gemeentegegevens invullen"

RULE "Gemeentegegevens ondertekenen": I |- {-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~)
MEANING "De gemeente heeft voor het door hen in te vullen onderdeel getekend."
MESSAGE "De gemeente moet nog tekenen."
ROLE Gemeente MAINTAINS "Gemeentegegevens ondertekenen"

RULE "VOG Aanvraag completeren": I |- {-sigAanvraagIsVolledig-}
 (
   {-vogBSNAanvrager-}(((vogAanvragerDigiDUserid;idmUsername~ /\ vogAanvragerDigiDWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~);V
{- Het is niet nodig dat de aanvrager ondertekent als zijn identiteit met DigiD is vastgesteld en de gegevens uit de GBA komen
   /\ vogAanvraagAanvragerDeel;[-sigAanvragerHeeftGetekend-](gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~);vogAanvraagAanvragerDeel~
-}
   /\ vogAanvraagOrganisatieDeel;{-sigOrganisatiegegevensZijnIngevuld-}(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
   /\ vogAanvraagOrganisatieDeel;{-sigOrganisatieHeeftGetekend-}(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
   /\ vogAanvraagGemeenteDeel;{-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~);vogAanvraagGemeenteDeel~
 ) 
MEANING "Het aanvraagformulier is volledig ingevuld" -- artikel xxxx
MESSAGE "Het aanvraagformulier is nog niet volledig ingevuld"
ROLE Gemeente MAINTAINS "VOG Aanvraag completeren"

RULE "Identiteit vaststellen": I /\ vogAanvraagGemeenteDeel;vogAanvraagGemeenteDeel~ |- vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~
MEANING "De Burgemeester moet de identiteit van  aanvragers met voldoende zekerheid zijn vastgesteld" -- artikel xxx
MESSAGE "De identiteit van de aanvrager moet nog (met voldoende zekerheid) worden vastgesteld"
ROLE Gemeente MAINTAINS "Identiteit vaststellen"

-- Gemeente mag alleen tekenen als het (a) volledigheid van invullen heeft vastgesteld en (b) de identiteit van de aanvrager met voldoende zekerheid heeft vastgesteld (art xxxx)
RULE "Gemeentedeel ondertekenen":
   {-sigAanvraagIsVolledig-}(
     {-vogBSNAanvrager-}(((vogAanvragerDigiDUserid;idmUsername~ /\ vogAanvragerDigiDWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~);V
{- Het is niet nodig dat de aanvrager ondertekent als zijn identiteit met DigiD is vastgesteld en de gegevens uit de GBA komen
   /\ vogAanvraagAanvragerDeel;[-sigAanvragerHeeftGetekend-](gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~);vogAanvraagAanvragerDeel~
-}
     /\ vogAanvraagOrganisatieDeel;{-sigOrganisatiegegevensZijnIngevuld-}(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
     /\ vogAanvraagOrganisatieDeel;{-sigOrganisatieHeeftGetekend-}(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
     /\ vogAanvraagGemeenteDeel;{-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~);vogAanvraagGemeenteDeel~
   ) 
/\ {-sigIdentiteitIsVastgesteld-}(I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~) 
|- vogAanvraagGemeenteDeel;{-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~);vogAanvraagGemeenteDeel~
MEANING "De gemeente mag alleen ondertekenen als (a) de aanvraag volledig is ingevuld en (b) de identiteit van de aanvrager met voldoende zekerheid is vastgesteld (art xxxx)"
MESSAGE "De gemeente moet de aanvraag ondertekenen"
ROLE Gemeente MAINTAINS "Gemeentedeel ondertekenen"

RULE "Gemeente ondertekening integriteit": 
   vogAanvraagGemeenteDeel;{-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~);vogAanvraagGemeenteDeel~ 
|- {-sigAanvraagIsVolledig-}
   (
     {-vogBSNAanvrager-}(((vogAanvragerDigiDUserid;idmUsername~ /\ vogAanvragerDigiDWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~);V
{- Het is niet nodig dat de aanvrager ondertekent als zijn identiteit met DigiD is vastgesteld en de gegevens uit de GBA komen
   /\ vogAanvraagAanvragerDeel;[-sigAanvragerHeeftGetekend-](gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~);vogAanvraagAanvragerDeel~
-}
     /\ vogAanvraagOrganisatieDeel;{-sigOrganisatiegegevensZijnIngevuld-}(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
     /\ vogAanvraagOrganisatieDeel;{-sigOrganisatieHeeftGetekend-}(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
     /\ vogAanvraagGemeenteDeel;{-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~);vogAanvraagGemeenteDeel~
   ) 
/\ {-sigIdentiteitIsVastgesteld-}(I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~) 
MEANING "Als de gemeente kan alleen dan tekenen als de aanvraag volledig is."

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT