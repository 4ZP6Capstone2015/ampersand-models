CONTEXT VOGAanvraag
------------------------------------------------------------
PATTERN "VOG Aanvraag"

vogAanvragerIDMSoort        :: VOGAanvraag * IdentificatiemiddelSoort [UNI]
vogAanvragerIDMNummer       :: VOGAanvraag * IdentificatiemiddelNummer [UNI]
vogAanvragerIDVastgesteld   :: VOGAanvraag * JaNee [UNI]

RULE "Toegelaten identificatiemiddelen": vogAanvragerIDMSoort~;vogAanvragerIDMSoort |- 'Paspoort' \/ 'ID-kaart' \/ 'Rijbewijs'
MEANING "Voor het identificeren van de aanvrager met fysieke middelen zijn toegestaan: Paspoort, ID-kaart, Rijbewijs, DigiD (art xxxx wet op de identificatie of zo?)"
MESSAGE "U mag hier alleen 'Paspoort', 'ID-kaart' of 'Rijbewijs' invullen (art xxxx Wyyy)."

RULE "Integriteit van het vaststellen van de aanvrager identiteit": vogAanvragerIDVastgesteld;'Ja' |- (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~);idmHouder;gbaBSN~;V;'Ja'[JaNee]
MEANING "De identiteit van een aanvrager kan alleen zijn vastgesteld als het middel op basis waarvan die identiteit is vastgesteld, is geregistreerd"
MESSAGE "Het identificatiemiddel is niet geregistreerd. Heeft u misschien een typefout gemaakt?"

vogAanvragerUserid     :: VOGAanvraag * Gebruikersnaam [UNI]
vogAanvragerWachtwoord :: VOGAanvraag * Wachtwoord [UNI]

RULE "Integriteit van identificatimiddel specificatie": 
(  (vogAanvragerUserid;idmUsername~ /\ vogAanvragerWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~)
   ;idmHouder;gbaBSN~
)~ ;I[VOGAanvraag];
(
   (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
   ;(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~)
   ;idmHouder;gbaBSN~
) |- I
MEANING "Wanneer de identiteit van de aanvrager door zowel de gemeente als via DigiD is vastgesteld, dan moeten die met elkaar overeenstemmen."

aanvragerTelefoonnummer :: VOGAanvraag * Telefoonnummer [UNI] -- Meerdere telefoonnummers zouden mogelijk kunnen zijn
aanvragerEmailadres :: VOGAanvraag * EmailAddr [UNI] -- Meerdere emailadressen zouden mogelijk kunnen zijn

aanvragerOndertekeningPlaats :: VOGAanvraag * Plaats [UNI]
aanvragerOndertekeningDatum :: VOGAanvraag * Datum [UNI]
aanvragerOndertekeningHandtekening :: VOGAanvraag * Handtekening [UNI]

--Regels m.b.t. de ondertekening zitten in het proces

vogAanvraagOrganisatieTemplate :: VOGAanvraag * VOGAanvraagOrganisatieTemplate [UNI]

ENDPATTERN
-----------------------------------------------------------
PATTERN "Gemeentelijke VOG zaken"

KEY "VOG aanvragen door gemeentes": GemeentelijkeVOGZaak(gemeenteAanvraagnummer)
gemeenteAanvraagnummer :: GemeentelijkeVOGZaak -> GemeenteAanvraagnummer

gemVOGAanvraag :: GemeentelijkeVOGZaak -> VOGAanvraag [INJ,UNI]

bijzonderhedenGeconstateerd :: GemeentelijkeVOGZaak * JaNee [UNI]
bijzonderhedenGeconstateerdToelichting :: GemeentelijkeVOGZaak * BijzonderhedenGeconstateerdToelichting [UNI]

RULE "Integriteit toelichting bijzonderheden geconstateerd": I /\ bijzonderhedenGeconstateerd;'Ja'[JaNee];bijzonderhedenGeconstateerd~ = I /\ bijzonderhedenGeconstateerdToelichting;bijzonderhedenGeconstateerdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

persisterenInDeAanvraag :: GemeentelijkeVOGZaak * JaNee [UNI]
persisterenInDeAanvraagToelichting :: GemeentelijkeVOGZaak * PersisterenInDeAanvraagToelichting [UNI]

RULE "Integriteit toelichting persisteren in de aanvraag": I /\ persisterenInDeAanvraag;'Ja'[JaNee];persisterenInDeAanvraag~ = I /\ persisterenInDeAanvraagToelichting;persisterenInDeAanvraagToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

covogOmAdviesGevraagd :: GemeentelijkeVOGZaak * JaNee [UNI]
covogOmAdviesGevraagdToelichting :: GemeentelijkeVOGZaak * COVOGOmAdviesGevraagdToelichting [UNI]

RULE "Integriteit toelichting COVOG om advies gevraagd": I /\ covogOmAdviesGevraagd;'Ja'[JaNee];covogOmAdviesGevraagd~ = I /\  covogOmAdviesGevraagdToelichting;covogOmAdviesGevraagdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

gemAmbtenaarUserid     :: GemeentelijkeVOGZaak * Gebruikersnaam [UNI]
gemAmbtenaarWachtwoord :: GemeentelijkeVOGZaak * Wachtwoord [UNI]
gemOndertekeningDatum  :: GemeentelijkeVOGZaak * Datum [UNI]

RULE "Ondertekenen door gemeenteambtenaar kan alleen met een 'GemeenteID'": (gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~) |- (gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);idmSoort;'GemeenteID';idmSoort~
MEANING "Ambtenaren mogen alleen inloggen met hun (persoonlijke) GemeenteID"
MESSAGE "U moet uw GemeenteID gebruiken."
 
RULE "Gemeente aanvraag integriteit": 
  ({-gemGemeente-}
    ((gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~);idmGemeente)
  )~;gemVOGAanvraag;{-vogBSNAanvrager-}
       (((vogAanvragerUserid;idmUsername~ /\ vogAanvragerWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
   ;gbaGemeenteVanInschrijving
|- I[Gemeente]
MEANING "Een VOG aanvraag kan alleen bij de gemeente worden ingediend waar de aanvrager volgens de GBA is ingeschreven"
MESSAGE "De aanvrager is niet ingeschreven in deze gemeente (art xxx)"

RULE "Controle op volledigheid en aanvrageridentiteit":
   {-sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld-}(I /\ (gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~);(gemAmbtenaarUserid;idmUsername~ /\ gemAmbtenaarWachtwoord;idmPassword~)~)
|-
   gemVOGAanvraag;{-sigAanvraagIsVolledig-}
     (  
        {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUsername~ /\ vogAanvragerWachtwoord;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~);V
     /\ 
        vogAanvraagOrganisatieTemplate;{-sigOrganisatiegegevensZijnIngevuld-}(orgNaam;orgNaam~ /\ orgVertegenwoordiger;orgVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ orgTelefoonnummer;orgTelefoonnummer~);vogAanvraagOrganisatieTemplate~
     /\ 
        vogAanvraagOrganisatieTemplate;{-sigOrganisatieHeeftGetekend-}(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieTemplate~
     )
   ;gemVOGAanvraag~
   /\ 
   {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ 
     /\ gemVOGAanvraag;(I /\ vogAanvraagOrganisatieTemplate;vogAanvraagOrganisatieTemplate~);gemVOGAanvraag~
     /\ bijzonderhedenGeconstateerd;bijzonderhedenGeconstateerd~
     /\ persisterenInDeAanvraag;persisterenInDeAanvraag~
     /\ covogOmAdviesGevraagd;covogOmAdviesGevraagd~)
MEANING "De ambtenaar mag alleen tekenen nadat de volledigheid van de aanvraag is vastgesteld en er voldoende zekerheid is betreffende de identiteit van de aanvrager (art xxx)"
MESSAGE "U mag pas tekenen als de aanvraag volledig is ingevuld"

--Regels m.b.t. de ondertekening zitten in het proces

ENDPATTERN
-----------------------------------------------------------
PATTERN "VOG Aanvraag templates"

orgVOGTemplateID :: VOGAanvraagOrganisatieTemplate * OrgVOGTemplateID
KEY "Organisatie VOG templates": VOGAanvraagOrganisatieTemplate(orgVOGTemplateID)

-- 2.1 Gegevens organisatie / instelling
--?Deze, of althans, de meeste van ondertaande gegevens, zouden uit het NHR kunnen komen.
orgNaam :: VOGAanvraagOrganisatieTemplate * VOGAanvraagOrganisatieTemplateNaam [UNI]
orgVertegenwoordiger :: VOGAanvraagOrganisatieTemplate * Geslachtsnaam [UNI]
orgStraatnaam :: VOGAanvraagOrganisatieTemplate * Straatnaam [UNI]
orgHuisnummer :: VOGAanvraagOrganisatieTemplate * Huisnummer [UNI]
orgPostcode :: VOGAanvraagOrganisatieTemplate * Postcode [UNI]
orgPlaats :: VOGAanvraagOrganisatieTemplate * Plaats [UNI]
orgLand :: VOGAanvraagOrganisatieTemplate * Land [UNI]
orgTelefoonnummer :: VOGAanvraagOrganisatieTemplate * Telefoonnummer [UNI]

-- 2.2 Doel van de aanvraag
werkrelatie :: VOGAanvraagOrganisatieTemplate * JaNee [UNI]
functie :: VOGAanvraagOrganisatieTemplate * Functie [UNI]
taakomschrijving :: VOGAanvraagOrganisatieTemplate * Taakomschrijving [UNI]

RULE "Integriteit functie-taakomschrijving": I /\ werkrelatie;'Ja'[JaNee];werkrelatie~ |- functie;functie~ /\ taakomschrijving;taakomschrijving~
MEANING "Als de aanvraag een werkrelatie betreft, moet de functie en taakomschrijving zijn opgegeven"
MESSAGE "U moet de functie en/of taakomschrijving opgeven"

overig :: VOGAanvraagOrganisatieTemplate * JaNee [UNI]
overigOmschrijving :: VOGAanvraagOrganisatieTemplate * OverigOmschrijving [UNI]

RULE "Integriteit overig": I /\ overig;'Ja'[JaNee];overig~ |- overigOmschrijving;overigOmschrijving~
MEANING "Als de aanvraag geen werkrelatie (dus: overig) betreft, moet het doel zijn omschreven"
MESSAGE "U moet het doel nog invullen (bij 'Omschrijving') "

RULE "Werkrelatie is niet 'overig'": werkrelatie;'Ja';overig~ |- -I
MEANING "De aanvraag mag niet zowel een werkrelatie als 'overig' betreffen"
MESSAGE "U mag niet zowel 'werkrelatie' als 'overig' aankruisen (met 'Ja' beantwoorden)"

-- 2.3 Specifiek screeningsprofiel
specifiekScreeningsProfielRelevant :: VOGAanvraagOrganisatieTemplate * JaNee [UNI]
specifiekScreeningsProfiel :: VOGAanvraagOrganisatieTemplate * SpecifiekScreeningsProfiel [UNI]

RULE "Beedigd tolk/vertaler aanvraag integriteit": I /\ specifiekScreeningsProfiel;'80 Beëdigd tolken/vertalers';specifiekScreeningsProfiel~ |- orgNaam;'Raad voor Rechtsbijstand';orgNaam~ --Bron: VOG Aanvraag formulier
MEANING "Het profiel 'Beëdigd tolken/vertalers' mag alleen worden gebruikt door de 'Raad voor Rechtsbijstand'"

RULE "Integriteit van specifieke screeningsprofielen invullen": specifiekScreeningsProfielRelevant;'Ja';specifiekScreeningsProfielRelevant~ |- specifiekScreeningsProfiel;specifiekScreeningsProfiel~
MEANING "Als de aanvraag heeft betrekking heeft op één van de specifieke screeningsprofielen, dan moet dit ook zijn gespecificeerd"
MESSAGE "U moet een specifiek screeningsprofiel kiezen"

RULE "Specifieke screeningsprofielen": I[SpecifiekScreeningsProfiel] = 
    '01 Politieke ambtsdragers'
 \/ '06 Visum en emigratie'
 \/ '25 (Buitengewoon) opsporingsambtenaar'
 \/ '40 Vakantiegezinnen en adoptie'
 \/ '45 Gezondheidszorg en welzijn van mens en dier'
 \/ '50 Exploitatievergunning'
 \/ '55 Juridische dienstverlening'
 \/ '60 Onderwijs'
 \/ '65 Taxibranche; chauffeurskaart'
 \/ '70 Taxibranche; ondernemersvergunning'
 \/ '75 (Gezins)voogd bij voogdijinstellingen, reclasseringswerker, raadsonderzoeker en maatschappelijk werker'
 \/ '80 Beëdigd tolken/vertalers' --Dit screeningsprofiel kan alleen worden gebruikt als de Raad voor Rechtsbijstand de organisatie is die de VOG verlangt
 \/ '85 Lidmaatschap schietvereniging'
 \/ '95 Financiële dienstverlening'
MEANING "Een specifiek screeningsprofiel moet worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

-- 2.4 Algemeen screeningsprofiel

algemeenScreeningsProfiel :: VOGAanvraagOrganisatieTemplate * AlgemeenScreeningsProfiel -- hier mogen meerdere van worden gekozen

RULE "Screeningsprofielen uitsluiten": specifiekScreeningsProfiel;specifiekScreeningsProfiel~ /\ algemeenScreeningsProfiel;algemeenScreeningsProfiel~ |- -V
MEANING "Alleen als u bij vraag 2.3 geen screeningsprofiel heeft aangekruist, kruist u bij vraag 2.4 de aspecten aan bij de functie/taak of ander doel" -- Aanvraagformulier, Toelichting bij 2.4

RULE "Algemene screeningsprofielen": I[AlgemeenScreeningsProfiel] = 
-- Informatie
    '11 Bevoegdheid hebben tot het raadplegen en/of bewerken van systemen'
 \/ '12 Met gevoelige/vertrouwelijke informatie omgaan'
 \/ '13 Kennis dragen van veiligheidssystemen, controlemechanismen en verificatieprocessen'
-- Geld
 \/ '21 Met contante en/of girale gelden en/of (digitale) waardepapieren omgaan'
 \/ '22 Budgetbevoegdheid hebben'
-- Goederen
 \/ '36 Het bewaken van productieprocessen'
 \/ '37 Het beschikken over goederen'
 \/ '38 Het voorhanden hebben van stoffen, objecten en voorwerpen e.d., die bij oneigenlijk of onjuist gebruik een risico vormen voor mens (en dier)'
-- Diensten
 \/ '41 Het verlenen van diensten (advies, beveiliging, schoonmaak, catering, onderhoud, etc)'
 \/ '43 Het verlenen van diensten in de persoonlijke leefomgeving'
-- Zakelijke transacties
 \/ '53 Beslissen over offertes (het voeren van onderhandelingen en het afsluiten van contracten) en het doen van aanbestedingen'
-- Proces
 \/ '61 Het onderhouden/ombouwen/bedienen van (productie)machines en/of apparaten, voertuigen en/of luchtvaartuigen'
 \/ '62 (Rijdend) vervoer waarbij goederen, producten, post en pakketten worden getransporteerd en/of bezorgd, anders dan het intern transport binnen een bedrijf'
 \/ '63 (Rijdend) vervoer waarbij personen worden vervoerd'
-- Aansturen organisatie
 \/ '71 Personen die vanuit hun functie mensen en/of een organisatie (of een deel daarvan) aansturen'
-- Personen
 \/ '84 Belast zijn met de zorg voor minderjarigen'
 \/ '85 Belast zijn met de zorg voor (hulpbehoevende) personen, zoals ouderen en gehandicapten'
MEANING "Een algemeen screeningsprofiel mag alleen worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

-- 2.5 Bijzondere omstandigheden m.b.t. de aanvraag

orgErZijnBijzondereOmstandigheden :: VOGAanvraagOrganisatieTemplate * JaNee [UNI]
orgErZijnBijzondereOmstandighedenToelichting :: VOGAanvraagOrganisatieTemplate * Toelichting [UNI]

RULE "Integriteit toelichting bijzondere omstandigheden m.b.t. de aanvraag": I /\ orgErZijnBijzondereOmstandigheden;'Ja'[JaNee];orgErZijnBijzondereOmstandigheden~ = I /\ orgErZijnBijzondereOmstandighedenToelichting;orgErZijnBijzondereOmstandighedenToelichting~
MEANING "Als er bijzondere omstandigheden m.b.t. de aanvraag zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzondere omstandigheden m.b.t. de aanvraag zijn. Dat moet u nog toelichten"

-- 2.6 - Ondertekening door organisatie/instelling

orgOndertekeningPlaats :: VOGAanvraagOrganisatieTemplate * Plaats [UNI]
orgOndertekeningDatum :: VOGAanvraagOrganisatieTemplate * Datum [UNI]
orgOndertekeningHandtekening :: VOGAanvraagOrganisatieTemplate * Handtekening [UNI]

--Regels m.b.t. de ondertekening zitten in het proces

ENDPATTERN
------------------------------------------------------------
ENDCONTEXT