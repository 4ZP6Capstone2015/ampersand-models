CONTEXT VOGAanvraag
------------------------------------------------------------
PROCESS "VOG Aanvraag invullen"

gemAanvragerID :: VOGAanvraag * Identificatiemiddel [UNI]

gaDigiDGebruikersnaam :: VOGAanvraag * Gebruikersnaam [UNI]
gaDigiDWachtwoord :: VOGAanvraag * Wachtwoord [UNI]

orgIdMiddelSoort :: VOGAanvraag * IdentificatiemiddelSoort [UNI]
orgIdMiddelNummer :: VOGAanvraag * IdentificatiemiddelNummer [UNI]
orgIdMiddelHouder :: VOGAanvraag * BurgerServiceNummer [UNI]

RULE "Integriteit van identificatimiddel specificatie": I /\ orgIdMiddelSoort;orgIdMiddelSoort~ /\ orgIdMiddelNummer;orgIdMiddelNummer~ /\ orgIdMiddelHouder;orgIdMiddelHouder~ |- I /\ (orgIdMiddelSoort;idMiddelSoort~ /\ orgIdMiddelNummer;idMiddelNummer~);idMiddelHouder;orgIdMiddelHouder~
MEANING "Als zowel het identificatiemiddel en (de BSN van) de houder zijn opgegeven, dan moeten de met elkaar overeenstemmen"
MESSAGE "Het gespecificeerde identificatiemiddel hoort niet bij de opgegeven houder"

--!allerlei regels ontbreken nog, met betrekking tot equivalentie van identificatiemiddelen, en dus ook het doorgeven van forumulieren aan de diverse instanties

RULE "Aanvragergegevens invullen": I[VOGAanvraag] |- {-sigAanvragergegevensZijnIngevuld-}((gaDigiDGebruikersnaam;idMiddelUsername~ /\ gaDigiDWachtwoord;idMiddelPassword~);(I /\ idMiddelSoort;'DigiD';idMiddelSoort~);idMiddelHouder;gbaBSN~;V) -- Later uitbreiden met de wijze waarop de gemeente de BSN vaststelt 
MEANING "Alle gegevens betreffende de aanvrager zijn volledig bekend."
MESSAGE "Er ontbreken nog gegevens betreffende de aanvrager."
ROLE Aanvrager, Gemeente MAINTAINS "Aanvragergegevens invullen"

RULE "Aanvragergegevens ondertekenen": I |- {-sigAanvragerHeeftGetekend-}(gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~) 
MEANING "De aanvrager heeft voor de gegevens betreffende hemzelf getekend."
MESSAGE "De aanvrager moet nog tekenen."
ROLE Aanvrager MAINTAINS "Aanvragergegevens ondertekenen"

RULE "Organisatiegegevens invullen": I |- {-sigOrganisatiegegevensZijnIngevuld-}vogAanvraagOrganisatieDeel;(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
MEANING "Alle gegevens betreffende de organisatie zijn volledig ingevuld."
MESSAGE "Er ontbreken nog gegevens betreffende de organisatie."
ROLE Aanvrager, Organisatie MAINTAINS "Organisatiegegevens invullen"

RULE "Organisatiegegevens ondertekenen": I |- {-sigOrganisatieHeeftGetekend-}vogAanvraagOrganisatieDeel;(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
MEANING "De (vertegenwoordiger van de) organisatie heeft voor de gegevens betreffende de organisatie getekend."
MESSAGE "De (vertegenwoordiger van de) organisatie moet nog tekenen."
ROLE Organisatie MAINTAINS "Organisatiegegevens ondertekenen"

RULE "Gemeentegegevens invullen": I |- {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~)
MEANING "Alle gegevens betreffende de gemeente zijn volledig ingevuld."
MESSAGE "Er ontbreken nog gegevens betreffende de gemeente."
ROLE Gemeente MAINTAINS "Gemeentegegevens invullen"

RULE "Gemeentegegevens ondertekenen": I |- {-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~)
MEANING "De gemeente heeft voor het door hen in te vullen onderdeel getekend."
MESSAGE "De gemeente moet nog tekenen."
ROLE Gemeente MAINTAINS "Gemeentegegevens ondertekenen"

RULE "VOG Aanvraag completeren": I |- {-sigAaanvraagIsVolledig-}
   ({-sigAanvragergegevensZijnIngevuld-}((gaDigiDGebruikersnaam;idMiddelUsername~ /\ gaDigiDWachtwoord;idMiddelPassword~);(I /\ idMiddelSoort;'DigiD';idMiddelSoort~);idMiddelHouder;gbaBSN~;V)
   /\ {-sigAanvragerHeeftGetekend-}(gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~)
   /\ {-sigOrganisatiegegevensZijnIngevuld-}vogAanvraagOrganisatieDeel;(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
   /\ {-sigOrganisatieHeeftGetekend-}vogAanvraagOrganisatieDeel;(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
   /\ {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~)
   ) 
MEANING "Het aanvraagformulier is volledig ingevuld" -- artikel xxxx
MESSAGE "Het aanvraagformulier is nog niet volledig ingevuld"
ROLE Gemeente MAINTAINS "VOG Aanvraag completeren"

RULE "Identiteit vaststellen": I |- {-sigIdentiteitIsVastgesteld-}(I[VOGAanvraag])
MEANING "De identiteit van de aanvrager moet met voldoende zekerheid zijn vastgesteld" -- artikel xxx
MESSAGE "De identiteit van de aanvrager moet nog (met voldoende zekerheid) worden vastgesteld"
ROLE Gemeente MAINTAINS "Identiteit vaststellen"

-- Gemeente mag alleen tekenen als het (a) volledigheid van invullen heeft vastgesteld en (b) de identiteit van de aanvrager met voldoende zekerheid heeft vastgesteld (art xxxx)
RULE "Gemeentedeel ondertekenen":
   {-sigAaanvraagIsVolledig-}
   ({-sigAanvragergegevensZijnIngevuld-}((gaDigiDGebruikersnaam;idMiddelUsername~ /\ gaDigiDWachtwoord;idMiddelPassword~);(I /\ idMiddelSoort;'DigiD';idMiddelSoort~);idMiddelHouder;gbaBSN~;V)
   /\ {-sigAanvragerHeeftGetekend-}(gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~)
   /\ {-sigOrganisatiegegevensZijnIngevuld-}vogAanvraagOrganisatieDeel;(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
   /\ {-sigOrganisatieHeeftGetekend-}vogAanvraagOrganisatieDeel;(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
   /\ {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~)
   ) 
/\ {-sigIdentiteitIsVastgesteld-}(I[VOGAanvraag]) 
|- {-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~)
MEANING "De gemeente mag alleen ondertekenen als (a) de aanvraag volledig is ingevuld en (b) de identiteit van de aanvrager met voldoende zekerheid is vastgesteld (art xxxx)"
MESSAGE "De gemeente moet de aanvraag ondertekenen"
ROLE Gemeente MAINTAINS "Gemeentedeel ondertekenen"

RULE "Gemeente ondertekening integriteit": 
   {-sigGemeenteHeeftGetekend-}(gemOndertekeningPlaats;gemOndertekeningPlaats~ /\ gemOndertekeningDatum;gemOndertekeningDatum~ /\ gemOndertekeningHandtekening;gemOndertekeningHandtekening~) 
|- {-sigAaanvraagIsVolledig-}
   ({-sigAanvragergegevensZijnIngevuld-}((gaDigiDGebruikersnaam;idMiddelUsername~ /\ gaDigiDWachtwoord;idMiddelPassword~);(I /\ idMiddelSoort;'DigiD';idMiddelSoort~);idMiddelHouder;gbaBSN~;V)
   /\ {-sigAanvragerHeeftGetekend-}(gaOndertekeningPlaats;gaOndertekeningPlaats~ /\ gaOndertekeningDatum;gaOndertekeningDatum~ /\ gaOndertekeningHandtekening;gaOndertekeningHandtekening~)
   /\ {-sigOrganisatiegegevensZijnIngevuld-}vogAanvraagOrganisatieDeel;(organisatieNaam;organisatieNaam~ /\ organisatieVertegenwoordiger;organisatieVertegenwoordiger~ /\ orgStraatnaam;orgStraatnaam~ /\ orgHuisnummer;orgHuisnummer~ /\ orgPostcode;orgPostcode~ /\ orgPlaats;orgPlaats~ /\ orgLand;orgLand~ /\ organisatieTelefoonnummer;organisatieTelefoonnummer~);vogAanvraagOrganisatieDeel~
   /\ {-sigOrganisatieHeeftGetekend-}vogAanvraagOrganisatieDeel;(orgOndertekeningPlaats;orgOndertekeningPlaats~ /\ orgOndertekeningDatum;orgOndertekeningDatum~ /\ orgOndertekeningHandtekening;orgOndertekeningHandtekening~);vogAanvraagOrganisatieDeel~
   /\ {-sigGemeentegegevensZijnIngevuld-}(gemeenteAanvraagnummer;gemeenteAanvraagnummer~ /\ naamGemeenteambtenaar;naamGemeenteambtenaar~ /\ gemGemeente;gemGemeente~)
   )  
/\ {-sigIdentiteitIsVastgesteld-}(I[VOGAanvraag])
MEANING "Als de gemeente kan alleen dan tekenen als de aanvraag volledig is."

ENDPROCESS
-----------------------------------------------------------
PATTERN "VOG Aanvraag"

--?De 'key' voor een VOG Aanvraag is - uiteraard - contextafhankelijk; voor de demo kiezen we er eentje die naar VOGAanvragen op een leuke manier verwijst. Er zou dus min of meer straffeloos mee gespeeld moeten kunnen/mogen worden voor de demo.
--KEY "VOG Aanvragen": VOGAanvraag(gaVoornamen, TXT ", ", gaNaam)

--[1. Invullen door Aanvrager]--
-- 1.1 Gegevens Aanvrager
--gaGeslacht :: VOGAanvraag * Geslacht [UNI]
--gaNaam :: VOGAanvraag * Geslachtsnaam [UNI] --Geslachtsnaam = tussenvoegsels ++ achternaam
--gaTussenvoegsels :: VOGAanvraag * Tussenvoegsels [UNI] -- WORDT NIET GEBRUIKT IN DE DEMO ivm identificatie van de aanvrager, omdat Tussenvoegsels niet in de GBA staat.
--gaVoornamen :: VOGAanvraag * Voornamen [UNI]
--gaGeboortegemeente :: VOGAanvraag * Gemeente [UNI]
--gaGeboorteland :: VOGAanvraag * Land [UNI]
--gaStraatnaam :: VOGAanvraag * Straatnaam [UNI]
--gaHuisnummer :: VOGAanvraag * Huisnummer [UNI]
--gaPostcode :: VOGAanvraag * Postcode [UNI]
--gaPlaats :: VOGAanvraag * Plaats [UNI]
--gaLand :: VOGAanvraag * Land [UNI]
--gaNationaliteit :: VOGAanvraag * Nationaliteit [UNI] -- Meerdere nationaliteiten zijn mogelijk
--gaBSN :: VOGAanvraag * BurgerServiceNummer [UNI]
gaTelefoonnummer :: VOGAanvraag * Telefoonnummer [UNI] -- Meerdere telefoonnummers zouden mogelijk kunnen zijn
gaEmailadres :: VOGAanvraag * EmailAddr [UNI] -- Meerdere emailadressen zouden mogelijk kunnen zijn

-- 1.1 - Ondertekening door aanvrager
gaOndertekeningPlaats :: VOGAanvraag * Plaats [UNI]
gaOndertekeningDatum :: VOGAanvraag * Datum [UNI]
gaOndertekeningHandtekening :: VOGAanvraag * Handtekening [UNI]

--[2. Invullen door organisatie / instelling die de VOG verlangt]--
vogAanvraagOrganisatieDeel :: VOGAanvraag * VOGAanvraagOrganisatieDeel
-- 2.1 - Gegevens organisatie-instelling"
orgVOGTemplateID :: VOGAanvraagOrganisatieDeel * OrgVOGTemplateID
KEY "Organisatie VOG templates": VOGAanvraagOrganisatieDeel(orgVOGTemplateID)

organisatieNaam :: VOGAanvraagOrganisatieDeel * VOGAanvraagOrganisatieDeelNaam [UNI]
organisatieVertegenwoordiger :: VOGAanvraagOrganisatieDeel * Geslachtsnaam [UNI]
orgStraatnaam :: VOGAanvraagOrganisatieDeel * Straatnaam [UNI]
orgHuisnummer :: VOGAanvraagOrganisatieDeel * Huisnummer [UNI]
orgPostcode :: VOGAanvraagOrganisatieDeel * Postcode [UNI]
orgPlaats :: VOGAanvraagOrganisatieDeel * Plaats [UNI]
orgLand :: VOGAanvraagOrganisatieDeel * Land [UNI]
organisatieTelefoonnummer :: VOGAanvraagOrganisatieDeel * Telefoonnummer [UNI]
-- 2.2 Doel van de aanvraag
werkrelatie :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
functie :: VOGAanvraagOrganisatieDeel * Functie [UNI]
taakomschrijving :: VOGAanvraagOrganisatieDeel * Taakomschrijving [UNI]
overig :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
overigOmschrijving :: VOGAanvraagOrganisatieDeel * OverigOmschrijving [UNI]
-- 2.3 Specifiek screeningsprofiel
specifiekScreeningsProfielRelevant :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
specifiekScreeningsProfiel :: VOGAanvraagOrganisatieDeel * SpecifiekScreeningsProfiel [UNI]
-- 2.4 Algemeen screeningsprofiel
algemeenScreeningsProfiel :: VOGAanvraagOrganisatieDeel * AlgemeenScreeningsProfiel -- hier mogen meerdere van worden gekozen
-- 2.5 Bijzondere omstandigheden m.b.t. de aanvraag
orgErZijnBijzondereOmstandigheden :: VOGAanvraagOrganisatieDeel * JaNee [UNI]
orgErZijnBijzondereOmstandighedenToelichting :: VOGAanvraagOrganisatieDeel * Toelichting [UNI]
-- 2.6 - Ondertekening door organisatie/instelling
orgOndertekeningPlaats :: VOGAanvraagOrganisatieDeel * Plaats [UNI]
orgOndertekeningDatum :: VOGAanvraagOrganisatieDeel * Datum [UNI]
orgOndertekeningHandtekening :: VOGAanvraagOrganisatieDeel * Handtekening [UNI]

--[3. Invullen door gemeente]--
gemeenteAanvraagnummer :: VOGAanvraag * GemeenteAanvraagnummer [UNI]
bijzonderhedenGeconstateerd :: VOGAanvraag * JaNee [UNI]
bijzonderhedenGeconstateerdToelichting :: VOGAanvraag * BijzonderhedenGeconstateerdToelichting [UNI]
persisterenInDeAanvraag :: VOGAanvraag * JaNee [UNI]
persisterenInDeAanvraagToelichting :: VOGAanvraag * PersisterenInDeAanvraagToelichting [UNI]
covogOmAdviesGevraagd :: VOGAanvraag * JaNee [UNI]
covogOmAdviesGevraagdToelichting :: VOGAanvraag * COVOGOmAdviesGevraagdToelichting [UNI]
naamGemeenteambtenaar :: VOGAanvraag * NaamGemeenteambtenaar [UNI]
gemGemeente :: VOGAanvraag * Gemeente [UNI]
gemOndertekeningPlaats :: VOGAanvraag * Plaats [UNI]
gemOndertekeningDatum :: VOGAanvraag * Datum [UNI]
gemOndertekeningHandtekening :: VOGAanvraag * Handtekening [UNI]

RULE "Screeningsprofielen uitsluiten": specifiekScreeningsProfiel;specifiekScreeningsProfiel~ /\ algemeenScreeningsProfiel;algemeenScreeningsProfiel~ |- -V
MEANING "Alleen als u bij vraag 2.3 geen screeningsprofiel heeft aangekruist, kruist u bij vraag 2.4 de aspecten aan bij de functie/taak of ander doel" -- Aanvraagformulier, Toelichting bij 2.4

RULE "Integriteit functie-taakomschrijving": I /\ werkrelatie;'Ja'[JaNee];werkrelatie~ |- functie;functie~ /\ taakomschrijving;taakomschrijving~
MEANING "Als de aanvraag een werkrelatie betreft, moet de functie en taakomschrijving zijn opgegeven"
MESSAGE "U moet de functie en/of taakomschrijving opgeven"

RULE "Integriteit overig": I /\ overig;'Ja'[JaNee];overig~ |- overigOmschrijving;overigOmschrijving~
MEANING "Als de aanvraag geen werkrelatie (dus: overig) betreft, moet het doel zijn omschreven"
MESSAGE "U moet het doel nog invullen (bij 'Omschrijving') "

RULE "Werkrelatie is niet 'overig'": werkrelatie;'Ja';overig~ |- -I
MEANING "De aanvraag mag niet zowel een werkrelatie als 'overig' betreffen"
MESSAGE "U mag niet zowel 'werkrelatie' als 'overig' aankruisen (met 'Ja' beantwoorden)"

RULE "Beedigd tolk/vertaler aanvraag integriteit": I /\ specifiekScreeningsProfiel;'80 Beëdigd tolken/vertalers';specifiekScreeningsProfiel~ |- organisatieNaam;'Raad voor Rechtsbijstand';organisatieNaam~ --Bron: VOG Aanvraag formulier
MEANING "Het profiel 'Beëdigd tolken/vertalers' mag alleen worden gebruikt door de 'Raad voor Rechtsbijstand'"

RULE "Integriteit van specifieke screeningsprofielen invullen": specifiekScreeningsProfielRelevant;'Ja';specifiekScreeningsProfielRelevant~ |- specifiekScreeningsProfiel;specifiekScreeningsProfiel~
MEANING "Als de aanvraag heeft betrekking heeft op één van de specifieke screeningsprofielen, dan moet dit ook zijn gespecificeerd"
MESSAGE "U moet een specifiek screeningsprofiel kiezen"

RULE "Specifieke screeningsprofielen": I[SpecifiekScreeningsProfiel] = 
    '01 Politieke ambtsdragers'
 \/ '06 Visum en emigratie'
 \/ '25 (Buitengewoon) opsporingsambtenaar'
 \/ '40 Vakantiegezinnen en adoptie'
 \/ '45 Gezondheidszorg en welzijn van mens en dier'
 \/ '50 Exploitatievergunning'
 \/ '55 Juridische dienstverlening'
 \/ '60 Onderwijs'
 \/ '65 Taxibranche; chauffeurskaart'
 \/ '70 Taxibranche; ondernemersvergunning'
 \/ '75 (Gezins)voogd bij voogdijinstellingen, reclasseringswerker, raadsonderzoeker en maatschappelijk werker'
 \/ '80 Beëdigd tolken/vertalers' --Dit screeningsprofiel kan alleen worden gebruikt als de Raad voor Rechtsbijstand de organisatie is die de VOG verlangt
 \/ '85 Lidmaatschap schietvereniging'
 \/ '95 Financiële dienstverlening'
MEANING "Een specifiek screeningsprofiel moet worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

RULE "Algemene screeningsprofielen": I[AlgemeenScreeningsProfiel] = 
-- Informatie
    '11 Bevoegdheid hebben tot het raadplegen en/of bewerken van systemen'
 \/ '12 Met gevoelige/vertrouwelijke informatie omgaan'
 \/ '13 Kennis dragen van veiligheidssystemen, controlemechanismen en verificatieprocessen'
-- Geld
 \/ '21 Met contante en/of girale gelden en/of (digitale) waardepapieren omgaan'
 \/ '22 Budgetbevoegdheid hebben'
-- Goederen
 \/ '36 Het bewaken van productieprocessen'
 \/ '37 Het beschikken over goederen'
 \/ '38 Het voorhanden hebben van stoffen, objecten en voorwerpen e.d., die bij oneigenlijk of onjuist gebruik een risico vormen voor mens (en dier)'
-- Diensten
 \/ '41 Het verlenen van diensten (advies, beveiliging, schoonmaak, catering, onderhoud, etc)'
 \/ '43 Het verlenen van diensten in de persoonlijke leefomgeving'
-- Zakelijke transacties
 \/ '53 Beslissen over offertes (het voeren van onderhandelingen en het afsluiten van contracten) en het doen van aanbestedingen'
-- Proces
 \/ '61 Het onderhouden/ombouwen/bedienen van (productie)machines en/of apparaten, voertuigen en/of luchtvaartuigen'
 \/ '62 (Rijdend) vervoer waarbij goederen, producten, post en pakketten worden getransporteerd en/of bezorgd, anders dan het intern transport binnen een bedrijf'
 \/ '63 (Rijdend) vervoer waarbij personen worden vervoerd'
-- Aansturen organisatie
 \/ '71 Personen die vanuit hun functie mensen en/of een organisatie (of een deel daarvan) aansturen'
-- Personen
 \/ '84 Belast zijn met de zorg voor minderjarigen'
 \/ '85 Belast zijn met de zorg voor (hulpbehoevende) personen, zoals ouderen en gehandicapten'
MEANING "Een algemeen screeningsprofiel mag alleen worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

RULE "Integriteit toelichting bijzonderheden geconstateerd": I /\ bijzonderhedenGeconstateerd;'Ja'[JaNee];bijzonderhedenGeconstateerd~ = I /\ bijzonderhedenGeconstateerdToelichting;bijzonderhedenGeconstateerdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

RULE "Integriteit toelichting persisteren in de aanvraag": I /\ persisterenInDeAanvraag;'Ja'[JaNee];persisterenInDeAanvraag~ = I /\ persisterenInDeAanvraagToelichting;persisterenInDeAanvraagToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

RULE "Integriteit toelichting COVOG om advies gevraagd": I /\ covogOmAdviesGevraagd;'Ja'[JaNee];covogOmAdviesGevraagd~ = I /\  covogOmAdviesGevraagdToelichting;covogOmAdviesGevraagdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"

ENDPATTERN
------------------------------------------------------------
ENDCONTEXT