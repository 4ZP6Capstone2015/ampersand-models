CONTEXT Atlas
INCLUDE "Fspec.adl"
PROCESS Student
{-
RULE cptos: cptos
RULE notcptos1: -cptos
RULE notcptos2: I[Concept];-cptos
RULE notcptos2dm: -(-I[Concept]!cptos)
RULE i1: -cptos \/ V
RULE i2: -(-cptos) \/ V
RULE x: -cptos;atomvalue
RULE y: cptos;atomvalue
RULE one_AtomID:(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos;atomvalue |- -cptos;atomvalue --TODO closure
MESSAGE "The value of two different atoms - the first of a specific concept, the second of a generic concept - have the same value. If these atoms represent the same real life object, than remove the one from the population of the generic concept."
-}
--RULE dummy: ctxnm|-ctxnm
RULE "specgenatoom": -(cptos;atomvalue /\ (genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos;atomvalue)
MESSAGE "<span class='warnsignal'>For an atom of a specific concept, there exists an equally named however different atom of a more generic concept.</span> This is temporarily allowed while you are editing the population untill you validate the changes. These atoms will be unified after you have validated the changes. The resulting atom will be of the most specific concept where it exists for now."
VIOLATION (TXT "Atom ", TGT I, TXT "will become more specific than ", SRC I)
RULE overtredingen: rrviols |- -rrviols
MESSAGE "<br/><br/><span class='errsignal'>Violations of RULEs or relation properties have been detected.</span> Determine actions to resolve them."
VIOLATION (TXT "RULE ",SRC I, TXT " is violated by pair ", TGT I, TXT ".")
ROLE Student MAINTAINS overtredingen, specgenatoom  --one_AtomID
ENDPROCESS

--CONCEPT Context "context"
--KEY Context: Context(ctxnm)
--ctxnm  ::Context->Conid [INJ]
--ctxpats::Context*Pattern
--ctxcs  ::Context*Concept
INTERFACE "Atlas (Play)" FOR Student: I[ONE]
BOX ["CONTEXT" : V[ONE*Context] BOX ["name":ctxnm,"number of RULEs":countrules,"number of relations":countdecls,"number of concepts":countcpts] 
    ,"PATTERNs" : V[ONE*Context];ctxpats
    ,"concepts" : V[ONE*Context];ctxcs
    ,"ISA-relations" : V[ONE*Context];ctxpats;ptgns 
    ,"relations" : V[ONE*Context];ctxpats;ptdcs  BOX ["relation":I, "with properties": decprps;declaredthrough] 
    ,"RULEs" : V[ONE*Context];ctxpats;ptrls;(I[Rule] /\ -I[PropertyRule])
    ]
INTERFACE "Validate" FOR Student: I[ONE]
BOX ["Click to commit and validate the next version":V[ONE*Context];savecontext
    ]
--CSS refers to most names in this interface
INTERFACE "CONTEXT files (Design / reload)" FOR Student: I[ONE]
BOX ["loaded into Atlas" : V[ONE*Context] 
    BOX ["CONTEXT":I,"source file (click to edit)":sourcefile \/ includes,"operations (click to perform)":sourcefile;applyto~]
    ,"overview of files" : V[ONE*User]
    BOX [ "open new source file" : newfile
        , "source files" : uploaded[User*AdlFile];I[AdlFile] BOX ["file name (click to edit)":I,"created at":filetime,"operations (click to perform)":applyto~]
        ]
    ]
--CSS refers to all names in this interface
INTERFACE "Extra functions"(filename,userrole) FOR Student: I[ONE]
BOX ["Export POPULATIONs to...":V[ONE*Context];savepopulation
    BOX ["file (INCLUDE only)":I[SavePopFile],"type a file name":filename]
    ,"User settings": V[ONE*User] BOX ["use role to load files":userrole]
    , "files with only POPULATIONs" : V[ONE*User];uploaded;(I[File] /\ -I[AdlFile]) BOX ["file name":I,"created at":filetime]
    ]


INTERFACE "Relation with theory (Manual)" FOR Student: I[ONE]
BOX ["introduction":V[ONE*MetaInformation];rapdescr
    ,"rule based process management":V[ONE*MetaInformation];engineimage;engineimage~ BOX ["explanation":engineexpl,"diagram": engineimage]
    ,"generate design artifacts":V[ONE*MetaInformation];genimage;genimage~ BOX ["explanation":genexpl,"diagram": genimage]
    ]

INTERFACE "CONTEXT" FOR Student: I[Context]
BOX ["name" : ctxnm 
    ,"PATTERNs" : ctxpats
    ,"concepts" : ctxcs 
    ,"ISA-relations" : ctxpats;ptgns 
    ,"relations" : ctxpats;ptdcs BOX ["relation":I, "with properties": decprps;declaredthrough]
    ,"RULEs" : ctxpats;ptrls;(I[Rule] /\ -I[PropertyRule]) 
    ]
--
--CONCEPT Pattern "pattern"
--KEY Pattern: Pattern(ptnm)
--ptnm  :: Pattern->Conid [INJ]
--ptrls :: Pattern*Rule
--ptgns :: Pattern*Gen
--ptdcs :: Pattern*Declaration
--ptxps :: Pattern*Blob
INTERFACE "PATTERN" FOR Student: I[Pattern]
BOX ["PURPOSEs" :ptxps
    ,"name" :ptnm
    ,"RULEs" :ptrls;(I[Rule] /\ -I[PropertyRule])
    ,"relations" :ptdcs  BOX ["relation":I, "with properties": decprps;declaredthrough]
    ,"ISA-relations" :ptgns
    ,"diagram" : ptpic
    ]
--
--CONCEPT Gen "generalization declaration"
--KEY Gen: Gen( genspc;cptnm , gengen;cptnm )
--gengen :: Gen->Concept
--genspc :: Gen->Concept
INTERFACE "ISA-relation" FOR Student: I[Gen]
BOX ["SPEC" : genspc
    ,"ISA" : gengen
    ,"in PATTERN" : ptgns~
    ]
--
--CONCEPT Concept "concept"
--KEY Concept: Concept(cptnm)
--cptnm :: Concept->Conid [INJ]
--cptos :: Concept*AtomID [INJ]
--cptdf :: Concept*Blob
--cptpurpose:: Concept*Blob
INTERFACE "Concept"(cptos,atomvalue) FOR Student: I[Concept]
BOX ["PURPOSEs" :cptpurpose
    ,"CONCEPT definition" :cptdf
    ,"name" :cptnm
    ,"more specific concepts":(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~ --TODO closure
    ,"more generic concepts" : genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen   --TODO closure
    ,"POPULATION" :cptos BOX ["atom":atomvalue]
    ,"POPULATION (of more specific)":(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos --TODO closure
    ,"used in relations": (decsgn;(src \/ trg))~
    ,"used in RULEs": (relsgn;(src \/ trg))~;(rrexp;rels)~;(I[Rule] /\ -I[PropertyRule])
    ,"diagram" : cptpic
    ]
--
--CONCEPT AtomID "An atom as an instance of a concept"
--CONCEPT Atom "trim <$> pString"
--KEY AtomID: AtomID(atomvalue)
--atomvalue::AtomID->Atom
--
--CONCEPT Sign "sign"
--KEY Sign: Sign( src;cptnm , trg;cptnm )
--src::Sign->Concept
--trg::Sign->Concept
--
--CONCEPT PairID "A pair as an instance of elements with a sign e.g. the population of a relation or the violations of a rule"
--KEY PairID: PairID( pairvalue;left;atomvalue , pairvalue;right;atomvalue )
--CONCEPT Pair   "(Atom,Atom)"
--KEY Pair: Pair( left;atomvalue , right;atomvalue )
--pairvalue::PairID->Pair
--left::Pair->AtomID
--right::Pair->AtomID
--
--CONCEPT Declaration "declaration"
--KEY Declaration: Declaration( decnm , decsgn;src;cptnm , decsgn;trg;cptnm )
--decnm   ::Declaration->Varid
--decsgn ::Declaration->Sign
--decprps::Declaration*PropertyRule [INJ]
--CONCEPT PropertyRule "a rule limited to one declaration, declared on that declaration through special syntax for properties"
--KEY PropertyRule: PropertyRule(rrnm[PropertyRule*ADLid])
--SPEC PropertyRule ISA Rule
--CONCEPT Property "..->.. or UNI<|>TOT<|>INJ<|>SUR<|>RFX<|>IRF<|>SYM<|>ASY<|>TRN<|>PROP"
--declaredthrough::PropertyRule*Property [TOT]
--decprL  ::Declaration*String[UNI]
--decprM  ::Declaration*String[UNI]
--decprR  ::Declaration*String[UNI]
--decmean ::Declaration * Blob
--decpurpose::Declaration * Blob
--decpopu ::Declaration*PairID
INTERFACE Relation(decpopu,left,right) FOR Student: I[Declaration]
BOX ["PURPOSEs" :decpurpose
    ,"MEANING" :decmean
    ,"example of basic sentence" :decexample
    ,"name" :decnm
    ,"type" :decsgn BOX ["source":src , "target":trg]
    ,"properties" :decprps;declaredthrough
    ,"from PATTERN" :ptdcs~
    ,"POPULATION" :decpopu BOX ["source":left , "target":right] --select existing atoms only!
    ,"used in RULEs": (rrexp;rels;reldcl)~;(I[Rule] /\ -I[PropertyRule])
    ]

--
--CONCEPT ExpressionID "An expression with a context-dependent meaning e.g. the meaning of a rule, a signal, a relation, ..."
--CONCEPT Expression "relation algebraic expression (Ampersand ASCII syntax)"
--KEY ExpressionID : ExpressionID(exprvalue)
--exprvalue :: ExpressionID->Expression
--rels  :: ExpressionID*Relation
--CONCEPT Relation "user-declared relation term"
--KEY Relation: Relation( relnm , relsgn;src;cptnm , relsgn;trg;cptnm )
--relnm :: Relation -> Varid
--relsgn:: Relation -> Sign
--reldcl:: Relation -> Declaration
--
--CONCEPT Rule "rule"
--KEY Rule: Rule(rrnm)
--rrnm  :: Rule -> ADLid [INJ]
--rrexp :: Rule -> ExpressionID
--rrmean:: Rule * Blob
--rrpurpose:: Rule * Blob
INTERFACE "RULE" FOR Student: I[Rule]
BOX ["PURPOSEs" :rrpurpose
    ,"MEANING" :rrmean
    ,"name" :rrnm
    ,"assertion" :rrexp
    ,"uses":rrexp;rels BOX ["relation":reldcl, "with properties": reldcl;decprps;declaredthrough, "source":relsgn;src, "target":relsgn;trg]
    ,"in PATTERN":ptrls~
    ,"diagram" : rrpic
    ]

--INTERFACE Relation FOR Student: I[Relation]
--BOX ["declaration":reldcl, "source":relsgn;src, "target":relsgn;trg]
ENDCONTEXT


