CONTEXT Atlas
INCLUDE "Fspec.adl"
PROCESS Student
{-
RULE cptos: cptos
RULE notcptos1: -cptos
RULE notcptos2: I[Concept];-cptos
RULE notcptos2dm: -(-I[Concept]!cptos)
RULE i1: -cptos \/ V
RULE i2: -(-cptos) \/ V
RULE x: -cptos;atomvalue
RULE y: cptos;atomvalue
RULE one_AtomID:(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos;atomvalue |- -cptos;atomvalue --TODO closure
MESSAGE "The value of two different atoms - the first of a specific concept, the second of a generic concept - have the same value. If these atoms represent the same real life object, than remove the one from the population of the generic concept."
-}
--RULE dummy: ctxnm|-ctxnm
RULE "specgenatoom": -(cptos;atomvalue /\ (genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos;atomvalue)
MESSAGE "Voor een atoom met een specifiek type bestaat er een gelijkend doch ander atoom met een generiek type. Deze atomen zullen pas identiek worden nadat u de atlasbewerkingen heeft ge-effectueerd door de wijzigingen in een volgende versie op te slaan en te laden. VerifiÃ«ren? Ga naar het generieke type (klik op linkerlid) en zie dat de atoomwaarde (rechterlid) voorkomt in de 'populatie' en de 'populatie (van specifieker)'."
RULE overtredingen: rrviols |- -rrviols
MESSAGE "Er worden regels overtreden."
ROLE Student MAINTAINS overtredingen, specgenatoom  --one_AtomID
ENDPROCESS

--CONCEPT Context "context"
--KEY Context: Context(ctxnm)
--ctxnm  ::Context->Conid [INJ]
--ctxpats::Context*Pattern
--ctxcs  ::Context*Concept
INTERFACE "Atlas (Plan - Do)" FOR Student: I[ONE]
BOX ["CONTEXT" : V[ONE*Context] BOX ["name":ctxnm,"number of RULEs":countrules,"number of declarations":countdecls,"number of concepts":countcpts] 
    ,"PATTERNs" : V[ONE*Context];ctxpats
    ,"RULEs" : V[ONE*Context];ctxpats;ptrls 
    ,"declarations" : V[ONE*Context];ctxpats;ptdcs 
    ,"ISA-relations" : V[ONE*Context];ctxpats;ptgns 
    ,"concepts" : V[ONE*Context];ctxcs
    ]
INTERFACE "Atlas (Check - Act)" FOR Student: I[ONE]
BOX ["Click on the next version to autocomplete the rule engine cycle (Check and Act)":V[ONE*Context];savecontext
    ]
--CSS refers to most names in this interface
INTERFACE "Edit or process CONTEXT files" FOR Student: I[ONE]
BOX ["loaded into Atlas" : V[ONE*Context] 
    BOX ["CONTEXT":I,"source file (click to edit)":sourcefile \/ includes,"operations (click to perform)":sourcefile;applyto~]
    ,"overview of files" : V[ONE*User]
    BOX [ "open new source file" : newfile
        , "uploaded or exported source files" : uploaded BOX ["file name (click to edit)":I,"created at":filetime,"operations (click to perform)":applyto~]
        ]
    ]
--CSS refers to all names in this interface
INTERFACE "Export Atlas"(filename) FOR Student: I[ONE]
BOX ["Export POPULATIONs to...":V[ONE*Context];savepopulation
    BOX ["file (INCLUDE only)":I[SavePopFile],"type a file name":filename]
    ]
INTERFACE "CONTEXT" FOR Student: I[Context]
BOX ["name" : ctxnm 
    ,"PATTERNs" : ctxpats
    ,"RULEs" : ctxpats;ptrls 
    ,"declarations" : ctxpats;ptdcs 
    ,"ISA-relations" : ctxpats;ptgns 
    ,"concepts" : ctxcs
    ]
--
--CONCEPT Pattern "pattern"
--KEY Pattern: Pattern(ptnm)
--ptnm  :: Pattern->Conid [INJ]
--ptrls :: Pattern*Rule
--ptgns :: Pattern*Gen
--ptdcs :: Pattern*Declaration
--ptxps :: Pattern*Blob
INTERFACE "PATTERN" FOR Student: I[Pattern]
BOX ["PURPOSEs" :ptxps
    ,"name" :ptnm
    ,"RULEs" :ptrls
    ,"declarations" :ptdcs
    ,"ISA-relations" :ptgns
    ,"diagram" : ptpic
    ]
--
--CONCEPT Gen "generalization declaration"
--KEY Gen: Gen( genspc;cptnm , gengen;cptnm )
--gengen :: Gen->Concept
--genspc :: Gen->Concept
INTERFACE "ISA-relation" FOR Student: I[Gen]
BOX ["SPEC" : genspc
    ,"ISA" : gengen
    ,"in PATTERN" : ptgns~
    ]
--
--CONCEPT Concept "concept"
--KEY Concept: Concept(cptnm)
--cptnm :: Concept->Conid [INJ]
--cptos :: Concept*AtomID [INJ]
--cptdf :: Concept*Blob
--cptpurpose:: Concept*Blob
INTERFACE "Concept"(cptos,atomvalue) FOR Student: I[Concept]
BOX ["PURPOSEs" :cptpurpose
    ,"CONCEPT definition" :cptdf
    ,"name" :cptnm
    ,"more specific concepts":(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~ --TODO closure
    ,"more generic concepts" : genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen   --TODO closure
    ,"POPULATION" :cptos BOX ["atoom":atomvalue]
    ,"POPULATION (of more specific)":(genspc~;gengen \/ genspc~;gengen;genspc~;gengen \/ genspc~;gengen;genspc~;gengen;genspc~;gengen)~;cptos --TODO closure
    ,"used in type of declarations": (decsgn;(src \/ trg))~ BOX ["declaration":I, "with properties": decprps;declaredthrough]
    ,"used in type of relations": (relsgn;(src \/ trg))~  BOX ["relation":I, "used in RULEs": (rrexp;rels)~]
    ,"diagram" : cptpic
    ]
--
--CONCEPT AtomID "An atom as an instance of a concept"
--CONCEPT Atom "trim <$> pString"
--KEY AtomID: AtomID(atomvalue)
--atomvalue::AtomID->Atom
--
--CONCEPT Sign "sign"
--KEY Sign: Sign( src;cptnm , trg;cptnm )
--src::Sign->Concept
--trg::Sign->Concept
--
--CONCEPT PairID "A pair as an instance of elements with a sign e.g. the population of a relation or the violations of a rule"
--KEY PairID: PairID( pairvalue;left;atomvalue , pairvalue;right;atomvalue )
--CONCEPT Pair   "(Atom,Atom)"
--KEY Pair: Pair( left;atomvalue , right;atomvalue )
--pairvalue::PairID->Pair
--left::Pair->AtomID
--right::Pair->AtomID
--
--CONCEPT Declaration "declaration"
--KEY Declaration: Declaration( decnm , decsgn;src;cptnm , decsgn;trg;cptnm )
--decnm   ::Declaration->Varid
--decsgn ::Declaration->Sign
--decprps::Declaration*PropertyRule [INJ]
--CONCEPT PropertyRule "a rule limited to one declaration, declared on that declaration through special syntax for properties"
--KEY PropertyRule: PropertyRule(rrnm[PropertyRule*ADLid])
--SPEC PropertyRule ISA Rule
--CONCEPT Property "..->.. or UNI<|>TOT<|>INJ<|>SUR<|>RFX<|>IRF<|>SYM<|>ASY<|>TRN<|>PROP"
--declaredthrough::PropertyRule*Property [TOT]
--decprL  ::Declaration*String[UNI]
--decprM  ::Declaration*String[UNI]
--decprR  ::Declaration*String[UNI]
--decmean ::Declaration * Blob
--decpurpose::Declaration * Blob
--decpopu ::Declaration*PairID
INTERFACE Declaration(decpopu,pairvalue,left,right) FOR Student: I[Declaration]
BOX ["PURPOSEs" :decpurpose
    ,"MEANING" :decmean
    ,"example fact with PRAGMA" :decexample
    ,"name" :decnm
    ,"type" :decsgn BOX ["source":src , "target":trg]
    ,"properties" :decprps;declaredthrough
    ,"from PATTERN" :ptdcs~
    ,"POPULATION" :decpopu BOX ["tuple":pairvalue BOX ["source":left , "target":right]] --select existing atoms only!
    ,"used in RULEs": (rrexp;rels;reldcl)~
    ]

--
--CONCEPT ExpressionID "An expression with a context-dependent meaning e.g. the meaning of a rule, a signal, a relation, ..."
--CONCEPT Expression "relation algebraic expression (Ampersand ASCII syntax)"
--KEY ExpressionID : ExpressionID(exprvalue)
--exprvalue :: ExpressionID->Expression
--rels  :: ExpressionID*Relation
--CONCEPT Relation "user-declared relation term"
--KEY Relation: Relation( relnm , relsgn;src;cptnm , relsgn;trg;cptnm )
--relnm :: Relation -> Varid
--relsgn:: Relation -> Sign
--reldcl:: Relation -> Declaration
--
--CONCEPT Rule "rule"
--KEY Rule: Rule(rrnm)
--rrnm  :: Rule -> ADLid [INJ]
--rrexp :: Rule -> ExpressionID
--rrmean:: Rule * Blob
--rrpurpose:: Rule * Blob
INTERFACE "RULE" FOR Student: I[Rule]
BOX ["PURPOSEs" :rrpurpose
    ,"MEANING" :rrmean
    ,"name" :rrnm
    ,"assertion" :rrexp
    ,"relations":rrexp;rels BOX ["declaration":reldcl, "with properties": reldcl;decprps;declaredthrough, "source":relsgn;src, "target":relsgn;trg]
    ,"in PATTERN":ptrls~
    ,"diagram" : rrpic
    ]

INTERFACE Relation FOR Student: I[Relation]
BOX ["declaration":reldcl, "source":relsgn;src, "target":relsgn;trg]
ENDCONTEXT


