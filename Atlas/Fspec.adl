CONTEXT Atlas
INCLUDE "AST.adl"

PATTERN Image
imageurl :: Image*URL
KEY Image: Image(PRIMHTML "<img src='", imageurl , PRIMHTML "'>")
ENDPATTERN

PATTERN FileManagement
filename :: File->FileName
filepath :: File*FilePath[UNI]
KEY File: File(PRIMHTML "<a href='../../index.php?file=", filepath, filename, PRIMHTML "'>", filename, PRIMHTML "</a>")
RULE "uniek path/filename": filename;filename~ /\ filepath;filepath~ |- I
filetime :: File*CalendarTime[UNI]

uploaded::User*File
MEANING "A user has uploaded files"

SPEC AdlFile ISA File
--KEY AdlFile: inherit from File
sourcefile::Context->AdlFile
includes  ::Context*File

applyto::G->AdlFile
MEANING "An Ampersand function (G) is applied to an adl-file"
functionname :: G->String
operation :: G->Int
KEY G: G(PRIMHTML "<a href='../../index.php?operation=",operation
        ,PRIMHTML "&file=", applyto;filepath , applyto;filename
        ,PRIMHTML "'>", functionname, PRIMHTML "</a>")


SPEC NewAdlFile ISA AdlFile
KEY NewAdlFile: NewAdlFile(PRIMHTML "<a href='../../index.php'>",filename[NewAdlFile*FileName],PRIMHTML"</a>")
newfile::User->NewAdlFile
MEANING "A user can open a new file"

SPEC SavePopFile ISA File
KEY SavePopFile: SavePopFile(PRIMHTML "<a href='../../index.php?operation=4&file=", filepath[SavePopFile*FilePath] , filename[SavePopFile*FileName]
                            ,PRIMHTML "'>", filename[SavePopFile*FileName] ,PRIMHTML "</a>")
savepopulation::Context->SavePopFile
MEANING "The population of a context can be exported to a file to INCLUDE"

SPEC SaveAdlFile ISA AdlFile
KEY SaveAdlFile: SaveAdlFile(PRIMHTML "<a href='../../index.php?operation=2&file=", filepath[SaveAdlFile*FilePath] , filename[SaveAdlFile*FileName]
                            ,PRIMHTML "'>", filename[SaveAdlFile*FileName], PRIMHTML "</a>")
savecontext::Context->SaveAdlFile
MEANING "A context can be exported to a file"
ENDPATTERN

PATTERN Metrics
countrules :: Context*Int[UNI]
countdecls :: Context*Int[UNI]
countcpts  :: Context*Int[UNI]
ENDPATTERN

PATTERN Conceptual
ptpic::Pattern*Image[UNI]
cptpic::Concept*Image[UNI]
rrpic::Rule*Image[UNI]
ENDPATTERN

PATTERN RuleEnforcement
SPEC Violation ISA PairID
rrviols::Rule*Violation
ENDPATTERN
ENDCONTEXT
