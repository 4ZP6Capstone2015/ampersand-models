CONTEXT ScopeEditingSvc IN ENGLISH

PROCESS "Scope Editing Service" -- Eigenlijk is dit een SERVICE
PURPOSE PROCESS "Scope Editing Service"
{+Editing a scope means that an existing scope is being subjected to modifications, such as the creation/copying of new subcomponents, ports, wires and constants. This service processes user requests for doing so.-}

--[Projects]--

sessionNewProject :: SESSION * ProjectName [UNI]
ROLE ExecEngine MAINTAINS "Create a new Project"
RULE "Create a new Project": '_SESSION';sessionNewProject |- -V
VIOLATION (TXT "{EX} NewStruct;Project"
              ,TXT ";projName;Project;_NEW;ProjectName;", TGT I
--!           ,TXT ";projOrg;Project;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;scopeNewProject;Scope;", SRC I, TXT ";ProjectName;", TGT I
          )

projCreateNamedApplication :: Project * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "In a project, create new (named) application"
RULE "In a project, create new (named) application": projCreateNamedApplication |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC projOrg
          ,TXT "{EX} DelPair;projCreateNamedApplication;Project;", SRC I, TXT ";ScopeName;", TGT I
          )

projCopyApplicationName :: Project * ScopeName [UNI]
projCopyApplicationFrom :: Project * Scope [UNI] 
ROLE ExecEngine MAINTAINS "In a project, create a new (named) application that is a copy of an existing one"
RULE "In a project, create a new (named) application that is a copy of an existing one":
  I /\ projCopyApplicationName;V;(I-scopeIsComponent);projCopyApplicationFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC projCopyApplicationName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC projCopyApplicationFrom
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC projOrg
          ,TXT "{EX} DelPair;projCopyApplicationName;Project;", SRC I, TXT ";ScopeName;", SRC projCopyApplicationName          
          ,TXT "{EX} DelPair;projCopyApplicationFrom;Project;", SRC I, TXT ";Scope;", SRC projCopyApplicationFrom          
          )

--[Scopes: applications (i.e.: non-components)]--

sessionCreateNamedApplication :: SESSION * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "Create new (named) application in session"
RULE "Create new (named) application in session": '_SESSION';sessionCreateNamedApplication |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sessionCreateNamedApplication;SESSION;", SRC I, TXT ";ScopeName;", TGT I
          )

sessionCopyApplicationName :: SESSION * ScopeName [UNI]
sessionCopyApplicationFrom :: SESSION * Scope [UNI] 
ROLE ExecEngine MAINTAINS "Create a new (named) application that is a copy of an existing one"
RULE "Create a new (named) application that is a copy of an existing one":
  '_SESSION' /\ sessionCopyApplicationName;V;(I-scopeIsComponent);sessionCopyApplicationFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC sessionCopyApplicationName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC sessionCopyApplicationFrom
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sessionCopyApplicationName;SESSION;", SRC I, TXT ";ScopeName;", SRC sessionCopyApplicationName          
          ,TXT "{EX} DelPair;sessionCopyApplicationFrom;SESSION;", SRC I, TXT ";Scope;", SRC sessionCopyApplicationFrom          
          )

--[Scopes: components]--

sessionCreateNamedComponent :: SESSION * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "Create new (named) component in session"
RULE "Create new (named) component in session": '_SESSION';sessionCreateNamedComponent |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
              ,TXT ";scopeIsComponent;Scope;_NEW;Scope;_NEW"
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sessionCreateNamedComponent;SESSION;", SRC I, TXT ";ScopeName;", TGT I
          )
          
sessionCopyComponentName :: SESSION * ScopeName [UNI]
sessionCopyComponentFrom :: SESSION * Scope [UNI] 
ROLE ExecEngine MAINTAINS "Create a new (named) component that is a copy of an existing one"
RULE "Create a new (named) component that is a copy of an existing one":
  '_SESSION' /\ sessionCopyComponentName;V;scopeIsComponent;sessionCopyComponentFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC sessionCopyComponentName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC sessionCopyComponentFrom
              ,TXT ";scopeIsComponent;Scope;_NEW;Scope;", SRC sessionCopyComponentFrom;scopeIsComponent
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sessionCopyComponentName;SESSION;", SRC I, TXT ";ScopeName;", SRC sessionCopyComponentName          
          ,TXT "{EX} DelPair;sessionCopyComponentFrom;SESSION;", SRC I, TXT ";Scope;", SRC sessionCopyComponentFrom          
          )

--[Scopes: general]--

scopeNewComponentFromTemplate :: Scope * Scope [UNI]
ROLE ExecEngine MAINTAINS "Within a scope, create a new subscope that is a copy of an existing scope"
RULE  "Within a scope, create a new subscope that is a copy of an existing scope": scopeNewComponentFromTemplate |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeIPO;Scope;_NEW;Scope;", SRC I
              ,TXT ";scopeICO;Scope;_NEW;Scope;", TGT I
          ,TXT "{EX} DelPair;scopeNewComponentFromTemplate;Scope;", SRC I, TXT ";Scope;", TGT I
          ,TXT "{EX} RerunExecEngine;scopeNewComponentFromTemplate" -- Attempt to fix needed re-run to fix transitive closure of scopeIPO
          )

--[Ports]--

scopeNewInputPortName :: Scope * Name [UNI]
ROLE ExecEngine MAINTAINS "Create a new Input on Component"
RULE "Create a new Input on Component": scopeNewInputPortName |- -V
VIOLATION (TXT "{EX} NewStruct;Port"
              ,TXT ";portIsInput;Port;_NEW;Port;_NEW"
              ,TXT ";portComponent;Port;_NEW;Scope;", SRC I
              ,TXT ";portName;Port;_NEW;PortName;", TGT I
          ,TXT "{EX} InsPair;scopeIsComponent;Scope;", SRC I, TXT ";Scope;", SRC I
          ,TXT "{EX} DelPair;scopeNewInputPortName;Scope;", SRC I, TXT ";Name;", TGT I
          )

scopeNewOutputPortName :: Scope * Name [UNI]
ROLE ExecEngine MAINTAINS "Create a new Output on Component"
RULE "Create a new Output on Component": scopeNewOutputPortName |- -V
VIOLATION (TXT "{EX} NewStruct;Port"
              ,TXT ";portIsOutput;Port;_NEW;Port;_NEW"
              ,TXT ";portComponent;Port;_NEW;Scope;", SRC I
              ,TXT ";portName;Port;_NEW;PortName;", TGT I
          ,TXT "{EX} InsPair;scopeIsComponent;Scope;", SRC I, TXT ";Scope;", SRC I
          ,TXT "{EX} DelPair;scopeNewOutputPortName;Scope;", SRC I, TXT ";Name;", TGT I
          )

ROLE ExecEngine MAINTAINS "Automatically set the 'scopeIsComponent' property"
RULE "Automatically set the 'scopeIsComponent' property": portComponent |- portComponent;scopeIsComponent
VIOLATION (TXT "{EX} InsPair;scopeIsComponent;Scope;", TGT I, TXT ";Scope;", TGT I)

--[Constants]--

scopeNewConstantforPort :: Scope * Port [UNI]
ROLE ExecEngine MAINTAINS "Create a new Constant for a Port in a Scope"
RULE "Create a new Constant for a Port in a Scope": scopeNewConstantforPort |- -V
VIOLATION (TXT "{EX} NewStruct;Constant"
              ,TXT ";constScope;Constant;_NEW;Scope;", SRC I
              ,TXT ";constPort;Constant;_NEW;Port;", TGT I
              ,TXT ";constName;Constant;_NEW;ConstName;", TGT portName
              ,TXT ";constType;Constant;_NEW;ConfigType;", TGT portType
              ,TXT ";constQstn;Constant;_NEW;Question;", TGT portConfigQstn
          ,TXT "{EX} DelPair;scopeNewConstantforPort;Scope;", SRC I, TXT ";Name;", TGT I
          )

--[Wires]--

scopeNewWireSrc :: Scope * Port [UNI]
scopeNewWireTgt :: Scope * Port [UNI]
ROLE ExecEngine MAINTAINS "Create a new Wire in a Scope"
RULE "Create a new Wire in a Scope": I /\ scopeNewWireSrc;scopeNewWireSrc~ /\ scopeNewWireTgt;scopeNewWireTgt~ |- -V
VIOLATION (TXT "{EX} NewStruct;Wire"
              ,TXT ";wScope;Wire;_NEW;Scope;", SRC I
              ,TXT ";wSrc;Wire;_NEW;Port;", SRC scopeNewWireSrc
              ,TXT ";wTgt;Wire;_NEW;Port;", SRC scopeNewWireTgt
          ,TXT "{EX} DelPair;scopeNewWireSrc;Scope;", SRC I, TXT ";Port;", SRC scopeNewWireSrc
          ,TXT "{EX} DelPair;scopeNewWireTgt;Scope;", SRC I, TXT ";Port;", SRC scopeNewWireTgt
          )

scopeDeleteWire :: Scope * Wire [UNI]
ROLE ExecEngine MAINTAINS "Delete a wire from Scope"
RULE "Delete a wire from Scope": scopeDeleteWire |- -V
VIOLATION (TXT "{EX} DelAtom;Wire;", TGT I)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT