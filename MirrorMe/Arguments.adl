CONTEXT Arguments IN ENGLISH
INCLUDE "Login.adl"
INCLUDE "Login.xlsx"

PATTERN Arguments
    RELATION supports[Argument*Statement] [UNI]
    RELATION reason[Argument*Statement] [UNI]
    RELATION arg[Statement*Text]
    RELATION ofClaim[Claim*Argument]  -- ofClaim = states;(supports~;reason)*;reason~
    RELATION legalGround[Argument*LegalGround]
    REPRESENT LegalGround TYPE ALPHANUMERIC

    RULE addPhrase : arg |- V;phrasing
    VIOLATION ( TXT "{EX} NewStruct;Statement"
              , TXT ";phrasing;Statement;_NEW;Text;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addPhrase

    RULE addSupport : arg;phrasing~ |- supports~;reason
    VIOLATION ( TXT "{EX} NewStruct;Argument"
              , TXT ";supports;Argument;_NEW;Statement;" , SRC I
              , TXT ";reason;Argument;_NEW;Statement;" , TGT I
              , TXT ";ofClaim;Claim;", SRC (states\/ofClaim;(reason\/supports))~, TXT ";Argument;_NEW"
              )
    ROLE ExecEngine MAINTAINS addSupport

    RULE clearArg : supports~;reason |- -(arg;phrasing~)
    VIOLATION ( TXT "{EX} DelPair;arg;Statement;", SRC I, TXT ";Text;", SRC arg )
    ROLE ExecEngine MAINTAINS clearArg

    RULE TOTsupports : I |- supports;supports~
    VIOLATION ( TXT "{EX} DelAtom;Argument;", SRC I )
    ROLE ExecEngine MAINTAINS TOTsupports

    RULE TOTreason : I |- reason;reason~
    VIOLATION ( TXT "{EX} DelAtom;Argument;", SRC I )
    ROLE ExecEngine MAINTAINS TOTreason


    RELATION madeBy[Statement*Account] [UNI]
    RELATION moment[Statement*Moment] [UNI]
    REPRESENT Moment TYPE DATE
    RELATION phrasing[Statement*Text] [UNI]
    REPRESENT Text TYPE BIGALPHANUMERIC
    RELATION evidence[Statement*Document]

    RELATION case[Claim*Case] [UNI]
    RELATION states[Claim*Statement] [UNI]
    RELATION object[Claim*LegalThing] [UNI]

    RELATION caseType[Case*CaseType] [UNI]
    RELATION owner[Case*Account] [UNI] -- must not be TOT to give the Exec-Engine time to compute the owner.
    RELATION created[Case*Moment] [UNI]
    RELATION claims[Case*Text]

    RULE sessionAuthor : (I[Statement]#'_SESSION'[SESSION]);ingelogd |- madeBy;V
    VIOLATION ( TXT "{EX} InsPair;madeBy;Statement;", SRC I, TXT ";Account;" , TGT I
              , TXT "{EX} InsPair;moment;Statement;", SRC I, TXT ";Date;{php}date(DATE_ISO8601)" -- Current date in ISO 8601 date format
              )
    ROLE ExecEngine MAINTAINS sessionAuthor

    RULE addStatement : claims~;owner |- phrasing~;madeBy
    VIOLATION ( TXT "{EX} NewStruct;Statement"
              , TXT ";phrasing;Statement;_NEW;Text;" , SRC I
              , TXT ";madeBy;Statement;_NEW;Account;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addStatement

    RULE addClaim : claims;phrasing~ |- case~;states
    VIOLATION ( TXT "{EX} NewStruct;Claim"
              , TXT ";case;Claim;_NEW;Case;" , SRC I
              , TXT ";states;Claim;_NEW;Statement;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addClaim

    RULE caseAuthor : V[Case*SESSION];'_SESSION';ingelogd |- owner;V
    VIOLATION ( TXT "{EX} InsPair;owner;Case;", SRC I, TXT ";Account;" , TGT I
              , TXT "{EX} InsPair;created;Case;", SRC I, TXT ";Date;{php}date(DATE_ISO8601)" -- Current date in ISO 8601 date format
              )
    ROLE ExecEngine MAINTAINS caseAuthor

ENDPATTERN

INTERFACE "Zaken" : '_SESSION';ingelogd
TABS [ Zaken : owner~
       COLS [ type : caseType
            , claims : case~;states
            ]
     ]

INTERFACE "Redeneerstap" (arg, reason, legalGround) : I[Statement]
ROWS [ "formuleer hier een onderbouwende stelling" : arg
     , "argumenten:" : supports~
       COLS [ stelling : reason
            , grond : legalGround
            ]
     ]

VIEW Statement : Statement(phrasing)
VIEW Claim : Claim(states;phrasing)

INTERFACE "Nieuwe Zaak" (caseType, claims, object) : I[Case]
COLS [ "aangemaakt door" : owner
     , "aangemaakt op"   : created
     , "type zaak"       : caseType
     , "te bewijzen"     : claims
     , claims : case~
       COLS [ betreft : object
            , uitspraak : states;phrasing
            ]
     ]

ENDCONTEXT