CONTEXT Arguments IN ENGLISH
INCLUDE "Login.adl"
INCLUDE "Login.xlsx"

PATTERN Arguments
    RELATION supports[Argument*Statement] [UNI, TOT]
    RELATION reason[Argument*Statement] [UNI, TOT]
    RELATION arg[Statement*Statement]
    RELATION legalGround[Argument*LegalGround]
    REPRESENT LegalGround TYPE ALPHANUMERIC

    RULE addSupport : arg |- supports~;reason
    VIOLATION ( TXT "{EX} NewStruct;Argument"
              , TXT ";supports;Argument;_NEW;Statement;" , SRC I
              , TXT ";reason;Argument;_NEW;Statement;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addSupport

    RULE resetArg : supports~;reason |- -arg
    VIOLATION ( TXT "{EX} DelPair;arg;Statement;", SRC I, TXT ";Statement;", TGT I )
    ROLE ExecEngine MAINTAINS resetArg

    RULE delArg : I |- supports;supports~
    VIOLATION ( TXT "{EX} DelPair;reason;Statement;", SRC I, TXT ";Statement;", TGT reason
              , TXT "{EX} DelAtom;Argument;", SRC I
              )
    ROLE ExecEngine MAINTAINS delArg


    RELATION madeBy[Statement*Account] [UNI,TOT]
    RELATION moment[Statement*Moment] [UNI]
    REPRESENT Moment TYPE DATE
    RELATION phrasing[Statement*Text] [UNI]
    REPRESENT Text TYPE BIGALPHANUMERIC
    RELATION evidence[Statement*Document]

    RELATION case[Claim*Case] [UNI]
    RELATION states[Claim*Statement] [UNI,TOT]
    RELATION object[Claim*LegalThing] [UNI]

    RELATION caseType[Case*CaseType] [UNI]
    RELATION owner[Case*Account] [UNI] -- must not be TOT to give the Exec-Engine time to compute the owner.
    RELATION created[Case*Moment] [UNI]
    RELATION claims[Case*Text]

    RULE sessionAuthor : (I[Statement]#'_SESSION'[SESSION]);ingelogd |- madeBy;V
    VIOLATION ( TXT "{EX} InsPair;madeBy;Statement;", SRC I, TXT ";Account;" , TGT I
              , TXT "{EX} InsPair;moment;Statement;", SRC I, TXT ";Date;{php}date(DATE_ISO8601)" -- Current date in ISO 8601 date format
              )
    ROLE ExecEngine MAINTAINS sessionAuthor

    RULE addStatement : claims~;owner |- phrasing~;madeBy
    VIOLATION ( TXT "{EX} NewStruct;Statement"
              , TXT ";phrasing;Statement;_NEW;Text;" , SRC I
              , TXT ";madeBy;Statement;_NEW;Account;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addStatement

    RULE addClaim : claims;phrasing~ |- case~;states
    VIOLATION ( TXT "{EX} NewStruct;Claim"
              , TXT ";case;Claim;_NEW;Case;" , SRC I
              , TXT ";states;Claim;_NEW;Statement;" , TGT I
              )
    ROLE ExecEngine MAINTAINS addClaim

    RULE caseAuthor : V[Case*SESSION];'_SESSION';ingelogd |- owner;V
    VIOLATION ( TXT "{EX} InsPair;owner;Case;", SRC I, TXT ";Account;" , TGT I
              , TXT "{EX} InsPair;created;Case;", SRC I, TXT ";Date;{php}date(DATE_ISO8601)" -- Current date in ISO 8601 date format
              )
    ROLE ExecEngine MAINTAINS caseAuthor

--    RULE newStatement : I[Legalthing] |- exists[Statement*Legalthing]~;exists[Statement*Legalthing]
--    VIOLATION ( TXT "{EX} NewStruct;Statement"
--              , TXT ";exists;Statement;_NEW;Legalthing;" , SRC I
--              , TXT ";onBehalfOf;Statement;_NEW;Account;" , SRC V[Legalthing*SESSION];'_SESSION'
--              )
--    ROLE ExecEngine MAINTAINS newStatement

ENDPATTERN

--PROCESS "Compute Closures"
--    RELATION supportsStar[Statement*Statement] -- Transitive, reflexive closure of supports, aka supports*
--    MEANING "s supports g  means that  s  is  g  or  each element of statement s  is an element of statement  g  as well."
--    RELATION supportsPlus[Statement*Statement] -- Transitive closure of supports, aka supports+
--    MEANING "s supports g  means that each element of statement  s  is an element of statement  g  as well."
--
--    RELATION supportsCopy[Statement*Statement] -- necessary only for calling the Transitive closure function.
--    ROLE ExecEngine MAINTAINS "Compute transitive closure of supports"
--    RULE "Compute transitive closure of supports" : supports = supportsCopy
--    VIOLATION (TXT "{EX} TransitiveClosure;supports;Statement;supportsCopy;supportsPlus")
--
--    ROLE ExecEngine MAINTAINS "Compute transitive closure of supports by Ins", "Compute transitive closure of supports by Del"
--    RULE "Compute transitive closure of supports by Ins" : supportsPlus\/I |- supportsStar
--    VIOLATION (TXT "{EX} InsPair;supportsStar;Statement;", SRC I, TXT ";Statement;", TGT I)
--    RULE "Compute transitive closure of supports by Del" : supportsStar |- supportsPlus\/I
--    VIOLATION (TXT "{EX} DelPair;supportsStar;Statement;", SRC I, TXT ";Statement;", TGT I)
--ENDPROCESS


INTERFACE "Zaken" (claims, legalGround, arg, supports) : '_SESSION';ingelogd
TABS [ Zaken : owner~
       COLS [ type : caseType
            , claims : case~
            ]
     ]

INTERFACE "Redenering" (claims, legalGround, arg) : I[Claim]
ROWS [ "Voeg hier nieuwe beweringen toe." : case
       COLS [ beweringen : claims ]
     , "Geef hier juridische gronden:"  : states
       COLS [ formulering : phrasing
            , "noem een onderbouwende bewering" : arg
            , "ondersteund door" : supports~
              COLS [ uitspraak : reason
                   , grond : legalGround
                   ]
            ]
     ]

INTERFACE "Onderbouwen" (claims, legalGround, arg) : I[Statement]
ROWS [ "Voeg hier nieuwe beweringen toe." : states~;case
       COLS [ beweringen : claims ]
     , "Geef hier juridische gronden:" : I
       COLS [ formulering : phrasing
            , "noem een onderbouwende uitspraak" : arg
            , "ondersteund door" : supports~
              COLS [ uitspraak : reason
                   , grond : legalGround
                   ]
            ]
     ]

VIEW Statement : Statement(phrasing)
VIEW Claim : Claim(states;phrasing)

INTERFACE "Claim" (phrasing, legalGround, supports) : I[Claim]
COLS [ bewering : states
     , gemaakt : states;moment
     , door : states;madeBy
     , onderbouwing : states;supports~;reason
       COLS [ bewering : phrasing
            ]
     ]

INTERFACE "Nieuw Statement" (phrasing) : I[Statement]
COLS [ formulering : phrasing
     , "gesteld door" : madeBy
     , moment      : moment
     ]

INTERFACE "Nieuwe Zaak" (caseType, claims, object) : I[Case]
COLS [ "aangemaakt door" : owner
     , "aangemaakt op"   : created
     , "type zaak"       : caseType
     , "te bewijzen"     : claims
     , claims : case~
       COLS [ betreft : object
            , uitspraak : states;phrasing
            ]
     ]

ENDCONTEXT