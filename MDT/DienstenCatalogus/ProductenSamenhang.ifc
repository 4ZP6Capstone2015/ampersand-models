CONTEXT DienstenCatalogus IN DUTCH

INCLUDE "alleRelaties.adl"
INCLUDE "allePopulaties.adl"

INTERFACE "Alle ProcIDs"  : V[SESSION*ProcID] BOX <SHCOLS>
  [ "ProcID"     : I[ProcID] LINKTO INTERFACE "ProcID"
  , "omschrijving" : omschrijving
  ]
INTERFACE "ProcIDs met product" : V[SESSION*Product];
                                    (   rdwProduct~;procID[ProcIdVerloop*ProcID]
                                     \/ rdwProduct~;procID[ProductVrs*ProcID]
                                    )
 BOX <SHCOLS>
  [ "ProcID"     : I[ProcID] LINKTO INTERFACE "ProcID"
  , "omschrijving" : omschrijving
  , "producten (via verloop)" : procID[ProcIdVerloop*ProcID]~;rdwProduct COLS
     [ code : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  , "producten (via VRS)" : procID[ProductVrs*ProcID]~;rdwProduct COLS
     [ code : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  ]
INTERFACE "ProcID" : I[ProcID] BOX
  [ "ProcID"  : procIDnr 
  , "omschrijving" : omschrijving
  , "syseem"       : systeem
  , "bevoegdheden"      : nodigVoor
  , "verloop" : I INTERFACE "ProcIDVerloop"
  , klantgroepen   :  toegekend INTERFACE "Klantgroep"
  ]

INTERFACE "Klantgroepen"  : V[SESSION*Klantgroep] BOX <SHCOLS>
  [ "klantgroep"     : I[Klantgroep] LINKTO INTERFACE "Klantgroep"
  , "omschrijving" : omschrijving
  ]
INTERFACE "Klantgroep" : I[Klantgroep] BOX
  [ "klantgroep" : I
  , "omschrijving" : omschrijving
  , "procIDs"      : toegekend~
  ]

INTERFACE ProcIDVerloop : I[ProcID] BOX
  [ "" : procID[ProcIdVerloop*ProcID]~ BOX <SHCOLS>
    [ nr : volgNummer
    , omschrijving : omschrijving[ProcIdVerloop*FunctieOmschrijving]
    , product      : rdwProduct[ProcIdVerloop*Product]
    ]
  ]


INTERFACE "Bevoegdheden" : V[SESSION*Bevoegdheid] BOX <SHCOLS>
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  ]

INTERFACE "Bevoegdheid" : I[Bevoegdheid] BOX
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  , "procIDs"      : nodigVoor~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           ]
  ]



INTERFACE "Producten VRS"  : V[SESSION*ProductVrs] BOX <SHCOLS>
  [ "ProductVrs"   : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  ]

INTERFACE "Product VRS"  : I[ProductVrs] BOX
  [ "ProductVrs"  : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  , "procID"      : procID
      BOX <SHCOLS>
           [ "procId"  : I[ProcID]
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           , "Product VRS" : procID[ProductVrs*ProcID]~
           , verloop : I[ProcID] INTERFACE "ProcIDVerloop"
           ]
  , "organisatieOnd"  : organisatieOnd
  , "soort voertuig" : soort
  , "lichtZwaar" : lichtZwaar
  , "bron(nen)" : bron
  , "presentatieGroep" : presentatieGroep
  ]



INTERFACE "Producten" : V[SESSION*Product] BOX <SHCOLS>
  [ "Product/Dienst"  : I[Product] --LINKTO INTERFACE "Product"
  , "omschrijving (TOA)" : omschrijvingToa
  , "EDS producten"      : rdwProduct[EDSProduct*Product]~
  , "VRS producten"      : rdwProduct[ProductVrs*Product]~
  ]

INTERFACE "Product" : I[Product] BOX
  [ "omschrijving (TOA)" : omschrijvingToa
  , "onderdeel"    : onderdeel
  , "tarief code"  : tarCodeProd
  , "componenten"  : product~ BOX <SHCOLS>
     [ "component"          : component
     ,  "soort"             : soort[ProductComponent*SoortProdComp]
     ,  "kostenplaats eind" : eind[ProductComponent*KostenPlaats]
     ,  "kostenplaats hulp" : hulp[ProductComponent*KostenPlaats]
     ,  "grootboek"         : gbrToa[ProductComponent*GrootboekRekening]
     ,  "ambt"              : ambt[ProductComponent*AmbtCode]
     ]
  , "artikelen" : product[Artikel*Product]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] -- LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "procIDs (via verloop)" : (procID[ProcIdVerloop*ProcID]~;rdwProduct)~ COLS
     [ procID : I[ProcID] LINKTO INTERFACE "ProcID"
     , "omschrijving" : omschrijving
     ]
  , "procIDs (via VRS)"     : (procID[ProductVrs*ProcID]~;rdwProduct[ProductVrs*Product])~ COLS
     [ procID : I[ProcID] LINKTO INTERFACE "ProcID"
     , "omschrijving" : omschrijving
     ]
   ]

INTERFACE "Kostenplaatsen" : V[SESSION*KostenPlaats] BOX <SHCOLS>
  [ "KostenPlaats"  : I[KostenPlaats]  INTERFACE "KostenPlaats"
  ] 
INTERFACE "KostenPlaats" : I[KostenPlaats] COLS
  [ ""  : I[KostenPlaats]
  --, "hulp+eind" : (hulp~ \/ eind~);product
  , "hulp"      : hulp~;product
  , "eind"      : eind~;product
  ]


INTERFACE "EDSProducten"  : V[SESSION*EDSProduct] BOX <SHCOLS>
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProduct"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "artikel"        : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "art omschrijving" : edsArtikel;omschrijving
  ]

INTERFACE "EDSProduct" : I[EDSProduct] BOX
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProduct"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "EdsProdCode"       : edsProductcode[EDSProduct*EDSProductcode]
  , "EdsProductDefinitie" : edsProductDefinitie
  , "RdwProduct"     : rdwProduct[EDSProduct*Product] LINKTO INTERFACE "Product"
  , "Tarief (eds)"   : edsTarief[EDSProduct*Tarief]
  , "Tarief (f&a)"   : edsArtikel;tarief
  , "Artikel"   : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "Verkoopgroep"   : edsVerkoopgroep[EDSProduct*EDSVerkoopgroep]
  , "Productgroep"   : edsProductgroep[EDSProduct*EDSProductgroep]
  , "Odersoort"   : edsOrdersoort[EDSProduct*EDSOrdersoort]
  , "InternalDisplayName"   : edsInternalDisplayName[EDSProduct*InternalDisplayName]
  ]

INTERFACE "Systeem" : I[Systeem] BOX
  [ "Systeem"   : I
  , "Bevoegdheden": systeem[Bevoegdheid*Systeem]~
  , "ProcIDs" : systeem[ProcID*Systeem]~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           ]
  ]
  


INTERFACE "Artikelen" : V[SESSION*Artikel] BOX <SHCOLS>
  [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
  , "omschrijving" : omschrijving
  ]

INTERFACE "Artikel" : I[Artikel] BOX 
  [ "omschrijving"     : omschrijving[Artikel*ArtikelOmschrijving] CRUD
  , "prodGroep"        : prodGroep[Artikel*ProductGroep] CRUD
  , "product"          : product[Artikel*Product]CRUD
  , "EDS producten"    : edsArtikel~ CRUD
  , "fmb"              : fmb[Artikel*FMB]CRUD
  , "wettelijkeTaak"   : wettelijkeTaak[Artikel*WettelijkeTaak] INTERFACE "WettelijkeTaak" CRUD
  , "verkoopKantoor"   : verkoopKantoor[Artikel*KostenPlaats]CRUD
  , "rubriceringsGroep": rubriceringsGroep[Artikel*RubriceringsGroep]CRUD
  , "grootboekRekening": grootboekRekening[Artikel*GrootboekRekening]CRUD
  , "ArtikelVrachtGroep" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]CRUD
  , "tarief"           : tarief[Artikel*Tarief]CRUD
  ]

INTERFACE "ProdGroepen"  : V[SESSION*ProductGroep] BOX <SHCOLS>
  [ "prodGroep"     : I[ProductGroep] LINKTO INTERFACE "ProductGroep"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "ProductGroep" : I[ProductGroep] COLS
  [ "prodGroep" : I
--  , "omschrijving"  : omschrijving
  , "artikelen" : prodGroep[Artikel*ProductGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

 
INTERFACE "Wettelijke Taken" : V[SESSION*WettelijkeTaak] BOX <SHCOLS>
  [ "wettelijke taak"     : I[WettelijkeTaak] LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving" : omschrijving
  ]
INTERFACE "WettelijkeTaak" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving"  : omschrijving
  ]

INTERFACE "WettelijkeTaakUitgebreid" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaak"
  , "omschrijving"  : omschrijving
  , "artikelen" : wettelijkeTaak[Artikel*WettelijkeTaak]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "grootboekRekeningen" : wettelijkeTaak~;grootboekRekening
  , "artikelvrachtgroepen"  : wettelijkeTaak~;artikelVrachtGroep
  , "verkoopkantoren" : wettelijkeTaak~;verkoopKantoor
  ]

ROLE Extra MAINTAINS dummy
RULE dummy : I[WettelijkeTaak] |- I


INTERFACE "RubriceringsGroepen" : V[SESSION*RubriceringsGroep];rubriceringsGroep~;rubriceringsGroep BOX <SHCOLS>
  [ "rubriceringsGroep"     : I[RubriceringsGroep] LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving" : omschrijving
  , "wettelijke taken" : rubriceringsGroep~;wettelijkeTaak INTERFACE WettelijkeTaak
  ]
INTERFACE "RubriceringsGroepUitgebreid" : I[RubriceringsGroep] COLS
  [ "rubriceringsGroep" : I -- LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving"  : omschrijving
  , "artikelen" : rubriceringsGroep[Artikel*RubriceringsGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "grootboek rekeningen" : rubriceringsGroep~;grootboekRekening
  ]


INTERFACE "GBRekeningen" : V[SESSION*GrootboekRekening] BOX <SHCOLS>
  [ "grootboek rekening"     : I[GrootboekRekening] LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "GrootboekRekeningUitgebreid" : I[GrootboekRekening] COLS
  [ "grootboek rekening" : I -- LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : grootboekRekening[Artikel*GrootboekRekening]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


INTERFACE "Verkoopkantoren"  : V[SESSION*KostenPlaats] BOX <SHCOLS>
  [ "verkoop kantoor"     : I[KostenPlaats] LINKTO INTERFACE "KostenPlaatsUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "KostenPlaatsUitgebreid" : I[KostenPlaats] COLS
  [ "verkoop kantoor" : I -- LINKTO INTERFACE "KostenPlaatsUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : verkoopKantoor[Artikel*KostenPlaats]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


INTERFACE "ArtikelVrachtGroepen" : V[SESSION*ArtikelVrachtGroep] BOX <SHCOLS>
  [ "vracht groep"     : I[ArtikelVrachtGroep] LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving" : omschrijving
  , "wettelijke taken"  : artikelVrachtGroep~;wettelijkeTaak
  ]
INTERFACE "ArtikelVrachtGroepUitgebreid" : I[ArtikelVrachtGroep] COLS
  [ "vracht groep" : I -- LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


ROLE Test MAINTAINS testregel1
RULE testregel1 : edsArtikel |- rdwProduct ; product~

ROLE Test MAINTAINS testregel2
RULE testregel2 : edsArtikel; product |- rdwProduct
MEANING "Productcode in EDS is komt niet overeen met productcode van artikel (P&C)" 

ROLE Test MAINTAINS testregel3
RULE testregel3 : (I[EDSProduct]/\edsArtikel;edsArtikel~);rdwProduct |- edsArtikel; product
MEANING "Het rdw-product van een EDS product komt niet overeen met het rdw-product van het artikel van het EDS product"


ENDCONTEXT