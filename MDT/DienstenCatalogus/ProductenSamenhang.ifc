CONTEXT DienstenCatalogus IN DUTCH

INCLUDE "alleRelaties.adl"
--INCLUDE "allePopulaties.adl"

INTERFACE "ProcIDs" FOR BBS : V[SESSION*ProcID] BOX <SHCOLS>
  [ "ProcID"     : I[ProcID] LINKTO INTERFACE "ProcID"
  , "omschrijving" : omschrijving
  ]
INTERFACE "Klantgroepen" FOR BBS : V[SESSION*Klantgroep] BOX <SHCOLS>
  [ "klantgroep"     : I[Klantgroep] LINKTO INTERFACE "Klantgroep"
  , "omschrijving" : omschrijving
  ]

INTERFACE "ProcID" : I[ProcID] BOX
  [ "ProcID"  : procIDnr 
  , "omschrijving" : omschrijving
  , "syseem"       : systeem
  , "bevoegdheden"      : nodigVoor
  , "verloop" : I INTERFACE "ProcIDVerloop"
  , klantgroepen   :  toegekend INTERFACE "Klantgroep"
  ]

INTERFACE "Klantgroep" : I[Klantgroep] BOX
  [ "klantgroep" : I
  , "omschrijving" : omschrijving
  , "procIDs"      : toegekend~
  ]

INTERFACE ProcIDVerloop : I[ProcID] BOX
  [ "" : procID[ProcIdVerloop*ProcID]~ BOX <SHCOLS>
    [ nr : volgNummer
    , omschrijving : omschrijving[ProcIdVerloop*FunctieOmschrijving]
    , product      : rdwProduct[ProcIdVerloop*Product]
    ]
  ]


INTERFACE "Bevoegdheden" FOR EenT : V[SESSION*Bevoegdheid] BOX <SHCOLS>
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  ]

INTERFACE "Bevoegdheid" FOR EenT : I[Bevoegdheid] BOX
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  , "procIDs"      : nodigVoor~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           ]
  ]



INTERFACE "Producten VRS" FOR RDW : V[SESSION*ProductVrs] BOX <SHCOLS>
  [ "ProductVrs"   : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  ]

INTERFACE "Product VRS" FOR RDW : I[ProductVrs] BOX
  [ "ProductVrs"  : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  , "procID"      : procID
      BOX <SHCOLS>
           [ "procId"  : I[ProcID]
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           , "Product VRS" : procID[ProductVrs*ProcID]~
           , verloop : I[ProcID] INTERFACE "ProcIDVerloop"
           ]
  , "organisatieOnd"  : organisatieOnd
  , "soort voertuig" : soort
  , "lichtZwaar" : lichtZwaar
  , "bron(nen)" : bron
  , "presentatieGroep" : presentatieGroep
  ]



INTERFACE "Producten" : V[SESSION*Product] BOX <SHCOLS>
  [ "Product/Dienst"  : I[Product] --LINKTO INTERFACE "Product"
  , "omschrijving (TOA)" : omschrijvingToa
  , "EDS producten"      : rdwProduct[EDSProduct*Product]~
  , "VRS producten"      : rdwProduct[ProductVrs*Product]~
  ]

INTERFACE "Product" : I[Product] BOX
  [ "omschrijving" : omschrijvingToa
  , "onderdeel"    : onderdeel
  , "tarief code"  : tarCodeProd
  , "componenten"  : product~ BOX <SHCOLS>
     [ "component"          : component
     ,  "soort"             : soort[ProductComponent*SoortProdComp]
     ,  "kostenplaats eind" : eind[ProductComponent*KostenPlaats]
     ,  "kostenplaats hulp" : hulp[ProductComponent*KostenPlaats]
     ,  "grootboek"         : gbrToa[ProductComponent*GrootboekRekening]
     ,  "ambt"              : ambt[ProductComponent*AmbtCode]
     ]
  , "artikelen" : product[Artikel*Product]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] -- LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

INTERFACE "Kostenplaatsen" : V[SESSION*KostenPlaats] BOX <SHCOLS>
  [ "KostenPlaats"  : I[KostenPlaats]  INTERFACE "KostenPlaats"
  ] 
INTERFACE "KostenPlaats" FOR Extra : I[KostenPlaats] COLS
  [ ""  : I[KostenPlaats]
  --, "hulp+eind" : (hulp~ \/ eind~);product
  , "hulp"      : hulp~;product
  , "eind"      : eind~;product
  ]


INTERFACE "EDSProducten" FOR RDW : V[SESSION*EDSProduct] BOX <SHCOLS>
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProductUitgebreid"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "artikel"        : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "art omschrijving" : edsArtikel;omschrijving
  ]

INTERFACE "EDSProductUitgebreid" : I[EDSProduct] BOX
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProductUitgebreid"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "EdsProdCode"       : edsProductcode[EDSProduct*EDSProductcode]
  , "RdwProdCode"   : rdwProduct[EDSProduct*Product] LINKTO INTERFACE "Product"
  , "Tarief (eds)"   : edsTarief[EDSProduct*Tarief]
  , "Tarief (f&a)"   : edsArtikel;tarief
  , "Artikel"   : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "Verkoopgroep"   : edsVerkoopgroep[EDSProduct*EDSVerkoopgroep]
  , "Productgroep"   : edsProductgroep[EDSProduct*EDSProductgroep]
  , "Odersoort"   : edsOrdersoort[EDSProduct*EDSOrdersoort]
  , "InternalDisplayName"   : edsInternalDisplayName[EDSProduct*InternalDisplayName]
  ]

INTERFACE "Systeem" : I[Systeem] BOX
  [ "Systeem"   : I
  , "Bevoegdheden": systeem[Bevoegdheid*Systeem]~
  , "ProcIDs" : systeem[ProcID*Systeem]~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           ]
  ]
  


INTERFACE "Artikelen" FOR RDW : V[SESSION*Artikel] BOX <SHCOLS>
  [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
  , "omschrijving" : omschrijving
  ]

INTERFACE "ArtikelKort" : I[Artikel] ROWS
  [ "artikel" : I LINKTO INTERFACE "Artikel"
  , "omschrijving" : omschrijving[Artikel*ArtikelOmschrijving]
  ]
INTERFACE "Artikel" FOR RDW : I[Artikel] BOX 
  [ "omschrijving"     : omschrijving[Artikel*ArtikelOmschrijving] CRUD
  , "prodGroep"        : prodGroep[Artikel*ProductGroep] CRUD
  , "product"          : product[Artikel*Product]CRUD
  , "fmb"              : fmb[Artikel*FMB]CRUD
  , "wettelijkeTaak"   : wettelijkeTaak[Artikel*WettelijkeTaak] INTERFACE "WettelijkeTaak" CRUD
  , "verkoopKantoor"   : verkoopKantoor[Artikel*VerkoopKantoor]CRUD
  , "rubriceringsGroep": rubriceringsGroep[Artikel*RubriceringsGroep]CRUD
  , "grootboekRekening": grootboekRekening[Artikel*GrootboekRekening]CRUD
  , "ArtikelVrachtGroep" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]CRUD
  , "tarief"           : tarief[Artikel*Tarief]CRUD
  ]

INTERFACE "ProdGroepen" FOR RDW : V[SESSION*ProductGroep] BOX <SHCOLS>
  [ "prodGroep"     : I[ProductGroep] LINKTO INTERFACE "ProductGroep"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "ProductGroep" : I[ProductGroep] COLS
  [ "prodGroep" : I
--  , "omschrijving"  : omschrijving
  , "artikelen" : prodGroep[Artikel*ProductGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

INTERFACE "ProductenTO" FOR RDW : V[SESSION*Product] BOX <SHCOLS>
  [ "product"     : I[Product] LINKTO INTERFACE "ProductUitgebreidTO"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "ProductTO" : I[Product] COLS
  [ "product" : I LINKTO INTERFACE "ProductUitgebreidTO"
--  , "omschrijving"  : omschrijving
  ]

INTERFACE "ProductUitgebreidTO" : I[Product] COLS
  [ "product" : I LINKTO INTERFACE "Product"
--  , "omschrijving"  : omschrijving
  , "artikelen" : product[Artikel*Product]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

 
INTERFACE "Wettelijke Taken" FOR Extra : V[SESSION*WettelijkeTaak] BOX <SHCOLS>
  [ "wettelijke taak"     : I[WettelijkeTaak] LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving" : omschrijving
  ]
INTERFACE "WettelijkeTaak" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving"  : omschrijving
  ]

INTERFACE "WettelijkeTaakUitgebreid" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaak"
  , "omschrijving"  : omschrijving
  , "artikelen" : wettelijkeTaak[Artikel*WettelijkeTaak]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "grootboekRekeningen" : wettelijkeTaak~;grootboekRekening
  , "artikelvrachtgroepen"  : wettelijkeTaak~;artikelVrachtGroep
  , "verkoopkantoren" : wettelijkeTaak~;verkoopKantoor
  ]

ROLE Extra MAINTAINS dummy
RULE dummy : I[WettelijkeTaak] |- I


INTERFACE "RubriceringsGroepen" FOR Extra : V[SESSION*RubriceringsGroep];rubriceringsGroep~;rubriceringsGroep BOX <SHCOLS>
  [ "rubriceringsGroep"     : I[RubriceringsGroep] LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving" : omschrijving
  , "wettelijke taken" : rubriceringsGroep~;wettelijkeTaak INTERFACE WettelijkeTaak
  ]
INTERFACE "RubriceringsGroepUitgebreid" : I[RubriceringsGroep] COLS
  [ "rubriceringsGroep" : I -- LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving"  : omschrijving
  , "artikelen" : rubriceringsGroep[Artikel*RubriceringsGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "grootboek rekeningen" : rubriceringsGroep~;grootboekRekening
  ]


INTERFACE "GBRekeningen" FOR Extra : V[SESSION*GrootboekRekening] BOX <SHCOLS>
  [ "grootboek rekening"     : I[GrootboekRekening] LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "GrootboekRekeningUitgebreid" : I[GrootboekRekening] COLS
  [ "grootboek rekening" : I -- LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : grootboekRekening[Artikel*GrootboekRekening]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


INTERFACE "Verkoopkantoren" FOR RDW : V[SESSION*VerkoopKantoor] BOX <SHCOLS>
  [ "verkoop kantoor"     : I[VerkoopKantoor] LINKTO INTERFACE "VerkoopKantoorUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "VerkoopKantoorUitgebreid" : I[VerkoopKantoor] COLS
  [ "verkoop kantoor" : I -- LINKTO INTERFACE "VerkoopKantoorUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : verkoopKantoor[Artikel*VerkoopKantoor]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


INTERFACE "ArtikelVrachtGroepen" FOR Extra : V[SESSION*ArtikelVrachtGroep] BOX <SHCOLS>
  [ "vracht groep"     : I[ArtikelVrachtGroep] LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving" : omschrijving
  , "wettelijke taken"  : artikelVrachtGroep~;wettelijkeTaak
  ]
INTERFACE "ArtikelVrachtGroepUitgebreid" : I[ArtikelVrachtGroep] COLS
  [ "vracht groep" : I -- LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]








ENDCONTEXT