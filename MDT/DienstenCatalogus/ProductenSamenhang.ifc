CONTEXT DienstenCatalogus IN DUTCH

INCLUDE "alleRelaties.adl"
INCLUDE "allePopulaties.adl"

INTERFACE "Alle ProcIDs"  : V[SESSION*ProcIdent] BOX <SHCOLS>
  [ "ProcIdent"     : I[ProcIdent] LINKTO INTERFACE "ProcIdent"
  , "omschrijving" : omschrijving
  ]
INTERFACE "ProcIDs met product" : V[SESSION*Product];
                                    (   rdwProduct~;procIdent[ProcVerloop*ProcIdent]
                                     \/ rdwProduct~;procIdent[ProductVrs*ProcIdent]
                                    )
 BOX <SHCOLS>
  [ "ProcIdent"     : I[ProcIdent] LINKTO INTERFACE "ProcIdent"
  , "omschrijving" : omschrijving
  , "producten (via verloop)" : procIdent[ProcVerloop*ProcIdent]~;rdwProduct COLS
     [ productcode : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  , "producten (via VRS)" : procIdent[ProductVrs*ProcIdent]~;rdwProduct COLS
     [ productcode : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  ]
INTERFACE "ProcIdent" : I[ProcIdent] BOX
  [ "ProcIdent"  : procIDnr 
  , "omschrijving" : omschrijving
  , "syseem"       : systeem
  , "erkenningen"      : recht~
  , "verloop" : I INTERFACE "ProcVerloop"
  , klantgroepen   :  toegekend INTERFACE "Klantgroep"
  ]

INTERFACE "Klantgroepen"  : V[SESSION*Klantgroep] BOX <SHCOLS>
  [ "klantgroep"     : I[Klantgroep] LINKTO INTERFACE "Klantgroep"
  , "omschrijving" : omschrijving
  ]
INTERFACE "Klantgroep" : I[Klantgroep] BOX
  [ "klantgroep" : I
  , "omschrijving" : omschrijving
  , "procIDs"      : toegekend~
  ]

INTERFACE ProcVerloop : I[ProcIdent] BOX
  [ "" : procIdent[ProcVerloop*ProcIdent]~ BOX <SHCOLS>
    [ nr : volgNummer
    , omschrijving : omschrijving[ProcVerloop*FunctieOmschrijving]
    , product      : rdwProduct[ProcVerloop*Product]
    ]
  ]


INTERFACE "Erkenningen" : V[SESSION*Erkenning] BOX <SHCOLS>
  [ "Erkenning"  : I[Erkenning] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  ]

INTERFACE "Erkenning" : I[Erkenning] BOX
  [ "Erkenning"  : I[Erkenning] 
  , "omschrijving" : omschrijving
  , "systeem"       : systeem
  , "procIDs"      : recht
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           ]
  ]



INTERFACE "Producten VRS"  : V[SESSION*ProductVrs] BOX <SHCOLS>
  [ "ProductVrs"   : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  ]

INTERFACE "Product VRS"  : I[ProductVrs] BOX
  [ "ProductVrs"  : I[ProductVrs] 
  , "omschrijving" : omschrijving
  , "rdwProduct"   : rdwProduct
  , "procIdent"      : procIdent
      BOX <SHCOLS>
           [ "procId"  : I[ProcIdent]
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           , "Product VRS" : procIdent[ProductVrs*ProcIdent]~
           , verloop : I[ProcIdent] INTERFACE "ProcVerloop"
           ]
  , "organisatieOnd"  : organisatieOnd
  , "soort voertuig" : soort
  , "lichtZwaar" : lichtZwaar
  , "bron(nen)" : bron
  , "presentatieGroep" : presentatieGroep
  ]

INTERFACE "Bron" : I[Bron] BOX
  [ "Bron" : I
  , "Producten vrs" : bron~ BOX <SHCOLS>
     [ "ProductVrs"   : I[ProductVrs] 
     , "omschrijving" : omschrijving
     , "rdwProduct"   : rdwProduct
     ]
  ]

INTERFACE "OrganisatieOnd" : I[OrganisatieOnderdeel] BOX
  [ "OrganisatieOnd" : I
  , "Producten vrs" : organisatieOnd~ BOX <SHCOLS>
     [ "ProductVrs"   : I[ProductVrs] 
     , "omschrijving" : omschrijving
     , "rdwProduct"   : rdwProduct
     ]
  ]


INTERFACE "Producten" : V[SESSION*Product] BOX <SHCOLS>
  [ "Product/Dienst"  : I[Product] --LINKTO INTERFACE "Product"
  , "omschrijving (TOA)" : omschrijvingToa
  , "EDS producten"      : rdwProduct[EDSProduct*Product]~
  , "VRS producten"      : rdwProduct[ProductVrs*Product]~
  ]

INTERFACE "Product" : I[Product] BOX
  [ "rdw-product"  : I
  , "omschrijving (TOA)" : omschrijvingToa
  , "onderdeel"    : onderdeel
  , "tarief code"  : tarCodeProd
  , "componenten"  : product~ BOX <SHCOLS>
     [ "component"          : component
     ,  "soort"             : soort[ProductComponent*SoortProdComp]
     ,  "kostenplaats eind" : eind[ProductComponent*KostenPlaats] LINKTO INTERFACE "KostenPlaatsUitgebreid"
     ,  "kostenplaats hulp" : hulp[ProductComponent*KostenPlaats] LINKTO INTERFACE "KostenPlaatsUitgebreid"
     ,  "grootboek"         : gbrToa[ProductComponent*GrootboekRekening]
     ,  "ambt"              : ambt[ProductComponent*AmbtCode]
     ]
  , "artikelen" : product[Artikel*Product]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] -- LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "procIdents (via verloop)" : (procIdent[ProcVerloop*ProcIdent]~;rdwProduct)~ COLS
     [ procIdent : I[ProcIdent] LINKTO INTERFACE "ProcIdent"
     , "omschrijving" : omschrijving
     ]
  , "procIdents (via VRS)"     : (procIdent[ProductVrs*ProcIdent]~;rdwProduct[ProductVrs*Product])~ COLS
     [ procIdent : I[ProcIdent] LINKTO INTERFACE "ProcIdent"
     , "omschrijving" : omschrijving
     ]
   ]
INTERFACE "TariefCode" : I[TariefCode] BOX
  [ "tarief code" : I
  , "producten"   : tarCodeProd~ BOX <SHCOLS>
       [ product : I
       , "omschrijving (TOA)" : omschrijvingToa
       ]
  ]
INTERFACE "Onderdeel" : I[Onderdeel] BOX
  [ "Onderdeel" : I
  , "producten"   : onderdeel~ BOX <SHCOLS>
       [ product : I
       , "omschrijving (TOA)" : omschrijvingToa
       ]
  ]
INTERFACE "Kostenplaatsen" : V[SESSION*KostenPlaats] BOX <SHCOLS>
  [ "KostenPlaats"  : I[KostenPlaats]  LINKTO INTERFACE "KostenPlaatsUitgebreid"
  ] 


INTERFACE "EDSProducten"  : V[SESSION*EDSProduct] BOX <SHCOLS>
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProduct"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "artikel"        : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "art omschrijving" : edsArtikel;omschrijving
  ]

INTERFACE "EDSProduct" : I[EDSProduct] BOX
  [ "EDSProduct"     : I[EDSProduct] LINKTO INTERFACE "EDSProduct"
  , "Omschrijving"   : omschrijving -- [EDSProduct*ArtikelOmschrijving]
  , "EdsProdCode"       : edsProductcode[EDSProduct*EDSProductcode]
  , "EdsProductDefinitie" : edsProductDefinitie
  , "RdwProduct"     : rdwProduct[EDSProduct*Product] LINKTO INTERFACE "Product"
  , "Tarief (eds)"   : edsTarief[EDSProduct*Tarief]
  , "Tarief (f&a)"   : edsArtikel;tarief
  , "Artikel"   : edsArtikel[EDSProduct*Artikel] LINKTO INTERFACE "Artikel"
  , "Verkoopgroep"   : edsVerkoopgroep[EDSProduct*EDSVerkoopgroep]
  , "Productgroep"   : edsProductgroep[EDSProduct*EDSProductgroep]
  , "Odersoort"   : edsOrdersoort[EDSProduct*EDSOrdersoort]
  , "InternalDisplayName"   : edsInternalDisplayName[EDSProduct*InternalDisplayName]
  ]

INTERFACE "Systeem" : I[Systeem] BOX
  [ "Systeem"   : I
  , "Erkenningen": systeem[Erkenning*Systeem]~
  , "ProcIDs" : systeem[ProcIdent*Systeem]~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           ]
  ]
  


INTERFACE "Artikelen" : V[SESSION*Artikel] BOX <SHCOLS>
  [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
  , "omschrijving" : omschrijving
  ]

INTERFACE "Artikel" : I[Artikel] BOX 
  [ "artikel"          : I
  , "omschrijving"     : omschrijving[Artikel*ArtikelOmschrijving] CRUD
  , "prodGroep"        : prodGroep[Artikel*ProductGroep] CRUD
  , "product"          : product[Artikel*Product]CRUD
  , "EDS producten"    : edsArtikel~ CRUD
  , "fmb"              : fmb[Artikel*FMB]CRUD
  , "subtaakcluster"   : fmb;subtaakcluster
  , "wettelijkeTaak"   : fmb;subtaakcluster;wettelijkeTaak INTERFACE "WettelijkeTaak" CRUD
  , "verkoopKantoor"   : verkoopKantoor[Artikel*KostenPlaats]CRUD
  , "rubriceringsGroep": rubriceringsGroep[Artikel*RubriceringsGroep]CRUD
  , "grootboekRekening": grootboekRekening[Artikel*GrootboekRekening]CRUD
  , "ArtikelVrachtGroep" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]CRUD
  , "tarief"           : tarief[Artikel*Tarief]CRUD
  ]

INTERFACE "FMB" : I[FMB] BOX
  [ "fmb-code" : I
  , "omschrijving" : omschrijving
  , "wettelijke taak" : subtaakcluster;wettelijkeTaak INTERFACE "WettelijkeTaak" CRUD
  , "artikelen"  : fmb~ BOX <SHCOLS>
     [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
     , "omschrijving" : omschrijving
     ]
  ]

INTERFACE "ProdGroepen"  : V[SESSION*ProductGroep] BOX <SHCOLS>
  [ "prodGroep"     : I[ProductGroep] LINKTO INTERFACE "ProductGroep"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "ProductGroep" : I[ProductGroep] COLS
  [ "prodGroep" : I
--  , "omschrijving"  : omschrijving
  , "artikelen" : prodGroep[Artikel*ProductGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

 
INTERFACE "Wettelijke Taken" : V[SESSION*WettelijkeTaak] BOX <SHCOLS>
  [ "wettelijke taak"     : I[WettelijkeTaak] LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving" : omschrijving
  ]
INTERFACE "WettelijkeTaak" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving"  : omschrijving
  ]

INTERFACE "WettelijkeTaakUitgebreid" : I[WettelijkeTaak] COLS
  [ "wettelijke taak" : I LINKTO INTERFACE "WettelijkeTaakUitgebreid"
  , "omschrijving"  : omschrijving
  , "artikelen (via fmb)" : wettelijkeTaak[Subtaakcluster*WettelijkeTaak]~;subtaakcluster~;fmb~ BOX <SHCOLS>
     [ "subtaak"  : fmb;subtaakcluster;subtaakNr
     , "fmb"      : fmb
     , "artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
     , "omschrijving" : omschrijving
     ]
--  , "artikelen" : wettelijkeTaak[Artikel*WettelijkeTaak]~ --BOX <SHCOLS>
--    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
--    , "omschrijving" : omschrijving
--    ]
--  , "grootboekRekeningen" : wettelijkeTaak~;grootboekRekening
--  , "artikelvrachtgroepen"  : wettelijkeTaak~;artikelVrachtGroep
--  , "kostenplaatsen" : wettelijkeTaak~;verkoopKantoor
  ]

INTERFACE "Subtaakclusters" : V[SESSION*Subtaakcluster] BOX <SHCOLS>
  [ subtaakcluster : I
  , "wettelijke taak" : wettelijkeTaak LINKTO INTERFACE "WettelijkeTaak"
  , "FMBs"  : subtaakcluster~  BOX <SHCOLS>
     [ fmb : I
     , "artikelen" : fmb~ BOX <SHCOLS>
       [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
       , "omschrijving" : omschrijving
       ]
     ]
  ] 
  
ROLE Extra MAINTAINS dummy
RULE dummy : I[WettelijkeTaak] |- I


INTERFACE "RubriceringsGroepen" : V[SESSION*RubriceringsGroep];rubriceringsGroep~;rubriceringsGroep BOX <SHCOLS>
  [ "rubriceringsGroep"     : I[RubriceringsGroep] LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving" : omschrijving
  ]
INTERFACE "RubriceringsGroepUitgebreid" : I[RubriceringsGroep] COLS
  [ "rubriceringsGroep" : I -- LINKTO INTERFACE "RubriceringsGroepUitgebreid"
  , "omschrijving"  : omschrijving
  , "artikelen" : rubriceringsGroep[Artikel*RubriceringsGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  , "grootboek rekeningen" : rubriceringsGroep~;grootboekRekening
  ]


INTERFACE "GBRekeningen" : V[SESSION*GrootboekRekening] BOX <SHCOLS>
  [ "grootboek rekening"     : I[GrootboekRekening] LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "GrootboekRekeningUitgebreid" : I[GrootboekRekening] COLS
  [ "grootboek rekening" : I -- LINKTO INTERFACE "GrootboekRekeningUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : grootboekRekening[Artikel*GrootboekRekening]~BOX <SHCOLS>
     [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
     , "omschrijving" : omschrijving
     ]
  , "producten (via TOA)"    : gbrToa~;product BOX <SHCOLS>
     [ productcode : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  , "producten (via artikelen)" : grootboekRekening~;product BOX <SHCOLS>
     [ productcode : I[Product]
     , "omschrijving (TOA)" : omschrijvingToa
     ]
  ]


INTERFACE "Verkoopkantoren"  : V[SESSION*KostenPlaats] BOX <SHCOLS>
  [ "verkoop kantoor"     : I[KostenPlaats] LINKTO INTERFACE "KostenPlaatsUitgebreid"
--  , "omschrijving" : omschrijving
  ]
INTERFACE "KostenPlaatsUitgebreid" : I[KostenPlaats] COLS
  [ "verkoop kantoor" : I
  , "omschrijving"  : omschrijving
  , "afdeling"  : afdeling
  , "hulp"      : hulp~;product
  , "eind"      : eind~;product
  , "artikelen" : verkoopKantoor[Artikel*KostenPlaats]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]

INTERFACE "Afdeling" : I[Afdeling] BOX <SHCOLS>
  [ "afdeling"       : I
  , "kostenplaatsen" : afdeling~
  , "artikelen"      : afdeling~;verkoopKantoor~
  ]

INTERFACE "ArtikelVrachtGroepen" : V[SESSION*ArtikelVrachtGroep] BOX <SHCOLS>
  [ "vracht groep"     : I[ArtikelVrachtGroep] LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving" : omschrijving
  , "wettelijke taken (oud)"  : artikelVrachtGroep~;wettelijkeTaak_1
  ]
INTERFACE "ArtikelVrachtGroepUitgebreid" : I[ArtikelVrachtGroep] COLS
  [ "vracht groep" : I -- LINKTO INTERFACE "ArtikelVrachtGroepUitgebreid"
--  , "omschrijving"  : omschrijving
  , "artikelen" : artikelVrachtGroep[Artikel*ArtikelVrachtGroep]~BOX <SHCOLS>
    [ "Artikel"     : I[Artikel] LINKTO INTERFACE "Artikel"
    , "omschrijving" : omschrijving
    ]
  ]


ROLE Test MAINTAINS testregel1
RULE testregel1 : edsArtikel |- rdwProduct ; product~

ROLE Test MAINTAINS testregel2
RULE testregel2 : edsArtikel; product |- rdwProduct
MEANING "Productcode in EDS is komt niet overeen met productcode van artikel (P&C)" 

ROLE Test MAINTAINS testregel3
RULE testregel3 : (I[EDSProduct]/\edsArtikel;edsArtikel~);rdwProduct |- edsArtikel; product
MEANING "Het rdw-product van een EDS product komt niet overeen met het rdw-product van het artikel van het EDS product"


ENDCONTEXT