CONTEXT RollenBeheer IN DUTCH



PATTERN Organisaties
CONCEPT Organisatie "Een bedrijf/organisatie met een registratie in het handelsregister."
CONCEPT Identiteit "Een inschrijving in (een van de) de klantenregistratie van RDW."

RELATION kvkNummer[Organisatie*KvkNummer][UNI,TOT]
MEANING "Elke organisatie heeft een registratie bij de KvK."
RELATION hoortBij[Identiteit*Organisatie][UNI]
MEANING "Elke identiteit hoort bij precies één organisatie"
REPRESENT KvkNummer TYPE ALPHANUMERIC

-- Invarianten
RULE "Organisaties worden geidentificeerd door hun KvK nummer" : kvkNummer;kvkNummer~ |- I[Organisatie]

ENDPATTERN

PATTERN Beheerders
RELATION beheerderVan[Identiteit*Identiteit][ASY,UNI,INJ]
MEANING "Een beheerder is een identiteit die een andere identiteit beheert. Een identiteit kan worden beheerd door maximaal één beheerder"

RULE "Beheerder wordt zelf niet beheerd" : -(beheerderVan~;beheerderVan~)
MEANING "Een beheerder wordt zelf niet beheerd."

RULE "Elke beheerder beheert precies één identiteit." : (beheerderVan~;beheerderVan~) |- I
MEANING "Elke beheerder beheert precies één identiteit."

ROLE ExecEngine MAINTAINS BeheerdersIdentiteitHoortBijZelfdeOrganisatie1
RULE BeheerdersIdentiteitHoortBijZelfdeOrganisatie1 : beheerderVan;hoortBij |- hoortBij
MEANING "De beheerder van een identiteit hoort bij de organisatie die hoort bij de identiteit die hij beheert." 
VIOLATION ( TXT "InsPair;hoortBij;Identiteit;", SRC I, TXT ";Organisatie;", TGT I )

--RULE "Beheerder hoort bij dezelfde organisatie als die waarvan hij de beheerder is" 
--    :  (I/\(beheerderVan;beheerderVan~));hoortBij |- beheerderVan;hoortBij 
--MEANING "Elke beheerder hoort bij dezelfde organisatie die hoort bij de identiteit waarvan hij de beheerder is."
--VIOLATION ( TXT "DelPair;hoortBij;Identiteit;", SRC I, TXT ";Organisatie;", SRC hoortBij )

ENDPATTERN




PATTERN Rollen

RELATION aangemaaktDoor[Identiteit*Identiteit] [ASY,UNI]
MEANING "Een rol is een identiteit die is aangemaakt door een beheerder. Een beheerder is zelf géén rol. Een rol is aangemaakt door precies één beheerder."

  
ROLE "ExecEngine" MAINTAINS "Aanmaken rol"
RULE "Aanmaken rol" : aangemaaktDoor |- hoortBij;hoortBij~
MEANING "Elke rol hoort bij de organisatie die hoort bij de beheerder die die rol heeft aangemaakt."
VIOLATION ( TXT "InsPair;hoortBij;Identiteit;", SRC I, TXT ";Organisatie;", TGT hoortBij)

ENDPATTERN




PATTERN "Toekennen van bevoegdheden"

CONCEPT ProcID "Een ProcID is een door een identiteit te verrichten soort handeling in de systemen van RDW."
CONCEPT Dienst "Een dienst is een samenhangend soort activiteit, waarvan uitvoering nuttig is voor een identiteit."
CONCEPT Klantgroep "Een klantgroep is een groepering naar de aard van die klanten."

RELATION procIDnr [ProcID*ProcIDNummer] [UNI,TOT,SUR]
MEANING "Elke ProcID wordt geïdentificeerd door een getal."
REPRESENT ProcIDNummer TYPE INTEGER

RELATION omschrijving[ProcID*ProcIDOmschrijving][UNI,TOT,SUR]
MEANING "Elke ProcID heeft precies één omschrijving."
REPRESENT ProcIDOmschrijving TYPE ALPHANUMERIC

RELATION toegekend[ProcID*Identiteit]
MEANING "Een ProcID kan zijn toegekend aan een identiteit."

RELATION toegekend[ProcID*Klantgroep]
MEANING "Een ProcID kan zijn toegekend aan een klantgroep."

PURPOSE RELATION beschikbaar[Dienst*Identiteit]
{+Een beheerder die rollen aanmaakt voor de moeder-identiteit van een organisatie doet dit met als doel dat die rol-identiteit bepaalde bevoegdheden kan hebben. De bevoegdheden zijn door RDW logisch gegroepeerd als diensten. De beheerder kan beslissen welke van deze diensten beschikbaar zijn voor de rol.
-}
RELATION beschikbaar[Dienst*Identiteit]
MEANING "Een dienst kan beschikbaar zijn voor een identiteit."

RELATION omschrijving[Klantgroep*KlantgroepOmschrijving][TOT,UNI,SUR]
MEANING "Elke klantgroep heeft één unieke omschrijving."

RELATION zitIn[Identiteit*Klantgroep] [UNI]
MEANING "Een identiteit zit in hooguit één klantgroep."

RELATION afwijking[ProcID*Identiteit]
MEANING "Een identiteit kan voor een ProcID afwijken ten opzichte van de aan de klantgroep toegekende ProcIDs."

RELATION nodigVoor [ProcID*Dienst]
MEANING "Een ProcID kan nodig zijn voor het uitvoeren van een dienst."

-- PURPOSE RELATION geautoriseerd --[Identiteit*ProcID]
-- {+Bevoegdheden die worden toegekend aan een identiteit, al dan niet indirect door middel van een klantgroep of dienst, maken dat die identiteit al dan niet is geautoriseerd een ProcID. We zeggen dan, dat die identiteit is geautoriseerd voor die ProcID. Deze eigenschap wordt afgeleid door deze eerdergenoemde instellingen.
---}
RELATION geautoriseerd[Identiteit*ProcID]
MEANING "Een identiteit kan geautoriseerd zijn voor een ProcID."


--[REGELS]--

RULE "Dienst alleen beschikbaar voor rol" : (I/\beschikbaar~;beschikbaar) |- aangemaaktDoor;aangemaaktDoor~
MEANING "Een dienst kan alleen beschikbaar zijn voor een identiteit, wanneer die indentiteit is aangemaakt door een beheerder."









ROLE ExecEngine MAINTAINS "toevoegen autorisatie", "verwijderen autorisatie"
--RULE "toevoegen autorisatie" : ((I/\-(aangemaaktDoor;aangemaaktDoor~)));((zitIn;toegekend~ /\ -afwijking~) \/ toegekend~) |- geautoriseerd 
RULE "toevoegen autorisatie" : (   (zitIn;toegekend~ /\ -afwijking~)
--                                \/ (-(zitIn;toegekend~) /\ afwijking~)
                                \/ (toegekend~)
                                \/ (  (aangemaaktDoor;beheerderVan;geautoriseerd)
                                    /\(beschikbaar~;nodigVoor~)
                                   )
                               ) |- geautoriseerd
MEANING "Een identiteit is geautoriseerd voor een ProcID als aan de voorwaarden is voldaan."
VIOLATION ( TXT "InsPair;geautoriseerd;Identiteit;", SRC I, TXT ";ProcID;", TGT I )
PURPOSE RULE "toevoegen autorisatie" MARKDOWN 
{+Een identiteit is geautoriseerd voor een ProcID als:

  * die identiteit zit in een klantgroep waaraan die ProcID is toegekend en waarvoor die identiteit geen afwijking heeft OF
  * die ProcID is toegekend aan die identiteit OF
  * Beide:
    * die identiteit is aangemaakt door de beheerder van een identiteit, en die identiteit is geautoriseerd voor die ProcID EN
    * er is een dienst beschikbaar voor die identiteit, waarbij de ProcID nodig is voor die dienst.

-}

RULE "verwijderen autorisatie" : 
                              -(   (zitIn;toegekend~ /\ -afwijking~)
--                                \/ (-(zitIn;toegekend~) /\ afwijking~)
                                \/ (toegekend~)
                                \/ (  (aangemaaktDoor;beheerderVan;geautoriseerd)
                                    /\(beschikbaar~;nodigVoor~)
                                   )
                               ) |- -geautoriseerd
-- ((I/\-(beheerderVan;beheerderVan~));(I/\-(aangemaaktDoor;aangemaaktDoor~)));geautoriseerd |- (zitIn;toegekend~ /\ -afwijking~) \/ toegekend~
VIOLATION ( TXT "DelPair;geautoriseerd;Identiteit;", SRC I, TXT ";ProcID;", TGT I )

RULE "beheerder heeft ProcID rollenbeheerder" : I/\(beheerderVan;beheerderVan~) |- toegekend~ ; I[ProcID]; '8888' ; toegekend
MEANING "De ProcID 'Beheren rollenpassen' is toegekend aan elke beheerder."
VIOLATION ( TXT "InsPair;toegekend;ProcID;8888;Identiteit;", TGT I)
ROLE ExecEngine MAINTAINS  "beheerder heeft ProcID rollenbeheerder"

RULE "Rollenbeheer is voorbehouden aan beheerder" : '8888' ; toegekend[ProcID*Identiteit] |- toegekend; (beheerderVan;beheerderVan~)
MEANING "De ProcID 'Beheren rollenpassen' is voorbehouden aan de beheerder (van de organisatie)."
ENDPATTERN





PATTERN "Identificatie middelen"
RELATION authenticeert[Certificaat*Identiteit] [UNI,TOT]
MEANING "Elk certificaat authenticeert voor precies één identiteit"
RELATION geldig[Certificaat*Certificaat] [PROP]
MEANING "Certificaten hebben de eigenschap van geldigheid"
RELATION drager[Certificaat*Pas][UNI,SUR,INJ]
MEANING "Een pas is drager van precies één uniek certificaat."


ENDPATTERN




RELATION rolnaam[Identiteit*Rolnaam][UNI]
MEANING "Een rol heeft hooguit één naam."
--ROLE Beheerder MAINTAINS "Naam van een rol"
--RULE "Naam van een rol" : I[Identiteit]/\aangemaaktDoor;aangemaaktDoor~ |- rolnaam;rolnaam~
--VIOLATION (TXT "Geef de rol een naam")


-- Specifieke views
VIEW Organisaties: Organisatie(kvkNummer)
--VIEW ProcIDs: ProcID(omschrijving)
VIEW Klantgroepen: Klantgroep (omschrijving)

ENDCONTEXT