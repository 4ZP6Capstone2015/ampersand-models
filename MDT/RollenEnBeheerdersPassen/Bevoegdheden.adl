CONTEXT Bevoegdheden IN DUTCH
INCLUDE "../DienstenCatalogus/BevoegdhedenEenT.xlsx"

PATTERN Bevoegdheden
PURPOSE PATTERN Bevoegdheden
{+Klanten van RDW kunnen onder voorwaarden informatie raadplegen en/of muteren. Om als klant in staat te zijn bepaalde handelingen in de systemen van RDW te verrichten, moet daartoe een recht bestaan op basis waarvan de autorisatie is ingericht. Deze paragraaf gaat over de taal die daarbij wordt gehanteerd. 
-}
PURPOSE CONCEPT Bevoegdheid
{+Een recht om bepaalde handelingen te verrichten kan bestaan op grond van een specifieke erkenning. Zo kan bijvoorbeeld een APK melding worden gedaan door een klant, op grond van het feit dat die klant een bevoegdheid APK heeft.
-}
CONCEPT Bevoegdheid "Een bevoegdheid is het recht om een (groep van) bepaalde handeling(en) uit te voeren."
PURPOSE RELATION recht[Bevoegdheid*ProcID]
{+De handelingen die uitgevoerd kunnen worden heeft RDW ingericht door middel van proc-id's. Een bevoegdheid kan daarom worden opgevat als een verzameling proc-id's, waarop recht bestaat om te mogen uitvoeren. We kunnen daarom zeggen dat een bevoegdheid het recht geeft om een procId uit te voeren. 
-}
RELATION recht[Bevoegdheid*ProcID]
MEANING "Een bevoegdheid kan recht geven een procId."

PURPOSE RELATION heeft[Klant*Bevoegdheid]
{+Aan een klant kan een bevoegdheid worden toegekend. Dat kan bijvoorbeeld omdat een klant daarom een verzoek heeft gedaan, en voldoet aan de voorwaarden om het recht te verkrijgen. Het kan echter ook, omdat de klant is wie hij is, en RDW op grond van bijvoorbeeld wet- en regelgeving een verplichting heeft ten opzichte van die klant.
-}
RELATION heeft[Klant*Bevoegdheid]
MEANING "Een klant kan een bevoegdheid hebben."

PURPOSE RELATION geautoriseerd[Identiteit*ProcID]
{+Een klant is een identiteit. Een identiteit kan geautoriseerd zijn voor procId's. 
-}
RELATION geautoriseerd[Identiteit*ProcID]
MEANING "Een identiteit kan geautoriseerd zijn voor een procId."

ROLE ExecEngine MAINTAINS "Een klant die een bevoegdheid heeft die recht geeft op een procId, mag die procId."
RULE "Een klant die een bevoegdheid heeft die recht geeft op een procId, mag die procId." : heeft;recht |- geautoriseerd
MEANING "Een klant die een bevoegdheid heeft die recht geeft op een procId, mag die procId."
VIOLATION (TXT "{EX} InsPair;geautoriseerd;Identiteit;", SRC I[Identiteit], TXT ";ProcID;", TGT I
          )     
 

ROLE ExecEngine MAINTAINS "Een klant zonder een bevoegdheid die recht geeft op een procId, mag die procId niet."
RULE "Een klant zonder een bevoegdheid die recht geeft op een procId, mag die procId niet." : I[Klant];geautoriseerd |- heeft;recht
MEANING "Een klant zonder een bevoegdheid die recht geeft op een procId, mag die procId niet."
VIOLATION (TXT "{EX} DelPair;geautoriseerd;Identiteit;", SRC I, TXT ";ProcID;", TGT I
          )     

ROLE ExecEngine MAINTAINS "Elke beheerder is geautoriseerd voor `Beheren Rollenpassen`."
RULE "Elke beheerder is geautoriseerd voor `Beheren Rollenpassen`." 
  : I[Identiteit] /\ I[Beheerder] |- geautoriseerd;'8888';geautoriseerd~
MEANING "Elke beheerder is geautoriseerd voor `Beheren Rollenpassen`."
VIOLATION (TXT "{EX} InsPair;geautoriseerd;Identiteit;", SRC I[Identiteit], TXT ";ProcID;", TXT "8888"
          )     


RELATION omschrijving[Bevoegdheid*BevoegdheidOmschrijving][TOT,UNI]
RELATION omschrijving[ProcID*ProcIDOmschrijving] PRAGMA "De omschrijving van ProcID " " is `""`."
REPRESENT BevoegdheidOmschrijving, BevoegdheidOmschrijving TYPE ALPHANUMERIC


ENDPATTERN

RELATION systeem[Bevoegdheid*Systeem][UNI]


ENDCONTEXT