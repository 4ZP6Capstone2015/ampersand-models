CONTEXT "Façade" IN DUTCH
INCLUDE "KlantRollen.ifc"
INCLUDE "Bevoegdheden.adl"
INCLUDE "../DienstenCatalogus/BevoegdhedenEenT.xlsx"
INCLUDE "ProcIds.xlsx"
INCLUDE "Identiteiten_extra.xlsx"
INCLUDE "RDWDienst.adl"

-- INCLUDE "Identiteiten_extra.xlsx"  --This does not work yet

RELATION systeem[ProcID*Systeem][UNI]

INTERFACE "Identiteiten" FOR RDW : V[SESSION*SESSION] TABS
  [ "Klanten" : V[SESSION*Klant] BOX <SHCOLS>
    [ "Klant"  : I[Klant]
    , "naam" : naam
  --  , "omschrijving" : omschrijving
    , "certificaten" : identiteit~
    , "Beheerder"      : beheerder
    , "Rollen"         : klant~
       COLS [ "" : I[KlantRol], "Rol naam" : naam]
    ]
  , "Beheerders" : V[SESSION*Beheerder] BOX <SHCOLS>
    [ "Beheerder"  : I[Beheerder]
  --  , "omschrijving" : omschrijving
    , "certificaten" : identiteit~
    , "Klant"      : beheerder~;I[Klant];naam
    , "Rollen"         : beheerder~;klant~;I[KlantRol];naam
    ]
  , "Rollen"     : V[SESSION*KlantRol] BOX <SHCOLS>
    [ "Klantrol"  : I[KlantRol] 
  --  , "omschrijving" : omschrijving
    , "Klant"      : klant
    , "naam"      : klant;naam
    , "certificaten" : identiteit~
        ]
  ]
INTERFACE "Bevoegdheden" FOR RDW : V[SESSION*Bevoegdheid] BOX <SHCOLS>
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"      : systeem
  ]
INTERFACE "ProcIds met en zonder bevoegdheden" FOR BBS   : V[SESSION*SESSION] TABS
  [ "met bevoegdheid" : V[SESSION*ProcID]  ;(I /\ recht~;recht)
      BOX <SHCOLS>
           [ "procId"       : I 
           , "omschrijving" : omschrijving[ProcID*ProcIDOmschrijving]
           , "bevoegdheden" : recht~
           ]
  , "zonder bevoegdheid" : V[SESSION*ProcID]  ;-(I /\ recht~;recht)
      BOX <SHCOLS>
           [ "procId"       : I 
           , "omschrijving" : omschrijving[ProcID*ProcIDOmschrijving]
           ]
  ]
 
INTERFACE "Bevoegdheid"  ( omschrijving[Bevoegdheid*BevoegdheidOmschrijving]
                        , systeem[Bevoegdheid * Systeem]
                        , recht
                        , heeft
                        ) FOR RDW : I[Bevoegdheid] CRUD BOX
  [ "Bevoegdheid"  : I[Bevoegdheid] 
  , "omschrijving" : omschrijving
  , "systeem"      : systeem
  , "procIDs"        : recht crud
  , "procIDs "      : recht CRUD
      BOX <SHCOLS>
           [ "procId"       : I 
           , "omschrijving" : omschrijving[ProcID*ProcIDOmschrijving]
           , "systeem"      : systeem
           ]
  , "klanten"      : heeft~
  ]
INTERFACE Dienst ( omschrijving[RdwDienst*DienstOmschrijving]
                 , nodigVoor
                 , gekoppeldAan[RdwDienst*KlantRol]
                 ) FOR RDW : I[RdwDienst] CRUD BOX
  [ "dienst"  : I
  , omschrijving   : omschrijving
  , "benodigde procId's " : nodigVoor~ crud
  , "benodigde procId's" : nodigVoor~ CRUd
      BOX <SHCOLS>
           [ "procId"       : I 
           , "omschrijving" : omschrijving[ProcID*ProcIDOmschrijving]
           ]
  , "gekoppelde klantrollen" : gekoppeldAan[RdwDienst*KlantRol] CRUd
  ]

INTERFACE "Klant" (beheerder,klant,heeft,naam) FOR "RDW" : I[Klant] D BOX
  [ "Klant"  : I[Klant] 
  , "naam" : naam
  , "Beheerder"      : beheerder
  , "Klantrollen"         : klant~
  , "bevoegdheden" : heeft CRUD
  , "authorisaties" : geautoriseerd crud
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           , "systeem" : systeem
           ]
  ]

INTERFACE "Beheerder" (beheerder,klant) FOR "RDW" : I[Beheerder] D BOX
  [ "Beheerder"      : I
  , "Klant"  : beheerder~  
  , "beheerder van" : beheerder~;naam
  , "geautoriseerd" : geautoriseerd
       BOX <SHCOLS> [ procId : I , omschrijving : omschrijving]
  , "selecteerbare procIds"  : selecteerbaarVoor[ProcID*Beheerder]~
       BOX <SHCOLS> [ procId : I , omschrijving : omschrijving]
  , "selecteerbare diensten" : selecteerbaarVoor[RdwDienst*Beheerder]~
       BOX <SHCOLS> [ dienst : I , omschrijving : omschrijving]
  , "Rollen"         : beheerder~;klant~;I[KlantRol]
       BOX <SHCOLS> [ " " : I , naam : naam]
  
  ]
INTERFACE "KlantRol" (klant, gekoppeldAan[RdwDienst*KlantRol],gekoppeldAan[ProcID*KlantRol], naam) FOR "RDW" : I[KlantRol] CRUD BOX
  [ "Klant"  : klant
  , "naam": klant;naam
  , "KlantRol"      : naam
  , "selecteerbare procIds" : klant;beheerder;selecteerbaarVoor[ProcID*Beheerder]~
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
        --   , "systeem" : systeem
           ]
  , "selecteerbare diensten" : klant;beheerder;selecteerbaarVoor[RdwDienst*Beheerder]~
       BOX <SHCOLS> [ dienst : I , omschrijving : omschrijving]
  , "gekoppelde procIds" : gekoppeldAan[ProcID*KlantRol]~     CRUD
  , "gekoppelde diensten" : gekoppeldAan[RdwDienst*KlantRol]~     CRUD
  , "authorisaties" : geautoriseerd crud
      BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           ]
  ]
INTERFACE "Certificaten" FOR "CA" : V[SESSION*SESSION] TABS
  [ "Dienstenpassen"   :  V[SESSION*Klant];identiteit~ D BOX <SHCOLS>
    [ "certificaat" : I
    , "klant"       : identiteit
    , "naam"        : identiteit;naam
    ]
  , "Beheerderspassen" : V[SESSION*Beheerder];identiteit~ d BOX <SHCOLS>
    [ "certificaat" : I
    , "beheerder"       : identiteit
    ]
  , "Rollenpassen"     : V[SESSION*KlantRol];identiteit~ CRUD BOX <SHCOLS>
    [ "certificaat" : I
    , "klantrol"       : identiteit
    , "klant"          : identiteit;klant;naam
    ]
  ]
INTERFACE "Certificaat" (identiteit) FOR "CA" : I[Certificaat] CRuD BOX
  [ "" : I   
  , "identiteit"  : identiteit  -- CRud
  ]
INTERFACE "Aanloggen" FOR Gebruiker : V[SESSION*Certificaat]  cRud BOX <SHCOLS>
  [ certificaat : I
  , "klant naam": identiteit;( 
                             (I[Klant];naam) 
                           \/(I[Beheerder];beheerder~;naam)
                           \/(I[KlantRol];klant;naam))
  , dienstenpas  : identiteit;I[Klant]     <IdentiteitProperty>
  , beheerderspas: identiteit;I[Beheerder] <IdentiteitProperty>
  , rollenpas : identiteit;I[KlantRol];naam
  ]

INTERFACE "ProcID" FOR BBS : I[ProcID] BOX
  [ procId : I 
  , omschrijving : omschrijving
  , "selecteerbaar voor" : selecteerbaarVoor[ProcID*Beheerder]
  , "nodig voor"    : nodigVoor
        COLS [ "" : I, dienst : omschrijving]
  ]
INTERFACE "Diensten" FOR RDW : V[SESSION*RdwDienst]  cRud BOX <SHCOLS>
  [ "dienst"  : I
  , omschrijving   : omschrijving
  ]


VIEW IdentiteitProperty   : Identiteit HTML TEMPLATE "PropertyCheckbox.html" ENDVIEW

INTERFACE "Menu voor gebruiker" FOR Gebruiker : I[Certificaat] cRud  BOX
  [ "U bent:" : I   COLS
    [ -- certificaat : I,
      klant       : identiteit;I[Klant] ;naam
    , "beheerder van"  : identiteit;I[Beheerder];beheerder~;naam
    , klantrol    : identiteit;I[KlantRol];naam
    ]
  , "U handelt namens: " : identiteit;( 
                              (I[Klant];naam) 
                            \/(I[Beheerder];beheerder~;naam)
                            \/(I[KlantRol];klant;naam))
  ,"U mag:" : identiteit;geautoriseerd crud
     BOX <SHCOLS>
           [ "procId"  : I 
           , "omschrijving" : omschrijving
           ]
  ]
ENDCONTEXT