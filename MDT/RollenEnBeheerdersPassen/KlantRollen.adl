CONTEXT RollenEnBeheerdersPassen IN DUTCH MARKDOWN
META "authors" "Han Joosten"

PURPOSE CONTEXT RollenEnBeheerdersPassen
{+(PURPOSE CONTEXT RollenEnBeheerdersPassen)
-}
PURPOSE PATTERN "Toegangslogica"
{+Externe organisaties hebben onder voorwaarden toegang tot de digitale dienstverlening van RDW. Hiertoe verleent RDW autorisaties. Bij sommige van deze organisaties bestaat de behoefte om genuanceerder met die autorisaties om te kunnen gaan dan dat nu kan. In de huidige situatie kan een medewerker van een organisatie alles doen waartoe die klant door RDW is geautoriseed. 

In de gewenste situatie moet het mogelijk worden dat een organisatie zelf klantrollen kan definieren. De organisatie kan via klantrollen een specifiek deel van haar autorisaties beschikbaar stellen aan bepaalde medewerkers van die organisatie, door een selectie te maken uit de autorisaties van die klant.

```
```
-}
PATTERN "Toegangslogica"

PURPOSE CONCEPT Klant
{+RDW heeft te maken met diverse bedrijven, relaties, overheidsinstanties e.d. waaraan zij diensten verleent. Het kan voorkomen, dat zo'n externe organisatie meerdere keren is geregistreerd binnen RDW, onder andere omdat RDW haar klanten in verschillende systemen registreert. Ten behoeve van De (digitale) toegang tot de diensten van RDW beschouwen we elke registratie als een separate klant.-}
CONCEPT Klant "Een registratie van een externe organisatie waarmee de afdelingen R&I en VT een relatie hebben."

--PURPOSE CONCEPT Product
--{+RDW levert goederen en diensten. Het is voor deze scope niet van belang of dit een dienst is of dat er een goed wordt geleverd. De elementen in de product- en diensten catalogus van RDW noemen we `product`.
---}
--CONCEPT Product "Een dienst of goed dat door RDW aan een klant kan worden geleverd."

PURPOSE CONCEPT ProcID
{+RDW heeft haar digitale dienstverlening ingericht door middel van zogenaamde procIDs. Deze spelen een belangrijke rol in de toegangscontrole. De term *procID* is dermate ingeburgerd in de organisatie, dat we die hier blijven gebruiken. Een procID staat voor een atomaire verzameling acties waaraan authorisaties kunnen worden verleend.
-}
CONCEPT ProcID "Een verzameling van acties die gericht is op het raadplegen en/of het muteren van gegevens. Een proces moet gezien worden als een afgerond geheel dat niet onderbroken mag worden: de verzameling acties moet als geheel doorgevoerd of als geheel teruggedraaid worden."

PURPOSE CONCEPT Beheerder
{+Een beheerder is een identiteit waarmee het rollenbeheer van/voor een bepaalde klant kan worden gerealiseerd.  
-}

PURPOSE CONCEPT KlantRol
{+Een klantrol is een identiteit voor een klant, waarmee die klant zijn toegangsrechten kan beperken.
-}

PURPOSE RELATION geautoriseerd[Identiteit*ProcID] 
{+RDW is verantwoordelijk voor de authenticiteit van haar gegevens. Daarnaast zijn gegevens vaak van dien aard, dat die niet zomaar aan iedereen verstrekt mogen worden. Daartoe autoriseert de RDW haar klanten voor specifieke ProcIDs. Voor klanten gebeurt dit aan de hand van bevoegdheden. Ook andere identiteiten (beheerders en klantrollen) kunen autorisaties hebben. Beheerders hebben specifieke autorisaties, klantrollen hebben autorisaties die afhangen van de autorisaties van de klant.  
-}
RELATION geautoriseerd[Identiteit*ProcID]
MEANING "Een identiteit kan geautoriseerd zijn voor een ProcID."

--PURPOSE RELATION nodigVoor[ProcID*Product]
--{+Om een product af te nemen, dienen bepaalde procIds te worden uitgevoerd. Daarom moet geregistreerd kunnen worden, welke prodIDs nodig zijn per product.
---}
--RELATION nodigVoor[ProcID*Product]
--MEANING "Het uitvoeren van een procID kan nodig zijn om een product te leveren."

PURPOSE RELATION klant[KlantRol*Klant]
{+Een klantrol wordt gedefinieerd door of voor een specifieke klant. Om verwarring te voorkomen met rollen van andere klanten, moet van elke klantrol bekend zijn bij welke klant die hoort.  
-}
RELATION klant[KlantRol*Klant][UNI,TOT]
MEANING "Elke klantrol hoort bij precies één klant."

PURPOSE RELATION gekoppeldAan[ProcID*KlantRol]
{+Een klantrol heeft als doel om een (groep) medewerker(s) van een klant precies dié toegang te verlenen, die nodig is voor het doen van zijn/haar werk. Hiertoe kan de beheerder van die klant procIDs koppelen aan een klantrol. 
-}
RELATION gekoppeldAan[ProcID*KlantRol]
MEANING "Een proc-ID kan gekoppeld zijn aan een klantrol."

--PURPOSE RELATION geautoriseerd[KlantRol*ProcID]
--{+De medewerker van een organisatie die in de hoedanigheid van een klantrol gebruik wil maken van diensten van RDW, heeft bepaalde autorisaties, die afgeleid zijn van de autorisaties van de klant waartoe die klantrol behoort en de producten die gekoppeld zijn aan die klantrol.
---}
--RELATION geautoriseerd[KlantRol*ProcID]
--MEANING "Een klantrol kan geautoriseerd zijn voor een ProcID."


ROLE ExecEngine MAINTAINS "Een klantrol is geautoriseerd voor een proc-id als de klant van die klantrol ervoor geautoriseerd is en die proc-id is door de beheerder gekoppeld aan die klantrol."
RULE "Een klantrol is geautoriseerd voor een proc-id als de klant van die klantrol ervoor geautoriseerd is en die proc-id is door de beheerder gekoppeld aan die klantrol." :
  I[KlantRol] ;klant;geautoriseerd /\ gekoppeldAan~ |- geautoriseerd
MEANING "Een klantrol is geautoriseerd voor een proc-id als de klant van die klantrol ervoor geautoriseerd is en die proc-id is door de beheerder gekoppeld aan die klantrol."
VIOLATION (TXT "{EX} InsPair;geautoriseerd;Identiteit;", SRC I[Identiteit], TXT ";ProcID;", TGT I
          )     

ROLE ExecEngine MAINTAINS "Een klantrol is niet geautoriseerd voor een proc-id als de klant van die klantrol er niet voor is geautoriseerd is of die proc-id is niet gekoppeld aan die klantrol."
RULE "Een klantrol is niet geautoriseerd voor een proc-id als de klant van die klantrol er niet voor is geautoriseerd is of die proc-id is niet gekoppeld aan die klantrol." : I[KlantRol];geautoriseerd |- klant;geautoriseerd /\ gekoppeldAan~
MEANING "Een klantrol is niet geautoriseerd voor een proc-id als de klant van die klantrol er niet voor is geautoriseerd is of die proc-id is niet gekoppeld aan die klantrol."
VIOLATION (TXT "{EX} DelPair;geautoriseerd;Identiteit;", SRC I, TXT ";ProcID;", TGT I
          )     



RULE klantRolAutorisatieDelete : I[KlantRol];-(klant;geautoriseerd /\ gekoppeldAan~) |- -geautoriseerd
MEANING "Wanneer de klant van de klantrol niet geautoriseerd is voor een procID of de klantrol is niet gekoppeld aan een product waarvoor die procID nodig is, dan is de klantrol niet geautoriseerd voor die procID."

PURPOSE RELATION beheerder[Klant*Beheerder]
{+Wanneer een klant gebruik wil maken van de mogelijkheid om rollen te definieren, dan wordt een beheerder aangemaakt voor die klant. Hiermee kan de klant zelf haar rollenbeheer doen, en klantrollen koppelen aan producten.  
-}
RELATION beheerder[Klant*Beheerder][UNI] --SUR,INJ, but that does not work (See Issue https://github.com/AmpersandTarski/ampersand/issues/256) (opgenomen in KlantRollen.ifc)
MEANING "Elke klant kan één beheerder hebben, en elke beheerder heeft precies één klant."

PURPOSE RULE "Elke klant met een of meerdere klantrollen moet een beheerder hebben."
{+
-}
RULE "Elke klant met een of meerdere klantrollen moet een beheerder hebben." 
   : I[Klant] /\ (klant[KlantRol*Klant]~;klant[KlantRol*Klant])
     |- beheerder;beheerder~
MEANING "Elke klant met een of meerdere klantrollen moet een beheerder hebben."
VIOLATION (SRC I,TXT " heeft geen beheerder.")
--ENDPATTERN
--
--PURPOSE PATTERN "Verfijnen van toegang" REF "BIA"
--{+Een aantal grotere afnemers zoals leasemaatschappijen, verzekeraars en service huis parkeren vragen of de toegang tot RDW dienstverlening verfijnder kan worden toegekend. Dit betekent dat in de toegangslogica een medewerker wordt geauthentiseerd en vervolgens wordt bepaald wat deze betreffende medewerker voor het betreffende bedrijf mag uitvoeren.
---}
--PATTERN "Verfijnen van toegang"

-- Klant, KlantRol en Beheerder zijn disjuncte deelverzamelingen van Identiteit
CLASSIFY Klant ISA Identiteit
CLASSIFY KlantRol ISA Identiteit
CLASSIFY Beheerder ISA Identiteit
RULE identiteiten : I[Identiteit] 
    |- (((I[Identiteit] - I[Beheerder])-I[KlantRol]) )
    \/ (((I[Identiteit] - I[KlantRol] )-I[Klant]))
    \/ (((I[Identiteit] - I[Klant]    )-I[Beheerder])) 
MEANING "Klant, beheerder en klantrol zijn identiteiten. Elke identiteit is ofwel een klant, ofwel een Beheerder ofwel een Klantrol." 

RULE "Elke identiteit is een klant, een beheerder of een klantrol." 
   : I[Identiteit] |- I[Klant] \/ I[Beheerder] \/ I[KlantRol]
MEANING "Elke identiteit is een klant, een beheerder of een klantrol."
VIOLATION (SRC I[Identiteit], TXT " is geen klant, geen beheerder én geen klantrol.")
   
PURPOSE RELATION identiteit[Certificaat*Identiteit]
{+Ten behoeve van authenticatie worden certificaten uitgegeven. Zo'n certificaat bevat een verwijzing naar de identiteit waarvoor het certificaat als authenticatiemiddel kan dienen. 
-}
RELATION identiteit [Certificaat*Identiteit][TOT,UNI]
MEANING "Elk certificaat hoort bij precies één identiteit"

RELATION naam [Identiteit*Naam][UNI] REPRESENT Naam TYPE ALPHANUMERIC

PURPOSE RELATION selecteerbaarVoor [ProcID*Beheerder]
{+De beheerder van een klant bepaalt welke autorisaties hij beschikbaar wil stellen voor een klantrol. Om dat te kunnen doen, moet hij een selectie maken uit de dingen waartoe de klant is geautoriseerd (proc-ids). Om te voorkomen, dat de beheerder uit de volledige lijst van alle proc-id's moet kiezen, wordt een voorselectie gemaakt. 
-}
RELATION selecteerbaarVoor [ProcID*Beheerder]
MEANING "Een product kan selecteerbaar zijn voor een beheerder."

ROLE ExecEngine MAINTAINS "Een proc-id is selecteerbaar voor een beheerder als de klant geautoriseerd is voor die proc-id."
RULE "Een proc-id is selecteerbaar voor een beheerder als de klant geautoriseerd is voor die proc-id." 
  :  I[Beheerder];beheerder~;geautoriseerd |- selecteerbaarVoor~
MEANING "Een proc-id is selecteerbaar voor een beheerder als de klant geautoriseerd is voor die proc-id."
VIOLATION (TXT "{EX} InsPair;selecteerbaarVoor;ProcID;", TGT I[ProcID], TXT ";Beheerder;", SRC I[Beheerder]
          )     
ROLE ExecEngine MAINTAINS "Een proc-id is niet selecteerbaar voor een beheerder als de klant niet geautoriseerd is voor die proc-id."
RULE "Een proc-id is niet selecteerbaar voor een beheerder als de klant niet geautoriseerd is voor die proc-id."
  :  selecteerbaarVoor~ |- I[Beheerder];beheerder~;geautoriseerd
VIOLATION (TXT "{EX} DelPair;selecteerbaarVoor;ProcID;", TGT I[ProcID], TXT ";Beheerder;", SRC I[Beheerder]
          )     

ENDPATTERN
ENDCONTEXT