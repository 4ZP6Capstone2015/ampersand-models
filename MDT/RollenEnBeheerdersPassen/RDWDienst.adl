CONTEXT Diensten IN DUTCH MARKDOWN
INCLUDE "RdwDienst.xlsx"
INCLUDE "adfsEenheden.xlsx"
INCLUDE "Autorisatie.adl"
INCLUDE "Identiteiten.adl"

PATTERN "RDW diensten"


RELATION omschrijving[RdwDienst*DienstOmschrijving][TOT,UNI] PRAGMA "de omschrijving van""is"
MEANING "Elke RDW dienst heeft precies één omschrijving."
REPRESENT DienstOmschrijving TYPE ALPHANUMERIC
PURPOSE RELATION nodigVoor[ProcIdent*RdwDienst]
{+Om succesvol van een RDW dienst gebruik te kunnen maken, moet bekend zijn welke procIdents hiervoor nodig zijn.
-}
RELATION nodigVoor[ProcIdent*RdwDienst] PRAGMA "procIdent""is nodig voor"
MEANING "Een procIdent kan nodig zijn voor een RDW dienst."
PURPOSE RELATION dienstSelecteerbaarVoor [RdwDienst*Beheerder]
 {+De beheerder van een klant bepaalt welke RDW diensten hij beschikbaar wil stellen voor een klantrol. Om dat te kunnen doen, moet hij een selectie maken uit RDW diensten. Om te voorkomen, dat de beheerder uit de volledige lijst van alle RDW diensten moet kiezen, wordt een voorselectie gemaakt. 
 -}
RELATION dienstSelecteerbaarVoor[RdwDienst*Beheerder]
MEANING "Een RDW dienst kan selecteerbaar zijn voor een beheerder."
PURPOSE RELATION beschikbaarVoor[RdwDienst*KlantRol]
{+De beheerder van een klant bepaalt welke RDW diensten beschikbaar zijn voor een klantrol. Hierdoor is hij in staat om de autorisaties voor die klantrol te beperken tot datgeen nodig is voor die rol. Het al dan niet beschikbaar stellen van een dienst aan een klantrol kan uitsluitend worden gedaan door de beheerder van de klant, waar die klantrol toe behoort.
-}
RELATION beschikbaarVoor[RdwDienst*KlantRol]
MEANING "Een RDW dienst kan beschikbaargesteld zijn aan een klantrol."

ROLE ExecEngine MAINTAINS "dienstselecteerbaarVoorInsPair" -- , "dienstselecteerbaarVoorDelPair"
PURPOSE RULE "dienstselecteerbaarVoorInsPair"
{+De RDW diensten, die selecteerbaar zijn voor een beheerder, worden automatisch bepaald. Dat gaat als volgt:
-}
RULE "dienstselecteerbaarVoorInsPair" 
--  : (I[RdwDienst] /\ (nodigVoor~;nodigVoor)) ;  (nodigVoor \ geautoriseerd~) ; beheerder 
    : nodigVoor~;geautoriseerd~;beheerder
       |- dienstSelecteerbaarVoor[RdwDienst*Beheerder]
MEANING "Een RDW dienst is selecteerbaar voor de beheerder van een klant, wanneer er een procIdent bestaat die nodig is voor die RDW dienst en waarvoor de klant is geautoriseerd."
VIOLATION (TXT "{EX} InsPair;dienstSelecteerbaarVoor;RdwDienst;", SRC I[RdwDienst], TXT ";Beheerder;", TGT I[Beheerder]
          )     
PURPOSE RULE "dienstselecteerbaarVoorDelPair"
{+
-}
ROLE ExecEngine MAINTAINS "dienstselecteerbaarVoorDelPair"
RULE "dienstselecteerbaarVoorDelPair"
--  :  dienstSelecteerbaarVoor |- (I[RdwDienst] /\ (nodigVoor~;nodigVoor)) ;  (nodigVoor \ geautoriseerd~) ; beheerder
    : dienstSelecteerbaarVoor |- nodigVoor~;geautoriseerd~;beheerder
MEANING "Een RDW dienst is **niet** selecteerbaar voor de beheerder van een klant, wanneer er **g**een procIdent bestaat die nodig is voor die RDW dienst en waarvoor de klant is geautoriseerd."
VIOLATION (TXT "{EX} DelPair;dienstSelecteerbaarVoor;RdwDienst;", SRC I[RdwDienst], TXT ";Beheerder;", TGT I[Beheerder]
          )     

ENDPATTERN

ENDCONTEXT