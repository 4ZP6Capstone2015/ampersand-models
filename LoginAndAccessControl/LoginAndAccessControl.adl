CONTEXT LoginAndAccessControl IN DUTCH LATEX
-----------------------
VIEW Persoon : Persoon(persoonNaam)
persoonNaam  :: Persoon * Naam [UNI,INJ]
persoonAdres :: Persoon * Adres [UNI]
persoonTel   :: Persoon * Telefoonnummer [INJ]
-- etc.
INTERFACE Persoonsgegevens (persoonNaam, persoonAdres, persoonTel): I[Persoon]
BOX[ "Naam"   : persoonNaam
   , "Adres"  : persoonAdres
   , "Mobiel" : persoonTel
   ]
-----------------------
VIEW Account : Account(accEmail)
accPersoon :: Account -> Persoon [INJ]
accEmail :: Account -> Emailadres [INJ]
accPassword :: Account * Wachtwoord [UNI]
accOrgUnit :: Account * OrganiekeEenheid [UNI] -- verantwoordelijke OrgUnit
accRol :: Account * Rol
-----------------------
POPULATION persoonNaam[Persoon*Naam]             CONTAINS [ ("p1", "Be Heerder") ]
POPULATION accPersoon[Account*Persoon]           CONTAINS [ ("a1", "p1") ]
POPULATION accEmail[Account*Emailadres]          CONTAINS [ ("a1", "admin@vb.nl")]
POPULATION accPassword[Account*Wachtwoord]     CONTAINS [ ("a1", "*****") ]
POPULATION accOrgUnit[Account* OrganiekeEenheid] CONTAINS [ ("a1", "VB B.V.") ]
POPULATION accRol[Account* Rol]                  CONTAINS [ ("a1", "SystemAdmin") ]

POPULATION persoonNaam[Persoon*Naam]             CONTAINS [ ("p3", "Piet van der Kluns") ]
POPULATION accPersoon[Account*Persoon]           CONTAINS [ ("a3", "p3") ]
POPULATION accEmail[Account*Emailadres]          CONTAINS [ ("a3", "pvdkluns@vb.nl")]
POPULATION accPassword[Account*Wachtwoord]     CONTAINS [ ("a3", "*****") ]
POPULATION accOrgUnit[Account* OrganiekeEenheid] CONTAINS [ ("a3", "VB B.V.") ]
POPULATION accRol[Account* Rol]                  CONTAINS [ ("a3", "CISO") ]
-----------------------
PROCESS "Session Management"
sessionUID :: SESSION * Emailadres [UNI]
sessionPW  :: SESSION * Wachtwoord [UNI] -- een accPassword is verplicht.
sessionAccount :: SESSION * Account [UNI] -- het Account waarmee is ingelogd

sessionUser :: SESSION * Persoon [UNI] -- ingelogde actor
sessionOrg  :: SESSION * OrganiekeEenheid [UNI] -- ingelogde verantwoordelijke OrgUnit
sessionRole :: SESSION * Rol -- geactiveerde rollen in de sessie

RULE "Account uniciteit": accEmail;accEmail~ /\ accPassword;accPassword~ |- I[Account]
MEANING "Elk account in de registratie kan worden geidentificeerd middels zijn emailadres en wachtwoord."

RULE "Identificeren en authenticeren": sessionUID~;sessionPW |- accEmail~;accPassword
MESSAGE "Verkeerde inloggegevens."
VIOLATION (TXT "Gebruik een geregistreerde Email-Wachtwoord combinatie.")

IDENT Accounts: Account(accEmail,accPassword)

ROLE ExecEngine MAINTAINS "Inloggen"
RULE "Inloggen": sessionUID;accEmail~ /\ sessionPW;accPassword~ |- sessionAccount
MEANING "De gebruiker in een sessie moet zijn geauthenticeerd."
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} InsPair;sessionUser;SESSION;", SRC I, TXT ";Persoon;", TGT accPersoon
          ,TXT "{EX} InsPair;sessionOrg;SESSION;", SRC I, TXT ";OrganiekeEenheid;", TGT accOrgUnit
          ,TXT "{EX} InsPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT accRol
          )

ROLE ExecEngine MAINTAINS "Uitloggen"
RULE "Uitloggen": sessionAccount |- sessionUID;accEmail~ /\ sessionPW;accPassword~
MEANING "Als de gebruikersnaam of het wachtwoord niet meer bekend is, wordt de gebruiker uitgelogd."
VIOLATION (TXT "{EX} DelPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;sessionUser;SESSION;", SRC I, TXT ";Persoon;", TGT accPersoon
          ,TXT "{EX} DelPair;sessionOrg;SESSION;", SRC I, TXT ";OrganiekeEenheid;", TGT accOrgUnit
          ,TXT "{EX} DelPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT accRol
          )

ROLE ExecEngine MAINTAINS "Activeren van rollen"
RULE "Activeren van rollen": sessionAccount;accRol |- sessionRole
MEANING "In een sessie moeten de rollen van de gebruiker zijn geactiveerd."
VIOLATION (TXT "{EX} InsPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT I)

ROLE ExecEngine MAINTAINS "Deactiveren van rollen"
RULE "Deactiveren van rollen": sessionRole |- sessionAccount;accRol
MEANING "Alleen de rollen van de gebruiker mogen zijn geactiveerd in een sessie."
VIOLATION (TXT "{EX} DelPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT I)

ENDPROCESS

INTERFACE "Login" (sessionUID, sessionPW): V[ONE*SESSION];'_SESSION'
BOX [ "User ID (email)" : sessionUID
    , "Wachtwoord" : sessionPW
    , "Accountgegevens" : sessionAccount
   BOX[ "Gebruiker" : accPersoon
      , "Emailadres"  : accEmail
      , "OrgEenheid" : accOrgUnit
      , "Rol(len)": accRol
      ]
    , "Sessie context" : I
    BOX [ "Responsible (Actor)" : sessionUser
        , "Accountable (OrgUnit)" :  sessionOrg
        , "Activated roles" : sessionRole
        ]
    ]

{- De volgende INTERFACEs implementeren een test voor de SESSION functionaliteit:
a) een interface voor het beheren van je eigen account in de MENU bar.
b) een interface voor het beheren van je account, niet in de MENU bar.
c) een interface voor een beheerder die alle accounts kan wijzigen.
d) een interface voor een beheerder die alle accounts kan wijzigen (behalve die van zichzelf en van andere organieke eenheden).
-} 

-- a) een interface voor het beheren van je eigen account in de MENU bar.
INTERFACE "Mijn account"
    (accPassword, sessionPW, persoonNaam, persoonAdres, persoonTel):
    V[ONE*SESSION];'_SESSION';sessionAccount
BOX [ "Gebruiker"   : accPersoon INTERFACE Persoonsgegevens
    , "Emailadres"  : accEmail
    , "Wachtwoord"  : accPassword
    , "Herhaal" : sessionAccount~       -- this is a bit of a hack: to allow changing the
      BOX [ "Wachtwoord" : sessionPW    -- accPassword, the sessionPW needs to change
          ]                             -- simultaneously, or rule login will fail.   
    ]

-- b) een interface voor het beheren van je account, niet in de MENU bar.
INTERFACE "Mijn Persoonsgegevens" -- Zelfde, maar dan als 'gewone' INTERFACE.
    (accPassword, sessionPW, persoonNaam, persoonAdres, persoonTel):
    I[Account] /\ sessionAccount~;'_SESSION'[SESSION];sessionAccount
BOX [ "Gebruiker"   : accPersoon INTERFACE Persoonsgegevens
    , "Emailadres"  : accEmail
    , "Wachtwoord"  : accPassword
    , "Herhaal" : sessionAccount~       -- this is a bit of a hack: to allow changing the
      BOX [ "Wachtwoord" : sessionPW    -- accPassword, the sessionPW needs to change
          ]                             -- simultaneously, or rule login will fail.   
    ]

-- c) een interface voor een beheerder die alle accounts kan wijzigen.
INTERFACE "Beheerde Accounts"
    (accEmail, accPassword, accOrgUnit, accRol):
    I[Account] /\ V;'_SESSION'[SESSION];sessionAccount;accRol;'SystemAdmin'[Rol];V
BOX [ "Gebruiker" : accPersoon
    , "Account" : I
    BOX[ "Emailadres" : accEmail
       , "Wachtwoord" : accPassword
       ]
    , "Organisatiegegevens" : I
    BOX[ "OrganiekeEenheid": accOrgUnit
       , "Toegewezen rol(len)" : accRol
       ]
    , "Uw eigen" : sessionAccount~      -- this is a bit of a hack: to allow changing the
    BOX [ "Wachtwoord" : sessionPW      -- accPassword in case you edit your own account, 
        ]                               -- the sessionPW needs to change
    ]                                   -- simultaneously, or rule login will fail.   

-- d) een interface voor een beheerder die alle accounts kan wijzigen (behalve die van zichzelf en van andere organieke eenheden).
INTERFACE "Beheerde Adminaccounts"
    (accEmail, accPassword, accOrgUnit, accRol):
    I[Account] /\ -sessionAccount~;'_SESSION';(sessionRole;'SystemAdmin'[Rol];V /\ sessionOrg;accOrgUnit~)
BOX [ "Gebruiker" : accPersoon
    , "Account" : I
    BOX[ "Emailadres"    : accEmail
       , "Wachtwoord"    : accPassword
       ]
    , "Organisatiegegevens" : I
    BOX[ "OrganiekeEenheid": accOrgUnit
       , "Toegewezen rol(len)" : accRol
       ]
    ]

INTERFACE "BeheerOverzicht" : I[ONE];V[ONE*Persoon];V[Persoon*ONE]
BOX [ "Personeen" : V[ONE*Persoon]
    , "Accounts"  : V[ONE*Account]    ;(I /\ V;'_SESSION'[SESSION];sessionOrg;accOrgUnit~) -- in dezelfde organisatie
    ]

ENDCONTEXT