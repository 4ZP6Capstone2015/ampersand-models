
CONTEXT OUNL 
--INCLUDE "cursus.adl" --themes = onderwijsproducten
--INCLUDE "inschrijving.adl" --themes+inschrijving
--INCLUDE "inschrijving1.adl" --losse cursus voor Klaas
--INCLUDE "leerpad1.adl" --leerpad voor Klaas
--INCLUDE "inschrijving2.adl" --rechten voor Klaas
--INCLUDE "leerpad2.adl" --'studie' van Klaas
--themes+tentamen
--INCLUDE "personeel.adl" --themes+personeelszaken

INCLUDE "vbcursus.adl" --themes = onderwijsproducten
INCLUDE "vbinschrijving.adl" --themes = Inschrijving
INCLUDE "vbtentamen.adl" --themes = Tentamen
INCLUDE "vbberekening.adl" --themes = Berekening

INCLUDE "cursusschermen.adl"
IN DUTCH
TEXTMARKUP LaTeX

THEMES  Berekeningen
      , Onderwijsproducten 
      , Inschrijving
      , Tentamen
   --   , Personeelszaken
   --   , Gebeurtenis


PURPOSE 
PATTERN Datum
{+ Voor tijdsgerelateerde concepten kan het van betekenis zijn om van \emph{eerder} of \emph{later} te kunnen spreken.
   We leggen tijdstippen vast tot op de dag nauwkeurig. -}
PATTERN Datum
CONCEPT Datum "Een datum is een dagtekening (dd/MM/YYYY)."
volgende :: Datum * Datum [UNI,INJ,IRF,ASY]
RULE "data": volgende;volgende~ \/ volgende~;volgende = I
MEANING "De vorige of volgende datum van een datum moet geregistreerd worden."
ENDPATTERN

PATTERN Gebeurtenis
CONCEPT Log "Een log is een verzameling van gebeurtenissen op basis waarvan functieberekeningen gedaan kunnen worden, zoals bijvoorbeeld het vaststellen van prestatie-indicatoren."
PURPOSE CONCEPT Gebeurtenis
{+
   Een gebeurtenis is een algemeen begrip om het moment van specifieke gebeurtenissen te kunnen bepalen.
-}
CONCEPT Gebeurtenis "Een gebeurtenis is een tijdsgebonden feit, ofwel een voorval."
PURPOSE RELATION
bestaatuit {+ Een log geeft een naam aan een verzameling van gebeurtenissen.
         Het opstellen van logs zou gereguleerd kunnen worden, maar is vrij gelaten in deze specificatie.
       -}
bestaatuit :: Log * Gebeurtenis [TOT]
MEANING "Een log bestaat uit gebeurtenissen."
PURPOSE RELATION
moment {+ Een gebeurtenis heeft op een zeker moment plaatsgevonden.
          In deze specificatie is een tijdsaanduiding gebruikt tot aan dagniveau.
        -}
moment :: Gebeurtenis -> Datum
MEANING "Het moment waarop een gebeurtenis heeft plaatsgevonden wordt geregistreerd."

CONCEPT Peilperiode "Een peilperiode is een log, die alle relevante gebeurtenissen bevat uit een bepaalde periode ten behoeve van een functieberekening."
SPEC Peilperiode ISA Log
KEY "periode": Peilperiode(begindatum,einddatum,tbv)
tbv :: Peilperiode * Functie [TOT]
MEANING "Iedere peilperiode is ten behoeve van één of meer functies"
PURPOSE RELATION
begindatum {+ Een peilperiode mag alleen gebeurtenissen bevatten van na de begindatum. -}
begindatum :: Peilperiode -> Datum
MEANING "Iedere peilperiode heeft een begindatum."
PURPOSE RELATION
begindatum {+ Een peilperiode mag alleen gebeurtenissen bevatten van voor de einddatum. -}
einddatum :: Peilperiode -> Datum
MEANING "Iedere peilperiode heeft een einddatum."
RULE "peilperiode": begindatum~;einddatum |- volgende*
MEANING "De begindatum van een periode ligt voor de einddatum."
peildatum :: Peilperiode * Peilperiode [PROP]
RULE "peildatum": begindatum;einddatum~ /\ I = peildatum
MEANING "Een peildatum is een peilperiode met identieke start- en einddatum."
ENDPATTERN


PURPOSE PATTERN Onderwijsproducten
{+
   Kenmerkend aan de OU zijn haar modulaire studieprogramma's.
   Naast opleidingen kunnen ook losse cursussen worden afgenomen.
-}
PATTERN Onderwijsproducten

PURPOSE CONCEPT Cursuscode
{+
   Om studieprogramma's modulair op te bouwen worden elementaire onderwijseenheden met een code gebruikt.
   Voor deze studie-eenheden wordt het begrip \emph{cursuscode} gehanteerd.   
-}
CONCEPT Cursuscode "Een cursuscode is de kleinste onderwijseenheid van de OUNL." "begrippenlijst_2009_mar"

PURPOSE CONCEPT Cursus
{+
   Een cursuscode kan op meerdere manieren ingevuld worden.
   Bijvoorbeeld, andere docenten, andere begeleidingswijzen, verschillende verkoopprijzen, etc.
   Voor de verschillende varianten van cursuscodes wordt het begrip \emph{cursus} gebruikt.   
-}
CONCEPT Cursus "Een cursus is een variant van een cursuscode." ""
KEY "cursus": Cursus(cursuscode,cursustype)
cursuscode :: Cursus -> Cursuscode
PRAGMA "" " is een variant van " ""
MEANING "Iedere cursus moet een cursuscode hebben."

PURPOSE CONCEPT Cursustype
{+
   In de begrippenlijst_2009_mar zijn enkele varianten van cursuscodes benoemd als \emph{cursustype}.
   Neem bijvoorbeeld, \emph{commerciële cursus}, \emph{compacte cursus}, of \emph{reguliere cursus}.
   In deze specificatie zijn cursustypes vrij te bepalen, zodat naar behoefte verschillende varianten kunnen bestaan.
   De reden om een bepaalde variant aan te bieden is niet voorspelbaar of kan complexe of specifieke vormen aannemen.
   Bijvoorbeeld 'BPMIT(zelfstudie)' is een cursustype, dat niet te voorspellen was en een complexe en specifieke vorm heeft.
   Deze variant was niet te voorspellen, omdat de ontwikkeling van het product BPMIT niet te voorspellen was.
   De 'BPMIT(zelfstudie)' is complex omdat het op meerdere punten variëert; op zijn minst twee als we de naam mogen interpreteren.
-}
CONCEPT Cursustype "Een cursustype is een aanduiding voor de variant van een cursuscode." ""
cursustype :: Cursus -> Cursustype
PRAGMA "" " is van het type " ""
MEANING "Iedere cursus moet een cursustype hebben."

PURPOSE RELATION modules
{+
   De grootte van een cursus wordt uitgedrukt in aantal modules.
   Een module wordt o.a. gebruikt als basiseenheid voor studiebelasting, cursusprijs en productafzet.
   Eén module komt overeen met 4,3 ECTS studiebelasting.
   Het aantal studie-uren wordt geschat op 100 à 120 uur per module.
-}
modules :: Cursuscode -> Aantal
PRAGMA "" " is " " modules groot"
MEANING "Het aantal modules van een cursuscode moet bepaald zijn."

PURPOSE CONCEPT Studieprogramma
{+
   Vanwege het modulaire onderwijs wordt het begrip \emph{studieprogramma} gereserveerd voor producten van de OU, waarin cursussen zijn verwerkt.
   Bijvoorbeeld:   
   \begin{iterate}
   \item[] BaMa-opleidingen, al dan niet verkort
   \item[] Focusopleidingen
   \item[] CPP's
   \item[] Startpakketten
   \item[] Voobereidingscursussen
   \item[] (Post)HBO-opleidingen
   \item[] Losse cursussen
   \item[] \dots
   \end {iterate}    
-}
CONCEPT Studieprogramma "Een studieprogramma is een product van de OU, dat één of meerdere cursussen omvat." "Cupido"
zitin :: Cursus * Studieprogramma [TOT,SUR]
PRAGMA "" " zit in " ""
MEANING "Iedere cursus moet in een studieprogramma zitten. Een studieprogramma moet minimaal één cursus bevatten."

PURPOSE
CONCEPT Product
{+ Naast studieprogramma's biedt de OU niet-modulaire onderwijsproducten aan, zoals losse tentamens, extra studiebegeleiding en gratis cursussen. -}
CONCEPT Product "Een onderwijsproduct van de OU." "http://www.ou.nl/ -> Ons studieaanbod"
SPEC Studieprogramma ISA Product


----------------
--Cursusinhoud--
----------------
PURPOSE CONCEPT Tentamenopdracht
{+
   Een cursus kan worden afgerond via een gevarieerd geheel aan tentamenvragen, opdrachten, mondelingen, en werkstukken omvatten.
   Voor deze specificatie is het voldoende om te weten of de uitwerking van dit geheel zodanig beoordeeld is dat een cursus is afgerond of niet.
   Voor het geheel wordt daarom in deze specificatie één begrip gebruikt, de \emph{tentamenopdracht}.
   De uitwerking van de tentamenopdracht door een student kan positief of negatief beoordeeld worden.
   De uitwerking en beoordeling van tentamens wordt in paragraaf \emph{Tentamen} beschreven.
-}
CONCEPT Tentamenopdracht "Een tentamenopdracht is het geheel van opdrachten waarvan de uitwerking door een examinator met een voldoende gewaardeerd moet worden om de bijbehorende cursus af te ronden." "Cupido"
opdracht :: Cursus -> Tentamenopdracht
PRAGMA "" " heeft de volgende tentamenopdracht: " ""
MEANING "Voor iedere cursus moet een tentamenopdracht gespecificeerd zijn."
examinators :: Cursus * Arbeidskracht [TOT]
PRAGMA "Een examinator van " " is "
MEANING "Een cursus moet examinatoren hebben om de uitwerkingen van de tentamenopdracht van de cursus te beoordelen."

PURPOSE CONCEPT Begeleidingsvorm
{+
   Via de inschrijving voor een studieprogramma, krijgt een student een bepaalde vorm van ondersteuning door docenten bij de cursussen in het programma.
   De beschrijving van deze diensten noemen we de \emph{begeleidingsvorm}. 
-}
CONCEPT Begeleidingsvorm "Een begeleidingsvorm is de beschrijving van de diensten binnen een cursus, die door docenten verzorgd worden." ""
begeleidingsvorm :: Cursus -> Begeleidingsvorm
PRAGMA "De begeleidingsvorm van " " is " ""
MEANING "Voor iedere cursus moet de begeleidingsvorm bepaald zijn."
docenten :: Cursus * Arbeidskracht [TOT]
PRAGMA "Een docent van " " is " " "
MEANING "Een cursus moet docenten hebben om de begeleiding te verzorgen."

PURPOSE CONCEPT Materiaal
{+
   In de cursusbeschrijving staat het benodigde studiemateriaal beschreven.
   Studiemateriaal dient uitgeleverd te worden aan studenten die de cursus volgen.
-}
CONCEPT Materiaal "Materiaal is de beschrijving van het pakket van studiebenodigdheden om de stof van een cursus te kunnen bestuderen." "Cupido"
cursusmateriaal :: Cursus -> Materiaal
PRAGMA "Het cursusmateriaal voor " " is " ""
MEANING "Voor een cursus moet cursusmateriaal bestaan."


---------------
--organisatie--
---------------
PURPOSE CONCEPT Orgaan
{+
   De OU is een organisatie die bestaat uit verschillende organen.
   Een begrip voor organen is nodig om over deze organisatie-onderdelen te kunnen spreken.
-}
CONCEPT Orgaan "Een orgaan is een onderdeel uit het organigram van de universiteit." "http://www.intranet.ou.nl/eCache/INT/50/384.html"

PURPOSE RELATION
ondergebracht 
{+ 
   Een cursuscode start met een letter, die aangeeft bij welke orgaan een cursus is ondergebracht (zie \url{http://www.intranet.ou.nl/INTRANET/Docs/Organisatie/OSC/OenE/IOD/Cursuscodelijsten/CP221_26-10-2011.RTF}).
   \begin{iterate}
   \item[B] Managementwetenschappen
   \item[C] Cultuurwetenschappen 
   \item[E] Managementwetenschappen
   \item[N] Natuurwetenschappen 
   \item[O] Onderwijswetenschappen 
   \item[R] Rechtswetenschappen 
   \item[S] Psychologie
   \item[T] Informatica
   \item[U] Rechtswetenschappen 
   \end{iterate}
   Alleen faculteiten blijken cursussen te hebben, maar er is geen reden om deze specialisatie van een orgaan te specificeren.
 -}
ondergebracht :: Cursuscode -> Orgaan
PRAGMA "" " is een cursuscode van " ""
MEANING "Iedere cursuscode moet ondergebracht zijn bij een orgaan."

PURPOSE CONCEPT Arbeidskracht
{+
   Om begeleidings- en examinatortaken gedaan te krijgen moet een orgaan mensen hebben die deze werkzaamheden kunnen verrichten.
   In paragraaf \emph{Personeelszaken} zijn enkele gebeurtenissen uitgewerkt die bepalen wie beschikbaar is om deze taken uit te voeren.   
-}
CONCEPT Arbeidskracht "Een arbeidskracht is een natuurlijk persoon die taken voor de OU verricht."
werktvoor :: Arbeidskracht * Orgaan [TOT]
PRAGMA "" " is werkzaam voor " ""
MEANING "Een arbeidskracht moet voor minimaal één orgaan werken."

RULE "begeleidingstaak": docenten~;cursuscode;ondergebracht |- werktvoor
MEANING "Een docent van een cursus, die ondergebracht is bij een faculteit, werkt voor dat orgaan."
RULE "examinatortaak": examinators~;cursuscode;ondergebracht |- werktvoor
MEANING "Een examinator van een cursus, die ondergebracht is bij een faculteit, werkt voor dat orgaan."


------------
--Studeren--
------------
PURPOSE CONCEPT Student
{+
   Er is een begrip nodig voor personen die voor studieprogramma's ingeschreven zijn, c.q., personen die cursussen volgen.
   De begrippenlijst_2009_mar onderkent specifieke begrippen, waaruit de staat van inschrijving is af te leiden.
   Neem bijvoorbeeld \emph{ingeschrevene}, \emph{instromer}, \emph{uitstromer}, \emph{individueel ingeschrevene}, etc.
   Dit onderscheid wordt in deze specificatie pas in paragraaf \emph{Inschrijving} gemaakt.
   Het algemene begrip voor deze personen is \emph{student}.
-}
CONCEPT Student "Een student is een natuurlijk persoon die voor een studieprogramma ingeschreven is of is geweest." "SPIL"
PURPOSE CONCEPT Leerpad 
{+
   Om een studieprogramma te voltooien volgt een student een traject van cursussen binnen de regels van een studieprogramma.
   De regels zijn o.a. bepaald door programma-ingangseisen, cursusingangseisen, verplichte cursussen, keuzeruimte en vrijstellingen.
   Een studie is afgerond als de student een geldig traject van het studieprogramma heeft doorlopen.
-}
CONCEPT Leerpad "Een leerpad van een student is het traject van cursussen uit een studieprogramma dat door de student gevolgd wordt."
                "???hoe weet de OU of een student een opleiding volgt, of dat hij zich toevallig voor bepaalde cursussen heeft ingeschreven???"
KEY "leerpad": Leerpad(studievan,studieprogramma)
studievan :: Leerpad -> Student
PRAGMA "" " is een studie van " ""
MEANING "Ieder leerpad moet van een student zijn."
studieprogramma :: Leerpad -> Studieprogramma
PRAGMA "" " is een studietraject van een student voor " ""
MEANING "Ieder leerpad moet betrekking hebben op één studieprogramma."

PURPOSE RELATION bevat
{+
   Het leerpad van een student bestaat uit afgeronde en lopende cursussen.

   Een cursus kan afgerond worden door het tentamen te halen, of afgerond zijn door bijzondere bepalingen zoals vrijstelling.
   Afronding via tentamens is beschreven in paragraaf \emph{Tentamen}.
   Afronding via bijzondere bepalingen zijn niet opgenomen in deze specificatie.

   Een lopende cursus is een niet afgeronde cursus met een geldige inschrijving (zie paragraaf \emph{Inschrijving}).
-}
bevat :: Leerpad * Cursus [UNI]
PRAGMA "De student van " " volgt " " of heeft de cursus afgerond."
MEANING "Een leerpad moet cursussen bevatten."
RULE "studie": bevat |- studieprogramma;zitin~
MEANING "Een leerpad bevat cursussen die in het studieprogramma moeten zitten waar het leerpad betrekking op heeft."
ENDPATTERN

PATTERN Inschrijving
PURPOSE PATTERN Inschrijving
{+
   Via inschrijvingen worden rechten verleend aan studenten om producten en diensten te ontvangen.
   Bij de OUNL heeft een student inschrijvingen voor cursussen.
   \emph{???Is de inschrijving voor opleidingen impliciet???}
-}

PURPOSE CONCEPT Aanmelding
{+
   Een natuurlijk persoon kan tot een inschrijving voor een cursus komen via een officiële aanmelding.
   Een aanmelding mag ingediend worden door een juridisch persoon namens een natuurlijk persoon.
   Ook mogen volgens de begrippenlijst_2009_mar meerdere aanmeldingen gebundeld worden in één aanmelding.
   Deze nuances zijn onnodig complicerend voor deze specificatie, ofwel de volgende definitie van een aanmelding volstaat.
-}
CONCEPT Aanmelding "Een aanmelding is een verzoek om een student in te schrijven voor een cursus."
SPEC Aanmelding ISA Gebeurtenis
KEY "aanmelding": Aanmelding (moment[Aanmelding*Datum],cursusaanmelding,studentaanmelding)
cursusaanmelding :: Aanmelding -> Cursus
PRAGMA "" " betreft " ""
MEANING "Een aanmelding moet betrekking hebben op een cursus."
studentaanmelding :: Aanmelding -> Student
PRAGMA "" " betreft " ""
MEANING "Een aanmelding moet betrekking hebben op een student."


PURPOSE CONCEPT Cursusinschrijving
{+
   Als een aanmelding goedgekeurd wordt, dan wordt de aanmelding omgezet naar een cursusinschrijving.
-}
CONCEPT Cursusinschrijving "Een cursusinschrijving is een vastlegging in SPIL van een verzoek tot inschrijving, nadat een persoon aan alle verplichtingen heeft voldaan, met vermelding van startdatum inschrijvingsrechten" "Begrippenlijst_2009_mar.xls"
SPEC Cursusinschrijving ISA Gebeurtenis
KEY "cursusinschrijving": Cursusinschrijving (moment[Cursusinschrijving*Datum],cursus,student)

cursus :: Cursusinschrijving -> Cursus
PRAGMA "" " betreft " ""
MEANING "Een cursusinschrijving moet betrekking hebben op een cursus."
student :: Cursusinschrijving -> Student
PRAGMA "" " betreft " ""
MEANING "Een cursusinschrijving moet betrekking hebben op een student."

goedgekeurd :: Cursusinschrijving -> Aanmelding
PRAGMA "" "is totstandgekomen door de goedkeuring van " ""
MEANING "Een cursusinschrijving moet het gevolg zijn van een goedgekeurde aanmelding."
RULE "goedgekeurd": goedgekeurd |- cursus;cursusaanmelding~ /\ student;studentaanmelding~
MEANING "De gegevens uit de goedgekeurde aanmelding moeten worden overgenomen in de cursusinschrijving."



PURPOSE CONCEPT Inschrijvingsvorm
{+
   De intentie van een aanmelding wordt aangeduid met een drie-letterige afkorting, de inschrijvingsvorm.
   Een student kan bijvoorbeeld een losse cursus willen volgen (LIC) of een kennismakingstraject (KMT).
   De inschrijvingsvorm bepaald de geldigheid van rechten op producten en diensten van een cursus, met name cursusmateriaal, begeleiding en tentaminering.
   In deze specificatie is ieder vorm van differentiatie van cursussen geregeld door cursussen varianten van cursuscodes te laten zijn (zie paragraaf \emph{onderwijsproducten}).
   Dus, een cursuscode kan met meerdere inschrijvingsvormen aangeboden worden, een cursus moet er precies één hebben.
-}
CONCEPT Inschrijvingsvorm "Een inschrijvingsvorm is een set van voorwaarden waaronder inschrijving voor een cursus kan plaatsvinden." "Begrippenlijst_2009_mar.xls"
RULE "inschrijvingsvormen": I[Inschrijvingsvorm] |- 'LIC' \/ 'LIS' \/ 'SIN' \/ 'JIN' \/ 'KMT' \/ 'INH' \/ 'SAS'
MEANING "Een inschrijvingsvorm moet één van de volgende afkortingen zijn: LIC, LIS, SIN, JIN, KMT, INH, of SAS."
inschrijvingsvormen :: Cursuscode * Inschrijvingsvorm [TOT]
PRAGMA "Een toegestane inschrijvingsvorm voor " " is " ""
MEANING "Iedere cursuscode moet worden aangeboden met toegestane inschrijvingsvormen."

inschrijvingsvorm :: Cursus -> Inschrijvingsvorm
PRAGMA "" " heeft " ""
MEANING "Een cursus moet een inschrijvingsvorm hebben."

RULE "cursusvarianten o.b.v. inschrijvingsvorm": cursuscode~;inschrijvingsvorm |- inschrijvingsvormen
MEANING "Er moet minimaal één cursus bestaan voor iedere toegestane inschrijvingsvorm van een cursuscode."

PURPOSE RULE "rechten krijgen"
{+
   Een cursusinschrijving van een zekere vorm geeft bepaalde rechten aan een student met betrekking tot een cursus.
   Rechten kunnen verlopen, maar het feit dat een student op het moment van de eerste inschrijving voor een cursus rechten gekregen heeft blijft.
   Een nuancering van rechten is (nog) niet gespecificeerd, bijvoorbeeld het \emph{verlopen} en \emph{verlengen} van rechten, \emph{tentamenkansen} en \emph{uitlevering van cursusmateriaal}.
-}

rechtop :: Student * Cursus
PRAGMA "" " heeft rechten gekregen op " ""
MEANING "Een student kan rechten krijgen op producten en diensten binnen een cursus."

RULE "rechten krijgen": student~;cursus[Cursusinschrijving*Cursus]  |- rechtop
MEANING "Een student met een inschrijving voor een cursus moet rechten hebben gekregen op de cursus."

PURPOSE RULE "cursusinschrijving in leerpad"
{+
   In deze specificatie is het begrip leerpad geïntroduceerd om de context waarin een student een cursus volgt te kunnen bepalen.
   Als een student zich heeft ingeschreven voor een cursus, dan kan een leerpad bijgewerkt of gecreëerd moeten worden vanwege de volgende eis:
-}
RULE "cursusinschrijving in leerpad": student[Cursusinschrijving*Student] |- cursus;bevat~;studievan
MEANING "Een cursus waarvoor een student zich inschrijft moet worden gevolgd door een student via een leerpad van die student."


ENDPATTERN

PATTERN Tentamen
PURPOSE PATTERN Tentamen
{+
   Een tentamen is een middel om te beoordelen of een student voldoet aan de eisen om een cursus af te ronden.
   Het begrip tentamen kent meerdere perspectieven en vormen.
   Wat wel vast staat is dat een student het tentamen van een cursus moet halen om die cursus af te ronden.
-}
PURPOSE CONCEPT Tentamen
{+
   Het begrip tentamen reserveren wij voor het moment waarop een student het laatste deel van de uitwerking van de tentamenopdracht ter beoordeling aan de examinator aanbiedt.
   Deze gebeurtenis is het moment dat vanuit alle tentamenvormen strookt met de uitdrukking 'de student heeft het tentamen gemaakt'.
-}
CONCEPT Tentamen "Een tentamen is een gebeurtenis waarin een student een tentamenopdracht uitwerkt en ter beoordeling aanbiedt."
SPEC Tentamen ISA Gebeurtenis
KEY "tentamen": Tentamen(tentamenvoor,gemaaktdoor,moment[Tentamen*Datum])
gemaaktdoor :: Tentamen -> Student
PRAGMA "" " is gemaakt door " ""
MEANING "Een tentamen moet gemaakt zijn door een student."
tentamenvoor :: Tentamen -> Cursus
PRAGMA "" " voor de cursus " ""
MEANING "Een tentamen moet voor een cursus gemaakt zijn."

RULE "rechten": gemaaktdoor~;tentamenvoor |- rechtop
MEANING "Een student moet rechten hebben gekregen om een tentamen te mogen maken."
--uitwerking :: Opdracht -> Uitwerking

--beoordeling
beoordeling :: Tentamen * Resultaat [UNI]
PRAGMA "" " is beoordeeld met een " ""
MEANING "Een tentamen mag maximaal één maal beoordeeld worden."
RULE "resultaat": I[Resultaat] = 'V' \/ 'O'
MEANING "Een resultaat moet of 'V' van voldoende of 'O' van onvoldoende zijn."
afgerond :: Student * Cursus
RULE "afgerond": gemaaktdoor~;(beoordeling;'V';beoordeling~ /\ I);tentamenvoor = afgerond
MEANING "Een cursus moet afgerond worden door een student via een positieve beoordeling van het tentamen voor de cursus."

beoordeeltdoor :: Tentamen -> Arbeidskracht
PRAGMA "" " is of wordt beoordeeld door " ""
MEANING "Een tentamen moet door een arbeidskracht beoordeeld worden."
RULE "examinator": beoordeeltdoor |- tentamenvoor;examinators
MEANING "Degene die een tentamen beoordeelt moet een examinator van de cursus zijn."
ENDPATTERN


PURPOSE
PATTERN Personeelszaken
{+
   De organisatie rondom arbeidskrachten is ondergebracht bij personeelszaken.
   Deze specificatie is beperkt tot de organisatie van de vaste arbeidskrachten van de OU.
   De gebeurtenissen in- en uitdiensttreding en ziektverzuim zijn uitgewerkt.
   Op basis van deze gebeurtenissen kan worden vastgesteld of een medewerker al dan niet beschikbaar is op een zeker moment.
   Het beschikbaar zijn van de medewerkers is van belang voor de werkverdeling binnen een orgaan.
   Een medewerker moet beschikbaar zijn op een moment om een taak op dat moment uit te kunnen voeren.
   Een medewerker hoeft niet aaneensluitend beschikbaar te zijn om een taak te hebben, bijvoorbeeld: de docent zijn van een cursus.
-}
PATTERN Personeelszaken
PURPOSE CONCEPT Medewerker
{+
   In- en uitdiensttreding hebben alleen betrekking op medewerkers.
   Daarom is een speciaal begrip nodig voor dit type arbeidskrachten.
-}
CONCEPT Medewerker "Een medewerker is een arbeidskracht die een arbeidscontract heeft met een orgaan."
SPEC Medewerker ISA Arbeidskracht

PURPOSE CONCEPT Arbeidscontract
{+
   Om als medewerker indienst te treden bij een orgaan is een arbeidscontract nodig.
   De medewerker heeft een dienstverband met het orgaan als beide partijen een arbeidscontract hebben ondertekend.
-}
CONCEPT Arbeidscontract "Een arbeidscontract is het contract dat ten grondslag ligt aan de indiensttreding van een medewerker bij een orgaan."
KEY "dienstverband": Arbeidscontract(medewerker,orgaan)
medewerker :: Arbeidscontract -> Medewerker [SUR]
PRAGMA "" " is een arbeidscontract van " ""
MEANING "Een arbeidscontract moet ondertekend zijn door de medewerker. Iedere medewerker moet minimaal één arbeidscontract hebben."
orgaan :: Arbeidscontract -> Orgaan
PRAGMA "" " legt een dienstverband vast met " ""
MEANING "Een arbeidscontract moet ondertekend zijn namens een orgaan."
medewerkers :: Orgaan * Medewerker
RULE "dienstverband": medewerkers = orgaan~;medewerker
MEANING "De medewerkers van een orgaan zijn vastgelegd via arbeidscontracten."

--PURPOSE RULE "beschikbaarheid"
--{+
--   De 'huidige' beschikbaarheid van medewerkers dient geen enkel doel
---}
--actief :: Medewerker * Medewerker [PROP]
--MEANING "Een medewerker kan actief zijn."
--nonactief :: Medewerker * Medewerker [PROP]
--MEANING "Een medewerker kan non-actief zijn."
--RULE "beschikbaarheid": I /\ -nonactief = actief
--MEANING "Medewerkers die niet non-actief zijn, zijn actief en v.v."

--RULE "uitdienst": oudmedewerker~;oudmedewerker |- nonactief
--MEANING "Een oudmedewerker is een uitdienst getreden medewerker, die non-actief is om die reden."

CONCEPT Indiensttreding "Een indiensttreding is het moment waarop een persoon een medewerker wordt."
SPEC Indiensttreding ISA Gebeurtenis
KEY "indiensttreding": Indiensttreding(moment[Indiensttreding*Datum],medewerker)
medewerker :: Indiensttreding -> Medewerker [INJ,SUR]
MEANING "Een indiensttreding moet een persoon tot medewerker maken."


CONCEPT Uitdiensttreding "Een uitdiensttreding is het moment waarop een medewerker een oudmedewerker wordt."
SPEC Uitdiensttreding ISA Gebeurtenis
KEY "uitdiensttreding": Uitdiensttreding(moment[Uitdiensttreding*Datum],oudmedewerker)
oudmedewerker :: Uitdiensttreding -> Medewerker [INJ]
MEANING "Een uitdiensttreding moet een medewerker tot oudmedewerker maken."


PURPOSE CONCEPT Ziekmelding
{+
   Medewerkers kunnen ziek worden, zodat ze tijdelijk niet beschikbaar zijn.
   Een medewerker meldt wanneer hij/zij ziek wordt en weer beter is. 
-}
CONCEPT Ziekmelding "Een ziekmelding is het moment waarop een medewerker zich ziek meldt."
SPEC Ziekmelding ISA Gebeurtenis
KEY "ziekmelding": Ziekmelding(moment[Ziekmelding*Datum],ziekmeldingvan)
ziekmeldingvan :: Ziekmelding -> Medewerker
MEANING "Een ziekmelding moet betrekking hebben op een medewerker."

CONCEPT Betermelding "Een betermelding is het moment waarop een medewerker zich beter meldt na een ziekmelding."
SPEC Betermelding ISA Gebeurtenis
KEY "betermelding": Betermelding(moment[Betermelding*Datum],heftop)
heftop :: Betermelding -> Ziekmelding [INJ]
MEANING "Een betermelding moet een ziekmelding opheffen. Een ziekmelding mag maar één keer opgeheven worden"
ENDPATTERN



-----------------------------------------------------------------------------------------------------------
PURPOSE
PATTERN Berekeningen
{+ Berekeningen moeten transparant en traceerbaar uitgevoerd worden. -}
PATTERN Berekeningen
CONCEPT Cel "Een cel representeert een functieberekening op basis van een log."

PURPOSE RELATION
functie {+ Iedere cel heeft een berekeningsfunctie met een log als parameter. -}
functie :: Cel -> Functie -- :: Log -> Waarde::Eenheid
PRAGMA "De functie van " " is " ""
MEANING "Iedere cel bevat een berekeningsfunctie."

PURPOSE RELATION
toegepast {+ De berekeningsfunctie van een cel wordt toegepast op een log. -}
toegepast :: Cel -> Log
PRAGMA "De functie van " " is toegepast op " ""
MEANING "Iedere cel refereert naar een log."

PURPOSE RELATION
resulteert {+ De berekeningsfunctie van een cel toegepast op de log van de cel resulteert in de waarde van de cel. -}
resulteert :: Cel -> Waarde
PRAGMA "De functie van " " toegepast op de log van die cel resulteert in " ""
MEANING "Iedere cel bevat een waarde."

PURPOSE RELATION
eenheid {+ De waarde van een cel is van een bepaalde eenheid. -}
eenheid :: Cel -> Eenheid
PRAGMA "Het resultaat van " " is een " ""
MEANING "Iedere cel heeft een eenheid."
--Tabel*Cel

CONCEPT PrestatieIndicator "???Is een prestatie-indicator een functie of een waarde???"
SPEC PrestatieIndicator ISA Functie
naam :: PrestatieIndicator -> Naam
PRAGMA "De naam van de functie " " is " ""
MEANING "Iedere prestatie-indicator heeft een naam."
ENDPATTERN

ENDCONTEXT

