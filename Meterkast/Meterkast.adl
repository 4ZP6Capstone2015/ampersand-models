CONTEXT Meterkast
  
  SQLPLUG OperationTbl : I[Operation]
   = [Id{"SQLTYPE=Id"} : I
     ,name{"SQLTYPE=Text"} : name
     ,call{"SQLTYPE=Text"} : call
     ,output{"SQLTYPE=Text"} : output
     ]
  SQLPLUG ActieTbl : I[Actie]
   = [Id{"SQLTYPE=Id"}     : I
     ,object{"SQLTYPE=Id"} : object
     ,type{"SQLTYPE=Id"}   : type
     ,done{"SQLTYPE=Bool"} : done
     ]
  SQLPLUG BestandTbl : I[Bestand]
   = [ Id{"SQLTYPE=Id"} : I
     , path{"SQLTYPE=Text"} : path
     ]
  SQLPLUG SessieTbl : I[Session]
   = [Id{"SQLTYPE=Text"} : I
     -- ipV6 heeft max 39 tekens: 8 groepen van 4 hex-digits met 7 : ertussen
     ,ip{"SQLTYPE=VARCHAR 40"} : ip
     ,bestand{"SQLTYPE=Id"} : session~
     ]

  PATTERN Meterkast
    path      :: Bestand->Text.
    session   :: Bestand->Session [INJ] PRAGMA "the owner of " " is ".
    ip        :: Session->Text.
    object    :: Actie -> Bestand PRAGMA "" " is being executed on ".
    type      :: Actie -> Operation PRAGMA "" " is being executed by activating ".
    done      :: Actie -> Flag.
    name      :: Operation->Text [INJ].
    call      :: Operation->Text.
    output    :: Operation->Compilation.
  ENDPATTERN
 
//TODO -> format strings in 'call' MUST be double quoted, not in 'output'
//TODO -> format strings loose $s?
POPULATION name[Operation*Text] CONTAINS
  [ ("1","Test") 
  ; ("2","Atlas(verbose)") 
  ; ("3","Prototype(verbose)") 
  ]  
POPULATION call[Operation*Text] CONTAINS
  [ ("1","adl --help") 
  ; ("2","adl --verbose -aD:/workspace/svnadl/trunk/apps/meterkast/atlas/ --user=%4$s %2$s") 
  ; ("3","adl -p%1$s --verbose %2$s") 
  ]
POPULATION output[Operation*Compilation] CONTAINS
  [ ("1","NULL") 
  ; ("2","atlas/Rules.php?User=%4$s&Script=%2$s") 
  ; ("3","%1$s") 
  ]  
  
  SERVICE Ops       : I[ONE]
   = [ Operations   : V[ONE*Operation]
        = [ name    : name
          , call    : call
          , outputURL: output
          ]
     ]

  SERVICE Sessions  : I[ONE]
   = [ Session      : V[ONE*Session]
        = [ id      : I
          , ip      : ip
          , file    : session~
          ]
     ]
  
  SERVICE Session : I[Session]
   = [ ip      : ip
     , file    : session~
     ]
  
  SERVICE Bestand   : I[Bestand]
   = [ path         : path
     , session      : session
     , compilations : object~
       = [ id: I, operatie : type ]
     ]
  
  SERVICE Actie     : I[Actie]
   = [ file         : object
     , operatie     : type
     , compiled     : done
     ]
  
  SERVICE Operatie  : I[Operation]
   = [ naam         : name
     , call         : call
     , outputURL: output
     ]
{-     
  SERVICE VolleOperatie  : I[Operation]
   = [ naam         : name
     , call         : call
     , acties : type~
       = [ bestand : object ]
     ]
-}
ENDCONTEXT
