CONTEXT Arbeidsmodaliteiten IN DUTCH LATEX
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Arbeidsmodaliteiten.xlsx"

PURPOSE PROCESS Arbeidsmodaliteiten REF "Circulaire Invoering 36-urige werkweek sector Rijk, sectie 4.3."
{+Het begrip modaliteit (of arbeidsmodaliteit) is afkomstig uit de circulaire ``Invoering 36-urige werkweek sector Rijk''.
Deze circulaire is bedoeld om de gemiddeld 36-urige werkweek op verschillende manieren vorm te geven.
Dit betreft compensatie gedurende een jaar door middel van vrije dagen.
-}

PROCESS Arbeidsmodaliteiten

RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Personeelsnummer] [UNI] -- TODO     TOT is temporarily disabled. fix its violations!
MEANING "De arbeidsmodaliteit van een persoon kan worden geregistreerd in de DTV-applicatie."
PURPOSE RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Personeelsnummer] REF "Werktijdenregeling NVWA"
{+Een medewerker wordt in staat gesteld om arbeid te verrichten volgens een jaarlijks vast te stellen arbeidsmodaliteit.
Deze arbeidsmodaliteit wordt gebruikt door de leidinggevende om het rooster van de medewerker te bepalen.
-}

-- RULE arbeidsmodaliteit : medewerker |- arbeidsmodaliteitPersoon~;V            -- EVEN UIT Ferlon
-- MEANING "Elke medewerker heeft een arbeidsmodaliteit."
-- PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] REF "Werktijdenregeling NVWA"
-- {+De arbeidsmodaliteit van een medewerker wordt in beginsel eens per jaar vastgesteld, en heeft dus een ingangsdatum.
-- -}
CONCEPT Arbeidsmodaliteit "De structurele afspraken die een medewerker met de leidinggevende heeft ten aanzien van de invulling van de 36-urige werkweek (of minder voor deeltijdwerkers). Voorbeeld: ma/di/wo/do 9u en vr VRIJ"
PURPOSE CONCEPT Arbeidsmodaliteit REF "Circulaire invoering 36-urige werkweek, hfst 4.3"
{+
-}

RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] [UNI,TOT]
MEANING "De modaliteiten van een medewerker gaan in op een bepaalde datum."
PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] REF "Werktijdenregeling NVWA"
{+De arbeidsmodaliteit van een medewerker wordt in beginsel eens per jaar vastgesteld, en heeft dus een ingangsdatum.
Een en ander wordt in P-Direkt vastgelegd. P-Direkt is dus ook de bron van waaruit DTV de ingangsdatum ophaalt.
Omdat het mogelijk is dat de modaliteiten van een medewerker wijzigen, kunnen er in de tijd meerdere modaliteiten bestaan voor een medewerker.
Die hebben dan allemaal een andere ingangsdatum. 
-}

CONCEPT Modaliteit "De modaliteit is het aantal uren dat een medewerker geacht wordt te werken op een gegeven dag."
PURPOSE CONCEPT Modaliteit REF "Circulaire invoering 36-urige werkweek, hfst 4.3"
{+Wanneer we zeggen: Peter van Dalen had een modaliteit van 6 uur op 22 juli 2015,
dan bedoelen we dat volgens de structurele afspraak met de werkgever, Peter van Dalen geacht werd om 6 uur te werken op 22 juli 2015.
Dit is nodig om overuren te kunnen berekenen.
We spreken van DE modaliteit van persoon P op dag D, zoals bijvoorbeeld in ``Op 21 April 2015 heeft de persoon met nummer 20045833 een modaliteit van 8 uren.''.
We mogen de modaliteit van persoon P op dag D echter ook periodiek duiden, als in ``Op elke dinsdag in oneven weken heeft de persoon met nummer 20045833 een modaliteit van 8 uren.''.
De modaliteit van een bepaalde dag wordt afgeleid van de arbeidsmodaliteit; de structurele afspraken die deze medewerker met de leidinggevende heeft ten aanzien van de invulling van de 36-urige werkweek (of minder voor deeltijdwerkers).
-}

RELATION arbeidsmod[Arbeidsmodaliteit*Modaliteit][INJ,SUR]
MEANING "De arbeidsmodaliteit van een medewerker beschrijft de modaliteit van elke dag, in een ritme van twee weken." 
PURPOSE RELATION arbeidsmod[Arbeidsmodaliteit*Modaliteit] 
{+Om voor een specifieke dag de modaliteit te kunnen bepalen, bevat de arbeidsmodaliteit van een medewerker alle modaliteiten voor alle dagen in een cyclisch ritme van twee weken."
Elke dag van de week is gecodeerd in een aantal dagen, gerekend vanaf het begin van de tweewekelijkse periode.
-}

--{-TODO fix violations
RULE tijdlijnArbeidsmodaliteiten : I[Arbeidsmodaliteit] = arbeidsmodaliteitIngangsdatum;arbeidsmodaliteitIngangsdatum~ 
                                                       /\ arbeidsmodaliteitPersoon;arbeidsmodaliteitPersoon~
PURPOSE RULE tijdlijnArbeidsmodaliteiten
{+De ingangsdatums van verschillende arbeidsmodaliteiten van één persoonsnummer moeten verschillend zijn, zodat arbeidsmodaliteiten in de tijd kunnen worden gesorteerd.
Hierdoor wordt het mogelijk te spreken over de arbeidsmodaliteit van een persoon op een gegeven peildatum. 
-}

RELATION dagNummer[Modaliteit*Aantaldagen] [UNI,TOT]
MEANING "Het aantal dagen, gerekend vanaf het begin van de tweewekelijkse periode, wordt aan de modaliteit gekoppeld.."
PURPOSE RELATION dagNummer[Modaliteit*Aantaldagen]
{+Bij de arbeidsmodaliteit wordt per dag van de week (ma,di,wo,do,vr,za,zo) aangegeven hoeveel uren een medewerker geacht wordt werkzaamheden te verrichten.
Om te kunnen optellen met tijd, wordt de dag als aantal dagen opgeslagen.
-}
REPRESENT Aantaldagen TYPE INTEGER

RELATION tijdsduur[Modaliteit*Tijdsduur] [UNI,TOT]
MEANING "Een aantal te werken uren is onderdeel van de modaliteit"
PURPOSE RELATION tijdsduur[Modaliteit*Tijdsduur]
{+Bij de arbeidsmodaliteit wordt het aantal uren dat een medewerker geacht wordt op een bepaalde dag van de week arbeid te verrichten, vastgelegd.
-}

ENDPROCESS

INTERFACE "Arbeidsmodaliteit inregelen"
         ( arbeidsmodaliteitPersoon
         , arbeidsmodaliteitIngangsdatum
         , arbeidsmod
         , dagNummer
         , tijdsduur[Modaliteit*Tijdsduur]
         )
        FOR Leidinggevende : I[Arbeidsmodaliteit]
      BOX [ "Medewerker : " :   arbeidsmodaliteitPersoon
          , "Ingangsdatum : " : arbeidsmodaliteitIngangsdatum
          , "Modaliteit" :      arbeidsmod  --Modaliteit
            BOX [ dag : dagNummer          
                , uren : tijdsduur        
                ]
          ]

INTERFACE "Arbeidsmodaliteit bekijken"
        FOR Leidinggevende : I[Personeelsnummer]
BOX [ "Modaliteiten van : " : I[Personeelsnummer]
    , "" : arbeidsmodaliteitPersoon~;arbeidsmod  --Modaliteit
      BOX [ dag : dagNummer
          , uren : tijdsduur
          ]
    ]

ENDCONTEXT