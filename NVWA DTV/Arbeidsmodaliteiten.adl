CONTEXT Arbeidsmodaliteiten IN DUTCH LATEX
INCLUDE "Tijdvakken.adl"
INCLUDE "Periodeverantwoordingen.adl"
INCLUDE "Arbeidsmodaliteiten.xlsx"

PURPOSE PROCESS Arbeidsmodaliteiten REF "Circulaire Invoering 36-urige werkweek sector Rijk, sectie 4.3."
{+Het begrip modaliteit (of arbeidsmodaliteit) is afkomstig uit de circulaire ``Invoering 36-urige werkweek sector Rijk''.
Deze circulaire is bedoeld om de gemiddeld 36-urige werkweek op verschillende manieren vorm te geven.
Dit betreft compensatie gedurende een jaar door middel van vrije dagen.
-}

PROCESS Arbeidsmodaliteiten

RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Personeelsnummer] [UNI,TOT]
MEANING "De arbeidsmodaliteit van een persoon kan worden geregistreerd in de DTV-applicatie."
PURPOSE RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Personeelsnummer] REF "Werktijdenregeling NVWA"
{+Een medewerker wordt in staat gesteld om arbeid te verrichten volgens een jaarlijks vast te stellen arbeidsmodaliteit.
Deze arbeidsmodaliteit wordt gebruikt door de leidinggevende om het rooster van de medewerker te bepalen.
-}

-- RULE arbeidsmodaliteit : medewerker |- arbeidsmodaliteitPersoon~;V            -- EVEN UIT Ferlon
-- MEANING "Elke medewerker heeft een arbeidsmodaliteit."
-- PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] REF "Werktijdenregeling NVWA"
-- {+De arbeidsmodaliteit van een medewerker wordt in beginsel eens per jaar vastgesteld, en heeft dus een ingangsdatum.
-- -}
CONCEPT Arbeidsmodaliteit "Een verzameling modaliteiten voor één medewerker, die geldig is vanaf een bepaalde datum."

RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum][UNI,TOT]
MEANING "De modaliteiten van een medewerker gaan in op een bepaalde datum."
PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] REF "Werktijdenregeling NVWA"
{+De arbeidsmodaliteit van een medewerker wordt in beginsel eens per jaar vastgesteld, en heeft dus een ingangsdatum.
Een en ander wordt in P-Direkt vastgelegd. P-Direkt is dus ook de bron van waaruit DTV de arbeidsmodaliteit ophaalt.
-}
PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum]
{+Omdat het mogelijk is dat de modaliteiten van een medewerker wijzigen, kunnen er in de tijd meerdere modaliteiten bestaan voor een medewerker. 
-}
RELATION wijzigbaar [Arbeidsmodaliteit*Arbeidsmodaliteit][PROP]


CONCEPT Modaliteit "Het aantal uren dat een medewerker geacht wordt te werken op een bepaalde dag."
PURPOSE CONCEPT Modaliteit
{+Om overuren te kunnen berekenen, is het nodig te weten hoeveel uren een bepaalde persoon op een bepaalde dag geacht wordt te werken.
Dat getal heet ``modaliteit''.
We spreken van DE modaliteit van persoon P op dag D, zoals bijvoorbeeld in ``Op 21 April 2015 heeft de persoon met nummer 20045833 een modaliteit van 8 uren.''.
We mogen de modaliteit van persoon P op dag D echter ook periodiek duiden, als in ``Op elke dinsdag in oneven weken heeft de persoon met nummer 20045833 een modaliteit van 8 uren.''.
-}

RELATION arbeidsmod[Arbeidsmodaliteit*Modaliteit][INJ,SUR]
MEANING "De arbeidsmodaliteit van een medewerker beschrijft de modaliteit van elke dag, in een ritme van twee weken." 
PURPOSE RELATION arbeidsmod[Arbeidsmodaliteit*Modaliteit] 
{+Om voor een specifieke dag de modaliteit te kunnen bepalen, bevat de arbeidsmodaliteit van een medewerker alle modaliteiten voor alle dagen in een cyclisch ritme van twee weken."
Elke dag van de week is gecodeerd in een aantal dagen, gerekend vanaf het begin van de tweewekelijkse periode.
-}

RULE tijdlijnArbeidsmodaliteiten : I[Arbeidsmodaliteit] = arbeidsmodaliteitIngangsdatum;arbeidsmodaliteitIngangsdatum~ 
                                                       /\ arbeidsmodaliteitPersoon;arbeidsmodaliteitPersoon~
PURPOSE RULE tijdlijnArbeidsmodaliteiten
{+De ingangsdatums van elke arbeidsmodaliteit van een persoon moeten verschillend zijn, zodat de arbeidsmodaliteiten in een tijdsperspectief kunnen worden gezien.
Hierdoor wordt het mogelijk om te praten over de arbeidsmodaliteit van een persoon op een gegeven peildatum. 
 
Om geschiedvervalsing tegen te gaan, mogen arbeidsmodaliteiten met een 
ingangsdatum in het verleden niet worden aangemaakt, verwijderd of 
gewijzigd. Dat geldt daarmee ook voor alle modaliteiten van die arbeidsmodaliteit.


-}

RELATION arbeidsmodDag[Modaliteit*Aantaldagen] [UNI,TOT]
MEANING "Het aantal dagen, gerekend vanaf het begin van de tweewekelijkse periode, wordt aan de modaliteit gekoppeld.."
PURPOSE RELATION arbeidsmodDag[Modaliteit*Aantaldagen]
{+Bij de arbeidsmodaliteit wordt per dag van de week (ma,di,wo,do,vr,za,zo) aangegeven hoeveel uren een medewerker geacht wordt werkzaamheden te verrichten.
Om te kunnen optellen met tijd, wordt de dag als aantal dagen opgeslagen.
-}
REPRESENT Aantaldagen TYPE INTEGER

RELATION arbeidsmodUren[Modaliteit*Tijdsduur] [UNI,TOT]
MEANING "Een aantal te werken uren is onderdeel van de modaliteit"
PURPOSE RELATION arbeidsmodUren[Modaliteit*Tijdsduur]
{+Bij de arbeidsmodaliteit wordt het aantal uren dat een medewerker geacht wordt op een bepaalde dag van de week arbeid te verrichten, vastgelegd.
-}

ENDPROCESS

INTERFACE "Arbeidsmodaliteit inregelen"
         ( arbeidsmodaliteitPersoon
         , arbeidsmodaliteitIngangsdatum
         , arbeidsmod
         , arbeidsmodDag
         , arbeidsmodUren
         )
        FOR Leidinggevende : I[Arbeidsmodaliteit]
      BOX [ "Medewerker : " :   arbeidsmodaliteitPersoon
          , "Ingangsdatum : " : arbeidsmodaliteitIngangsdatum
          , "Modaliteit" :      arbeidsmod  --Modaliteit
            BOX [ dag : arbeidsmodDag          
                , uren : arbeidsmodUren        
                ]
          ]

INTERFACE "Arbeidsmodaliteit bekijken"
        FOR Leidinggevende : I[Personeelsnummer]
BOX [ "Modaliteiten van : " : I[Personeelsnummer]
    , "" : arbeidsmodaliteitPersoon~;arbeidsmod  --Modaliteit
      BOX [ dag : arbeidsmodDag
          , uren : arbeidsmodUren
          ]
    ]

ENDCONTEXT