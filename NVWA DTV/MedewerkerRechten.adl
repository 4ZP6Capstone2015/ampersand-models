CONTEXT DTV IN DUTCH LATEX
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Referentielijsten.adl"
INCLUDE "Medewerkersrechten_pop.xlsx"

PROCESS MedewerkerRechten
PURPOSE PROCESS MedewerkerRechten REF "Tijdschrijven Fatijdec sept 2013"
{+Naast de daadwerkelijk gewerkte uren en de van toepassing zijnde diensten heeft de kennismotor nog een aantal zaken nodig
om te bepalen óf en in welke mate een medewerker een toelage ontvangt.
Dat zijn de salarissschaal van de medewerker (1 t/m 18, cf. het BBRA),
de categorie waar hij in valt (1, 2, 3, 14, 24 of 34) en
de rechten op vaste toelagen, die voor hem of haar gelden.
Een medewerker die bijvoorbeeld een vaste toelage voor beschikbaarheidsdienst krijgt,
krijgt hiervoor een vast bedrag per maand en krijgt om die reden geen variabele toelage die gebaseerd is op zijn gewerkte uren.
De DTV-applicatie administreert salarisschaal, categorie en rechten op uniforme wijze in de tabel ``medewerkerrechten''
en registreert daarbij tevens de periode van geldigheid.
Een registratie is dan bijvoorbeeld: ``Medewerker Janssen heeft per 1-1-2015 het recht \"Salarisschaal 12\"'', of
``Medewerker Joosten heeft per 1-1-2015 recht \"Categorie 3\"''.
Voor elke registratie dient de begindatum bekend te zijn.
Een registratie zonder einddatum betekent dit dat het betreffende recht voor onbepaalde tijd is toegekend.

Toegekende rechten kunnen wijzigen in de loop van de tijd.
Iemand kan bijvoorbeeld onder een andere categorie komen te vallen, geen vaste toelage meer krijgen (of juist wel) of van salarisschaal wijzigen.
De DTV-applicatie houdt de volledige historie bij, zodat de kennismotor op elk moment de periodeverwerking van een willekeurig tijdvak kan doen.
Deze geschiedenis wordt vastgelegd in records in de tabel MedewerkerRechten,
waarbij elk record één recht van één medewerker in één aaneengesloten tijdspanne weergeeft.
Deze registratie dient te voldoen aan de eis dat op ieder willekeurig moment gedurende het dienstverband
de salarisschaal en de categorie van een medewerker eenduidig gedefinieerd zijn.
Voor de rechten op vaste toelagen geldt dat
op elk moment gedurende het dienstverband van een medewerker ondubbelzinnig vast moet liggen welke rechten een medewerker had.
Daarmee registreert de DTV-applicatie de volledige geschiedenis van elke medewerker in de vorm van een verzameling records in de tabel MedewerkerRechten.
De actuele toestand van een recht kan worden afgelezen door het meest recente record voor dat recht van de betreffende medewerker te bekijken.

Deze informatie wordt beheerd door medewerkers P\&O (rol: HR).
Een medewerker mag niet zijn eigen rechten beheren.
Wanneer een medewerker P\&O een recht van een medewerker wijzigt, wordt onder water een nieuw record aangemaakt en ingevuld.
Op deze wijze blijft de geschiedenis intact.
Bij het wijzigen van rechten door een medewerker P\&O dient het systeem te zorgen dat de geschiedenis niet kan worden gewijzigd.
Om correcties van rechten in het verleden door te voeren, is een afzonderlijke interface voorhanden.
In die interface kunnen rechten uit het verleden wél worden aangepast.
Ook deze interface is alleen beschikbaar voor de rol HR.
-}

CONCEPT MedewerkerRecht "Een medewerkerRecht is een registratie van het recht van een specifieke persoon op een vaste toelage, een salarisschaal of een categorie-indeling."
PURPOSE CONCEPT MedewerkerRecht
{+Als een medewerker recht heeft op een vaste vergoeding in plaats van een variabele toelage,
wordt dit als medewerkerrecht geregistreerd.
Dit maakt het mogelijk om na te gaan welk recht op enig moment geldig is voor die medewerker.
Eén medewerkerrecht geeft voor één medewerker aan welk recht in een bepaald tijdvak van toepassing is.
De geldigheidsduur wordt vastgelegd in de vorm van begin- en einddatum.
Wanneer er geen einddatum geregistreerd is, dan geldt het recht voor onbepaalde tijd.
De data, waarbinnen een medewerkerrecht geldig is,
hoeven niet overeen te komen met het begin- of eindtijdstip van een periodeverantwoording.
Daarom wordt bij een periodeverantwoording alle medewerkkerrechten betrokken,
die gelden op de data van de dagverantwoordingen binnen dat tijdvak.
-}

RELATION begindatum[MedewerkerRecht*Datum] [UNI,TOT] PRAGMA "" " heeft als begindatum " ""  -- 
MEANING "De ingangsdatum van elk medewerkerrecht wordt geregistreerd."
RELATION einddatum[MedewerkerRecht*Datum] [UNI] PRAGMA "" " heeft als einddatum " ""  -- 
MEANING "De einddatum van een medewerkerrecht kan worden geregistreerd."

RELATION geldigOp[MedewerkerRecht*Datum] [AUT] PRAGMA "" " is geldig op " ""  -- 
MEANING "Of een medewerkerrecht geldt op een gegeven datum, wordt door de computer uitgerekend."
-- RULE "geldig Medewerkerrecht" : geldigOp = einddatum;geqDatum /\ begindatum;geqDatum~
-- Stap 1: Deze invariant in stand houden doe je door:
-- Eerst de relatie te gaan definieren:
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insGeldigOp
RULE insGeldigOp: einddatum;geqDatum /\ begindatum;geqDatum~ |- geldigOp
VIOLATION (TXT "{EX} InsPair;geldigOp;MedewerkerRecht;", SRC I, TXT ";Datum;", TGT I)
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS delGeldigOp
RULE delGeldigOp: geldigOp |- I[MedewerkerRecht];einddatum;geqDatum /\ begindatum;geqDatum~
VIOLATION (TXT "{EX} DelPair;geldigOp;MedewerkerRecht;", SRC I, TXT ";Datum;", TGT I)
-- Klaar.

RELATION overlap[MedewerkerRecht*MedewerkerRecht] [AUT]
MEANING "Of een medewerkerrecht met een verantwoordingsperiode in de tijd overlapt, wordt door de computer uitgerekend."
-- RULE overlap = einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~
-- Stap 1: Deze invariant in stand houden doe je door:
-- Eerst de relatie te gaan definieren:
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insoverlap
RULE insoverlap: I[MedewerkerRecht];einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~ |- overlap
VIOLATION (TXT "{EX} InsPair;overlap;MedewerkerRecht;", SRC I, TXT ";MedewerkerRecht;", TGT I)
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS deloverlap
RULE deloverlap: overlap |- I[MedewerkerRecht];einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~
VIOLATION (TXT "{EX} DelPair;overlap;MedewerkerRecht;", SRC I, TXT ";MedewerkerRecht;", TGT I)
-- Klaar.

PURPOSE RULE "Ingangsdatum verplicht in medewerkerRecht"
{+ Opdat duidelijk is wanneer welke rechten gelden, is een ingangsdatum voor elk medewerkerRecht verplicht.
Wanneer ook een einddatum is gespecificeerd, gelden de rechten uit dit medewerkerRecht tot de einddatum.
-}

RULE "Ingangsdatum verplicht in medewerkerRecht" : I[MedewerkerRecht] |- begindatum;begindatum~
MEANING "Elk medewerkerRecht dient voorzien te zijn van de datum vanaf welke de geldigheidsperiode ingaat."
MESSAGE "Elk medewerkerRecht dient voorzien te zijn van een ingangsdatum."
VIOLATION (TXT "Het medewerkerRecht voor ", SRC persoon, TXT " van ", SRC begindatum, TXT " tot ", SRC einddatum, TXT " moet nog voorzien worden van een ingangsdatum.")

{-
RELATION salarisschaal[MedewerkerRecht*Salarisschaal] [UNI] PRAGMA "Medewerker " " zit in salarisschaal " " "   -- 
MEANING "Van medewerkers, die worden bezoldigd onder het BBRA 1984, registreert het systeem de actuele salarisschaal in een medewerkerRecht."
PURPOSE RELATION salarisschaal[MedewerkerRecht*Salarisschaal]  REF "BBRA Artikel 2 sub c"
{+Een salarisschaal is een als zodanig in de bijlage B van het Bezoldigingsbesluit Burgerlijke Rijksambtenaren 1984 (BBRA) vermelde reeks van genummerde salarissen.
Omdat bepaalde regels van toepassing zijn afhankelijk van iemand's salarisschaal, wordt dit gegeven in de DTV-applicatie geregistreerd.
-}

PURPOSE RULE "Salarisschaal verplicht voor medewerker"
{+ Elke medewerker, die onder de BBRA bezoldigd wordt, is ingedeeld in een salarisschaal die hoort bij zijn of haar functie.
In DTV worden echter geen contractgegevens geregistreerd van medewerkers. Daarom verwacht de DTV-applicatie 
dat er tenminste één medewerkerRecht is waarin een salarisschaal zit. Als dat niet het geval is, 
signaleert de DTV-applicatie dat aan een gebruiker in de rol HR.
-}
RULE "Salarisschaal verplicht voor medewerker" : I[Personeelsnummer] |- persoon~;recht;refLijst;'Salarisschalen';refLijst~;recht~;persoon
MEANING "Elke medewerker dient voorzien te zijn van de salarisschaal die geldt voor de geldigheidsperiode van het bijbehorende medewerkerRecht."
MESSAGE "Elke medewerker dient voorzien te zijn van een salarisschaal."
VIOLATION (TXT "De medewerker met personeelsnummer ", SRC I, TXT " heeft nog geen salarisschaal.")
ROLE "HR" MAINTAINS "Salarisschaal verplicht voor medewerker"

PURPOSE RULE "Categorie verplicht voor medewerker"
{+ Elke medewerker, die onder de BBRA bezoldigd wordt, is ingedeeld in een categorie die hoort bij zijn of haar functie.
In DTV worden echter geen contractgegevens geregistreerd van medewerkers. Daarom verwacht de DTV-applicatie 
dat er tenminste één medewerkerRecht is waarin een categorie zit. Als dat niet het geval is, 
signaleert de DTV-applicatie dat aan een gebruiker in de rol HR.
-}RULE "Categorie verplicht voor medewerker" : I[Personeelsnummer] |- persoon~;recht;refLijst;'Categorieën';refLijst~;recht~;persoon
MEANING "Elke medewerker dient ingedeeld te zijn in een categorie, die geldt voor de geldigheidsperiode van het bijbehorende medewerkerRecht."
MESSAGE "Elke medewerker dient ingedeeld te zijn in een categorie."
VIOLATION (TXT "De medewerker met personeelsnummer ", SRC I, TXT " is nog niet ingedeeld in een categorie.")
ROLE "HR" MAINTAINS "Categorie verplicht voor medewerker"

RULE geenSalarisOverlap : -(recht;refLijst;'Salarisschalen';refLijst~;recht~/\persoon;persoon~/\overlap)
MEANING "Voor iedere medewerker is op enig moment hooguit één medewerkerrecht geldig dat de salarisschaal bepaalt."
MESSAGE "Er is overlap m.b.t. een salarisschaal."
VIOLATION (TXT "De persoon met personeelsnummer ", SRC persoon, TXT " heeft op enig moment zowel recht op ", SRC recht;varNaam, TXT " als ook op ", TGT recht;varNaam, TXT ".")
RULE geenCategorieOverlap : -(recht;refLijst;'Categorieën';refLijst~;recht~/\persoon;persoon~/\overlap)
MEANING "Voor iedere medewerker is op enig moment hooguit één medewerkerrecht geldig dat de categorie bepaalt."
MESSAGE "Er is overlap m.b.t. een categorie."
VIOLATION (TXT "De persoon met personeelsnummer ", SRC persoon, TXT " heeft op enig moment zowel recht op ", SRC recht;varNaam, TXT " als ook op ", TGT recht;varNaam, TXT ".")
{-
RELATION categorie[MedewerkerRecht*Categorie] [UNI,TOT]
MEANING "Een persoon is ingedeeld in een categorie (1,2 of 3)."
PURPOSE RELATION categorie[MedewerkerRecht*Categorie] REF "WTR 2012"
{+In een medewerkerRecht is aangegeven tot welke categorie een medewerker hoort.
Op basis van deze categorie wordt o.a. bepaald of voor deze medewerker een werkperiode van 1 dag of van 28 dagen geldt, alsook voor welke toelagen
de medewerker in aamkerking kan komen.
-}

CONCEPT Recht "Onder een recht verstaan we de juridische grondlag van het recht van een medewerker op een vaste toelage, die in de plaats komt van het recht op een toelage op basis van uren."
PURPOSE CONCEPT Recht
{+Om rechten te kunnen beheren, moet een functioneel beheerder de naam en de wettelijke grondslag van een recht kunnen veranderen.
Om die reden is Recht een specialisatie van Variable.
-}
CLASSIFY Recht ISA StamItem

 
PURPOSE RELATION recht[MedewerkerRecht*Recht] REF "BBRA Artikel 13-20"
{+De bezoldigingscomponenten waar een medewerker recht op heeft, zoals salarisschaal en toelagen moeten in de DTV-applicatie worden geadministreerd.
De medewerker heeft recht op een bezoldigingscomponent, indien daarvoor in het betreffende tijdvak een medewerkerRecht is geregistreerd.
-}
RELATION recht[MedewerkerRecht*Recht] [UNI,TOT] PRAGMA "" " geeft recht op "
MEANING "De toelage(s) waar een medewerker recht op heeft worden in de DTV-applicatie gekoppeld aan het medewerkerrecht van de medewerker."

--HJO, 20150809: Regel tijdelijk uitgecommentarieerd, omdat datumVorigekeerInlezen geen geldige datum is.
--PURPOSE RULE bevriezenMedewerkersrechten 
--{+Om geschiedvervalsing tegen te gaan, mogen medewerkersrechten met een 
--ingangsdatum vóór de laatste keer dat de gegevens zijn ingelezen (via de bulk import), niet worden aangemaakt, verwijderd of 
--gewijzigd.
--Deze regel kan door de HR medewerker worden overruled, in het uitzonderlijke geval dat er een administratieve correctie achteraf nodig is. Dit valt onder `geschiedvervalsing` 
-- (zie scherm Beheer > Medewerkers > Medewerker).
---}
--RULE bevriezenMedewerkersrechten : I[MedewerkerRecht];begindatum;gtDatum;'datumVorigekeerInlezen';V[Datum*MedewerkerRecht] = -wijzigbaar
RELATION wijzigbaar [MedewerkerRecht*MedewerkerRecht][PROP]


PURPOSE RULE salarisGedurendeContractduur
{+De DTV-applicatie registreert geen arbeidscontracten. Desondanks mag de salarisschaal 
alleen bestaan binnen een arbeidscontract. Dat wordt als volgt opgelost: Op 
elke datum dat de medewerker een geldig medewerkersrecht heeft, dient die 
medewerker precies één geldig medewerkersrecht van het soort ’salarisschaal’ 
te hebben. Immers, op een datum dat een medewerker een geldig medewerkersrecht 
heeft, kan worden aangenomen dat er een geldige arbeidsverhouding bestaat voor 
die medewerker.
-}
RULE salarisGedurendeContractduur : I[MedewerkerRecht] |- geldigOp;geldigOp~;recht;refLijst;'Salarisschalen';refLijst~;recht~
PURPOSE RULE categorieGedurendeContractduur
{+De DTV-applicatie registreert geen arbeidscontracten. Desondanks mag de categorie 
alleen bestaan binnen een arbeidscontract. Dat wordt als volgt opgelost: Op 
elke datum dat de medewerker een geldig medewerkersrecht heeft, dient die 
medewerker precies één geldig medewerkersrecht van het soort ’categorie’ 
te hebben. Immers, op een datum dat een medewerker een geldig medewerkersrecht 
heeft, kan worden aangenomen dat er een geldige arbeidsverhouding bestaat voor 
die medewerker.
-}
RULE categorieGedurendeContractduur : I[MedewerkerRecht] |- geldigOp;geldigOp~;recht;refLijst;'Categorieën';refLijst~;recht~
ROLE HR  MAINTAINS categorieGedurendeContractduur, salarisGedurendeContractduur
ROLE FAB MAINTAINS inRefLijstMedewerkerRecht
RULE inRefLijstMedewerkerRecht : I[MedewerkerRecht] |- recht;refLijst;('Categorieën'/\'Salarisschalen'/\'Toelage');refLijst~;recht~
MESSAGE "Elke medewerkerrecht dient in de referentielijst van Salarisschalen, Categorieën of Toelagen voor te komen:"
VIOLATION (TXT "Medewerkerrecht ", SRC I, TXT " komt niet voor in de referentielijst.") 
PURPOSE RULE inRefLijstMedewerkerRecht
{+Omdat een HR-medewerker alleen rechten mag toekennen voor bestaande regelingen, dient 
het recht van elk medewerkerrecht dient in de referentielijst van Salarisschalen, Categorieën of Toelagen voor te komen. 
-}
{-
RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage TOD."
PURPOSE RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 17 lid 4 en 7 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het werken op onregelmatige tijden.
Dit houdt in dat geen variabele toelage op basis van de werktijden aan deze medewerker wordt toegekend.
Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage voor Bereik en beschikbaarheidsdiensten."
PURPOSE RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 18a lid 4 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het zich regelmatig of vrij regelmatig bereikbaar en beschikbaar moet houden. Dit houdt in dat hiervoor geen variabele toelage aan deze 
medewerker wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen aanspraak maakt op de afbouwregeling."
PURPOSE RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht]
{+In het "Onderhandelaarsakkoord arbeidsvoorwaarden nVWA 28062011" hoofdtstuk B is opgenomen dat een bepaalde groep medewerkers op jaarbasis een vaste inconveniëntentoelage krijgt, die bestaat uit: de toelage voor onregelmatige dienst, overwerk,
bereikbaarheids/beschikbaarheidsdienst. Dit houdt in dat hiervoor geen variabele toelage aan deze medewerkers wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerkers geregistreerd.
-}

RELATION persoon[MedewerkerRecht*Personeelsnummer] [UNI,TOT]
MEANING "De rechten op vaste toelagen worden op personeelsnummer geregistreerd."
PURPOSE RELATION persoon[MedewerkerRecht*Personeelsnummer]
{+Om vast te leggen welke rechten in een bepaalde verantwoordingsperiode van toepassing zijn voor een bepaalde medewerker, wordt aan elk medewerkerrecht één persoon gekoppeld.
Omdat wijzigingen in de rechten op vaste toelages worden bewaard, ontstaan meerdere medewerkerrechten voor dezelfde persoon.
Bijvoorbeeld kan een medewerker meerdere rechten hebben op salarisschalen in verschillende periodes.
Rechten van één persoon op salarisschalen mogen elkaar niet in de tijd overlappen.
Datzelfde geldt voor rechten van één persoon op categorieën. Ook die mogen elkaar niet in de tijd overlappen.
De periodes, die afgedekt worden door rechten van één persoon op salarisschalen moeten overeenkomen met de periodes, die afgedekt worden door rechten van één persoon op  categorieën.
-}

ENDPROCESS


{-
RULE salarisschaal : refLijst;'Salarisschalen';refLijst~ /\ recht~;(begindatum;begindatum~ /\ persoon;persoon~);recht |- I[Recht]
MEANING "Eén medewerker heeft op elk willekeurig moment precies één salarisschaal."
MESSAGE "Eén medewerker mag niet meer dan één salarisschaal hebben!"
VIOLATION ( TXT "Deze persoon heeft tegelijkertijd de salarisschalen ", SRC I, TXT " en ", TGT I, TXT ".")

RULE     categorie :   refLijst;'Categorieën';refLijst~   /\ recht~;(begindatum;begindatum~ /\ persoon;persoon~);recht |- I[Recht]
MEANING "Eén medewerker is op enig moment in hooguit één categorie ingedeeld."
MESSAGE "Eén medewerker mag niet in meer dan één categorie zijn ingedeeld!"
VIOLATION ( TXT "Deze persoon heeft tegelijkertijd de categorieën ", SRC I, TXT " en ", TGT I, TXT ".")
-}

ENDCONTEXT