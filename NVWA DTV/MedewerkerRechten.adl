CONTEXT DTV IN DUTCH LATEX

PROCESS MedewerkerRechten
PURPOSE PROCESS MedewerkerRechten REF "Tijdschrijven Fatijdec sept 2013"
{+Naast de daadwerkelijk gewerkte uren en de van toepassing zijnde diensten heeft de kennismotor nog een aantal zaken nodig
om te bepalen óf en in welke mate een medewerker een toelage ontvangt.
Dat zijn de salarissschaal van de medewerker (1 t/m 18, cf. het BBRA),
de categorie waar hij in valt (1 t/m 4) en
de rechten op vaste toelagen en toeslagen, die voor hem of haar gelden.
Een medewerker die bijvoorbeeld een vaste toelage voor beschikbaarheidsdienst krijgt,
krijgt hiervoor een vast bedrag per maand en krijgt om die reden geen variabele toelage die gebaseerd is op zijn gewerkte uren.
De DTV-applicatie administreert salarisschaal, categorie en rechten op uniforme wijze in de tabel ``medewerkerrechten''
en registreert daarbij tevens de periode van geldigheid.
Een registratie is dan bijvoorbeeld: ``Medewerker Janssen heeft per 1-1-2015 het recht \"Salarisschaal 12\"'', of
``Medewerker Joosten heeft per 1-1-2015 recht \"Categorie 3\"''.
Voor elke registratie dient de begindatum bekend te zijn.
Een registratie zonder einddatum betekent dit dat het betreffende recht voor onbepaalde tijd is toegekend.

Toegekende rechten kunnen wijzigen in de loop van de tijd.
Iemand kan bijvoorbeeld onder een andere categorie komen te vallen, geen vaste toelage meer krijgen (of juist wel) of van salarisschaal wijzigen.
DTV houdt de volledige historie bij, zodat de kennismotor op elk moment de periodeverwerking van een willekeurige periode kan doen.
Deze geschiedenis wordt vastgelegd in records in de tabel MedewerkerRechten,
waarbij elk record één recht van één medewerker in één aaneengesloten tijdspanne weergeeft.
Deze registratie dient te voldoen aan de eis dat op ieder willekeurig moment gedurende het dienstverband
de salarisschaal en de categorie van een medewerker eenduidig gedefinieerd zijn.
Voor de rechten op vaste toelagen en toeslagen geldt dat
op elk moment gedurende het dienstverband van een medewerker ondubbelzinnig vast moet liggen welke rechten een medewerker had.
Daarmee registreert de DTV-applicatie de volledige geschiedenis van elke medewerker in de vorm van een verzameling records in de tabel MedewerkerRechten.
De actuele toestand van een recht kan worden afgelezen door het meest recente record voor dat recht van de betreffende medewerker te bekijken.

Deze informatie wordt beheerd door medewerkers P\&O (rol: HR).
Een medewerker mag niet zijn eigen rechten beheren.
Wanneer een medewerker P\&O een recht van een medewerker wijzigt, wordt onder water een nieuw record aangemaakt en ingevuld.
Op deze wijze blijft de geschiedenis intact.
Bij het wijzigen van rechten door een medewerker P\&O dient het systeem te zorgen dat de geschiedenis niet kan worden gewijzigd.
Om correcties van rechten in het verleden door te voeren, is een afzonderlijke interface voorhanden.
In die interface kunnen rechten uit het verleden wél worden aangepast.
Ook deze interface is alleen beschikbaar voor de rol HR.
-}

CONCEPT MedewerkerRecht "Een medewerkerRecht is een vooraf pakket van rechten die van toepassing zijn op een specifieke persoon."
PURPOSE CONCEPT MedewerkerRecht
{+Vaste afspraken, die voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding),
worden als recht geregistreerd in medewerkerrechten.
Dit maakt het mogelijk om na te gaan welk recht op enig moment geldig is voor die medewerker.
Eén medewerkerrecht geeft voor één medewerker aan welk recht in een bepaalde periode van toepassing is.
De geldigheidsduur wordt vastgelegd in de vorm van begin- en einddatum.
Wanneer er geen einddatum geregistreerd is, dan geldt het recht voor onbepaalde tijd.
De data, waarbinnen een medewerkerrecht geldig is,
hoeven niet overeen te komen met het begin- of eindtijdstip van een medewerkerrecht.
Daarom wordt bij een periodeverantwoording alle medewerkkerrechten betrokken,
die in de tijd overlap vertonen met de te verantwoorden periode.
-}
--bevat nog fout: VIEW MedewerkerRecht : Persoon (TXT "mdw ", personeelsnummer, TXT " heeft recht op ", recht, TXT".")

RELATION begindatum[MedewerkerRecht*Datum] [UNI,TOT] PRAGMA "" " heeft als begindatum " ""  -- 
MEANING "De ingangsdatum van elk medewerkerrecht wordt geregistreerd."
RELATION einddatum[MedewerkerRecht*Datum] [UNI] PRAGMA "" " heeft als einddatum " ""  -- 
MEANING "De einddatum van een medewerkerrecht kan worden geregistreerd."
RELATION overlap[MedewerkerRecht*Verantwoordingsperiode] [AUT]
MEANING "Of een medewerkerrecht met een verantwoordingsperiode in de tijd overlapt, wordt door de computer uitgerekend."
RULE overlap = einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~
-- Stap 1: Deze invariant in stand houden doe je door:
-- Eerst de relatie te gaan definieren:
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insoverlap
RULE insoverlap: einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~ |- overlap
VIOLATION (TXT "{EX} InsPair;overlap;Periode;", SRC I, TXT ";Periode;", TGT I)
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS deloverlap
RULE deloverlap: overlap |- einddatum;geqDatum;begindatum~ /\ begindatum;geqDatum~;einddatum~
VIOLATION (TXT "{EX} DelPair;overlap;Periode;", SRC I, TXT ";Periode;", TGT I)
-- Klaar.

PURPOSE RULE "Ingangsdatum verplicht in medewerkerRecht"
{+ Opdat duidelijk is wanneer welke rechten gelden, is een ingangsdatum voor elk medewerkerRecht verplicht.
Wanneer ook een einddatum is gespecificeerd, gelden de rechten uit dit medewerkerRecht tot de einddatum.
-}

RULE "Ingangsdatum verplicht in medewerkerRecht" : I[MedewerkerRecht] |- begindatum;begindatum~
MEANING "Elk medewerkerRecht dient voorzien te zijn van de datum vanaf welke de geldigheidsperiode ingaat."
MESSAGE "Elk medewerkerRecht dient voorzien te zijn van een ingangsdatum."
VIOLATION (TXT "Het medewerkerRecht voor ", SRC persoon;personeelsnummer, TXT " van ", SRC begindatum, TXT " tot ", SRC einddatum, TXT " moet nog voorzien worden van een ingangsdatum.")

{-
RELATION salarisschaal[MedewerkerRecht*Salarisschaal] [UNI] PRAGMA "Medewerker " " zit in salarisschaal " " "   -- 
MEANING "Van medewerkers, die worden bezoldigd onder het BBRA 1984, registreert het systeem de actuele salarisschaal in een medewerkerRecht."
PURPOSE RELATION salarisschaal[MedewerkerRecht*Salarisschaal]  REF "BBRA Artikel 2 sub c"
{+Een salarisschaal is een als zodanig in de bijlage B van het Bezoldigingsbesluit Burgerlijke Rijksambtenaren 1984 (BBRA) vermelde reeks van genummerde salarissen.
Omdat bepaalde regels van toepassing zijn afhankelijk van iemand's salarisschaal, wordt dit gegeven in de DTV geregistreerd.
-}

PURPOSE RULE "Salarisschaal verplicht voor persoon"
{+ Elke medewerker, die onder de BBRA bezoldigd wordt, is ingedeeld in een salarisschaal die hoort bij zijn of haar functie.
Dus heeft elke medewerker een salarisschaal.
Om die reden wordt de salarisschaal als verplicht medewerkerRecht toegevoegd.
De salarisschaal van een medewerker op enig moment $t$, kan dan ook altijd gevonden worden
door het medewerkerRecht te nemen van diezelfde medewerker waarbij $t$ binnen de periode valt.
-}
RULE "Salarisschaal verplicht voor persoon" : I[Persoon] |- persoon~;recht;rechtSoort;'salarisschaal';rechtSoort~;recht~;persoon
MEANING "Elke medewerker dient voorzien te zijn van de salarisschaal die geldt voor de geldigheidsperiode van het bijbehorende medewerkerRecht."
MESSAGE "Elke medewerker dient voorzien te zijn van een salarisschaal."
VIOLATION (TXT "De persoon met personeelsnummer ", SRC personeelsnummer, TXT " heeft nog geen salarisschaal voor de periode van ", SRC persoon~;begindatum, TXT " tot ", SRC persoon~;einddatum, TXT ".")
ROLE HR MAINTAINS "Salarisschaal verplicht voor persoon"

RULE "Categorie verplicht voor persoon" : I[Persoon] |- persoon~;recht;rechtSoort;'categorie';rechtSoort~;recht~;persoon
MEANING "Elke medewerker dient ingedeeld te zijn in een categorie, die geldt voor de geldigheidsperiode van het bijbehorende medewerkerRecht."
MESSAGE "Elke medewerker dient ingedeeld te zijn in een categorie."
VIOLATION (TXT "De persoon met personeelsnummer ", SRC personeelsnummer, TXT " is nog niet ingedeeld in categorie voor de periode van ", SRC persoon~;begindatum, TXT " tot ", SRC persoon~;einddatum, TXT ".")
ROLE HR MAINTAINS "Categorie verplicht voor persoon"

{-
RELATION categorie[MedewerkerRecht*Categorie] [UNI,TOT]
MEANING "Een persoon is ingedeeld in een categorie (1,2 of 3)."
PURPOSE RELATION categorie[MedewerkerRecht*Categorie] REF "WTR 2012"
{+In een medewerkerRecht is aangegeven tot welke categorie een medewerker hoort.
Op basis van deze categorie wordt o.a. bepaald of voor deze medewerker een werkperiode van 1 dag of van 28 dagen geldt, alsook voor welke toeslagen
de medewerker in aamkerking kan komen.
-}

RELATION naam[Recht*Naam] [UNI,TOT] PRAGMA "" " heeft " " als naam"
MEANING "Elk recht heeft een gestandaardiseerde naam."

PURPOSE RELATION recht[MedewerkerRecht*Recht] REF "BBRA Artikel 13-20"
{+De bezodigingscomponenten waar een medewerker recht op heeft, zoals salarisschaal, toelagen en toeslagen moeten in DTV worden geadministreerd.
De medewerker heeft recht op de betreffende bezodigingscomponent voor de periode, die in het betreffende medewerkerRecht is geregistreerd.
-}
RELATION recht[MedewerkerRecht*Recht] [UNI,TOT] PRAGMA "" " geeft recht op "
MEANING "De toelage(s) waar een medewerker recht op heeft worden in DTV gekoppeld aan het medewerkerRecht van de medewerker."

PURPOSE RULE "Unieke naam voor rechten"
{+Om eenduidig over de rechten te kunnen spreken krijgt elk recht een gestandaardiseerde naam.-}
RULE "Unieke naam voor rechten" : naam;naam~ |- I[Recht]
MEANING "Elke recht dient van een unieke naam voorzien te zijn."
MESSAGE "Rechten voor medewerkers moeten een unieke naam hebben."
VIOLATION (TXT "De naam ", SRC naam, TXT " is al in gebruik.")
VIEW Recht : Recht (naam)

PURPOSE RELATION grond[Recht*Wetsartikel]
{+De rechtsgrond van elke toelage dient in het systeem bekend te zijn,
zodat een auditor de rechtmatigheid kan traceren vanuit een juridische werkelijkheid.
-}
RELATION grond[Recht*Wetsartikel] [UNI] PRAGMA "" " heeft als rechtsgrond "
MEANING "Van elk toelage wordt geregistreerd welk wetsartikel als rechtsgrond dient."

{-
RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage TOD."
PURPOSE RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 17 lid 4 en 7 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het werken op onregelmatige tijden.
Dit houdt in dat geen variabele toelage op basis van de werktijden aan deze medewerker wordt toegekend.
Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage voor Bereik en beschikbaarheidsdiensten."
PURPOSE RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 18a lid 4 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het zich regelmatig of vrij regelmatig bereikbaar en beschikbaar moet houden. Dit houdt in dat hiervoor geen variabele toelage aan deze 
medewerker wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen aanspraak maakt op de afbouwregeling."
PURPOSE RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht]
{+In het "Onderhandelaarsakkoord arbeidsvoorwaarden nVWA 28062011" hoofdtstuk B is opgenomen dat een bepaalde groep medewerkers op jaarbasis een vaste inconveniëntentoelage krijgt, die bestaat uit: de toelage voor onregelmatige dienst, overwerk,
bereikbaarheids/beschikbaarheidsdienst. Dit houdt in dat hiervoor geen variabele toelage aan deze medewerkers wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerkers geregistreerd.
-}

RELATION persoon[MedewerkerRecht*Persoon] [UNI,TOT]
MEANING "De rechten op vaste toelagen worden per per persoon geregistreerd."
PURPOSE RELATION persoon[MedewerkerRecht*Persoon]
{+Om vast te leggen welke rechten in een bepaalde verantwoordingsperiode van toepassing zijn voor een bepaalde medewerker, wordt aan elk medewerkerrecht één persoon gekoppeld.
Omdat wijzigingen in de rechten op vaste toelages worden bewaard, ontstaan meerdere medewerkerrechten voor dezelfde persoon.
categorie van één persoon mogen niet in de tijd overlappen, en tezamen moeten zij het volledige dienstverband (voor zover in de administratie aanwezig) overdekken.
-}

ENDPROCESS

PURPOSE INTERFACE "Opvoeren nieuwe medewerker"
{+Vvoor een medewerker, die nog niet in de DTV-applicatie voorkomt,
dient een medewerker P\&O tenminste een salarisschaal en categorie in te vullen.
Daarnaast krijgt hij/zij de mogelijkheid om rechten van deze medewerkers in te vullen.
Het personeelsnummer wordt daarbij als identificerend kenmerk gebruikt, omdat dat in alle SPIN-berichten wordt meegeleverd.
-}
INTERFACE "Opvoeren nieuwe medewerker"
          ( persoon
          , personeelsnummer
          , begindatum[MedewerkerRecht*Datum]
          , einddatum[MedewerkerRecht*Datum]
          , recht
          ) FOR HR: I[Persoon]
BOX [ medewerker : personeelsnummer
    , rechten : persoon~
      BOX [ "geldig vanaf : " : begindatum
          , "geldig tot : " : einddatum
          , recht : recht
          ]
    ]

PURPOSE INTERFACE "Beheren rechten van medewerkers"
{+Deze interface geeft de medewerker P\&O de mogelijkheid een nieuwe medewerker op te voeren in DTV.
Voor een medewerker, die nog niet in de DTV-applicatie voorkomt, dient een medewerker P\&O tenminste een salarisschaal en categorie in te vullen.
Het personeelsnummer wordt daarbij als identificerend kenmerk gebruikt, omdat dat in alle SPIN-berichten wordt meegeleverd.

Deze interface geeft daarnaast de medewerker P\&O de mogelijkheid om rechten van medewerkers in te vullen en te beheren.
De DTV-applicatie is immers de plek waar dit soort rechten wordt opgeslagen.
Voordat een periode voor die medewerker verwerkt kan worden moet het personeelsnummer bekend zijn in DTV.
Ook moet in deze interface een of meerdere rechten toegekend worden en indien nodig aangepast voor een medewerker.

De salarisschaal en de categorie waarin de medewerker valt worden ook vastgelegd als rechten die toegekend kunnen worden.
Daarvoor onderscheidt de DTV-applicatie drie soorten recht: ``salarisschaal'', ``categorie'' en ``recht''.

De interface is dynamisch en toont alle rechten van een medewerker gebundeld per type.
Alleen de actueel geldende rechten kunnen worden gewijzigd.
Om de geschiedenis in stand te houden zijn rechten uit het verleden zijn alleen te raadplegen.
-}
INTERFACE "Beheren rechten van medewerkers"
          ( persoon , begindatum[MedewerkerRecht*Datum], einddatum[MedewerkerRecht*Datum], recht)
          FOR HR: V[ONE*SESSION];'_SESSION'
BOX [ "Op dit moment ingelogd" : sessionLoginnaam;loginnaam~
    , Medewerkers : sessionLoginnaam;loginnaam~;-I[Persoon]
      BOX [ "medewerker : " : I
            BOX [ naam : volledigenaam
                , personeelsnummer : personeelsnummer
                ]
          , "maak of wijzig recht" : persoon~
            BOX [ recht : recht
                , "geldig vanaf : " : begindatum
                , "geldig tot : " : einddatum
                ]
          ]
    ]

PURPOSE INTERFACE "Corrigeer rechten van medewerkers"
{+Deze interface is gelijk aan de interface Beheren Medewerkers en rechten,
met als verschil dat in deze interface wél de rechten in het verleden aangepast kunnen worden.

Deze interface mag alleen in zeer uitzonderlijke gevallen worden aangeroepen,
namelijk alleen wanneer er rechten uit het verleden gecorrigeerd moeten worden.
Daarom moet de interface ook voorzien worden van passende waarschuwingen.
Correcties in uitbetalingen, die voortvloeien uit dit soort correcties, worden niet door de DTV-applicatie naar P-Direkt verstuurd.
Deze mutaties moeten daarom met de hand in P-Direkt worden verwerkt.
-}
INTERFACE "Corrigeer rechten van medewerkers"
          ( persoon , begindatum[MedewerkerRecht*Datum], einddatum[MedewerkerRecht*Datum], recht)
          FOR HR: V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~;-I[Persoon]
BOX [ "medewerker : " : I
      BOX [ personeelsnummer : personeelsnummer
          , naam : volledigenaam
          ]
    , "maak of wijzig recht" : persoon~
      BOX [ recht : recht
          , "geldig vanaf : " : begindatum
          , "geldig tot : " : einddatum
          ]
    ]

INTERFACE "Overzicht rechten van medewerkers"
          FOR HR:V[ONE*Persoon] INTERFACE "Overzicht rechten"

INTERFACE "Overzicht rechten" : I[Persoon]
BOX [ "medewerker : " : I
      BOX [ personeelsnummer : personeelsnummer
          , naam : volledigenaam
          ]
    , rechten : persoon~
      BOX [ "1" : recht;rechtSoort;'salarisschaal';rechtSoort~;recht~/\I
            BOX [ "Salarisschaal : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          , "2" : recht;rechtSoort;'categorie';rechtSoort~;recht~/\I
            BOX [ "Categorie : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          , "3" : recht;rechtSoort;(-'categorie'/\-'salarisschaal');rechtSoort~;recht~/\I
            BOX [ "Overig : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          ]
    ]

POPULATION begindatum[MedewerkerRecht*Datum] CONTAINS
  [ ("mw1", "01-01-2014")
  ; ("mw3", "01-01-2014")
  ; ("mw6", "01-01-2014")
  ; ("mw7", "01-01-2014")
  ; ("mw9", "01-01-2014")
  ; ("mw10", "01-01-2014")
  ]

POPULATION recht CONTAINS
  [ ("mw1",  "salarisschaal3")
  ; ("mw3",  "categorie4")
  ; ("mw6",  "categorie3")
  ; ("mw7",  "salarisschaal3")
  ; ("mw9",  "categorie3")
  ; ("mw10", "salarisschaal3")
  ]

RULE salarisschaal : rechtSoort;'salarisschaal';rechtSoort~ /\ recht~;(begindatum;begindatum~ /\ persoon;persoon~);recht |- I[Recht]
MEANING "Eén medewerker heeft op elk willekeurig moment precies één salarisschaal."
MESSAGE "Eén medewerker mag niet meer dan één salarisschaal hebben!"
VIOLATION ( TXT "Deze persoon heeft tegelijkertijd de salarisschalen ", SRC I, TXT " en ", TGT I, TXT ".")

RULE     categorie :   rechtSoort;'categorie';rechtSoort~   /\ recht~;(begindatum;begindatum~ /\ persoon;persoon~);recht |- I[Recht]
MEANING "Eén medewerker is op enig moment in hooguit één categorie zijn ingedeeld."
MESSAGE "Eén medewerker mag niet in meer dan één categorie zijn ingedeeld!"
VIOLATION ( TXT "Deze persoon heeft tegelijkertijd de categorieën ", SRC I, TXT " en ", TGT I, TXT ".")


POPULATION persoon[MedewerkerRecht*Persoon] CONTAINS
  [ ("mw1",  "p1")
  ; ("mw3",  "p1")
  ; ("mw6",  "p2")
  ; ("mw7",  "p2")
  ; ("mw9",  "p3")
  ; ("mw10", "p3")
  ]

ENDCONTEXT