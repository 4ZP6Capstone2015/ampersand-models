CONTEXT DTV IN DUTCH LATEX

PATTERN Urenpools
PURPOSE PATTERN Urenpools
{+Dit thema beschrijft de tegoeden in uren van individuele medewerkers.
Een urentegoed is een aantal uren dat een medewerker tegoed heeft, bijvoorbeeld voor overwerk-verlof of persoonlijke meeruren.
De aanduidingen ``overwerk-verlof'' en ``persoonlijke meeruren'' worden urenpool genoemd,
omdat deze aanduidingen refereren naar een pool van uren waarover de medewerker kan beschikken.
De namen van de urenpools worden beheerd door een FAB in een referentielijst.

Het actuele saldo wordt bijgehouden in het systeem, voorzien van de datum van de laatste mutatie.
Om een saldo uit het verleden te berekenen, worden de toelageresultaten uit het verleden gebruikt voor zover zij mutaties bevatten van deze specifieke urenpool van deze medewerker.
Met deze toelageresultaten zijn saldi uit het verleden reconstrueerbaar.
Deze wijze van administreren maakt het mogelijk om toelageresultaten uit de verre geschiedenis na een zeker aantal jaren te kunnen vernietigen,
zonder de recente geschiedenis te veranderen.
-}
RELATION medewerker[Persoon*Urentegoed] [INJ,SUR]
MEANING "Urentegoeden zijn aan een persoon gekoppeld"
PURPOSE RELATION medewerker[Persoon*Urentegoed]
{+Om opgebouwde urentegoeden, zoals persoonlijke meeruren en overwerkverlof, te registreren en bij te werken,
wordt elk urentegoed aan één persoon gekoppeld.
-}

RELATION urenpool[Urentegoed*Urenpool] [UNI,TOT]
MEANING "Een urentegoed is van een bepaalde soort."
PURPOSE RELATION urenpool[Urentegoed*Urenpool]
{+Een urenpool is de benaming van een persoonsgebonden urentegoed. Voorbeelden zijn peroonlijke meeruren of overwerkverlof.
Om uren van en naar het juiste tegoed te kunnen boeken registreert de DTV-applicatie de poolsoort.
-}

RELATION tegoed[Urentegoed*AantalUren] [UNI,TOT]
MEANING "Een urenpool heeft een waarde (aantal uren)"
PURPOSE RELATION tegoed[Urentegoed*AantalUren]
{+In een urenpool wordt voor een persoon geregistreerd hoeveel uren van een bepaalde poolsoort die persoon heeft opgebouwd.
-}

RELATION vanaf[Urentegoed*Datum] [UNI,TOT]
MEANING "Van elke urenpool geldt op enig moment een tegoed in uren, waar de betreffende medewerker aanspraak op kan maken.."
PURPOSE RELATION vanaf[Urentegoed*Datum]
{+Om het saldo van een urenpool van een medewerker te administreren, wordt elke urensaldo gekoppeld aan de verantwoordingsperiode waarover het urensaldo is berekend.
Als gevolg daarvan is het geregistreerde saldo de waarde van de urenpool tot en met de einddatum van de betreffende verantwoordingsperiode.
Het saldo van de eerstvolgende dag wordt geregistreerd.
Daardoor is het urentegoed geldig vanaf deze geregistreerde datum.
-}

RULE inRefLijstUrentegoed : V[ONE*Urentegoed];urenpool |- V[ONE*Referentielijst];'Urenpools';refLijstUrenpools
MEANING "De urenpool in elk urentegoed dient in de referentielijst van urenpools voor te komen."
MESSAGE "Elke urenpool dient in de referentielijst van urenpools voor te komen:"
VIOLATION (TXT "Urenpool \"", TGT I, TXT "\" komt niet voor in de referentielijst.")

ENDPATTERN

INTERFACE "Overzicht urentegoeden" FOR "P&O": V[ONE*Persoon]
BOX [ "" : I[Persoon]
      BOX [ medewerker : personeelsnummer
          , naam : volledigenaam
          , "Soort urentegoed" : medewerker;urenpool
            BOX [ soort : I
                , overzicht : urenpool~
                  BOX [ medewerker : medewerker~;volledigenaam
                      , personeelsnummer : medewerker~;personeelsnummer
                      , "saldo" : tegoed
                      , vanaf : vanaf
                      ]
                ]
          ]
    ]

PURPOSE INTERFACE "Wijzig urentegoeden"
{+In deze interface kunnen urentegoeden geraadpleegd en gemuteerd worden.
De interface opent standaard met een zoekveld voor personeelsnummer.
Als de betreffende medewerker gevonden is wordt de naam van de medewerker erbij getoond en
wordt het tegoed van elke urenpool van deze gebruiker getoond.

In het veld Mutatie vult de gebruiker het aantal uren (positief of negatief) in waarmee het gekozen saldo gemuteerd dient te worden en de datum.
Na klikken op 'Muteer' wordt een toelageresultaat vastgelegd van de gekozen soort voor het aantal uren.
Het toelageresultaat wordt gekoppeld aan de periodeverantwoording waar de ingevulde datum in valt.
Als die periodeverantwoording nog niet bestaat wordt deze eerst aangemaakt. 
-}
INTERFACE "Wijzig urentegoeden" (urenpool, medewerker[Persoon*Urentegoed], tegoed, vanaf) FOR "P&O" : I[Persoon]
BOX [ medewerker : personeelsnummer
    , naam : volledigenaam
    , urentegoed : medewerker
      BOX [ urenpool : urenpool
          , "saldo" : tegoed
          , vanaf : vanaf
          ]
    ]

ENDCONTEXT