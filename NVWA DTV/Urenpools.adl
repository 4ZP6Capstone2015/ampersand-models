CONTEXT DTV IN DUTCH LATEX

PATTERN Urentegoeden
PURPOSE PATTERN Urentegoeden
{+Een urentegoed is een aantal uren dat een medewerker tegoed heeft,
bijvoorbeeld voor overwerk-verlof of persoonlijke meeruren.
De aanduidingen ``overwerk-verlof'' en ``persoonlijke meeruren'' duiden de soort van urentegoed aan en worden vermeld in het attribuut ``urenpool''.
Urentegoeden kunnen worden gewijzigd als resultaat van berekeningen van de kennismotor.
Voor het overige kunnen zij alleen worden gewijzigd door een medewerker P&O om specifieke correcties door te voeren.
Om hierop een audit trail te krijgen dienen alle mutaties op urentegoeden te worden gelogd door het systeem.

Het actuele saldo wordt bijgehouden in het systeem, voorzien van de datum van de laatste mutatie.
Om een saldo uit het verleden te kunnen berekenen worden de toelageresultaten uit het verleden bewaard.
Met deze toelageresultaten zijn saldi uit het verleden reconstrueerbaar,
door terug te rekenen vanaf het huidige saldo.
Deze wijze van administreren maakt het mogelijk om toelageresultaten uit de verre geschiedenis na een zeker aantal jaren te kunnen vernietigen,
zonder de recente geschiedenis te veranderen.
-}
RELATION medewerker[Persoon*Urentegoed] [INJ,SUR]
MEANING "Urentegoeden zijn aan een persoon gekoppeld"
PURPOSE RELATION medewerker[Persoon*Urentegoed]
{+Om opgebouwde urentegoeden, zoals persoonlijke meeruren en overwerkverlof, te registreren en bij te werken,
wordt elk urentegoed aan één persoon gekoppeld.
-}

RELATION urenpool[Urentegoed*Urenpool] [UNI,TOT]
MEANING "Een urentegoed is van een bepaalde soort."
PURPOSE RELATION urenpool[Urentegoed*Urenpool]
{+Een urenpool is de soortnaam van een urentegoed. Voorbeelden zijn ``peroonlijke meeruren'' of ``overwerkverlof''.
Om uren van en naar het juiste tegoed te kunnen boeken registreert de DTV-applicatie de urenpool waartoe het tegoed behoort.
(Het woord urenpool verwijst naar het gegeven dat het een pool van uren is waarover de medewerker kan beschikken.)
De namen van de urenpools worden beheerd door een FAB in een referentielijst.
-}

RELATION tegoed[Urentegoed*AantalUren] [UNI,TOT]
MEANING "Een urentegoed heeft een waarde (aantal uren)"
PURPOSE RELATION tegoed[Urentegoed*AantalUren]
{+In een urenpool wordt voor een persoon geregistreerd hoeveel uren van een bepaalde poolsoort die persoon heeft opgebouwd.
-}

RELATION vanaf[Urentegoed*Datum] [UNI,TOT]
MEANING "Van elke urenpool geldt op enig moment een tegoed in uren, waar de betreffende medewerker aanspraak op kan maken."
PURPOSE RELATION vanaf[Urentegoed*Datum]
{+Omdat een urentegoed van een medewerker te administreren verandert in de tijd, wordt elk urentegoed voorzien van een datum.
Het urentegoed geldt vanaf die datum op 0:00u.
-}

ENDPATTERN

INTERFACE "Overzicht urentegoeden" CLASS UF FOR "P&O": V[ONE*Persoon]
BOX [ "" : I[Persoon]
      BOX [ medewerker : personeelsnummer
          , naam : volledigenaam
          , "Soort urentegoed" : medewerker;urenpool
            BOX [ soort : I
                , overzicht : urenpool~
                  BOX [ medewerker : medewerker~;volledigenaam
                      , personeelsnummer : medewerker~;personeelsnummer
                      , "saldo" : tegoed
                      , vanaf : vanaf
                      ]
                ]
          ]
    ]

PURPOSE INTERFACE "Beheren urentegoeden"
{+In deze interface kan een P&O medewerker urentegoeden raadplegen en wijzigen.
De interface opent standaard met een zoekveld voor personeelsnummer.
Als de betreffende medewerker gevonden is wordt de naam van de medewerker erbij getoond en
wordt het tegoed van elke urenpool van deze gebruiker getoond.

Het personeelsnummer en de naam zijn vooraf ingevuld,
wanneer de gebruiker vanaf het “overzicht urentegoeden” naar dit scherm heeft genavigeerd.
In het scherm mag de gebruiker ook zelf personeelsnummer of naam invullen, en met de Zoek knop de gegevens corrigeren/aanvullen.
In deze interface worden ook de actuele urentegoeden getoond.
Wanneer de P&O medewerker een tegoed wil wijzigen, selecteert hij/zij het tegoed en voert de mutatie in.
 
-}
INTERFACE "Beheren urentegoeden" (urenpool, medewerker[Persoon*Urentegoed], tegoed, vanaf) FOR "P&O" : I[Persoon]
BOX [ medewerker : personeelsnummer
    , naam : volledigenaam
    , urentegoed : medewerker
      BOX [ urenpool : urenpool
          , "saldo" : tegoed
          , vanaf : vanaf
          ]
    ]

ENDCONTEXT