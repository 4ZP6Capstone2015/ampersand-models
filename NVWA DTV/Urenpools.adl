CONTEXT DTV IN DUTCH LATEX

PATTERN Urenpools
PURPOSE PATTERN Urenpools
{+In dit pattern worden alle zaken verzameld, die met het registreren van tijd te maken hebben.
-}
RELATION persoonUrentegoed[Persoon*Urentegoed] [INJ,SUR]
MEANING "Urenpools kunnen aan een persoon zijn gekoppeld"
PURPOSE RELATION persoonUrentegoed[Persoon*Urentegoed]
{+Om de persoonlijke meeruren en uren aan overwerkverlof die een persoon heeft opgebouwd te registreren en bij te werken, worden deze urenpools aan de persoon gekoppeld.
-}

RELATION urenpool[Urentegoed*Urenpool] [UNI,TOT]
MEANING "Een urenpool is van een bepaalde soort."
PURPOSE RELATION urenpool[Urentegoed*Urenpool]
{+Een urenpool kan een pool zijn van peroonlijke meeruren of overwerkverlof. Om dit onderscheid te kunnen maken wordt de poolsoort van een urenpool geregistreerd
-}

RELATION urenpoolSaldo[Urentegoed*AantalUren] [UNI,TOT]
MEANING "Een urenpool heeft een waarde (aantal uren)"
PURPOSE RELATION urenpoolSaldo[Urentegoed*AantalUren]
{+In een urenpool wordt voor een persoon geregistreerd hoeveel uren van een bepaalde poolsoort die persoon heeft opgebouwd.
-}

RELATION periode[Urentegoed*Verantwoordingsperiode] [UNI,TOT]
MEANING "Van elke urenpool geldt het saldo op de einddatum van de betreffende periode"
PURPOSE RELATION periode[Urentegoed*Verantwoordingsperiode]
{+Om het saldo van een urenpool van een medewerker te administreren, wordt elke urensaldo gekoppeld aan de verantwoordingsperiode waarover het urensaldo is berekend.
Als gevolg daarvan is het geregistreerde saldo de waarde van de urenpool tot en met de einddatum van de betreffende verantwoordingsperiode.
-}

ENDPATTERN

INTERFACE "Overzicht urentegoeden" FOR HR: V[ONE*Persoon]
BOX [ "" : I[Persoon]
      BOX [ medewerker : personeelsnummer
          , naam : volledigenaam
          , "Soort urentegoed" : persoonUrentegoed;urenpool
            BOX [ soort : I
                , overzicht : urenpool~
                  BOX [ medewerker : persoonUrentegoed~;volledigenaam
                      , personeelsnummer : persoonUrentegoed~;personeelsnummer
                      , "saldo" : urenpoolSaldo
                      , "tot en met" : periode;einddatum
                      ]
                ]
          ]
    ]

INTERFACE "Wijzig urentegoeden" (urenpool, persoonUrentegoed, urenpoolSaldo, periode) FOR HR : I[Persoon]
BOX [ medewerker : personeelsnummer
    , naam : volledigenaam
    , urentegoed : persoonUrentegoed
      BOX [ urenpool : urenpool
          , "saldo" : urenpoolSaldo
          , "tot en met" : periode
          ]
    ]

ENDCONTEXT