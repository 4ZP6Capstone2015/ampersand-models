CONTEXT DTV IN DUTCH LATEX

PATTERN PDirektBestanden
PURPOSE PATTERN PDirektBestanden
{+De resultaten van toelageberekeningen worden als bestand naar P-Direkt gestuurd.
Om die reden moet een bestand worden samengesteld.
Het is zaak om te garanderen dat elke verantwoorde tijdsperiode precies één keer naar P-Direkt wordt gestuurd,
omdat elk uur door P-Direkt zal worden uitbetaald.
-}
RELATION inBestand[Resultaat*PDirektBestand] [UNI]
MEANING "Elk toelagerecord wordt uiteindelijk in een bestand naar P-Direkt verstuurd. Dat bestand wordt aan het toelagerecord gekoppeld."

PURPOSE RELATION pDirektPeriode
{+Om te zorgen dat elk toelagerecord slechts eenmaal naar P-Direkt kan worden verstuurd,
wordt het bestand geregistreerd waarin dit toelagerecord naar P-Direkt is verstuurd.
-}
RELATION pDirektPeriode[PDirektBestand*Periode] [UNI,TOT]
MEANING "Van elk P-Direkt bestand is de verantwoordingsperiode bekend."

RULE "Resultaat in bestand" : inBestand = binnenTP;pDirektPeriode~
MEANING "Alle toelagerecords, die binnen de verantwoordingsperiode vallen, dienen in het P-Direkt bestand te worden opgenomen."

PURPOSE RELATION periodebestand
{+De periodeverantwoordingen van alle medewerkers worden verzameld in één bestand.
Om dubbele toekenning van toeslagen te voorkomen mag elke periodeverantwoording in slechts één bestand voorkomen.
-}
RELATION periodebestand[Periodeverantwoording*PDirektBestand] [UNI]
MEANING "Een P-Direkt bestand bevat de informatie uit een aantal periodeverantwoordingen."

RULE "periodebestanden" : verantwoordingsperiode;pDirektPeriode~ |- periodebestand
ENDPATTERN

POPULATION pDirektPeriode[PDirektBestand*Periode] CONTAINS
  [ ("bestand1", "p201409")
  ]

POPULATION inBestand[Resultaat*PDirektBestand] CONTAINS
  [ ("toel1", "bestand1")
  ; ("toel2", "bestand1")
  ; ("toel3", "bestand1")
  ; ("res2", "bestand1")
  ; ("res3", "bestand1")
  ; ("res5", "bestand1")
  ; ("res6", "bestand1")
  ; ("res7", "bestand1")
  ; ("res8", "bestand1")
  ; ("res9", "bestand1")
  ; ("res10", "bestand1")
  ; ("res11", "bestand1")
  ]

INTERFACE "P-Direkt periodebestand" : I[PDirektBestand]
BOX [ toelagerecords : periodebestand~;periodeResultaat~  -- Resultaat
      BOX [ Personeelsnummer : periodeResultaat;medewerkerPer;personeelsnummer
          , Code : code
          , "Datum_tot_met" : periodeResultaat;verantwoordingsperiode;einddatum
          , Uren : resultaatWaarde
          ]
    ]

ENDCONTEXT