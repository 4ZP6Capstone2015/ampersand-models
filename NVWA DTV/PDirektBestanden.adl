CONTEXT DTV IN DUTCH LATEX
INCLUDE "Resultaten.adl"

PATTERN PDirektBestanden
PURPOSE PATTERN PDirektBestanden
{+De resultaten van toelageberekeningen worden als bestand naar P-Direkt gestuurd.
Om die reden moet een bestand worden samengesteld.
Het is zaak om te garanderen dat elke verantwoorde tijdsperiode precies één keer naar P-Direkt wordt gestuurd,
omdat elk uur door P-Direkt zal worden uitbetaald.
-}

PURPOSE RELATION periodebestand
{+De periodeverantwoordingen van alle medewerkers worden verzameld in één bestand.
Om te kunnen nagaan in welk bestand een periodeverantwoording verstuurd is,
worden naam en locatie van het P-Direkt bestand bijgehouden in het veld “periodeBestand”.
Zolang een periodeverantwoording nog niet verwerkt is, blijft dit veld leeg.
-}
RELATION periodebestand[Periodeverantwoording*PDirektBestand] [UNI]
MEANING "Een P-Direkt bestand bevat de informatie uit een aantal periodeverantwoordingen."
CONCEPT PDirektBestand "Deze string bepaalt de locatie en naam van het P-Direkt bestand."

PURPOSE RULE "foutvrij P-Direkt bestand"
{+Fouten, die door de kennismotor worden gedetecteerd, moeten verholpen worden voordat een periodeverantwoording aan P-Direkt mag worden aangeboden.
-}
RULE "foutvrij P-Direkt bestand" : 'Fout';resultaatSoort~;perioderesultaat;res |- -(V[Resultaatsoort*PDirektBestand];periodebestand~)
MEANING "Alleen foutvrije periodeverantwoordingen mogen in P-Direkt worden verwerkt."
MESSAGE "Zolang er fouten in een toelageresultaat van een periodeverantwoording zitten, mag die periodeverantwoording niet in P-Direkt worden verwerkt."
VIOLATION (TGT I, TXT " bevat nog fouten.")

ENDPATTERN

PURPOSE INTERFACE "P-Direkt periodebestand"
{+Dit bericht wordt door de DTV-applicatie als bestand klaargezet, en bevat een aantal toelagerecords in het XML formaat van P-Direkt.

Dit bestand bevat alle toelageresultaten, die voor P-Direkt bestemd zijn. Dat wil zeggen: elk toelageresultaat uit de te verwerken periodeverantwoordingen,
waarvan de resultaatsoort 'P-Direktcode' is, wordt hierin meegenomen. Van deze toelageresultaten bevat het bericht:
\begin{enumerate}
\item	Personeelsnummer
\item	P-Direktcode
\item	De datum tot en met welke de verantwoording definitief is.
\item	Het aantal uren dat uitbetaald moet worden onder de genoemde P-Direktcode.
\end{enumerate}
Het personeelsnummer is van de medewerker over wie de periodeverantwoording gaat.
De P-Direktcode en het aantal uren komen uit het toelageresultaat.
De datum is de einddatum van de verantwoordingsperiode van de periodeverantwoording.
-}
INTERFACE "P-Direkt periodebestand" CLASS Message : I[PDirektBestand]
BOX [ toelagerecords : periodebestand~;res~;perioderesultaat~;(resultaatSoort;'P-Direktcode';V /\I[ToelageResultaat])  -- ToelageResultaat
      BOX [ Personeelsnummer : perioderesultaat;res[Perioderesultaat*Periodeverantwoording];afkomstigVan
          , Code : code
          , "Datum_tot_met" : perioderesultaat;res;verantwoordingsperiode;eind;date
          , Uren : resultaatWaarde
          ]
    ]


ENDCONTEXT