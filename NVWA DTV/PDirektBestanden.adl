CONTEXT DTV IN DUTCH LATEX

PATTERN PDirektBestanden
PURPOSE PATTERN PDirektBestanden
{+De resultaten van toelageberekeningen worden als bestand naar P-Direkt gestuurd.
Om die reden moet een bestand worden samengesteld.
Het is zaak om te garanderen dat elke verantwoorde tijdsperiode precies één keer naar P-Direkt wordt gestuurd,
omdat elk uur door P-Direkt zal worden uitbetaald.
-}
RELATION inBestand[ToelageResultaat*PDirektBestand] [UNI]
MEANING "Elk toelagerecord wordt uiteindelijk in een bestand naar P-Direkt verstuurd. Dat bestand wordt aan het toelagerecord gekoppeld."

PURPOSE RELATION periodebestand
{+De periodeverantwoordingen van alle medewerkers worden verzameld in één bestand.
Om te kunnen nagaan in welk bestand een periodeverantwoording verstuurd is,
worden naam en locatie van het P-Direkt bestand bijgehouden in het veld “periodeBestand”.
Zolang een periodeverantwoording nog niet verwerkt is, blijft dit veld leeg.
-}
RELATION periodebestand[Periodeverantwoording*PDirektBestand] [UNI]
MEANING "Een P-Direkt bestand bevat de informatie uit een aantal periodeverantwoordingen."

PURPOSE RELATION pDirektPeriode
{+Om te zorgen dat elk toelagerecord slechts eenmaal naar P-Direkt kan worden verstuurd,
wordt het bestand geregistreerd waarin dit toelagerecord naar P-Direkt is verstuurd.
-}
RELATION pDirektPeriode[PDirektBestand*Verantwoordingsperiode] [UNI,TOT]
MEANING "Van elk P-Direkt bestand is de verantwoordingsperiode bekend."

RULE "periodebestanden" : verantwoordingsperiode;pDirektPeriode~ |- periodebestand

ENDPATTERN

POPULATION pDirektPeriode[PDirektBestand*Verantwoordingsperiode] CONTAINS
  [ ("bestand1", "p201409")
  ]

POPULATION inBestand[ToelageResultaat*PDirektBestand] CONTAINS
  [ ("toel1", "bestand1")
  ; ("toel2", "bestand1")
  ; ("toel3", "bestand1")
  ; ("res2", "bestand1")
  ; ("res3", "bestand1")
  ; ("res5", "bestand1")
  ; ("res6", "bestand1")
  ; ("res7", "bestand1")
  ; ("res8", "bestand1")
  ; ("res9", "bestand1")
  ; ("res10", "bestand1")
  ; ("res11", "bestand1")
  ]

INTERFACE "P-Direkt periodebestand" CLASS Message : I[PDirektBestand]
BOX [ toelagerecords : periodebestand~;periodeResultaat~  -- ToelageResultaat
      BOX [ Personeelsnummer : periodeResultaat;medewerkerPer;personeelsnummer
          , Code : code
          , "Datum_tot_met" : periodeResultaat;verantwoordingsperiode;einddatum
          , Uren : resultaatWaarde
          ]
    ]

POPULATION periodebestand[Periodeverantwoording*PDirektBestand] CONTAINS
  [ ("pv13", "bestand1")
  ; ("pv14", "bestand1")
  ; ("pv15", "bestand1")
  ]

ENDCONTEXT