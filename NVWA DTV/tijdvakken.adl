CONTEXT DTV IN DUTCH LATEX

PATTERN Tijdvakken
PURPOSE PATTERN Tijdvakken REF "Artikel 6 Circulaire Invoering 36-urige werkweek"
{+Per 1 januari 1997 komt een ambtenaar in aanmerking voor een overwerkvergoeding,
indien hij op een werkdag tenminste 1 uur in opdracht van het bevoegd gezag overwerk heeft verricht, op grond van Artikel 6 Circulaire Invoering 36-urige werkweek
Aan de ambtenaar, die anders dan bij wijze van overwerk,
regelmatig of vrij regelmatig arbeid verricht op andere tijden dan op de dagen maandag tot en met vrijdag tussen 8 en 18 uur,
wordt een toelage toegekend op grond van Artikel 17 BBRA.
-}
PURPOSE CONCEPT Uurtijdvak REF "Artikel 6 Circulaire Invoering 36-urige werkweek"
{+Om de overwerkvergoeding en vergoeding onregelmatige diensten te kunnen voorzien van de juiste percentages,
kijken we naar de Circulaire Invoering 36-urige werkweek.
We voeren het begrip ``uurtijdvak'' in om geregistreerde uren te kunnen vertalen naar overwerk en onregelmatige dienst.
Elk moment van de dag valt en één uurtijdvak, en voor elk uurtijdvak geldt één percentage overwerkvergoeding en één percentage toelage onregelmatige dienst.
Op die manier kan voor elk moment de juiste overwerkvergoeding en toelage onregelmatige dienst worden berekend.
Bijvoorbeeld: het uurtijdvak w3 duidt een weekdag (ma-vr) aan van 8:00u t/m 18:00u.
Voor dit uurtijdvak geldt een overwerkpercentage van 0\%.
Op vergelijkbare wijze kan voor elk ander moment van de week een uurtijdvak worden bepaald en het juiste percentage worden berekend.
-}

CONCEPT Dagsoort "We onderscheiden de dagsoorten: ma-vr, za-zo, feestdag."
PURPOSE CONCEPT Dagsoort
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toeslagpercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}



RELATION overwerk[Uurtijdvak*Uurtijdvak] [PROP]
MEANING "Onder overwerk wordt verstaan arbeid buiten de werktijden geldende voor de ambtenaar krachtens de WTR, voor zover daardoor het per werkperiode vastgestelde aantal arbeidsuren wordt overschreden."
PURPOSE RELATION overwerk[Uurtijdvak*Uurtijdvak] REF "BBRA Artikel 23"
{+Of een uurtijdvak als overwerk geldt, wordt beschreven in de BBRA. Dit wordt als eigenschap van een uurtijdvak geregistreerd.
Voor de berekening is van belang dat overwerk alleen wordt toegekend aan de ambtenaar, voor wie een salarisschaal geldt met een lager maximumsalaris dan dat van schaal 11 en die in opdracht van het bevoegde gezag overwerk verricht.
Voor overwerk dat gedurende korter dan een uur aansluitend aan de vastgestelde dagelijkse werktijd wordt verricht, wordt geen vergoeding toegekend.
De werkperiode, waarvan in de definitie van overwerk sprake is, wordt gesteld op één dag, indien aanvang en einde van de werktijd in de regel niet aan wisselingen onderhevig zijn.
Indien de tijdstippen van aanvang en einde van de werktijd volgens een tevoren vastgesteld rooster wisselen, wordt deze werkperiode gesteld op een tijdvak van tenminste zeven dagen.
-}
CONCEPT Uurtijdvak "Een uurtijdvak is een aaneengesloten periode van tijd waarbinnen één percentage geldt voor de vergoeding van overwerk en één percentage voor de vergoeding van onregelmatige dienst."
VIEW Uurtijdvak : Uurtijdvak(begintijdU,TXT "-", eindtijdU,TXT " op ", dagSoort)
RELATION dagSoort[Uurtijdvak*Dagsoort] [UNI,TOT] PRAGMA "" " valt op dagsoort "
MEANING "Een uurtijdvak valt op een dagsoort."
--PURPOSE RELATION dagSoort[Uurtijdvak*Dagsoort] REF "BBRA Artikel 17 lid 2"

RELATION pCodeOverwerk[Uurtijdvak*PDirektCode]
RELATION grondslagOverwerk[Uurtijdvak*Wetsartikel]
RELATION overwerkpercentage[Uurtijdvak*Percentage] [UNI,TOT] PRAGMA "Voor uurtijdvak " " geldt het overwerkpercentage "
MEANING "Het overwerkpercentage is gekoppeld aan uurtijdvakken."
PURPOSE RELATION overwerkpercentage[Uurtijdvak*Percentage] REF "Artikel 6.1 Circulaire Invoering 36-urige werkweek"
{+
Het overwerkpercentage hangt af van het uurtijdvak waarbinnen deze arbeid is verricht.
Om het juiste percentage te kunnen berekenen is het rooster verdeeld in uurtijdvakken cf. de tabel in Artikel 6.1 Circulaire Invoering 36-urige werkweek
-}

RELATION pCodeTOD[Uurtijdvak*PDirektCode]
RELATION grondslagTOD[Uurtijdvak*Wetsartikel]
RELATION onregelmatigheidspercentage[Uurtijdvak*Percentage] [UNI,TOT] PRAGMA "Voor uurtijdvak " " geldt het onregelmatigheidspercentage "
MEANING "Het onregelmatigheidspercentage is gekoppeld aan uurtijdvakken."
PURPOSE RELATION onregelmatigheidspercentage[Uurtijdvak*Percentage] REF "Artikel 17 lid 2 BBRA"
{+
Het percentage dat gebruikt wordt bij de berekening van de toeslag onregelmatige diensten hangt af van het uurtijdvak waarbinnen deze arbeid is verricht.
-}

RELATION uurtijdvak[TijdvakResultaat*Uurtijdvak] [UNI,TOT] PRAGMA "" " valt binnen "
MEANING "Van elk tijdvakresultaat worden uurtijdvakken berekend, waar dat tijdvakresultaat binnen valt"
PURPOSE RELATION uurtijdvak[TijdvakResultaat*Uurtijdvak]
{+Wanneer een tijdregistratie binnen verschillende uurtijdvakken valt dient er splitsing in tijdvakresultaten plaats te vinden.
Het resultaat is dat elke tijdsleuf in precies één uurtijdvak valt.
Op die manier kan de juiste toelage worden berekend.
-}
RELATION bedrijfstijd[Uurtijdvak*Uurtijdvak] [PROP]
MEANING "Voor elke tijdsleuf is eenduidig bepaald of het bedrijfstijd betreft of niet."
PURPOSE RELATION bedrijfstijd[Uurtijdvak*Uurtijdvak] REF "WTR Artikel 2 lid 1"
{+De bedrijfstijd is vastgesteld van 08.00 uur tot 18.00 uur op maandag tot en met vrijdag.
-}
RELATION begintijdU[Uurtijdvak*Tijdstip] [UNI,TOT] PRAGMA "" " heeft " " als begintijd"
RELATION eindtijdU[Uurtijdvak*Tijdstip] [UNI,TOT] PRAGMA "" " heeft " " als eindtijd"
ENDPATTERN

POPULATION dagSoort[Uurtijdvak*Dagsoort] CONTAINS
  [ ("w1", "ma-vr")
  ; ("w2", "ma-vr")
  ; ("w3", "ma-vr")
  ; ("w4", "ma-vr")
  ; ("w5", "ma-vr")
  ; ("z1", "za-zo")
  ; ("z2", "za-zo")
  ; ("z3", "za-zo")
  ; ("f1", "feestdag")
  ; ("f2", "feestdag")
  ; ("f3", "feestdag")
  ]

POPULATION begintijdU[Uurtijdvak*Tijdstip] CONTAINS
  [ ("w1", "0:00")
  ; ("w2", "6:00")
  ; ("w3", "8:00")
  ; ("w4", "18:00")
  ; ("w5", "22:00")
  ; ("z1", "0:00")
  ; ("z2", "6:00")
  ; ("z3", "22:00")
  ; ("f1", "0:00")
  ; ("f2", "6:00")
  ; ("f3", "22:00")
  ]

POPULATION bedrijfstijd[Uurtijdvak*Uurtijdvak] CONTAINS
  [ ("w3", "w3") ]

POPULATION eindtijdU[Uurtijdvak*Tijdstip] CONTAINS
  [ ("w1", "6:00")
  ; ("w2", "8:00")
  ; ("w3", "18:00")
  ; ("w4", "22:00")
  ; ("w5", "0:00")
  ; ("z1", "6:00")
  ; ("z2", "22:00")
  ; ("z3", "0:00")
  ; ("f1", "6:00")
  ; ("f2", "22:00")
  ; ("f3", "0:00")
  ]

POPULATION overwerkpercentage[Uurtijdvak*Percentage] CONTAINS --  REF "Artikel 6 Circulaire Invoering 36-urige werkweek"
  [ ("w1",  "50")  --  tussen  0:00u en  6:00u
  ; ("w2",  "25")  --  tussen  6:00u en  8:00u
  ; ("w3",  "25")  --  tussen  8:00u en 18:00u
  ; ("w4",  "25")  --  tussen 18:00u en 22:00u
  ; ("w5",  "50")  --  tussen 22:00u en 24:00u
  ; ("z1", "100")  --  tussen  0:00u en  6:00u
  ; ("z2",  "50")  --  tussen  6:00u en 22:00u
  ; ("z3", "100")  --  tussen 22:00u en 24:00u
  ; ("f1", "100")  --  tussen  0:00u en  6:00u
  ; ("f2",  "50")  --  tussen  6:00u en 22:00u
  ; ("f3", "100")  --  tussen 22:00u en 24:00u
  ]

POPULATION pCodeOverwerk[Uurtijdvak*PDirektCode] CONTAINS
  [ ("w1", "8411")
  ; ("w2", "8401")
  ; ("w3", "8401")
  ; ("w4", "8401")
  ; ("w5", "8411")
  ; ("z1", "8421")
  ; ("z2", "8411")
  ; ("z3", "8421")
  ; ("f1", "8421")
  ; ("f2", "8411")
  ; ("f3", "8421")
  ]

POPULATION grondslagOverwerk[Uurtijdvak*Wetsartikel] CONTAINS
  [ ("w1", "BBRA Artikel 23 lid 8 sub a")
  ; ("w2", "BBRA Artikel 23 lid 8 sub a")
  ; ("w3", "BBRA Artikel 23 lid 8 sub a")
  ; ("w4", "BBRA Artikel 23 lid 8 sub a")
  ; ("w5", "BBRA Artikel 23 lid 8 sub a")
  ; ("z1", "BBRA Artikel 23 lid 8 sub a")
  ; ("z2", "BBRA Artikel 23 lid 8 sub a")
  ; ("z3", "BBRA Artikel 23 lid 8 sub a")
  ; ("f1", "BBRA Artikel 23 lid 8 sub b")
  ; ("f2", "BBRA Artikel 23 lid 8 sub b")
  ; ("f3", "BBRA Artikel 23 lid 8 sub b")
  ]

POPULATION onregelmatigheidspercentage[Uurtijdvak*Percentage] CONTAINS --  REF "Artikel 17 lid 2 BBRA"
  [ ("w1",  "40")   --  tussen  0:00u en  6:00u (BBRA Artikel 17 lid 2 sub b)
  ; ("w2",  "20")   --  tussen  6:00u en  8:00u (BBRA Artikel 17 lid 2 sub a)
  ; ("w3",   "0")   --  tussen  8:00u en 18:00u
  ; ("w4",  "20")   --  tussen 18:00u en 22:00u (BBRA Artikel 17 lid 2 sub a)
  ; ("w5",  "40")   --  tussen 22:00u en 24:00u (BBRA Artikel 17 lid 2 sub b)
  ; ("z1",  "70")   --  tussen  0:00u en  6:00u (BBRA Artikel 17 lid 2 sub c en d)
  ; ("z2",  "70")   --  tussen  6:00u en 22:00u (BBRA Artikel 17 lid 2 sub c en d)
  ; ("z3",  "70")   --  tussen 22:00u en 24:00u (BBRA Artikel 17 lid 2 sub c en d)
  ; ("f1", "100")   --  tussen  0:00u en  6:00u (BBRA Artikel 17 lid 2 sub e)
  ; ("f2", "100")   --  tussen  6:00u en 22:00u (BBRA Artikel 17 lid 2 sub e)
  ; ("f3", "100")   --  tussen 22:00u en 24:00u (BBRA Artikel 17 lid 2 sub e)
  ]

POPULATION pCodeTOD[Uurtijdvak*PDirektCode] CONTAINS
  [ ("w1", "8361")
  ; ("w2", "8341")
  ; ("w4", "8341")
  ; ("w5", "8361")
  ; ("z1", "8381")
  ; ("z2", "8381")
  ; ("z3", "8381")
  ; ("f1", "8391")
  ; ("f2", "8391")
  ; ("f3", "8391")
  ]

POPULATION grondslagTOD[Uurtijdvak*Wetsartikel] CONTAINS
  [ ("w1", "BBRA Artikel 17 lid 2 sub b")
  ; ("w2", "BBRA Artikel 17 lid 2 sub a")
  ; ("w4", "BBRA Artikel 17 lid 2 sub a")
  ; ("w5", "BBRA Artikel 17 lid 2 sub b")
  ; ("z1", "BBRA Artikel 17 lid 2 sub c en d")
  ; ("z2", "BBRA Artikel 17 lid 2 sub c en d")
  ; ("z3", "BBRA Artikel 17 lid 2 sub c en d")
  ; ("f1", "BBRA Artikel 17 lid 2 sub e")
  ; ("f2", "BBRA Artikel 17 lid 2 sub e")
  ; ("f3", "BBRA Artikel 17 lid 2 sub e")
  ]

{- zon- en feestdagen (Artikel 21, lid 7 sub a)
Geen dienst wordt geëist op zondagen, alsmede op de Nieuwjaarsdag, de Tweede Paasdag, de Hemelvaartsdag, de Tweede Pinksterdag, de beide Kerstdagen, de dag, waarop de verjaardag van de Koning wordt gevierd en op 5 mei.
-}

{-
RELATION pCodeUurtijdvak[Uurtijdvak*PDirektCode] PRAGMA "" " heeft " " als code voor P-Direkt"
MEANING "Aan elke toegestane combinatie van uurtijdvak en toelagecategorie is een viercijferige code in P-direkt gekoppeld."
PURPOSE RELATION pCodeUurtijdvak[Uurtijdvak*PDirektCode]
{+Om de juiste code te kunnen berekenen in een P-direktbericht, worden 4-cijferige codes gebruikt.
--
POPULATION pCodeUurtijdvak[Uurtijdvak*PDirektCode] CONTAINS
  [-- ("", "8301")  --Beschikbaarheidsdienst, Consignatie 5%
--  ; ("", "8311")  --Beschikbaarheidsdienst, Consignatie 10%
--  ; ("", "8321")  --Beschikbaarheidsdienst, Consignatie 20 %
    ("w1", "8411")  --Overwerk 50 %
  ; ("w2", "8401")  --Overwerk 25 %
  ; ("w3", "8401")  --Overwerk 25 %
  ; ("w4", "8401")  --Overwerk 25 %
  ; ("w5", "8411")  --Overwerk 50 %
  ; ("w5", "8411")  --Overwerk 50 %
  ; ("z1", "8421")  --Overwerk 100 %
  ; ("z2", "8411")  --Overwerk 50 %
  ; ("z3", "8421")  --Overwerk 100 %
  ; ("f1", "8421")  --Overwerk 100 %
  ; ("f2", "8411")  --Overwerk 50 %
  ; ("f3", "8421")  --Overwerk 100 %
--  ; ("", "8431")  --Overwerk 125 %
--  ; ("", "8441")  --Overwerk 150 %
--  ; ("", "8451")  --Overwerk 200 %
--  ; ("", "8K61")  --Twee ploegendienst LNV
--  ; ("", "0235")  --Banduren - Bel. Bezw. Omstandigheden
  ; ("w2", "8341")  --Onregelmatige dienst 20 %
  ; ("w4", "8341")  --Onregelmatige dienst 20 %
--  ; ("", "8351")  --Onregelmatige dienst 25 %
  ; ("w1", "8361")  --Onregelmatige dienst 40 %
  ; ("w5", "8361")  --Onregelmatige dienst 40 %
--  ; ("", "8371")  --Onregelmatige dienst 45 %
  ; ("z1", "8381")  --Onregelmatige dienst 70 %
  ; ("z2", "8381")  --Onregelmatige dienst 70 %
  ; ("z3", "8381")  --Onregelmatige dienst 70 %
  ; ("f1", "8391")  --Onregelmatige dienst 100 %
  ; ("f2", "8391")  --Onregelmatige dienst 100 %
  ; ("f3", "8391")  --Onregelmatige dienst 100 %
--  ; ("", "0301")  --Nominale toeslag ord.
--  ; ("", "8331")  --Verschoven uren - Verschoven dienst 45%
  ]
-}

POPULATION begindatumP[Betaalperiode*Datum] CONTAINS
  [ ("p201401", "01-01-2014")
  ; ("p201402", "29-01-2014")
  ; ("p201403", "26-02-2014")
  ; ("p201404", "26-03-2014")
  ; ("p201411", "27-08-2014")
  ]

POPULATION einddatumP[Betaalperiode*Datum] CONTAINS
  [ ("p201401", "28-01-2014")
  ; ("p201402", "25-02-2014")
  ; ("p201403", "25-03-2014")
  ; ("p201404", "22-04-2014")
  ; ("p201411", "24-09-2014")
  ]

PATTERN Betaalperiodes
RELATION begindatumP[Betaalperiode*Datum] PRAGMA "betaalperiode " " begint op "
MEANING "De begindatum van een betaalperiode wordt geregistreerd."
PURPOSE RELATION begindatumP[Betaalperiode*Datum] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Salarissen en toeslagen worden berekend en verwerkt over een betaalperiode.
-}
RELATION einddatumP[Betaalperiode*Datum] PRAGMA "betaalperiode " " eindigt op "
MEANING "De einddatum van een betaalperiode wordt geregistreerd."
PURPOSE RELATION einddatumP[Betaalperiode*Datum] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om te weten binnen welke betaalperiode werkzaamheden zijn uitgevoerd, moet worden nagegaan wanneer een betaalperiode eindigt. 
-}
PURPOSE CONCEPT DatumTijd
{+In sommige gevallen worden dag en datum los weergegeven, en in andere gevallen in combinatie.
Om die reden gebruikt DTV het concept datumtijd.
-}
CONCEPT DatumTijd "Een datumtijd is de combinatie van datum en tijdstip, waarmee een tijdstip volledig kan worden weergegeven."

RELATION date[DatumTijd*Datum] PRAGMA "" " heeft als datum "
MEANING "Een DatumTijd heeft een datum"
PURPOSE RELATION date[DatumTijd*Datum]
{+
-}
RELATION time[DatumTijd*Tijdstip] PRAGMA "" " heeft als tijdstip "
MEANING "Een DatumTijd heeft een tijdstip"
PURPOSE RELATION time[DatumTijd*Tijdstip]
{+
-}



-- Welke transacties tussen Tijdverantwoording en Toezichtuitvoering?
-- Toezichtuitvoering is de leverancier is van gegevens met betrekking tot productieuren.


-- Welke transacties tussen Tijdverantwoording en Personeelsadministratie?
-- Personeelsadministratie is de leverancier van gegevens met betrekking tot medewerkers en organisatie inrichting.


-- Welke transacties tussen Tijdverantwoording en Salarisadministratie?
-- Salarisadministratie is afnemer van gegevens voor het correct toepassen van toeslagen


-- Welke transacties tussen Tijdverantwoording en Operationele sturing/planning?
-- Operationele sturing/planning is een mogelijke leverancier van gegevens met betrekking tot roosters.


-- interface naar FaTijDec
-- binnen FaTijDec wordt voor een deel van de medewerkers het proces van tijd registreren en - verwerken ondersteund. Het is o.a. ook de gegevensleverancier voor P-Direkt.


-- interface naar SPIN Productie (Verificatie, Controle, Interventie)
-- SPIN Productie is een applicatie waarmee een deel van de toezichtuitvoering wordt ondersteund en belangrijkste leverancier van gegevens met betrekking tot productie-uren.


-- interface vanuit SPIN Tijdregistratie
-- SPIN Tijdregistratie is een module waarmee een groot deel van de organisatie op dit moment tijd registreert.


-- interface vanuit P-Direkt
-- P-Direkt is de leverancier van gegevens omtrent medewerkers en organisatie inrichting. Tevens is het de afnemer van gegevens met betrekking tot uren en toeslagpercentages.


-- interface vanuit Rostar CAS
-- Rostar CAS is een applicatie waarmee planning wordt ondersteund en mogelijk leverancier van gegevens met betrekking tot roosters. 


-- interface naar Oracle eBS
-- Oracle eBS is een applicatie waarmee de financiële administratie wordt ondersteund en afnemer van gegevens als 'reversed billing' voor partitioners wordt geïntroduceerd.


-- interface naar Dashboard Centraal (OBIEE)
-- Dashboard Centraal is een management rapportage omgeving die onder andere inzicht geeft en geplande en gerealiseerde uren. Een wijziging het gegevensmodel voor tijdregistratie en tijdverwerking heeft impact op de rapportages.

PURPOSE CONCEPT Tijdregistratie REF "Contextanalyse 'Duurzaam Tijdverantwoorden' versie 0.3"
{+Op de dagverantwoording c.q. periodeverantwoording verantwoordt een medewerker één afzonderlijke activiteit op één regel.
Daarvoor wordt het concept Tijdregistratie gebruikt.
Dit komt overeen met het concept dagverantwoordingRegel in de IA en SPN\_TIJDVERANTWOORDING in het SPIN gegevensmodel.
-}
CONCEPT Tijdregistratie "Een tijdregistratie is een gebeurtenis, waarin een aaneengesloten hoeveelheid tijd van een medewerker wordt geregistreerd ten behoeve van verantwoording, salarisberekening en/of facturatie."
-- 
RELATION activiteit[Tijdregistratie*Activiteit] [UNI,TOT] PRAGMA "" " heeft als activiteit " "" -- Het concept Activiteit komt overeen met tijdRubriek in de IA. Daarvan bestaat een referentielijst
MEANING "De activiteit van elke tijdverantwoording wordt geregistreerd"
PURPOSE RELATION activiteit[Tijdregistratie*Activiteit] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Medewerkers kunnen alleen uren registreren op activiteiten waar ze voor zijn geautoriseerd. Daarom moet de activiteit binnen een tijdregistratie bekend zijn.
-}
RELATION cat[Activiteit*TijdRubriekCategorie] [UNI] PRAGMA "" " valt binnen de categorie "
MEANING "Een activiteit valt binnen een categorie."
PURPOSE RELATION cat[Activiteit*TijdRubriekCategorie] REF "Business Case - Project Duurzaam Tijdverantwoorden versie 0.9"
{+Op basis van Uurtijdvak, Categorie en Rooster wordt bepaald aan welke Uurgroep de tijd wordt toegekend
-}

RELATION project[Tijdregistratie*Project] [UNI] PRAGMA "" " heeft betrekking op het project "
MEANING "Een tijdverantwoording heeft betrekking op een project"
PURPOSE RELATION project[Tijdregistratie*Project] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een medewerker wordt geautoriseerd om uren te mogen
schrijven op een bepaalde combinatie van: - Activiteit,- Project,- Kostenplaats
-}
RELATION kostenplaats[Tijdregistratie*Kostenplaats] [UNI] PRAGMA "" " heeft als kostenplaats "
MEANING "Een tijdverantwoording heeft betrekking op een kostenplaats."

RELATION dagS[Tijdregistratie*Dagverantwoording]  PRAGMA "" " is onderdeel van dagverantwoording " "" --  [UNI,TOT]
MEANING "Een tijdverantwoording maakt deel uit van een dagverantwoording."
PURPOSE RELATION dagS[Tijdregistratie*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een tijdregistratie bevat een aaneengesloten hoeveelheid tijd op een bepaalde dag.
-}



RELATION bron[TijdvakResultaat*Tijdregistratie] [UNI,TOT]
MEANING "Voor elk TijdvakResultaat wordt één Tijdregistratie als bron geregistreerd."
PURPOSE RELATION bron[TijdvakResultaat*Tijdregistratie] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Op een tijdregistratie mag een inspecteur in principe een willekeurig tijdvak aangeven, ook wanneer dat tijdvak over meerdere uurtijdvakken en/of roostersoorten loopt.
Om die reden splitst het systeem een tijdregistratie op in tijdvakresultaten.
Deze opsplitsing zorgt dat elke tijdvakresultaat binnen precies één uurTijdvak en precies één roosterSoort valt.
Daarom is er een relatie nodig om bij te houden welke Tijdregistratie bij een tijdvakresultaat hoort.
-}
RELATION tijdvak[TijdvakResultaat*UurTijdvak] [UNI,TOT] PRAGMA "" " heeft als uurtijdvak "
MEANING "Bij elk tijdvakresultaat bepaalt het systeem het uurtijdvak waarbinnen dit tijdvakresultaat valt."
RELATION roosterSoortT[TijdvakResultaat*RoosterSoort] [UNI,TOT] PRAGMA "" "valt binnen een rooster van de soort"
MEANING "Bij elk tijdvakresultaat bepaalt het systeem de roostersoort waarbinnen dit tijdvakresultaat valt."
RELATION categorie[TijdvakResultaat*TijdRubriekCategorie] [UNI,TOT] PRAGMA "" "heeft als categorie"
MEANING "Elk TijdvakResultaat heeft betrekking op een categorie."
PURPOSE RELATION categorie[TijdvakResultaat*TijdRubriekCategorie]
{+
-}
RELATION uurgroep[TijdvakResultaat*Uurgroep] [UNI,TOT] PRAGMA "" " heeft als uurgroep "
MEANING "Een TijdvakResultaat heeft betrekking op een uurgroep"
PURPOSE RELATION uurgroep[TijdvakResultaat*Uurgroep] 
{+Op basis van Uurtijdvak, Categorie en Rooster wordt bepaalt aan welke Uurgroep de tijd wordt toegekend
-}
VIEW Dagverantwoording : Dagverantwoording(TXT "DV:", datum)
RELATION medewerkerD[Dagverantwoording*Persoon] PRAGMA "" " is een dagverantwoording van " "" --[UNI,TOT] 
MEANING "Elke dagverantwoording hoort bij 1 persoon."
PURPOSE RELATION medewerkerD[Dagverantwoording*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Er wordt per medewerker een dagverantwoording aangemaakt om de tijd te verantwoorden.
-}
RELATION datum[Dagverantwoording*Datum] [UNI,TOT] PRAGMA "" " is de verantwoording van dag "
MEANING "Elke dagverantwoording gaat over een dag"
PURPOSE RELATION datum[Dagverantwoording*Datum]  REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Iedere medewerker moet per dag de (modaliteit) tijd verantwoorden.
-}
RELATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus] [UNI] PRAGMA "" " heeft de status "
MEANING "De status van een dagverantwoording wordt geregistreerd"
PURPOSE RELATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus]
{+Op basis van de status van een dagverantwoording wordt deze of aangeboden ter verwerking (goedgekeurd), of ter correctie opnieuw aangeboden aan de werknemer (retour). Daarom moet de status van een dagverantwoording bekend zijn.
-}
RELATION toelichting[Dagverantwoording*Tekst] [UNI] PRAGMA "" " heeft bij registratie als toelichting meegekregen "
MEANING "Een medewerker kan een toelichting toevoegen bij het registreren van een dagverantwoording."
PURPOSE RELATION toelichting[Dagverantwoording*Tekst]
{+Een medewerker kan aan een dagverantwoording een toelichting toevoegen voordat deze wordt aangeboden.
-}
RELATION opmerkingValidatie[Dagverantwoording*Tekst] [UNI] PRAGMA "" " heeft bij validatie \"" "\" als opmerking meegekregen"
MEANING "Een leidinggevende kan bij het valideren van  een dagverantwoording een opmerking toevoegen."
PURPOSE RELATION opmerkingValidatie[Dagverantwoording*Tekst]
{+Een leidinggevende kan een opmerking toevoegen tijdens het valideren van een dagverantwoording.
-}
RELATION resultaat[Dagverantwoording*Resultaat] 
MEANING "Het verwerken van een dagverantwoording lijdt tot een resultaat."
PURPOSE RELATION resultaat[Dagverantwoording*Resultaat]
{+Elke dagverantwoording wordt uiteindelijk verwerkt en het resultaat van die verwerking wordt vastgelegd.
-}

RELATION resultaatUurgroep[Resultaat*Uurgroep]
MEANING "Het resultaat van een verwerkte dagverantwoording heeft een uurgroep (waar een aantal uren aan wordt toegekend) als onderdeel."
PURPOSE RELATION resultaatUurgroep[Resultaat*Uurgroep]
{+Als een dagverantwoording is verwerkt, resulteert dit in het toekennen van een bepaald aantal uren aan een bepaalde uurgroep.
-}

RELATION resultaatWaarde[Resultaat*Uren]
MEANING "Het resultaat van een verwerkte dagverantwoording heeft een aantal uren (die aan een bepaalde uurgroep worden toegekend) als onderdeel."
PURPOSE RELATION resultaatWaarde[Resultaat*Uren]
{+
-}


--RELATION uurgroepInPeriode[Betaalperiode*Uurgroep] PRAGMA "betaalperiode " " heeft als onderdeel uurgroep "
--MEANING "Elke uurgroep valt binnen een bepaalde betaalperiode."
--PURPOSE RELATION uurgroepInPeriode[Betaalperiode*Uurgroep] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
-- {+Voorafgaand aan de uitvoering van betaalperiode worden er per uurGroep totalen gemaakt
-- -}
RELATION urenInUurgroep[Uren*Uurgroep] PRAGMA "" " uren zijn onderdeel van uurgroep "
MEANING "Een uurgroep bestaat uit een aantal uren."
PURPOSE RELATION urenInUurgroep[Uren*Uurgroep] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De tijd die binnen het rooster of binnen een dagverantwoording is geregistreerd kan worden toegekend aan
een uurGroep
-}

-- RELATION betaalperiodePersoon[Persoon*Betaalperiode] PRAGMA "" " is de betreffende medewerker voor betaalperiode "
-- MEANING "Een betaalperiode geldt voor een persoon."
-- PURPOSE RELATION betaalperiodePersoon[Persoon*Betaalperiode]
-- {+De geregistreerde uren van een medewerker worden per betaalperiode verwerkt. Het totaal van een betaalperiode bevat dus de te verwerken uren van een bepaalde persoon.
-- -}

-- RELATION wezenlijkeOnderbreking[WezenlijkeOnderbreking*JaOfNee] [UNI,TOT]
-- MEANING "Er is wel of geen sprake van een wezenlijke onderbreking."
-- PURPOSE RELATION wezenlijkeOnderbreking[WezenlijkeOnderbreking*JaOfNee] REF "BBRA Artikel 17 lid 4"
-- {+n BBRA Artikel 17 lid 4 is bij het bepalen van ongebruikelijke uren is het minstens 5 jaar zonder wezenlijke onderbreking ontvangen van een toelage een relevante factor. 
-- -}

RELATION periodeverantwoordingWezenlijkeOnderbreking[Periodeverantwoording*WezenlijkeOnderbreking]
MEANING "Bij een medewerker kan er bij het bepalen van ongebruikelijke werktijden (BBRA Artikel 17) sprake zijn van een wezenlijke onderbreking."
PURPOSE RELATION periodeverantwoordingWezenlijkeOnderbreking[Periodeverantwoording*WezenlijkeOnderbreking] REF "BBRA Artikel 17 lid 4"
{+Een persoon kan binnen de afgelopen 5 jaar wel of zonder wezenlijke onderbrekging een toelage hebben ontvangen. Dit is in BBRA Artikel 17 lid 4 een relavante factor bij het bepalen van de ongebruikelijke uren voor die persoon.
** Uitzoeken hoe wordt bepaald of er sprake is van een wezenlijke onderbreking*
-}

RELATION periodeverantwoordingToelageVoorafgaande36Kalendermaanden[Periodeverantwoording*Bedrag]
MEANING "Een persoon kan in de 36 maanden voorafgaand aan de maand waarin hij 55 jaar is geworden een gemiddeld bedrag aan toelagen hebben ontvangen."
PURPOSE RELATION periodeverantwoordingToelageVoorafgaande36Kalendermaanden[Periodeverantwoording*Bedrag] REF "BBRA Artikel 17 lid 4"
{+Bij het berekenen van de toelage voor ongebruikelijke tijden is het bedrag dat de ambtenaar, over de zesendertig kalendermaanden voorafgaande aan de maand 
waarin hij de leeftijd van 55 jaar bereikt, gemiddeld per maand aan toelage heeft ontvangen een relevante factor zijn. 
-}

RELATION dagverantwoordingVanBetaalperiode[Dagverantwoording*Betaalperiode] PRAGMA "" " valt binnen betaalperiode "
MEANING "Een dagverantwoording valt binnen een betaalperiode."
PURPOSE RELATION dagverantwoordingVanBetaalperiode[Dagverantwoording*Betaalperiode] REF "BBRA Artikel 17"
{+Om de ongebruikelijke uren binnen een bepaalde betaalperiode te berekenen, moeten alle dagverantwoordingen die binnen die betaalperiode vallen worden geraadpleegd.
-}

RELATION periodeverantwoordingOngebruikelijkeUren[Periodeverantwoording*Uren] PRAGMA "" " heeft als gevolg van ongebruikelijke werktijden als totaal aantal extra uren "
MEANING "Een periodeverantwoording heeft een totaal aan extra uren als gevolg van ongebruikelijke werktijden."
PURPOSE RELATION periodeverantwoordingOngebruikelijkeUren[Periodeverantwoording*Uren]
{+Het totaal aantal extra uren dat moet worden uitbetaald als gevolg van ongebruikelijke werktijden moet worden doorgegeven aan de organisatie die zorgt voor de salarisuitkering.
-}

-- RELATION periodeverantwoordingPersoon[Periodeverantwoording*Persoon][UNI,TOT] PRAGMA "" " is een verantwoordiging van medewerker "
-- MEANING "Een periodeverantwoording geldt voor één persoon."
-- PURPOSE RELATION periodeverantwoordingPersoon[Periodeverantwoording*Persoon]
-- {+De geregistreerde uren van een medewerker worden per betaalperiode verwerkt. Het totaal van een periodeverantwoording bevat dus de te verwerken uren van één bepaalde persoon voor een bepaalde betaalperiode.
-- -}

RELATION periodeverantwoordingPeriode[Periodeverantwoording*Betaalperiode] [UNI,TOT] PRAGMA "" " is een verantwoordiging voor betaalperiode "
MEANING "Voor elke periodeverantwoording registreert het systeem de betaalperiode."
PURPOSE RELATION periodeverantwoordingPeriode[Periodeverantwoording*Betaalperiode]
{+De geregistreerde uren van een medewerker worden per betaalperiode verwerkt. Het totaal van een periodeverantwoording bevat dus de te verwerken uren van een bepaalde persoon voor één bepaalde betaalperiode.
-}

RELATION periodeverantwoordingDagverantwoording[Periodeverantwoording*Dagverantwoording] [INJ]
MEANING "Van elke dagverantwoording staat eenduidig vast tot welke periodeverantwoording hij behoort."
PURPOSE RELATION periodeverantwoordingDagverantwoording[Periodeverantwoording*Dagverantwoording]
{+Omdat inspecteurs hun uren per dag verantwoorden, maar per betaalperiode laten goedkeuren, houdt het systeem bij welke dagverantwoordingen tot welke periodeverantwoording behoren.
Een periodeverantwoording van een persoon bestaat uit de dagverantwoordigingen van de dagen die vallen binnen de betreffende betaalperiode.
-}

RELATION periodeverantwoordingSalarisnummer10Salarisschaal7[Periodeverantwoording*Bedrag]
MEANING "Bij een periodeverantwoording hoort een uurtarief dat hoort bij salarisnummer 10 van salarisschaal 17."
PURPOSE RELATION periodeverantwoordingSalarisnummer10Salarisschaal7[Periodeverantwoording*Bedrag] REF "BBRA Artikel 17 lid 2e"
{+Bij het bepalen van de toelage wanneer er is gewerkt op feestdagen, is in BBRA Artikel 17 lid 2e het salaris behorende bij salarisnummer 10 van salarisschaal 7 een relavante factor.
-}

RELATION periodeverantwoordingVasteToelageArt17lid4[Periodeverantwoording*Bedrag] PRAGMA "" " bevat als gevolg van BBRA Artikel 17 lid 4 een vaste toelage van "
MEANING "Een vaste toelage als gevolg van BBRA Artikel 17 lid 4 kan onderdeel zijn van een periodeverantwoording."
PURPOSE RELATION periodeverantwoordingVasteToelageArt17lid4[Periodeverantwoording*Bedrag] REF "BBRA Artikel 17 lid 4"
{+Onder bepaalde voorwaarden die zijn beschreven in BBRA Artikel 17 lid 5 en 6, wordt bij het berekenen van ongebruikelijke uren volgens BBRA Artikel 17 lid 4 een vaste toelage toegekend aan een medewerker.
-}

RELATION periodeverantwoordingVasteToelageArt17lid7[Periodeverantwoording*Bedrag] PRAGMA "" " bevat als gevolg van BBRA Artikel 17 lid 7 een vaste toelage van "
MEANING "Een vaste toelage als gevolg van BBRA Artikel 17 lid 7 kan onderdeel zijn van een periodeverantwoording."
PURPOSE RELATION periodeverantwoordingVasteToelageArt17lid7[Periodeverantwoording*Bedrag] REF "BBRA Artikel 17 lid 7"
{+Onder bepaalde voorwaarden die zijn beschreven in BBRA Artikel 17 lid 8, wordt bij het berekenen van ongebruikelijke uren volgens BBRA Artikel 17 lid 7 een vaste toelage toegekend aan een medewerker.
-}

RELATION werktijdenpakketRuleSoort[Werktijdenpakket*WerktijdRuleSoort]
MEANING "Een werktijdenpakket bevat de werktijdRules die voor dat pakket van toepassing zijn."
PURPOSE RELATION werktijdenpakketRuleSoort[Werktijdenpakket*WerktijdRuleSoort]
{+In een werktijdenpakket zijn de werktijdRules die voor dat pakket van toepassing zijn opgenomen. Deze rules zijn dus geldig voor alle medewerkers waarvoor het werktijdenpakket geldig is.
-}
RELATION medewerkerWerktijdenpakket[MedewerkerWerktijdenpakket*Werktijdenpakket]
MEANING "Voor een MedewerkerWerktijdenPakket geldt één werktijdenpakket"
PURPOSE RELATION medewerkerWerktijdenpakket[MedewerkerWerktijdenpakket*Werktijdenpakket]
{+Een medewerkerWerktijdenpakket geeft voor een medewerker aan welk werktijdenpakket in een bepaalde betaalperiode van toepassing is. Elk medewerkerWerktijdenpakket heeft dus één relevant werktijdenpakket.
-}
RELATION werktijdenpakketPersoon[MedewerkerWerktijdenpakket*Persoon]
MEANING "Een MedewerkerWerktijdenpakket geldt voor één persoon"
PURPOSE RELATION werktijdenpakketPersoon[MedewerkerWerktijdenpakket*Persoon]
{+Een medewerkerWerktijdenpakket geeft voor een medewerker aan welk werktijdenpakket in een bepaalde betaalperiode van toepassing is. Elk medewerkerWerktijdenpakket is dus van toepassing voor één medewerker.
-}

RELATION pakketGeldigVan[MedewerkerWerktijdenpakket*Datum]
MEANING "Een MedewekerWerktijdenpakket is vanaf een begindatum geldig voor een medewerker"
PURPOSE RELATION pakketGeldigVan[MedewerkerWerktijdenpakket*Datum]
{+Een medewerkerWerktijdenpakket geeft voor een medewerker aan welk werktijdenpakket in een bepaalde betaalperiode van toepassing is. Elk medewerkerWerktijdenpakket heeft dus een begindatum van geldigheid.
-}

RELATION pakketGeldigTot[MedewerkerWerktijdenpakket*Datum]
MEANING "Een MedewerkerWerktijdenpakket is tot een einddatum geldig voor een medewerker"
PURPOSE RELATION pakketGeldigTot[MedewerkerWerktijdenpakket*Datum]
{+Een medewerkerWerktijdenpakket geeft voor een medewerker aan welk werktijdenpakket in een bepaalde betaalperiode van toepassing is. Een medewerkerWerktijdenpakket kan dus een einddatum van geldigheid hebben.
-}
ENDPATTERN


ENDCONTEXT