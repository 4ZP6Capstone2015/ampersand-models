CONTEXT DTV IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Persoonsinformatie.adl"
PATTERN Medewerkers
PURPOSE CONCEPT Persoon
{+Van medewerkers is het personeelsnummer het identificerende kenmerk.
Om de persoonlijke gegevens te ontzien, werkt DTV zoveel mogelijk met het personeelsnummer.
De noodzaak om persoonsgegevens aan het personeelsnummer te koppelen is daardoor minimaal.

Van medewerkers, die toegang hebben tot DTV wordt een wachtwoord en loginnaam geregistreerd conform de richtlijnen van DICTU.
Dit betreft een beperkt deel van de medewerkers.
-}
RELATION volledigenaam[Personeelsnummer*Naam] [UNI,TOT]
RELATION salaris[Personeelsnummer*Salarisschaal] [UNI,TOT]
RELATION cat[Personeelsnummer*Categorie] [UNI,TOT]
ENDPATTERN


PURPOSE RELATION persoonsnr[Persoon*Personeelsnummer]
{+Om een minimum aan persoonsgegevens te verwerken, gebruikt DTV het personeelsnummer van medewerkers.
-}
RELATION persoonsnr[Persoon*Personeelsnummer] [SUR]
MEANING "Van personen zijn de personeelsnummers bekend, die ook in P-Direkt worden gebruikt."

PURPOSE INTERFACE "Wijzig account"
{+De interface ``Wijzig account'' is bedoeld om een persoon haar eigen persoonsgegevens te laten invullen en zelf het wachtwoord te bepalen.
-}
INTERFACE "Wijzig account"(wachtwoord) FOR Medewerker, Leidinggevende, FAB, "P&O": '_SESSION';ingelogd
BOX [ "loginnaam"  : loginnaam
    , "Wachtwoord" : wachtwoord
    ]

PURPOSE INTERFACE logingegevens
{+De interface ``logingegevens'' mag door een FAB worden gebruikt om de login-gegevens van willekeurig welke persoon te wijzigen.
-}
INTERFACE "logingegevens" (persoonsnr, loginnaam, wachtwoord) FOR FAB: I[Personeelsnummer]
BOX [ Naam :         volledigenaam
    , logingegevens : persoonsnr~
    BOX [ "loginnaam" :  loginnaam
        , "Wachtwoord" : wachtwoord
        ] 
    ]


ENDCONTEXT