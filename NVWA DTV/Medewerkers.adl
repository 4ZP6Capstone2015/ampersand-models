CONTEXT DTV IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Persoonsinformatie.adl"
PATTERN Medewerkers
CONCEPT Naam "De volledige naam, waarmee een persoon binnen de NVWA bekend is."
RELATION volledigenaam[Personeelsnummer*Naam] [UNI,TOT]
RELATION salaris[Personeelsnummer*Salarisschaal] [UNI,TOT]
RELATION cat[Personeelsnummer*Categorie] [UNI,TOT]
ENDPATTERN

CONCEPT Salarisschaal "Een salarisschaal, zoals benoemd in bijlage 1 van de BBRA."
PURPOSE CONCEPT Salarisschaal
{+Binnen DTV is kennis van de salarisschalen-systematiek uit de BBRA nodig.-}
CONCEPT Personeelsnummer "Het nummer, wat in P-Direkt gebruikt wordt ter identificering van een arbeidsrelatie heet personeelsnummer."
PURPOSE CONCEPT Personeelsnummer
{+Van medewerkers is het personeelsnummer een identificerend kenmerk.
Om de persoonlijke gegevens te ontzien, werkt DTV zoveel mogelijk met het personeelsnummer.
De noodzaak om persoonsgegevens te bewerken is daardoor minimaal.
-}

PURPOSE RELATION persoonsnr[Account*Personeelsnummer]
{+Om een minimum aan persoonsgegevens te verwerken, gebruikt DTV het personeelsnummer van medewerkers.
-}
RELATION persoonsnr[Account*Personeelsnummer] [UNI]
MEANING "Van personen zijn de personeelsnummers bekend, die ook in P-Direkt worden gebruikt."

PURPOSE INTERFACE "Wijzig account"
{+De interface ``Wijzig account'' is bedoeld om een persoon haar eigen persoonsgegevens te laten invullen en zelf het wachtwoord te bepalen.
-}
INTERFACE "Wijzig account"(wachtwoord) FOR Medewerker, Leidinggevende, FAB, "P&O": '_SESSION';ingelogd
BOX [ "loginnaam"  : loginnaam
    , "Wachtwoord" : wachtwoord
    ]

PURPOSE INTERFACE logingegevens
{+De interface ``logingegevens'' mag door een FAB worden gebruikt om de login-gegevens van willekeurig welke persoon te wijzigen.
-}
INTERFACE "logingegevens" (persoonsnr, loginnaam, wachtwoord) FOR FAB: I[Personeelsnummer]
BOX [ Naam :         volledigenaam
    , logingegevens : persoonsnr~
    BOX [ "loginnaam" :  loginnaam
        , "Wachtwoord" : wachtwoord
        ] 
    ]


ENDCONTEXT