CONTEXT DTV IN DUTCH

PATTERN Medewerkers
--PURPOSE PATTERN "Artikel 17"

{-
RELATION medewerker[Dagverantwoording*Persoon] [UNI,TOT] PRAGMA "" " is door " " ingevuld"
MEANING "Elke dagverantwoording wordt geacht te zijn ingevuld door de persoon die erop vermeld staat."
PURPOSE RELATION medewerker[Dagverantwoording*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Er wordt per medewerker een dagverantwoording aangemaakt om de tijd te verantwoorden.
-}

PURPOSE RELATION personeelsnummer[Persoon*Personeelsnummer]
{+Om medewerkers te identificeren gebruikt DTV het personeelsnummer uit P-Direkt.
Dit nummer is ook bekend onder de naam SAP-nummer.
Om het personeelsnummer als attribuut op te kunnen nemen in de tabel van personen,
is het nodig dat elke persoon niet meer dan één personeelsnummer heeft.
Voor iemand met een meervoudige aanstelling betekent dit dat hij of zij meerdere records in de personentabel bezet.
-}
RELATION personeelsnummer[Persoon*Personeelsnummer] [UNI] PRAGMA "" " heeft als personeelsnummer " -- TOT is in de regel RULE "ontbrekend personeelsnummer" ondergebracht.
MEANING "Per persoon wordt één personeelsnummer geregistreerd."

PURPOSE RULE "ontbrekend personeelsnummer"
{+Omdat P-Direkt het personeelsnummer gebruikt ter identificatie van personen, doet DTV dat ook.
Als gevolg daarvan eist de DTV-applicatie dat elke persoon een personeelsnummer heeft.
-}
RULE "ontbrekend personeelsnummer" : I[Persoon] |- personeelsnummer;personeelsnummer~
MEANING "Er dient een personeelsnummer te zijn voor elke medewerker."
MESSAGE "Er ontbreken personeelsnummers."
VIOLATION ( SRC I, TXT " heeft geen personeelsnummer.")

PURPOSE RULE "identificerend personeelsnummer"
{+Om te zorgen dat het personeelsnummer naar slechts één persoon verwijst, wordt daarop getest bij het opvoeren van nieuwe personen.
-}
RULE "identificerend personeelsnummer" : personeelsnummer;personeelsnummer~ |- I[Persoon]
MEANING "Het personeelsnummer dient een persoon eenduidig te identificeren."
MESSAGE "Het personeelsnummer van een persoon moet uniek zijn."
VIOLATION ( TXT "Het personeelsnummer ", SRC personeelsnummer, TXT " is al in gebruik.")

POPULATION personeelsnummer[Persoon*Personeelsnummer] CONTAINS
  [ ("p1", "11111111")
  ; ("p2", "22222222")
  ; ("p3", "33333333")
  ]

PURPOSE RELATION functie[Persoon*Functie] REF "WTR Artikel 1 lid 2"
{+Een medewerker is een ambtenaar in de zin van artikel 1 van het ARAR die werkzaam is bij of voor de NVWA.
Of een medewerker werkzaam is bij of voor de NVWA wordt in de DTV-applicatie niet als zodanig geregistreerd.
Het systeem weet echter wel of de persoon een salarisschaal heeft.
Daarom beschouwt het systeem elke persoon, die een salarisschaal heeft, als medewerker.
Dit gegeven is ook tijdafhankelijk.
Op een zeker moment beschouwt het systeem dus iemand als medewerker als hij of zij op dat moment een salarisschaal heeft.
De (tijdafhankelijke) administratie van salarisschalen gebeurt bij "medewerkerrechten."
Mede daarom dient de functie van elke medewerker in het systeem bekend te zijn.
De Werktijdenregeling NVWA (hierna: WTR) spreekt van "Medewerker", en het ERD van SPIN bevat een entiteit SPN\_Medewerker.
Daarom gebruikt DTV dit begrip ook, en wel conform de WTR.
-}
RELATION functie[Persoon*Functie] PRAGMA "Medewerker " " heeft " " als functie"   -- [UNI] 
MEANING "Het feit dat een persoon een functie heeft bij de NVWA maakt hem of haar tot medewerker."
ENDPATTERN

PROCESS "Opvoeren nieuwe medewerker"
ROLE "P&O" MAINTAINS "onbekend personeelsnummer"
PURPOSE RULE "onbekend personeelsnummer"
{+Als SPIN een personeelsnummer noemt, wat niet in DTV bekend is,
dient een P&O-medewerker rechten op te voeren. Tenminste zullen categorie en salarisschaal moeten worden aangemaakt.
-}
RULE "onbekend personeelsnummer" : V[ONE*Periodeverantwoording];personeelsnummer |- V[ONE*MedewerkerRecht];persoon;personeelsnummer
MEANING "Voor elk personeelsnummer in een dagverantwoording dienen er medewerkerrechten te zijn. Dat betreft tenminste de salarisschaal en de categorie."
MESSAGE "Er komen onbekende personeelsnummers binnen vanuit SPIN."
VIOLATION ( TXT "Personeelsnummer ", TGT I, TXT " is niet bekend binnen DTV.")
ENDPROCESS

ENDCONTEXT