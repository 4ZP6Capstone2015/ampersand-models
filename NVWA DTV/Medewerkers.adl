CONTEXT DTV IN DUTCH

PATTERN Medewerkers
--PURPOSE PATTERN "Artikel 17"

PURPOSE RELATION personeelsnummer[Persoon*Personeelsnummer]
{+Om medewerkers te identificeren gebruikt DTV het personeelsnummer, ook bekend onder de naam SAP-nummer.-}
RELATION personeelsnummer[Persoon*Personeelsnummer] [UNI] PRAGMA "" " heeft als personeelsnummer "
MEANING "Het unieke personeelsnummer van een persoon wordt geregistreerd."

RULE "ontbrekend personeelsnummer" : I[Persoon] |- personeelsnummer;personeelsnummer~
MEANING "Het personeelsnummer identificeert een persoon eenduidig."
MESSAGE "Er ontbreken personeelsnummers."
VIOLATION ( SRC volledigenaam, TXT " heeft geen personeelsnummer.")

RULE "uniek personeelsnummer" : personeelsnummer;personeelsnummer~ |- I[Persoon]
MEANING "Het personeelsnummer identificeert een persoon eenduidig."
MESSAGE "Het personeelsnummer van een persoon moet uniek zijn."
VIOLATION ( TXT "Er zijn meerdere personen met personeelsnummer ", SRC personeelsnummer, TXT ".")

PURPOSE RELATION volledigenaam[Persoon*Naam]
{+Om medewerkers ook bij naam te kunnen noemen, registreert DTV ook de volledige naam van een persoon.
Deze informatie wordt door SPIN aangeleverd, en wordt verder niet door DTV gebruikt.
In beginsel zou DTV zijn werk kunnen doen zonder de naam te weten die bij een personeelsnummer hoort.
-}
RELATION volledigenaam[Persoon*Naam] [UNI]

POPULATION personeelsnummer[Persoon*Personeelsnummer] CONTAINS
  [ ("p1", "11111111")
  ; ("p2", "22222222")
  ; ("p3", "33333333")
  ]

POPULATION volledigenaam[Persoon*Naam] CONTAINS
  [ ("p1", "Stef Joosten")
  ; ("p2", "Harry in 't Honderd")
  ; ("p3", "Barry Stevens")
  ]

PURPOSE RELATION functie[Persoon*Functie] REF "WTR Artikel 1 lid 2"
{+Een medewerker is een ambtenaar in de zin van artikel 1 van het ARAR die werkzaam is bij of voor de NVWA.
De Werktijdenregeling NVWA (hierna: WTR) spreekt van "Medewerker", en het ERD van SPIN bevat een entiteit SPN_Medewerker.
Daarom gebruikt DTV dit begrip ook, en wel conform de WTR.
Op basis van de functie van een persoon wordt bepaald welke processtappen mogen worden uitgevoerd. Daarom moet de functie van elke medewerker bekend zijn.
-}
RELATION functie[Persoon*Functie] PRAGMA "Medewerker " " heeft " " als functie"   -- [UNI] 
MEANING "Het feit dat een persoon een functie heeft bij de NVWA maakt hem of haar tot medewerker."

ENDPATTERN

ENDCONTEXT