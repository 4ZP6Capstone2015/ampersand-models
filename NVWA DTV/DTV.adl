CONTEXT DTV IN DUTCH LATEX

INCLUDE "Login.adl"                    -- definieert het inloggen, maar uitsluiten voor prototyping doeleinden.
INCLUDE "Tijdvakken.adl"
INCLUDE "Berichten.adl"                -- definieert de uit te wisselen berichten
INCLUDE "Referentielijsten.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "Arbeidsmodaliteiten.adl"
INCLUDE "Periodeverantwoordingen.adl"
INCLUDE "batches.adl"
INCLUDE "MedewerkerRechten.adl"
INCLUDE "MedewerkerRechten.ifc"
INCLUDE "Resultaten.adl"
INCLUDE "Urenpools.adl"
--INCLUDE "Uurtijdvakken.adl"

INCLUDE "PDirektBestanden.adl"
INCLUDE "DTV_populaties.xlsx"
--INCLUDE "Startpagina.ifc"

INTERFACE Periodeverantw FOR FAB, "HR" : I[Perioderesultaat]
BOX [ verantwoordingsperiode : verantwoordingsperiode;I[Verantwoordingsperiode]
    , resultaten : goedresultaat~
    ]

VIEW Periodeverantwoording : Periodeverantwoording (TXT "Verantwoording ",verantwoordingsperiode, TXT " van ", personeelsnummer;volledigenaam)

VIEW Medewerker : Personeelsnummer (TXT "mdw ", I, TXT " (", volledigenaam, TXT")")

INTERFACE Periodeverantwoording FOR FAB, "HR" : I[Periodeverantwoording]
BOX [ medewerker : personeelsnummer
    , verantwoordingsperiode : verantwoordingsperiode;I[Verantwoordingsperiode]
    , dagverantwoordingen : periodeverantwoording~ -- INTERFACE DagverantwoordingDTV
      BOX [ Diensten : dag[Dienst*Dagverantwoording]~
            BOX [ roostersoort :  roosterSoort
                , begin :         begin
                , eind :          eind
                ]
          , Timesheet : dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ begin :         begin
                , eind :          eind
                , tijdrubriek :   tijdrubriek
                ]
          ]
    ]

PURPOSE INTERFACE "Opvoeren nieuwe medewerker"
{+Het opvoeren van een nieuwe medewerker gebeurt door een HR-medewerker,
die in een scherm het personeelsnummer, de naam, de salarisschaal en de categorie invult.

Dit opvoeren kan ook door SPIN gebeuren, die naam en personeelsnummer naar DTV stuurt in een periodeverantwoording.
In DTV is dat zichtbaar doordat in een periodeverantwoording vanuit SPIN een personeelsnummer wordt gebruikt, dat niet voorkomt in de lijst van rechten.
Omdat salarisnummer en categorie in dat geval ontbreken, verschijnt deze periodeverantwoording in het tabblad “te corrigeren” van de startpagina.
De medewerker P\&O krijgt de mogelijkheid om rechten van deze medewerkers in te vullen,
waarvan hij/zij in elk geval een salarisschaal en categorie in dient te vullen om deze periodeverantwoording te kunnen verwerken.
-}
INTERFACE "Opvoeren nieuwe medewerker"
          ( persoon
          , begindatum[MedewerkerRecht*Datum]
          , einddatum[MedewerkerRecht*Datum]
          , recht
          ) FOR "HR": I[Personeelsnummer]
BOX [ medewerker : I
    , rechten : persoon~
      BOX [ "geldig vanaf : " : begindatum
          , "geldig tot : " : einddatum
          , recht : recht
          ]
    ]


INTERFACE "Overzicht resultaten" : V[SESSION*Resultaat]
BOX [ "" : I
      BOX [ code : code
          , soort : resultaatSoort
          , waarde : resultaatWaarde
          , resultaat : goedresultaat
          , verzonden : goedresultaat;periodebestand
          ]
    ]

{-
INTERFACE MedewerkerRechten FOR "HR" : I[Personeelsnummer]
      BOX [ medewerkerRecht : persoon~
            BOX [ rechten : recht
                , begin :   begindatum
                , eind :    einddatum
                ]
          ]
-}

INTERFACE "Overzicht urentegoeden" CLASS UF FOR "HR": V[SESSION*Personeelsnummer]
BOX [ "" : I[Personeelsnummer]
      BOX [ medewerker : I
          , naam : volledigenaam
          , "Soort urentegoed" : medewerker;urenpool
            BOX [ soort : I
                , overzicht : urenpool~
                  BOX [ medewerker : medewerker[Personeelsnummer*Urentegoed]~;volledigenaam
                      , personeelsnummer : medewerker~
                      , "saldo" : tegoed
                      , vanaf : vanaf
                      ]
                ]
          ]
    ]

PURPOSE INTERFACE "Beheren urentegoeden"
{+In deze interface kan een HR medewerker urentegoeden raadplegen en wijzigen.
De interface opent standaard met een zoekveld voor personeelsnummer.
Als de betreffende medewerker gevonden is wordt de naam van de medewerker erbij getoond en
wordt het tegoed van elke urenpool van deze gebruiker getoond.

Het personeelsnummer en de naam zijn vooraf ingevuld,
wanneer de gebruiker vanaf het “overzicht urentegoeden” naar dit scherm heeft genavigeerd.
In het scherm mag de gebruiker ook zelf personeelsnummer of naam invullen, en met de Zoek knop de gegevens corrigeren/aanvullen.
In deze interface worden ook de actuele urentegoeden getoond.
Wanneer de HR medewerker een tegoed wil wijzigen, selecteert hij/zij het tegoed en voert de mutatie in.
 
-}
INTERFACE "Beheren urentegoeden" (urenpool, medewerker[Personeelsnummer*Urentegoed], tegoed, vanaf) FOR "HR" : I[Personeelsnummer]
BOX [ medewerker : I
    , naam : volledigenaam
    , urentegoed : medewerker
      BOX [ urenpool : urenpool
          , "saldo" : tegoed
          , vanaf : vanaf
          ]
    ]



ENDCONTEXT