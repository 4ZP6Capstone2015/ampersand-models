CONTEXT DTV IN DUTCH LATEX

PATTERN Periodeverantwoordingen
PURPOSE RULE "verantwoording per medewerker"
{+Omdat elke medewerker zelf verantwoordelijkheid neemt voor de correcte registratie van tijd,
geschiedt de periodeverantwoording ook per medewerker.
-}
RULE "verantwoording per medewerker" : periodeverantwoording~;medewerkerD |- medewerkerPer
MEANING "De medewerker die op een dagverantwoording vermeldt staat, wordt ook als medewerker vermeldt op de bijbehorende periodeverantwoording."
MESSAGE "Onjuiste registratie van medewerker op dagverantwoording of periodeverantwoording:"

--RULE "dagverantwoording binnen de juiste periode" : datum[Dagverantwoording*Datum] |- periodeverantwoording;verantwoordingsperiode;(einddatum;geqDatum /\ begindatum;leqDatum)
RULE "dagverantwoording binnen de juiste periode" : periodeverantwoording;verantwoordingsperiode = datum;geqDatum;begindatum~ /\ datum;leqDatum;einddatum~
MEANING "De dag waarover een dagverantwoording rapporteert valt binnen de verantwoordingsdatum. Dat wil zeggen: deze dag valt op of vóór de einddatum van de verantwoordingsperiode en op of na de begindatum van die periode."
PURPOSE RULE "dagverantwoording binnen de juiste periode"
{+Om te borgen dat dagverantwoordingen aan de juiste periodeverantwoordingen worden gekoppeld,
stellen we de eis dat de dag (van de dagverantwoording) waarover gerapporteerd wordt binnen de verantwoordingsperiode valt.
-}

-- CLASSIFY Verantwoordingsperiode ISA Periode

CONCEPT Periodeverantwoording "Een periodeverantwoording is een periodiek overzicht van de gewerkte uren van één medewerker in één verantwoordingsperiode."
PURPOSE CONCEPT Periodeverantwoording 
{+Voor elke medewerker worden de uren per verantwoordingsperiode verwerkt.
De urenverantwoording van één medewerker voor één verantwoordingsperiode wordt een Periodeverantwoording genoemd.
Het woord ``timesheet'' wordt hiervoor als synoniem gebruikt. Dit woord is afkomstig uit de informatie-analyse.
-}

--VIEW Periodeverantwoording : Periodeverantwoording (TXT "dagperiode ",verantwoordingsperiode, TXT " van ", periodeverantwoording~;medewerkerPerD)

-- RELATION periodeverantwoordingToelageVoorafgaande36Kalendermaanden[Periodeverantwoording*Bedrag] [UNI]
-- MEANING "Een persoon kan in de 36 maanden voorafgaand aan de maand waarin hij 55 jaar is geworden een gemiddeld bedrag aan toelagen hebben ontvangen."
-- PURPOSE RELATION periodeverantwoordingToelageVoorafgaande36Kalendermaanden[Periodeverantwoording*Bedrag] REF "BBRA Artikel 17 lid 4"
-- {+Bij het berekenen van de toelage voor ongebruikelijke tijden is het bedrag dat de ambtenaar, over de zesendertig kalendermaanden voorafgaande aan de maand 
-- waarin hij de leeftijd van 55 jaar bereikt, gemiddeld per maand aan toelage heeft ontvangen een relevante factor zijn. 
-- -}

-- RELATION periodeverantwoordingOngebruikelijkeUren[Periodeverantwoording*AantalUren] [UNI] PRAGMA "" " heeft als gevolg van ongebruikelijke werktijden als totaal aantal extra uren "
-- MEANING "Een periodeverantwoording heeft een totaal aan extra uren als gevolg van ongebruikelijke werktijden."
-- PURPOSE RELATION periodeverantwoordingOngebruikelijkeUren[Periodeverantwoording*AantalUren]
-- {+Het totaal aantal extra uren dat moet worden uitbetaald als gevolg van ongebruikelijke werktijden moet worden doorgegeven aan de organisatie die zorgt voor de salarisuitkering.
-- -}

RELATION medewerkerPer[Periodeverantwoording*Persoon] [UNI,TOT] PRAGMA "" " verantwoordt de tijdbesteding van "
MEANING "Een periodeverantwoording verantwoordt de tijdbesteding van één medewerker over één periode."
PURPOSE RELATION medewerkerPer[Periodeverantwoording*Persoon]
{+De geregistreerde uren van een medewerker worden per verantwoordingsperiode verwerkt.
Het totaal van een periodeverantwoording bevat dus de te verwerken uren van één bepaalde persoon voor een bepaalde verantwoordingsperiode.
-}

RELATION verantwoordingsperiode[Periodeverantwoording*Periode] [UNI,TOT] PRAGMA "" " is een verantwoordiging voor verantwoordingsperiode "
MEANING "Voor elke periodeverantwoording registreert het systeem de verantwoordingsperiode."
PURPOSE RELATION verantwoordingsperiode[Periodeverantwoording*Periode]
{+De geregistreerde uren van een medewerker worden per verantwoordingsperiode verwerkt.
Een verantwoordingsperiode is de periode waarover de NVWA normaliter de toeslagen berekent; dat gebeurt elke vier weken.
Dit loopt niet precies in de pas met de betaalperiodes van de overheid, die per maand gaan.
Het totaal van een periodeverantwoording bevat dus de te verwerken uren van een bepaalde persoon voor één bepaalde verantwoordingsperiode.
-}

ENDPATTERN

INTERFACE PeriodeVerantwoordingsSheet
      ( periodeResultaat
      , code
      , resultaatWaarde
      , resultaatSoort
      ) FOR KODE : I[Periodeverantwoording]
BOX [ medewerker : medewerkerPer
    , periode : verantwoordingsperiode
    , Werktijdenpakket : medewerkerPer;werktijdenpakket/\verantwoordingsperiode;binnen~
      BOX [ personeelsnummer : werktijdenpakket~;personeelsnummer
          , salarisschaal : salarisschaal
          , salarisnummer : salarisnummer
          , "Categorie" : werktijdenpakketCategorie
          , rechten : recht
          ] 
    , dagverantwoordingen : periodeverantwoording~ INTERFACE Dagverantwoording
    , resultaten : periodeResultaat~
      BOX [ soort : resultaatSoort
          , "aantal uren" : resultaatWaarde
          , code : code
          ]
    ]


ENDCONTEXT