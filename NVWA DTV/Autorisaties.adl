CONTEXT Autorisaties IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Periodeverantwoordingen.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "Tijdregistratie.adl"
INCLUDE "Accordering.adl"
INCLUDE "Autorisaties.xlsx"


PATTERN Autorisaties
CONCEPT Autorisatie "Een autorisatie is de toestemming aan een of meer medewerkers om in een gegeven periode tijd te verantwoorden in de dagverantwoording op een bepaalde combinatie van tijdRubriek."
RELATION occurA[Autorisatie*Datum] [UNI,TOT]
MEANING "Van elk autorisatie registreert het systeem de datum waarop deze autorisatiegebeurtenis heeft plaatsgevonden."
RELATION eventType[Autorisatie*EventType] [UNI]
MEANING "Van elk autorisatie wordt het type geregistreerd."
RELATION medewerkerA[Autorisatie*Personeelsnummer] [TOT]    -- UNI,
MEANING "Het systeem registreert de persoon, aan wie een autorisatie wordt verleend."
RELATION geldigVan[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum vanaf wanneer de autorisatie geldig is."
RELATION geldigTot[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum tot wanneer de autorisatie geldig is."
RELATION tijdCategorie[Autorisatie*TijdRubriekCategorie]
MEANING "Het systeem registreert tijdrubiekcategorieën, waarvoor autorisatie is verleend om tijd te registreren."
RELATION verleendDoor[Autorisatie*Personeelsnummer] [UNI]
MEANING "Het systeem registreert de persoon, door wie de autorisatie is verleend."

RULE I[EventType] = 'Verleend' \/ 'Ingetrokken'

--RULE autorisatieregel : dag;medewerker |- ((activiteit;cat)~\tijdCategorie~);medewerker       ---- EVEN UIT wegens violation 14-10-2014
--RULE autorisatregel : dag;medewerker |- ((tijdCategorie/(activiteit;cat))~);medewerker
--RULE autorisatieregel : dag;medewerker |- medewerker
MEANING "Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd."
VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")



ENDPATTERN

PROCESS Autorisatie

RULE autorisatieZonderBeslissing: I[Autorisatie] |- eventType;eventType~
MEANING "Een autorisatieverzoek moet verleend of ingetrokken worden." 
VIOLATION (TXT "Er zijn openstaande autorisatieverzoeken")

ROLE Leidinggevende MAINTAINS autorisatieZonderBeslissing

ENDPROCESS
{-
INTERFACE "Aanvragen Autorisatie" (geldigVan, geldigTot, tijdCategorie, occurA)
FOR Medewerker : I[Autorisatie]
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : V;'_SESSION';ingelogd;leidinggevende
          ]
-}

INTERFACE "Aanvragen Autorisatie" (geldigVan, geldigTot, tijdCategorie, occurA, medewerkerA, verleendDoor)
FOR Medewerker :  I[Autorisatie]
      BOX [ "voor medewerker" : medewerkerA[Autorisatie*Personeelsnummer]
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" :verleendDoor
          ]


INTERFACE "Verkregen Autorisaties" 
FOR Medewerker : '_SESSION';ingelogd;persoonsnr
BOX ["Autorisaties van " : I[Personeelsnummer]
     , "" : medewerkerA~ /\ V;'Verleend';eventType~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , "verleend door" : verleendDoor
          ]
    ]

INTERFACE "Aangevraagde Autorisaties" 
FOR Medewerker : '_SESSION';ingelogd;persoonsnr
BOX ["Autorisaties van " : I[Personeelsnummer]
     , "" : medewerkerA~ /\ -(V;eventType~)
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : verleendDoor
          ]
    ]

INTERFACE "Openstaande autorisaties" (eventType) FOR Leidinggevende : '_SESSION';ingelogd;persoonsnr
BOX ["Autorisaties verleend door " : I[Personeelsnummer]
    , "" : verleendDoor~/\-(V;eventType~)   -- Autorisatie
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , status :          eventType
          ]
    ]


INTERFACE "Autorisaties beheren" (eventType) FOR Leidinggevende : '_SESSION';ingelogd;persoonsnr
BOX ["Autorisaties verleend door " : I[Personeelsnummer]
    , "" : verleendDoor~/\V;eventType~   -- Autorisatie
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , tijdCategorieën : tijdCategorie
          , status :          eventType
          ]
    ]

INTERFACE "Nieuwe autorisatie"
           ( verleendDoor
           , eventType
           , medewerkerA[Autorisatie*Personeelsnummer]
           , geldigVan
           , geldigTot
           , tijdCategorie
           , occurA
           ) FOR Leidinggevende : I[Autorisatie]
      BOX [ "geautoriseerd door" :       verleendDoor
          , "Nieuw/Intrekken/Wijzigen" : eventType
          , "verleend aan" :             medewerkerA[Autorisatie*Personeelsnummer]
          , "geldig van" :               geldigVan
          , "geldig tot" :               geldigTot
          , "tijdcategorie" :            tijdCategorie
          , "tijdstip van verlenen" :    occurA
          ]

INTERFACE "Te valideren"
           ( dagverantwoordingStatus
           , tijdrubriek
           , opmerkingValidatie
           ) FOR Leidinggevende : '_SESSION';ingelogd;persoonsnr
BOX ["Tijdverantwoordingen voor " : I[Personeelsnummer]
    , "" : leidinggevende~;afkomstigVan~;periodeverantwoording~ /\-((V[Personeelsnummer*DagverantwoordingStatus]#'Akkoord');dagverantwoordingStatus~)/\V;dag[Tijdverantwoording*Dagverantwoording]   -- Dagverantwoording
      BOX [ medewerker :         periodeverantwoording;afkomstigVan
          , "betreft datum" :    datum
          , akkoord :            dagverantwoordingStatus
          , tijdverantwoording : dag~
            BOX [ begin :        begin
                , eind :         eind
                , duur :         tijdsduur
                , tijdrubriek :  tijdrubriek
                ]
          , roosterverantwoording : dag~
            BOX [ roosterSoort :    roosterSoort
                , begin :           begin
                , eind :            eind
                ]
          , "Commentaar bij validatie" : opmerkingValidatie
          ]
    ]

ENDCONTEXT