CONTEXT Autorisaties IN DUTCH

PATTERN Autorisaties
CONCEPT Autorisatie "Een autorisatie is de toestemming aan een of meer medewerkers om in een gegeven periode tijd te verantwoorden in de dagverantwoording op een bepaalde combinatie van tijdRubriek en eventueel project."
RELATION occurA[Autorisatie*Datum] [UNI,TOT]
MEANING "Van elk autorisatie registreert het systeem de datum waarop deze autorisatiegebeurtenis heeft plaatsgevonden."
RELATION eventType[Autorisatie*EventType] [UNI]
MEANING "Van elk autorisatie wordt het type geregistreerd."
RELATION medewerkerA[Autorisatie*Persoon] [TOT]    -- UNI,
MEANING "Het systeem registreert de persoon, aan wie een autorisatie wordt verleend."
RELATION geldigVan[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum vanaf wanneer de autorisatie geldig is."
RELATION geldigTot[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum tot wanneer de autorisatie geldig is."
RELATION tijdCategorie[Autorisatie*TijdRubriekCategorie]
MEANING "Het systeem registreert tijdrubiekcategorieën, waarvoor autorisatie is verleend om tijd te registreren."
RELATION projectA[Autorisatie*Project] [UNI]
MEANING "Het systeem registreert het project, waarvoor autorisatie is verleend om tijd te registreren."
RELATION verleendDoor[Autorisatie*Persoon] [UNI]
MEANING "Het systeem registreert de persoon, door wie de autorisatie is verleend."

RULE I[EventType] = 'Verleend' \/ 'Ingetrokken'

--RULE autorisatieregel : dag;medewerkerD |- (project;projectA~/\(activiteit;cat)~\tijdCategorie~);medewerker       ---- EVEN UIT wegens violation 14-10-2014
--RULE autorisatregel : dag;medewerkerD |- (project;projectA~/\(tijdCategorie/(activiteit;cat))~);medewerker
--RULE autorisatieregel : dag;medewerkerD |- project;projectA~;medewerker
MEANING "Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek en project waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd."
VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")



ENDPATTERN

POPULATION occurA       [Autorisatie*Datum]                CONTAINS [ ("e2", "18 juli 2014"); ("e3", "18 juli 2014")]
POPULATION eventType    [Autorisatie*EventType]            CONTAINS [ ("e2", "Verleend"); ("e3", "Verleend")]
POPULATION medewerkerA  [Autorisatie*Persoon]              CONTAINS [ ("e2", "p2"); ("e3", "p3")]
POPULATION geldigVan    [Autorisatie*Datum]                CONTAINS [ ("e2", "1 januari 2014"); ("e3", "1 januari 2014")]
POPULATION geldigTot    [Autorisatie*Datum]                CONTAINS [ ("e3", "31 december 2014"); ("e2", "31 december 2014")]
POPULATION tijdCategorie[Autorisatie*TijdRubriekCategorie] CONTAINS [ ("e2", "Werktijd"); ("e2", "Pauze")]
POPULATION tijdCategorie[Autorisatie*TijdRubriekCategorie] CONTAINS [ ("e3", "Werktijd"); ("e3", "Pauze")]
POPULATION projectA     [Autorisatie*Project]              CONTAINS [ ("e2", "DTV"); ("e3", "DTV")]
POPULATION verleendDoor [Autorisatie*Persoon]              CONTAINS [ ("e2", "p1"); ("e3", "p1")]

PROCESS Autorisatie

RULE autorisatieZonderBeslissing: I[Autorisatie] |- eventType;eventType~
MEANING "Een autorisatieverzoek moet verleend of ingetrokken worden." 
VIOLATION (TXT "Er zijn openstaande autorisatieverzoeken")

ROLE Leidinggevende MAINTAINS autorisatieZonderBeslissing

ENDPROCESS
{-
INTERFACE "Aanvragen Autorisatie" (geldigVan, geldigTot, projectA, tijdCategorie, occurA)
FOR Medewerker : I[Autorisatie]
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : V;'_SESSION';sessionLoginnaam;loginnaam~;leidinggevende
          ]
-}

INTERFACE "Aanvragen Autorisatie" (geldigVan, geldigTot, projectA, tijdCategorie, occurA, medewerkerA, verleendDoor)
FOR Medewerker :  I[Autorisatie]
      BOX [ "voor medewerker" : medewerkerA[Autorisatie*Persoon]
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" :verleendDoor
          ]


INTERFACE "Verkregen Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~ /\ V;'Verleend';eventType~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "verleend door" : verleendDoor
          ]
    ]

INTERFACE "Aangevraagde Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~ /\ -(V;eventType~)
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : verleendDoor
          ]
    ]

INTERFACE "Openstaande autorisaties" (eventType) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX ["Autorisaties verleend door " : I[Persoon]
    , "" : verleendDoor~/\-(V;eventType~)   -- Autorisatie
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , status :          eventType
          ]
    ]


INTERFACE "Autorisaties beheren" (eventType) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX ["Autorisaties verleend door " : I[Persoon]
    , "" : verleendDoor~/\V;eventType~   -- Autorisatie
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , status :          eventType
          ]
    ]

INTERFACE "Nieuwe autorisatie"
           ( verleendDoor
           , eventType
           , medewerkerA[Autorisatie*Persoon]
           , geldigVan
           , geldigTot
           , tijdCategorie
           , projectA
           , occurA
           ) FOR Leidinggevende : I[Autorisatie]
      BOX [ "geautoriseerd door" :       verleendDoor
          , "Nieuw/Intrekken/Wijzigen" : eventType
          , "verleend aan" :             medewerkerA[Autorisatie*Persoon]
          , "geldig van" :               geldigVan
          , "geldig tot" :               geldigTot
          , "tijdcategorie" :            tijdCategorie
          , project :                    projectA
          , "tijdstip van verlenen" :    occurA
          ]

INTERFACE "Te valideren"
           ( dagverantwoordingStatus
           , tijdrubriek
           , project
           , opmerkingValidatie
           ) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX ["Tijdverantwoordingen voor " : I[Persoon]
    , "" : leidinggevende~;medewerkerD~/\-((V[Persoon*DagverantwoordingStatus]#'Akkoord');dagverantwoordingStatus~)/\V;dag[Tijdverantwoording*Dagverantwoording]   -- Dagverantwoording
      BOX [ ontvangen :          ontvangen
          , medewerker :         medewerkerD
          , "betreft datum" :    datum
          , akkoord :            dagverantwoordingStatus
          , tijdverantwoording : dag~
            BOX [ begin :        begin
                , eind :         eind
                , duur :         tijdsduur
                , tijdrubriek :  tijdrubriek
                , project :      project
                ]
          , roosterverantwoording : dag~
            BOX [ roosterSoort :    roosterSoort
                , begin :           begin
                , eind :            eind
                ]
          , "Commentaar bij validatie" : opmerkingValidatie
          ]
    ]

ENDCONTEXT