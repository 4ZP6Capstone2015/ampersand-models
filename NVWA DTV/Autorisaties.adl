CONTEXT Autorisaties IN DUTCH

PATTERN Autorisaties
CONCEPT Autorisatie "Een autorisatie is de toestemming aan een of meer medewerkers om in een gegeven periode tijd te verantwoorden in de dagverantwoording op een bepaalde combinatie van tijdRubriek, kostenplaats en eventueel project."
RELATION occurA[Autorisatie*Datum] [UNI,TOT]
MEANING "Van elk autorisatie registreert het systeem de datum waarop deze autorisatiegebeurtenis heeft plaatsgevonden."
RELATION eventType[Autorisatie*EventType] [UNI]
MEANING "Van elk autorisatie wordt het type geregistreerd."
RELATION medewerkerA[Autorisatie*Persoon]
MEANING "Het systeem registreert de persoon, aan wie een autorisatie wordt verleend."
RELATION geldigVan[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum vanaf wanneer de autorisatie geldig is."
RELATION geldigTot[Autorisatie*Datum] [UNI,TOT]
MEANING "Het systeem registreert de datum tot wanneer de autorisatie geldig is."
RELATION kostenPlaats[Autorisatie*Kostenplaats] [UNI] --  Kostenplaatsen komen uit een referentielijst
MEANING "Het systeem registreert de kostenplaats, waarvoor autorisatie is verleend om tijd te registreren."
RELATION tijdCategorie[Autorisatie*TijdRubriekCategorie]
MEANING "Het systeem registreert de tijdrubiekcategorie, waarvoor autorisatie is verleend om tijd te registreren."
RELATION projectA[Autorisatie*Project] [UNI]
MEANING "Het systeem registreert het project, waarvoor autorisatie is verleend om tijd te registreren."
RELATION verleendDoor[Autorisatie*Persoon] [UNI]
MEANING "Het systeem registreert de persoon, door wie de autorisatie is verleend."

RULE I[EventType] = 'Verleend' \/ 'Ingetrokken'

RULE autorisatieregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~/\(activiteit;cat)~\tijdCategorie~);medewerkerA
--RULE autorisatregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~/\(tijdCategorie/(activiteit;cat))~);medewerkerA
--RULE autorisatieregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~);medewerkerA
MEANING "Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd."
VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")
ENDPATTERN

INTERFACE "Aanvragen Autorisatie" (geldigVan, geldigTot, kostenPlaats, projectA, tijdCategorie, occurA)
FOR Medewerker : I[Autorisatie]
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : V;'_SESSION';sessionUsername;emailadres~;leidinggevende
          ]

INTERFACE "Verkregen Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~ /\ V;'Verleend';eventType~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "verleend door" : verleendDoor
          ]
    ]

INTERFACE "Aangevraagde Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~ /\ V;-eventType~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          , "tijdstip van aanvragen" : occurA
          , "te verlenen door" : verleendDoor
          ]
    ]

INTERFACE "Verleende autorisaties" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties verleend door " : I[Persoon]
    , "" : verleendDoor~/\V;'Verleend';eventType~   -- Autorisatie
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          ]
    ]

INTERFACE "Nieuwe autorisatie"
           ( verleendDoor
           , eventType
           , medewerkerA
           , geldigVan
           , geldigTot
           , tijdCategorie
           , kostenPlaats
           , projectA
           , occurA
           ) FOR Leidinggevende : I[Autorisatie]
      BOX [ "geautoriseerd door" :       verleendDoor
          , "Nieuw/Intrekken/Wijzigen" : eventType
          , "verleend aan" :             medewerkerA
          , "geldig van" :               geldigVan
          , "geldig tot" :               geldigTot
          , "tijdcategorie" :            tijdCategorie
          , kostenplaats :               kostenPlaats
          , project :                    projectA
          , "tijdstip van verlenen" :    occurA
          ]

INTERFACE "Te valideren"
           ( dagverantwoordingStatus
           , activiteit
           , project
           , kostenplaats
           , opmerkingValidatie
           ) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties voor " : I[Persoon]
    , "" : leidinggevende~;medewerkerD~/\-((V[Persoon*DagverantwoordingStatus]#'Akkoord');dagverantwoordingStatus~)   -- Dagverantwoording
      BOX [ ontvangen :          ontvangen
          , medewerker :         medewerkerD
          , "betreft datum" :    datum
          , akkoord :            dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ begin :        begin
                , eind :         eind
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort :    roosterSoortR
                , begin :           begin
                , eind :            eind
                ]
          , "Commentaar bij validatie" : opmerkingValidatie
          ]
    ]

ENDCONTEXT