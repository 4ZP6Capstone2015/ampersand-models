CONTEXT MedewerkerRechten IN DUTCH LATEX

INCLUDE "Login.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "MedewerkerRechten.adl"
INCLUDE "Medewerkers.ifc"

PURPOSE INTERFACE "Beheren rechten" REF "DTV5010"
{+Deze interface geeft de medewerker P\&O de mogelijkheid een nieuwe medewerker op te voeren in de DTV-applicatie.
Voor een medewerker, die nog niet in de DTV-applicatie voorkomt, dient een medewerker P\&O tenminste een salarisschaal en categorie in te vullen.
Het personeelsnummer wordt daarbij als identificerend kenmerk gebruikt, omdat dat in alle SPIN-berichten wordt meegeleverd.

Deze interface geeft daarnaast de medewerker P\&O de mogelijkheid om rechten van medewerkers in te vullen en te beheren.
De DTV-applicatie is immers de plek waar dit soort rechten wordt opgeslagen.
Voordat een periodeverantwoording voor die medewerker verwerkt kan worden moet het personeelsnummer bekend zijn in de DTV-applicatie.
Ook moet in deze interface een of meerdere rechten toegekend worden en indien nodig aangepast voor een medewerker.

De salarisschaal en de categorie waarin de medewerker valt worden ook vastgelegd als rechten die toegekend kunnen worden.
Daarvoor onderscheidt de DTV-applicatie drie soorten recht: ``salarisschaal'', ``categorie'' en ``recht''.

De interface is dynamisch en toont alle rechten van een medewerker gebundeld per type.
Alleen de actueel geldende rechten kunnen worden gewijzigd.
Om de geschiedenis in stand te houden zijn rechten uit het verleden zijn alleen te raadplegen.
-}
INTERFACE "Beheren rechten"
          ( persoon , begindatum[MedewerkerRecht*Datum], einddatum[MedewerkerRecht*Datum], recht)
          FOR "HR": '_SESSION';ingelogd;rol;'P&O';rol~;ingelogd~
BOX [ "Op dit moment ingelogd" : ingelogd
    , Medewerkers : ingelogd;-I[Account];account~
      BOX [ naam : volledigenaam
          , personeelsnummer : account~~
          , "maak of wijzig recht" : persoon~
            BOX [ recht : recht
                , "geldig vanaf : " : begindatum
                , "geldig tot : " : einddatum
                ]
          ]
    ]

PURPOSE INTERFACE "Corrigeer rechten"
{+Deze interface is gelijk aan de interface Beheren Medewerkers en rechten,
met als verschil dat in deze interface wél de rechten in het verleden aangepast kunnen worden.

Deze interface mag alleen in zeer uitzonderlijke gevallen worden aangeroepen,
namelijk alleen wanneer er rechten uit het verleden gecorrigeerd moeten worden.
Daarom moet de interface ook voorzien worden van passende waarschuwingen.
Correcties in uitbetalingen, die voortvloeien uit dit soort correcties, worden niet door de DTV-applicatie naar P-Direkt verstuurd.
Deze mutaties moeten daarom met de hand in P-Direkt worden verwerkt.
-}
INTERFACE "Corrigeer rechten" CLASS IF
          ( persoon , begindatum[MedewerkerRecht*Datum], einddatum[MedewerkerRecht*Datum], recht)
          FOR "HR": '_SESSION';ingelogd;rol;rol~;ingelogd~
BOX [ "Ingelogd als" : I -- COLS ["Op dit moment ingelogd" : ingelogd, "rol" : ingelogd;rol]
    , Medewerkers : ingelogd;-I[Account];account~
      BOX [ "gebruiker : " : I
          , naam : volledigenaam
          , "maak of wijzig recht" : persoon~
            BOX [ recht : recht
                , "geldig vanaf : " : begindatum
                , "geldig tot : " : einddatum
                ]
          ]
    ]


--INTERFACE "Overzicht rechten"
--          FOR "HR":V[SESSION*Personeelsnummer] INTERFACE "Overzicht rechten"

INTERFACE "Overzicht rechten" : I[Personeelsnummer]
BOX [ "medewerker : " : I
      BOX [ personeelsnummer : I
     --     , naam : volledigenaam
          ]
    , rechten : persoon~
      BOX [ "1" : recht;refLijst;'Salarisschalen';refLijst~;recht~/\I
            BOX [ "Salarisschaal : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          , "2" : recht;refLijst;'Categorieën';refLijst~;recht~/\I
            BOX [ "Categorie : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          , "3" : recht;refLijst;'Toelagen';refLijst~;recht~/\I
            BOX [ "Overig : " : I
                  BOX [ "geldig vanaf : " : begindatum
                      , "geldig tot : " : einddatum
                      , recht : recht
                      ]
                ]
          ]
    ]




ENDCONTEXT