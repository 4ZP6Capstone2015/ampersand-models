CONTEXT Tijdverantwoording IN DUTCH LATEX
META "authors" "Stef Joosten"
INCLUDE "History.adl"
INCLUDE "Arbeidsmodaliteiten.adl"
INCLUDE "Referentielijsten.xlsx"

CONCEPT Referentielijst "Een referentielijst is een stamtabel in de DTV-applicatie, waarin een functioneel beheerder vaste gegevens in variabelen kan opslaan."
CONCEPT StamItem "Een stam-item is een element uit een referentielijst."
CLASSIFY StamItem ISA Variabele

RELATION refLijst[StamItem*Referentielijst] [UNI,TOT]

PROCESS "Update variables"
CONCEPT Wetsartikel "Een verwijzing naar een artikel in de wet of in een (beleids-)regel met kracht van wet."
RELATION varGrond[StamItem*Wetsartikel] [UNI]
  MEANING "De rechtsgrond wordt in een variabele bijgehouden."
RELATION mutGrond[Mutatie*Wetsartikel] [UNI]
  MEANING "De rechtsgrond van elke variabele wordt tevens in een mutatie wordt bijgehouden."
RELATION varNaam[StamItem*VariableName] [UNI,TOT]
  MEANING "De naam wordt in een variabele bijgehouden."
RELATION mutNaam[Mutatie*VariableName] [UNI]
  MEANING "De naam van elke waarde wordt in een mutatie bijgehouden."
ROLE FAB MAINTAINS "TOT refLijst::StamItem*Referentielijst", "TOT varNaam::StamItem*VariableName"
REPRESENT VariableName TYPE ALPHANUMERIC

ROLE ExecEngine MAINTAINS updateGrond
RULE updateGrond : varGrond |- trace;mutGrond
MEANING "Het wijzigen van de juridische grondslag van een variabele dient in een mutatie te worden geregistreerd." 
VIOLATION ( TXT "{EX} NewStruct;Mutatie"
          , TXT ";var;Mutatie;_NEW;Variabele;", SRC I[StamItem]
          , TXT ";mutNaam;Mutatie;_NEW;VariableName;", SRC trace;mutNaam
          , TXT ";mutGrond;Mutatie;_NEW;Wetsartikel;", TGT I
          , TXT ";pred;Mutatie;_NEW;Mutatie;", SRC trace
          , TXT ";trace;Variabele;", SRC I[StamItem], TXT ";Mutatie;_NEW"
          , TXT ";mutDatum;Mutatie;_NEW;MutDatum;{php}date(DATE_ISO8601)"
          )

ROLE ExecEngine MAINTAINS updateNaam
RULE updateNaam : varNaam |- trace;mutNaam
MEANING "Het wijzigen van de naam van een variabele dient in de meest recente mutatie te worden geregistreerd."
VIOLATION ( TXT "{EX} NewStruct;Mutatie"
          , TXT ";var;Mutatie;_NEW;Variabele;", SRC I[StamItem]
          , TXT ";mutNaam;Mutatie;_NEW;VariableName;", TGT I
          , TXT ";mutGrond;Mutatie;_NEW;Wetsartikel;", SRC trace;mutGrond
          , TXT ";pred;Mutatie;_NEW;Mutatie;", SRC trace
          , TXT ";trace;Variabele;", SRC I[StamItem], TXT ";Mutatie;_NEW"
          , TXT ";mutDatum;Mutatie;_NEW;MutDatum;{php}date(DATE_ISO8601)"
          )

ENDPROCESS

PURPOSE CONCEPT Referentielijst
{+Om een functioneel beheerder de gelegenheid te geven de applicatie aan te passen, krijgt hij of zij de mogelijkheid om variabelen aan te passen.
Daarvan wordt wel de geschiedenis bijgehouden, zodat achteraf is na te gaan welke variabelen er op enig moment waren
en welke veranderingen een variabele heeft ondergaan gedurende zijn leven.
-}

PURPOSE CONCEPT StamItem
{+Stam-items zijn bedoeld om vaste waarden in de software te kunnen veranderen zonder aan de software iets te veranderen.
Dit stelt een functioneel beheerder in de gelegenheid om de applicatie aan te passen.
Daarvan wordt wel de geschiedenis bijgehouden, zodat achteraf is na te gaan welke variabelen er op enig moment waren
en welke veranderingen een variabele heeft ondergaan gedurende zijn leven.
-}

CONCEPT Dagsoort "W (werkdag), Z(za-zo) of F(feestdag)."
PURPOSE CONCEPT Dagsoort
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toelagepercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}

PURPOSE RELATION mutNaam[Mutatie*VariableName]
{+Om veranderingen in de naam te kunnen signaleren, wordt de naam ook in mutaties bijgehouden.-}
PURPOSE RELATION varNaam[StamItem*VariableName]
{+Van elke waarde dient een naam te hebben-}
PURPOSE RELATION varGrond[StamItem*Wetsartikel]
{+De rechtsgrond van elke variabele dient in het systeem bekend te zijn,
zodat een auditor de rechtmatigheid kan traceren vanuit een juridische werkelijkheid.
-}
PURPOSE RELATION mutGrond[Mutatie*Wetsartikel]
{+Om veranderingen in de grond te kunnen signaleren, wordt de grond ook in mutaties bijgehouden.-}
PURPOSE RULE updateGrond
{+Om edit acties op de grond van een variabele te detecteren,
verlangen we dat de juridische grond van een variabele gelijk is aan de juridische grond van de eerste (dus meest recente) mutatie in de trace van deze variabele.
Als dat niet zo is, dan moet er een nieuwe mutatie worden gemaakt, de de nieuwe grond registreert.-}
PURPOSE RULE updateNaam
{+Om edit acties op de naam van een variabele te detecteren,
verlangen we dat de naam van een variabele gelijk is aan de naam van de meest recente mutatie van deze variabele.
Als dat niet zo is, dan moet er een nieuwe mutatie worden gemaakt, de de nieuwe naam registreert.-}

{-
PURPOSE RULE "Unieke naam in referentielijsten"
{+Om eenduidige taal te krijgen, mag elk element dat niet verwijderd is slechts één keer in een referentielijst voorkomen.
-}

--RULE "Unieke naam in referentielijsten" : ((varNaam;varNaam~ /\ refLijst;refLijst~) - deleted;V[StamItem*StamItem]) - V[StamItem*StamItem];deleted |- I[StamItem]
--MEANING "Elk element in een referentielijst dient van een unieke naam voorzien te zijn."
--MESSAGE "Elementen in een referentielijst moeten een unieke naam hebben."
--VIOLATION (TXT "De naam ", SRC varNaam, TXT " is al in gebruik.")

VIEW Variabele : Variabele(varNaam, TXT " (uit: ", refLijst, TXT ")")

INTERFACE RefLijsten (refLijst, deleted, varNaam, varGrond) FOR FAB : '_SESSION'[SESSION]#I[Referentielijst]
 COLS [ "Actueel": I LINKTO INTERFACE ReferentielijstEdit
      ]

INTERFACE Geschiedenis FOR FAB : '_SESSION'[SESSION]#I[Referentielijst]
 COLS [ "Geschiedenis": I LINKTO INTERFACE ReferentielijstGeschiedenis
      ]

INTERFACE Verwijderd FOR FAB : ('_SESSION'[SESSION]#I[StamItem]);deleted INTERFACE "overzicht Stam-items"

INTERFACE ReferentielijstEdit (refLijst, deleted, varNaam, varGrond) FOR FAB : I[Referentielijst]
 ROWS [ "Actueel" : refLijst~;(I[StamItem]-deleted)
          COLS[ naam: varNaam
              , mutGrond: varGrond
              , verwijderen : deleted <VariableDeletedCheckbox>
              ]
      , "Geschiedenis van": I LINKTO INTERFACE ReferentielijstGeschiedenis
      ]

INTERFACE ReferentielijstGeschiedenis FOR FAB : I[Referentielijst]
 ROWS [ "Geschiedenis" : refLijst~;(I-deleted) INTERFACE "overzicht Stam-items"
      ]

INTERFACE "overzicht Stam-items" FOR FAB : I[StamItem]
 COLS[ value: varNaam
     , mutGrond: varGrond
     , ""   : var~
       COLS[ mutatiedatum : mutDatum
--         , mutatie : I
--         , pred : pred
           , naam : mutNaam
           , grond : mutGrond
           , status : (I /\ trace~;trace);V;'Deleted value'[VariableName] \/ (I - trace~;trace);V;'Archived value'[VariableName]
           ]
     ]

INTERFACE "nieuwe Stam-items" (refLijst, varNaam, varGrond) FOR FAB : I[Variabele]
COLS [ lijst : refLijst
     , naam  : varNaam
     , grond : varGrond
     ]

VIEW VariableDeletedCheckbox: Variabele { value : I } HTML TEMPLATE "VariableDeletedCheckbox.html" ENDVIEW
RELATION deleted[Variabele*Variabele] [PROP]
MEANING "A variable may have the property of being deleted."

VIEW StatusIcon : VariabeleNaam {value: I} HTML TEMPLATE "StatusIcon.html" ENDVIEW

ENDCONTEXT
