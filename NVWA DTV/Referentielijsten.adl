CONTEXT Tijdverantwoording IN DUTCH LATEX
--META "authors" "Stef Joosten"
INCLUDE "History.adl"
INCLUDE "Referentielijsten.xlsx"

CONCEPT Referentielijst "Een referentielijst is een stamtabel in de DTV-applicatie, waarin een functioneel beheerder vaste gegevens in variabelen kan opslaan."
PURPOSE CONCEPT Referentielijst
{+Om een functioneel beheerder de gelegenheid te geven de applicatie aan te passen, krijgt hij of zij de mogelijkheid om variabelen aan te passen.
Daarvan wordt wel de geschiedenis bijgehouden, zodat achteraf is na te gaan welke variabelen er op enig moment waren
en welke veranderingen een variabele heeft ondergaan gedurende zijn leven.
-}

RELATION refLijst[Variable*Referentielijst] [UNI,TOT]
CONCEPT Wetsartikel "Een verwijzing naar een artikel in de wet of in een (beleids-)regel met kracht van wet."
PROCESS "Update variables"
PURPOSE RELATION varGrond[Variable*Wetsartikel]
{+De rechtsgrond van elke variabele dient in het systeem bekend te zijn,
zodat een auditor de rechtmatigheid kan traceren vanuit een juridische werkelijkheid.
-}
RELATION varGrond[Variable*Wetsartikel] [UNI]
MEANING "De rechtsgrond wordt in een variabele bijgehouden."

PURPOSE RELATION mutGrond[Mutation*Wetsartikel]
{+Om veranderingen in de grond te kunnen signaleren, wordt de grond ook in mutaties bijgehouden.-}
RELATION mutGrond[Mutation*Wetsartikel] [UNI]
MEANING "De rechtsgrond van elke variabele wordt tevens in een mutatie wordt bijgehouden."

PURPOSE RULE updateGrond
{+Om edit acties op de grond van een variabele te detecteren,
verlangen we dat de juridische grond van een variabele gelijk is aan de juridische grond van de eerste (dus meest recente) mutatie in de trace van deze variabele.
Als dat niet zo is, dan moet er een nieuwe mutatie worden gemaakt, de de nieuwe grond registreert.-}
ROLE ExecEngine MAINTAINS updateGrond
RULE updateGrond : varGrond |- trace;mutGrond
MEANING "Het wijzigen van de juridische grondslag van een variabele dient in een mutatie te worden geregistreerd." 
VIOLATION ( TXT "{EX} NewStruct;Mutation"
          , TXT ";var;Mutation;NULL;Variable;", SRC I
          , TXT ";mutNaam;Mutation;NULL;VariabeleNaam;", SRC trace;mutNaam
          , TXT ";mutGrond;Mutation;NULL;Wetsartikel;", TGT I
          , TXT ";pred;Mutation;NULL;Mutation;", SRC trace
          , TXT ";trace;Variable;", SRC I, TXT ";Mutation;NULL"
          )

PURPOSE RELATION varNaam[Variable*VariabeleNaam]
{+Van elke waarde dient een naam te hebben-}
RELATION varNaam[Variable*VariabeleNaam] [UNI,TOT]
MEANING "De naam wordt in een variabele bijgehouden."

PURPOSE RELATION mutNaam[Mutation*VariabeleNaam]
{+Om veranderingen in de naam te kunnen signaleren, wordt de naam ook in mutaties bijgehouden.-}
RELATION mutNaam[Mutation*VariabeleNaam] [UNI]
MEANING "De naam van elke waarde wordt in een mutatie bijgehouden."

PURPOSE RULE updateNaam
{+Om edit acties op de naam van een variabele te detecteren,
verlangen we dat de naam van een variabele gelijk is aan de naam van de meest recente mutatie van deze variabele.
Als dat niet zo is, dan moet er een nieuwe mutatie worden gemaakt, de de nieuwe naam registreert.-}
ROLE ExecEngine MAINTAINS updateNaam
RULE updateNaam : varNaam |- trace;mutNaam
MEANING "Het wijzigen van de naam van een variabele dient in de meest recente mutatie te worden geregistreerd." 
VIOLATION ( TXT "{EX} NewStruct;Mutation"
          , TXT ";var;Mutation;NULL;Variable;", SRC I
          , TXT ";mutNaam;Mutation;NULL;VariabeleNaam;", TGT I
          , TXT ";mutGrond;Mutation;NULL;Wetsartikel;", SRC trace;mutGrond
          , TXT ";pred;Mutation;NULL;Mutation;", SRC trace
          , TXT ";trace;Variable;", SRC I, TXT ";Mutation;NULL"
          )

ROLE FAB MAINTAINS "TOT refLijst::Variable*Referentielijst", "TOT varNaam::Variable*VariabeleNaam"

ENDPROCESS

CONCEPT Dagsoort "W (werkdag), Z(za-zo) of F(feestdag)."
PURPOSE CONCEPT Dagsoort
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toelagepercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}

PURPOSE RULE "Unieke naam in referentielijsten"
{+Om eenduidige taal te krijgen, mag elk element maar één keer in een referentielijst voorkomen.-}

RULE "Unieke naam in referentielijsten" : ((varNaam;varNaam~ /\ refLijst;refLijst~) - deleted;V) - V;deleted |- I[Variable]
MEANING "Elke element in een referentielijst dient van een unieke naam voorzien te zijn."
MESSAGE "Elementen in een referentielijst moeten een unieke naam hebben."
VIOLATION (TXT "De naam ", SRC varNaam, TXT " is al in gebruik.")

VIEW Variable : Variable(varNaam, TXT " (uit: ", refLijst, TXT ")")

INTERFACE RefLijsten FOR FAB : '_SESSION'[SESSION]#I[Referentielijst]
 COLS [ "Actueel": I LINKTO INTERFACE ReferentielijstEdit
      ]

INTERFACE Geschiedenis FOR FAB : '_SESSION'[SESSION]#I[Referentielijst]
 COLS [ "Geschiedenis": I LINKTO INTERFACE ReferentielijstGeschiedenis
      ]

INTERFACE Verwijderd FOR FAB : ('_SESSION'[SESSION]#I[Variable]);deleted INTERFACE Variables

INTERFACE ReferentielijstEdit (refLijst, deleted, varNaam, varGrond) FOR FAB : I[Referentielijst]
 ROWS [ "Actueel" : refLijst~;(I /\ -deleted)
          COLS[ value: varNaam
              , mutGrond: varGrond
              , verwijderen : deleted <VariableDeletedCheckbox>
              ]
      , "Geschiedenis van": I LINKTO INTERFACE ReferentielijstGeschiedenis
      ]

INTERFACE ReferentielijstGeschiedenis FOR FAB : I[Referentielijst]
 ROWS [ "Geschiedenis" : refLijst~;(I-deleted) INTERFACE Variables
      ]

INTERFACE Variables FOR FAB : I[Variable]
 COLS[ value: varNaam
     , mutGrond: varGrond
     , ""   : var~
       COLS[ mutatie : I
           , pred : pred
           , naam : mutNaam
           , mutGrond : mutGrond
           , status : (I /\ trace~;trace);V;'Deleted value'[VariabeleNaam] \/ (I - trace~;trace);V;'Archived value'[VariabeleNaam]
           ]
     ]

INTERFACE newVariable (refLijst, varNaam, varGrond) FOR FAB : I[Variable]
COLS [ lijst : refLijst
     , naam  : varNaam
     , grond : varGrond
     ]

VIEW VariableDeletedCheckbox: Variable { value : I } HTML TEMPLATE "VariableDeletedCheckbox.html" ENDVIEW
RELATION deleted[Variable*Variable] [PROP]
MEANING "A variable may have the property of being deleted."

VIEW StatusIcon : VariabeleNaam {value: I} HTML TEMPLATE "StatusIcon.html" ENDVIEW

ENDCONTEXT
