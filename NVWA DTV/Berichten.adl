CONTEXT Berichten IN DUTCH LATEX

META "authors" "Stef Joosten, Ferlon Illes"

PURPOSE INTERFACE "Periodebatch"
{+De DTV-applicatie maakt periodeverantwoordingen direct nadat hij de definitieve c.q. geaccordeerde dagverantwoordingen van een periode heeft opgehaald uit SPIN.
Dit gebeurt in een periodebatch, waarbij de DTV-applicatie een vraag stelt aan SPIN om dagverantwoordingen te sturen.
Daarop stuurt SPIN een periodebatch terug.
De interface "Periodebatch" wordt dus door SPIN gebruikt om de benodigde gegevens in DTV te laden.
-}
INTERFACE "Periodebatch" CLASS Message
    ( in[Dagverantwoording*Periodebatch]
    , datum
    , dagsoort
    , personeelsnummer[Dagverantwoording*Personeelsnummer]
    , dag[Tijdverantwoording*Dagverantwoording]
    , dag[Dienst*Dagverantwoording]
    , tijdrubriek
    , roosterSoort[Dienst*Roostersoort]
    , begin
    , eind
    , tijdsduur[Tijdvak * Tijdsduur]
    )
    FOR SPIN : I[Periodebatch]
      BOX [ dagverantwoording : in~
            COLS[ personeelsnummer : personeelsnummer
                , datum : datum
                , volledigenaam : volledigenaam
                , dagsoort : dagsoort
                , tijdverantwoordingen : dag[Tijdverantwoording*Dagverantwoording]~
                  ROWS[ tijdrubriek : tijdrubriek
                      , begintijd : begin
                      , eindtijd : eind
                      , tijdsduur : tijdsduur
                      ]
                , diensten : dag[Dienst*Dagverantwoording]~
                  ROWS[ roosterSoort : roosterSoort
                      , begintijd : begin
                      , eindtijd : eind
                      ]
                ]
          ]

INTERFACE Tijdverantwoording CLASS Message
      ( begin[Tijdvak*DateTime]   --  eigenlijk:  [Tijdverantwoording*DateTime]
      , eind[Tijdvak*DateTime]   --  eigenlijk:  [Tijdverantwoording*DateTime]
      , tijdrubriek
      ) FOR DTV : I[Tijdverantwoording]
BOX [ begintijd :   begin[Tijdvak*DateTime]   --  eigenlijk:  [Tijdverantwoording*DateTime]
    , eindtijd :    eind[Tijdvak*DateTime]   --  eigenlijk:  [Tijdverantwoording*DateTime]
    , tijdrubriek : tijdrubriek
    ]

PURPOSE INTERFACE "P-Direkt periodebestand"
{+Dit bericht wordt door de DTV-applicatie als bestand klaargezet, en bevat een aantal toelagerecords in het XML formaat van P-Direkt.

Dit bestand bevat alle resultaten, die voor P-Direkt bestemd zijn. Dat wil zeggen: elk resultaat uit de te verwerken periodeverantwoordingen,
waarvan de resultaatsoort 'p-direkt' is, wordt hierin meegenomen. Van deze resultaten bevat het bericht:
\begin{enumerate}
\item	Personeelsnummer
\item	P-Direktcode
\item	De datum tot en met welke de verantwoording definitief is.
\item	Het aantal uren dat uitbetaald moet worden onder de genoemde P-Direktcode.
\end{enumerate}
Het personeelsnummer is van de medewerker over wie de periodeverantwoording gaat.
De P-Direktcode en het aantal uren komen uit het resultaat.
De datum is de einddatum van de verantwoordingsperiode van de periodeverantwoording.
-}
INTERFACE "P-Direkt periodebestand" CLASS Message : I[PDirektBestand]
BOX [ toelagerecords : periodebestand~;goedresultaat~;(resultaatSoort;'p-direkt';V /\I[Resultaat])  -- Resultaat
      BOX [ Personeelsnummer : goedresultaat;personeelsnummer[Periodeverantwoording*Personeelsnummer]
          , Code : code
          , "Datum_tot_met" : goedresultaat;verantwoordingsperiode;eind;date
          , Uren : uren
          ]
    ]


PURPOSE INTERFACE PeriodeverantwoordingKODE
{+Dit bericht gaat van de DTV-applicatie naar de kennismotor, en bevat één periodeverantwoording.
De bedoeling is dat de kennismotor hierop antwoordt met een bericht ``berekenen resultaten''.

Dit bericht bevat:
\begin{itemize}
\item	de verantwoordingsperiode;
\item	het personeelsnummer van de medewerker (= de afzender van het bericht);
\item	alle rechten van de medewerker, waarvan de verantwoordingsperiode en de geldigheidsperiode overlap vertonen;
	Hiervan worden begin- en einddatum en het recht in het bericht opgenomen.
\item	Alle actuele urentegoeden van de betreffende medewerker.
	Van elk tegoed wordt de urenpool, het tegoed (aantal uren) en de datum vanaf welke dat tegoed geldig is in het bericht opgenomen.
\item	Alle dagverantwoordingen, die in de periodeverantwoording voorkomen.
\end{itemize}
-}
INTERFACE PeriodeverantwoordingKODE CLASS Message
    FOR DTV : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : personeelsnummer
    , MedewerkerRechten :  personeelsnummer;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ begindatum : begindatum
          , einddatum :  einddatum
          , rechten :    recht
          ]
    , Urentegoeden : personeelsnummer;medewerker
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~
      BOX [ datum :                     datum
          , verroosterd :               dag[Dienst*Dagverantwoording]~ 
            BOX [ roostersoort : roosterSoort
                , begintijd :    begin
                , eindtijd :     eind
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ begintijd :   begin
                , eindtijd :    eind
                , tijdrubriek : tijdrubriek
                ]
          ]
    ]


ENDCONTEXT