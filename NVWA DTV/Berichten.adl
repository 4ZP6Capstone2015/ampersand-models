CONTEXT Berichten IN DUTCH LATEX

META "authors" "Stef Joosten, Ferlon Illes"

--THEMES Berichten

PATTERN Berichten
PURPOSE PATTERN "Berichten"
{+
-}

--------------------
-- Specialisaties --
--------------------

CONCEPT Bericht "Een bericht is een object dat fysiek verplaatst wordt van of namens een persoon naar een andere met de bedoeling om informatie over te dragen."
PURPOSE CONCEPT Bericht
{+ Voor het woord ``Bericht'' is een definitie nodig die het mogelijk maakt te spreken over afzender en ontvanger.
Ook willen we kunnen spreken over de gebeurtenis van het verzenden c.q. ontvangen.
De fysieke verplaatsing omvat uiteraard ook elektronisch overgedragen berichten,
maar sluit paranormale communicatie uit.
-}

RELATION aan[Bericht*Personeelsnummer] PRAGMA "" " is gericht aan "
MEANING "Elke persoon aan wie een bericht is gericht, wordt geregistreerd."
PURPOSE RELATION aan[Bericht*Personeelsnummer]
{+Een bericht kan aan meerdere personen gericht zijn. Zolang het bericht niet verzonden is, hoeft een geaddresseerde niet geregistreerd te staan.
-}

RELATION verzonden[Bericht*DateTime] [UNI] PRAGMA "" " is verzonden op "
MEANING "Het tijdstip waarop een bericht is verzonden wordt geregistreerd."
PURPOSE RELATION verzonden[Bericht*DateTime]
{+Omdat het verzenden van een bericht juridisch relevant kan zijn, registreert het systeem het tijdstip van verzenden.
-}

RELATION ontvangen[Bericht*DateTime] [UNI] PRAGMA "" " is ontvangen op "
MEANING "Het tijdstip waarop een bericht is ontvangen wordt geregistreerd."
PURPOSE RELATION ontvangen[Bericht*DateTime]
{+Omdat het verzenden van een bericht juridisch relevant kan zijn, registreert het systeem het tijdstip van ontvangst.
-}

afkomstigVan :: Bericht * Personeelsnummer [UNI] PRAGMA "" " is afkomstig van "
MEANING "De afzender van elk ontvangen bericht kan worden geregistreerd."
PURPOSE RELATION afkomstigVan [Bericht*Personeelsnummer]
{+ Omdat de afzender van elk ontvangen bericht aangesproken kan worden op hetgeen hij/zij beweert,
dient het systeem de ontvanger te registreren.
-}

PURPOSE RULE "berichtZonderAfzender"
{+ Berichten kunnen niet verzonden worden zonder afzender.
-}
RULE berichtZonderAfzender: verzonden;verzonden~ /\ I[Bericht] |- afkomstigVan;afkomstigVan~
MEANING "Berichten hebben altijd een bron."
MESSAGE "Ontvangen berichten zonder afzender:"
VIOLATION (TXT "Bericht ",SRC I)

ENDPATTERN

ENDCONTEXT