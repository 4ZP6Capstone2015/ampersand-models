CONTEXT DTV IN DUTCH LATEX
INCLUDE "Gebeurtenissen.adl"

PATTERN Dagverantwoordingen
PURPOSE PATTERN Dagverantwoordingen
{+Een dagverantwoording is de verantwoording van werk-/pauze- en rusttijden op één dag.
-} 
VIEW Dagverantwoording : Dagverantwoording(TXT "DV:", datum)
RELATION datum[Dagverantwoording*Datum] [UNI,TOT] PRAGMA "" " is de verantwoording van dag "
MEANING "Elke dagverantwoording gaat over één dag."
PURPOSE RELATION datum[Dagverantwoording*Datum]  REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Omdat elke medewerker per dag zijn of haar tijd verantwoordt, kent elke dagverantwoording een datum.
-}

RELATION dagsoort[Dagverantwoording*Dagsoort] [UNI,TOT]
MEANING "Elke dagverantwoording kent een aanduiding F, W, of Z voor de dagsoort."
PURPOSE RELATION dagsoort[Dagverantwoording*Dagsoort]
{+Omdat SPIN bijhoudt welke dagen feestdagen zijn, en we die informatie niet zowel in SPIN als in KODE willen administreren,
laten we SPIN aan KODE vertellen welke dagen Zon- en Feestdagen zijn.
-}
ENDPATTERN

PATTERN Diensten
PURPOSE PATTERN Diensten
{+Als er sprake is van wisselende werktijden in verband met vroege-, avond- en nachtdiensten,
werken op zaterdag en zondag
dan wel als door de aard van het voorhanden werk de individuele medewerker niet of nauwelijks invloed Perheeft op de begin- en eindtijd waarop dat werk moet worden verricht,
wordt volgens een voor die diensten vastgesteld rooster gewerkt (WTR Artikel 3 lid 1).
Iedere medewerker kan in overleg met de leidinggevende voorkeuren aangeven om op specifieke weekdagen een specifiek aantal uren te werken.
Dit wordt ``modaliteit'' (ook ``arbeidsmodaliteit'') genoemd, en staat beschreven in de circulaire invoering 36-urige werkweek.
Duurzame tijdverantwoording heeft informatie nodig over het rooster van elke medewerker, om te kunnen bepalen op welke roostersoort een tijdvakresultaat kan worden geboekt.
Deze informatie wordt met de dagverantwoording uit SPIN meegestuurd naar de DTV-applicatie.
-}

PURPOSE CONCEPT Dienst REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, pg. 14."
{+ Het werkrooster bepaalt welke regels worden toegepast tijdens de verwerking van tijdverantwoordingen.
Het rooster is opgebouwd uit diensten die per tijdvak aangeven wat de geplande activiteit voor die werknemer is.
De informatieanalyse en SPIN spreken van roosterregel als synoniem van dienst. 
-}
CONCEPT Dienst "Een dienst is een aaneengesloten periode, inclusief de voorgeschreven pauze, waarin arbeid wordt verricht." "WTR Artikel 1 lid 7"
--CONCEPT Dienst "Een dienst is een aaneengesloten periode waarin arbeid wordt verricht en die gelegen is tussen twee opeenvolgende onafgebroken rusttijden van ten minste 8 uren." "Arbeidstijdenwet Artikel 1:7 lid 1 sub c"
CLASSIFY Dienst ISA Tijdvak

CONCEPT Nachtdienst "een dienst waarin meer dan een uur arbeid wordt verricht tussen 00.00 uur en 06.00 uur." "Arbeidstijdenwet Artikel 1:7 lid 1 sub d"
CONCEPT Pauze "een periode van ten minste 15 achtereenvolgende minuten, waarmee de arbeid tijdens de dienst wordt onderbroken en de werknemer geen enkele verplichting heeft ten aanzien van de bedongen arbeid." "Arbeidstijdenwet Artikel 1:7 lid 1 sub e"
CONCEPT Consignatie "een periode tussen twee opeenvolgende diensten of tijdens een pauze, waarin de werknemer uitsluitend verplicht is bereikbaar te zijn om in geval van onvoorziene omstandigheden op oproep zo spoedig mogelijk de bedongen arbeid te verrichten." "Arbeidstijdenwet Artikel 1:7 lid 1 sub g"
CONCEPT Arbeidstijd "Arbeidstijd is de tijd dat de werknemer onder gezag van de werkgever arbeid verricht." "Arbeidstijdenwet Artikel 1:7 lid 1 sub k"
CONCEPT Rusttijd "Rusttijd is de tijd die geen arbeidstijd is." "Arbeidstijdenwet Artikel 1:7 lid 1 sub l"

{- Definities uit de werktijdenregeling, inclusief toelichting
PURPOSE CONCEPT "Ambulante medewerker" REF "Toelichting Werktijdenregeling NVWA, hfst 2"
{+Medewerker zoals genoemd in `bijlage 1 van de Overeenkomst Arbeidsvoorwaarden NVWA'van 8 juli2011 met de daarbij behorende conversietabel van de regeling dienstauto's NVWA.
-}
CONCEPT Roostersoort "De string, die door SPIN als roostersoort wordt aangeleverd."
RELATION roosterSoort[Dienst*Roostersoort] [UNI,TOT] PRAGMA "" " valt onder roostersoort "
MEANING "Binnen een dienst is een bepaalde roostersoort van toepassing."
PURPOSE RELATION roosterSoort[Dienst*Roostersoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, p14."
{+De roostersoort bepaalt welke regels worden toegepast op tijdverantwoordingen.
-}
RELATION dag[Dienst*Dagverantwoording] [UNI,TOT]  PRAGMA "" " is onderdeel van dagverantwoording " "" -- [UNI,TOT]
MEANING "Elke dienst is onderdeel van een dagverantwoording."
PURPOSE RELATION dag[Dienst*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een dienst heeft betrekking op werkzaamheden die op een bepaalde dag moeten worden uitgevoerd en is daarmee onderdeel van een dagverantwoording.
-}

{- HJO, 20150610: begintijd en eindtijd zijn vervangen door begin resp. eind uit het generiekere concept `Tijdvak`. 
RELATION begintijd[Dienst*DateTime] [UNI,TOT] PRAGMA "" " heeft als begintijdstip " ""  -- [UNI,TOT]
MEANING "Van elke dienst wordt een begintijdstip geregistreerd."
RELATION eindtijd[Dienst*DateTime] [UNI] PRAGMA "" " heeft als eindtijdstip " ""  -- [UNI,TOT]
MEANING "Van elke dienst wordt een eindtijdstip geregistreerd."
-}
--RULE dienst : dag~;begin |- dag~;begin
--MEANING "Voor elke dag en begintijd van een dienst moet er een dag en begintijd van een dagverantwoording zijn."
--VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")
ENDPATTERN

PROCESS AutomatischBerekenenBinnen
RELATION binnen[Dienst*Verantwoordingsperiode] [UNI,AUT] PRAGMA "" " valt binnen verantwoordingsperiode "
MEANING "Of een dienst binnen een verantwoordingsperiode valt, wordt door de computer uitgerekend."
-- RULE dienstBinnenVerantwoordingsperiode : binnen = dag;datum;geqDatum;begindatum~ /\ dag;datum;geqDatum~;einddatum~
-- Stap 2: Deze invariant in stand houden doe je door:
-- Stap 2a: bijpopuleren
ROLE ExecEngine MAINTAINS insbinnen
RULE insbinnen: dag;datum;geqDatum;date~;begin~ /\ dag;datum;geqDatum~;date~;eind~ |- binnen
VIOLATION (TXT "{EX} InsPair;binnen;Dienst;", SRC I, TXT ";Verantwoordingsperiode;", TGT I[Tijdvak])
-- Stap 2a: afpopuleren
ROLE ExecEngine MAINTAINS delbinnen
RULE delbinnen: binnen |- dag;datum;geqDatum;date~;begin~ /\ dag;datum;geqDatum~;date~;eind~
VIOLATION (TXT "{EX} DelPair;binnen;Dienst;", SRC I, TXT ";Verantwoordingsperiode;", TGT I[Tijdvak])
ENDPROCESS

CLASSIFY Verantwoordingsperiode ISA Tijdvak
CONCEPT Verantwoordingsperiode "Een verantwoordingsperiode is een tijdvak van 28 dagen, te rekenen vanaf de door de NVWA vastgestelde begindata."
PURPOSE CONCEPT Verantwoordingsperiode REF "BBRA Artikel 23"
{+De NVWA berekent overwerk, toelagen en persoonlijke meeruren in tijdvakken van 28 dagen.
Deze periodes worden Verantwoordingsperiode genoemd.
De werkperiode, waarvan in de definitie van overwerk sprake is, wordt gesteld op één dag, indien aanvang en einde van de werktijd in de regel niet aan wisselingen onderhevig zijn.
Indien de tijdstippen van aanvang en einde van de werktijd volgens een tevoren vastgesteld rooster wisselen, wordt deze werkperiode gesteld op de verantwoordingsperiode.
-}

ENDCONTEXT
