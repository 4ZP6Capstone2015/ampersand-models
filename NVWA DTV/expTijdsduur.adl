CONTEXT Optelling IN DUTCH

--$ Tijsleuf-relaties moeten wel multipliciteiten hebben (anders krijg je straks gekke resultaten -probeer maar eens)
RELATION begintijd[Tijdvak*Getal] [UNI]
RELATION eindtijd[Tijdvak*Getal] [UNI]
RELATION duur[Tijdvak*Getal] [UNI]
VIEW tijdsleufview : Tijdvak(TXT "van ", begintijd, TXT " tot ", eindtijd, TXT "; duur: ", duur )

PROCESS Uitrekenen
RELATION l[Optelling*Getal] [UNI,TOT]
RELATION r[Optelling*Getal] [UNI]
--$Onderstaande relatie mag niet TOT zijn, omdat er geen garanties zijn dat de som bestaat en we geen zin hebben om geconfronteerd te worden met een foutmelding waar niemand, maar dan ook niemand iets mee kan.
RELATION som[Optelling*Getal] [UNI]
VIEW optellingview : Optelling(l, TXT "+", r, TXT " = ", som )

--$Optellingen moeten uniek gekarakteriseerd zijn door zijn argumenten (l en r)
IDENT Optellingen: Optelling(l,r)

RELATION berekening[Tijdvak*Optelling] [UNI]

--$Onderstaande regel kan niet volledig geautomatiseerd worden bijgehouden - er is user input nodig om de gevolgen te kunnen berekenen van veranderingen in waardes van begintijd, eindtijd of duur; deze user input zegt dan welke van de andere variabelen vast blijft staan.
--RULE uitrekenen : I[Tijdvak] |- (begintijd;l~ /\ eindtijd;som~ /\ duur;r~);V
--ROLE rekenmachien MAINTAINS uitrekenen

--$We vervangen deze invariant dus door een andere, die altijd de 'duur' berekent c.q. aanpast als een begintijd en eindtijd zijn gegeven c.q. veranderen. Dat gaat in de volgende 3 stappen:
--1: het creeeren van het Optelling-atoom:
ROLE ExecEngine MAINTAINS berekenEindtijd
RULE berekenEindtijd: I /\ begintijd;begintijd~ /\ duur;duur~ |- begintijd;l~;r;duur~
MEANING "Als een sommetje kan worden berekend, moet daar een berekeningsatoom voor zijn."
VIOLATION (TXT "{EX} NewStruct;Optelling" -- maak een nieuw atoom in concept Optelling
                ,TXT ";l;Optelling;NULL;Getal;", SRC begintijd -- vul relatie l in vanaf dit nieuwe atoom
                ,TXT ";r;Optelling;NULL;Getal;", TGT duur      -- vul relatie r in vanaf dit nieuwe atoom
          )

ROLE ExecEngine MAINTAINS berekenDuur
RULE berekenDuur: I /\ begintijd;begintijd~ /\ eindtijd;eindtijd~ |- begintijd;l~;som;eindtijd~
MEANING "Als een sommetje kan worden berekend, moet daar een berekeningsatoom voor zijn."
VIOLATION (TXT "{EX} NewStruct;Optelling" -- maak een nieuw atoom in concept Optelling
                ,TXT ";l;Optelling;NULL;Getal;", SRC begintijd -- vul relatie l in vanaf dit nieuwe atoom
                ,TXT ";som;Optelling;NULL;Getal;", TGT eindtijd      -- vul relatie som in vanaf dit nieuwe atoom
          )

--2: het maken van het sommetje:
ROLE ExecEngine MAINTAINS somBerekeningen
RULE somBerekeningen: l;l~ /\ r;r~ |- som;som~
MEANING "Voor elke Optelling waarvan de argumenten bekend zijn moet de som zijn berekend."
VIOLATION (TXT "{EX} berekenSom;", SRC l, TXT ";", SRC r, TXT ";som;Optelling;", SRC I, TXT ";Getal") --$ 'berekenSom' is een PHP functie (plugin); die staat in 'expTijdsduur.php' en dat bestand moet in de 'plugins' directory staan

ROLE ExecEngine MAINTAINS verschilBerekeningen
RULE verschilBerekeningen: l;l~ /\ som;som~ |- r;r~
MEANING "Voor elke Optelling waarvan de argumenten bekend zijn moet de som zijn berekend."
VIOLATION (TXT "{EX} berekenVerschil;", SRC som, TXT ";", SRC l, TXT ";r;Optelling;", SRC I, TXT ";Getal") --$ 'berekenVerschil' is een PHP functie (plugin); die staat in 'expTijdsduur.php' en dat bestand moet in de 'plugins' directory staan

--3: het invullen van de eindtijd
ROLE ExecEngine MAINTAINS eindtijdInvullen
RULE eindtijdInvullen: (begintijd;l~ /\ duur;r~);som |- eindtijd
VIOLATION (TXT "{EX} InsPair;eindtijd;Tijdvak;", SRC I, TXT ";Getal;", TGT I,
           TXT "{EX} InsPair;duur;Tijdvak;", SRC I, TXT ";Getal;", SRC duur
          )

ROLE ExecEngine MAINTAINS duurInvullen
RULE duurInvullen: (begintijd;l~ /\ eindtijd;som~);r |- duur
VIOLATION (TXT "{EX} InsPair;eindtijd;Tijdvak;", SRC I, TXT ";Getal;", SRC eindtijd,
           TXT "{EX} InsPair;duur;Tijdvak;", SRC I, TXT ";Getal;", TGT I
          )

ENDPROCESS

--$ Je mag niet in I[Tijdvak] editen - dat is een 'bekende' bug (en ik meen zelfs ook al: een ticket)
--INTERFACE overzicht (I[Tijdvak]) : I[SESSION]
INTERFACE overzicht : I[SESSION]
BOX [ periodes : V[SESSION*Tijdvak]
    , sommetjes   : V[SESSION*Optelling]
    ]

--$ Omdat ik heb afgesproken dat 'eindtijd' niet variabel is, halen we die uit de editable velden
INTERFACE tijdvak (begintijd, eindtijd, duur): I[Tijdvak]
BOX [ begintijd : begintijd
    , eindtijd  : eindtijd
    , duur      : duur
    ]

--$ Omdat alles met sommetjes automatisch gaat, mag je die niet editen
--INTERFACE rekenwerk (l, r, som) FOR rekenmachien : I[Optelling]
INTERFACE rekenwerk FOR rekenmachien : I[Optelling]
BOX [ l   : l
    , r   : r
    , som : som
    ]


ENDCONTEXT