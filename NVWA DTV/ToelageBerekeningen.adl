CONTEXT DTV IN DUTCH LATEX
INCLUDE "TijdvakResultaten.adl"
INCLUDE "Resultaten.adl"

PATTERN ToelageBerekeningen
PURPOSE PATTERN ToelageBerekeningen REF "Artikel 6 Circulaire Invoering 36-urige werkweek"
{+Per 1 januari 1997 komt een ambtenaar in aanmerking voor een overwerkvergoeding,
indien hij op een werkdag tenminste 1 uur in opdracht van het bevoegd gezag overwerk heeft verricht, op grond van Artikel 6 Circulaire Invoering 36-urige werkweek
Aan de ambtenaar, die anders dan bij wijze van overwerk,
regelmatig of vrij regelmatig arbeid verricht op andere tijden dan op de dagen maandag tot en met vrijdag tussen 8 en 18 uur,
wordt een toelage toegekend op grond van Artikel 17 BBRA.
-}

-- Overwerk
-- ** Deze relatie is al gedefinieerd in TijdvakResultaten.adl
-- RELATION overwerk[TijdvakResultaat*TijdvakResultaat] [PROP]
-- MEANING "Onder overwerk wordt verstaan arbeid buiten de werktijden geldende voor de ambtenaar krachtens de WTR, voor zover daardoor het per werkperiode vastgestelde aantal arbeidsuren wordt overschreden."
-- PURPOSE RELATION overwerk[TijdvakResultaat*TijdvakResultaat] REF "BBRA Artikel 23"
-- {+Of een uurtijdvak als overwerk geldt, wordt beschreven in de BBRA. Dit wordt als eigenschap van een uurtijdvak geregistreerd.
-- Voor de berekening is van belang dat overwerk alleen wordt toegekend aan de ambtenaar, voor wie een salarisschaal geldt met een lager maximumsalaris dan dat van schaal 11 en die in opdracht van het bevoegde gezag overwerk verricht.
-- Voor overwerk dat gedurende korter dan een uur aansluitend aan de vastgestelde dagelijkse werktijd wordt verricht, wordt geen vergoeding toegekend.
-- De werkperiode, waarvan in de definitie van overwerk sprake is, wordt gesteld op één dag, indien aanvang en einde van de werktijd in de regel niet aan wisselingen onderhevig zijn.
-- Indien de tijdstippen van aanvang en einde van de werktijd volgens een tevoren vastgesteld rooster wisselen, wordt deze werkperiode gesteld op een tijdvak van tenminste zeven dagen.
-- -}

RELATION pCodeOverwerk[Uurtijdvak*Code] [UNI,TOT]
RELATION grondslagOverwerk[Uurtijdvak*Wetsartikel] [UNI]
RELATION overwerkpercentage[Uurtijdvak*Percentage] [UNI,TOT] PRAGMA "Voor uurtijdvak " " geldt het overwerkpercentage "
MEANING "Het overwerkpercentage is gekoppeld aan uurtijdvakken."
PURPOSE RELATION overwerkpercentage[Uurtijdvak*Percentage] REF "Artikel 6.1 Circulaire Invoering 36-urige werkweek"
{+
Het overwerkpercentage hangt af van het uurtijdvak waarbinnen deze arbeid is verricht.
Om het juiste percentage te kunnen berekenen is het rooster verdeeld in uurtijdvakken cf. de tabel in Artikel 6.1 Circulaire Invoering 36-urige werkweek
-}

RELATION verantw[SomOverwerk*Periodeverantwoording] [UNI,TOT]
RELATION resultaatOverwerk[SomOverwerk*Resultaat] [UNI,TOT]

PURPOSE RULE "Welke tijdvakresultaten in overwerk"
{+
Om te bepalen welke tijdvakresultaten betrokken worden bij het berekenen van het aantal overwerkuren,
geldt de volgende afspraak:
-}
RULE "Welke tijdvakresultaten in overwerk" :
     pCodeOverwerk~;uurtijdvak~;overwerk;neemtdeel |- code~;resultaatOverwerk~;verantw[SomOverwerk*Periodeverantwoording]
MEANING "Voor elk tijdvakresultaat die deelneemt in een periodeverantwoording bestaat er een som om het overwerk te berekenen voor elke overwerkcode."

-- Onregelmatige diensten

RELATION pCodeTOD[Uurtijdvak*Code] [UNI]
RELATION grondslagTOD[Uurtijdvak*Wetsartikel] [UNI]
RELATION onregelmatigheidspercentage[Uurtijdvak*Percentage] [UNI,TOT] PRAGMA "Voor uurtijdvak " " geldt het onregelmatigheidspercentage "
MEANING "Het onregelmatigheidspercentage is gekoppeld aan uurtijdvakken."
PURPOSE RELATION onregelmatigheidspercentage[Uurtijdvak*Percentage] REF "Artikel 17 lid 2 BBRA"
{+
Het percentage dat gebruikt wordt bij de berekening van de toelage onregelmatige diensten is voor elk uurtijdvak beschreven in Artikel 17 lid 2 BBRA.
-}

RELATION verantw[SomOnregelmDienst*Periodeverantwoording] [UNI,TOT]
RELATION resultaatOnregelmDienst[SomOnregelmDienst*Resultaat] [UNI,TOT]

PURPOSE RULE "Welke tijdvakresultaten in onregelmatige diensten"
{+
Om te bepalen welke tijdvakresultaten betrokken worden bij het berekenen van het aantal uren in onregelmatige diensten,
geldt de volgende afspraak:
-}
RULE "Welke tijdvakresultaten in onregelmatige diensten" :
     pCodeTOD~;uurtijdvak~;neemtdeel |- code~;resultaatOnregelmDienst~;verantw[SomOnregelmDienst*Periodeverantwoording]
MEANING "Voor elk tijdvakresultaat die deelneemt in een periodeverantwoording bestaat er een som om onregelmatige diensten te berekenen voor elke P-Direktcode."


-- Beschikbaarheid

RELATION beschikbaarheidspercentage[Uurtijdvak*Percentage] [UNI] PRAGMA "Voor uurtijdvak " " geldt het bereikbaarheid -en beschikbaarheidspercentage "
MEANING "Het onregelmatigheidspercentage is gekoppeld aan uurtijdvakken."
PURPOSE RELATION beschikbaarheidspercentage[Uurtijdvak*Percentage] REF "Artikel 18A lid 2 BBRA"
{+
Het percentage dat gebruikt wordt bij de berekening van de toelage voor bereikbaarheid en beschikbaarheid is voor elk uurtijdvak beschreven in Artikel 18A lid 2 BBRA.
-}
RELATION pCodeBeschikbaarheid[Uurtijdvak*Code] [UNI,TOT]
RELATION grondslagBeschikbaarheid[Uurtijdvak*Wetsartikel] [UNI]

RELATION verantw[SomBeschikbaarheid*Periodeverantwoording] [UNI,TOT]
RELATION resultaatBeschikbaarheid[SomBeschikbaarheid*Resultaat] [UNI,TOT]
PURPOSE RULE "Welke tijdvakresultaten in beschikbaarheid"
{+
Om te bepalen welke tijdvakresultaten betrokken worden bij het berekenen van het aantal beschikbaarheidsuren,
geldt de volgende afspraak:
-}
RULE "Welke tijdvakresultaten in beschikbaarheid" :
     pCodeBeschikbaarheid~;uurtijdvak~;neemtdeel |- code~;resultaatBeschikbaarheid~;verantw[SomBeschikbaarheid*Periodeverantwoording]
MEANING "Voor elk tijdvakresultaat die deelneemt in een periodeverantwoording bestaat er een som om de beschikbaarheidsuren te berekenen per P-Direktcode."
ENDPATTERN


ENDCONTEXT