CONTEXT AccountsSessionsLogin IN DUTCH LATEX

--[Account informatie]--
PROCESS "Account Management"

RULE "Account uniciteit": accUserID;accUserID~ /\ accPassword;accPassword~ |- I[Account]
MEANING "Elke persoon in de registratie kan worden geidentificeerd middels zijn emailadres en wachtwoord."

RELATION accPersoon[Account*Persoon] [UNI,TOT] PRAGMA "" " wordt gebruikt door "
MEANING "Het account wordt gebruikt door een zeker persoon."

PURPOSE RULE "Synchroniseren van emailadressen"
{+Er zijn verschillende manieren voor gebruikers om een UserID te kiezen (waarmee hun account wordt geidentificeerd). Voorbeelden hiervan zijn:

- de gebruiker kiest zelf een UserID die nog niet bestaat.
- De organisatie kiest een UserID die overeenkomt met een medewerkersgegeven, zoals het personeelsnummer of het emailadres dat de organisatie aan haar medewerkers uitgeeft.
- de gebruiker geeft een persoonlijk emailadres op (dat dus bij zijn persoonsgegevens hoort) en gebruikt dat.

In dit geval is voor het laatste gekozen. Dat houdt dus in dat de UserID van Accounts gesynchroniseerd moet blijven met het emailadres dat in de registratie van persoonsgegevens voorkomt. Dat gebeurt geheel automatisch.-}
ROLE ExecEnging MAINTAINS "Synchroniseren van emailadressen"
RULE "Synchroniseren van emailadressen": accPersoon;emailadres |- accUserID
MEANING "Het UserID van een account is het (persoonlijke) emailadres van diens eigenaar (gebruiker)."
VIOLATION (TXT "{EX} InsPair;accUserID;Account;", SRC I, TXT ";Emailadres;", TGT I)

RELATION accUserID[Account*Emailadres] [UNI] PRAGMA "" " heeft als UserID/emailadres "
MEANING "De UserID/emailadres van een account moet zijn geregistreerd."

RELATION accPassword[Account*Wachtwoord] [UNI,TOT] PRAGMA "" " heeft als wachtwoord "
MEANING "Het Wachtwoord van een account moet zijn geregistreerd."

RELATION accRole[Account* Rol] PRAGMA "" " heeft Rol " " toegewezen gekregen "
MEANING "De Rol(len) van een account kan/kunnen worden geregistreerd."

ENDPROCESS
---------------------------------------------------------------------
VIEW Account : Account(accUserID)

INTERFACE "Accountgegevens" : I[Account]
   BOX[ "Gebruiker" : accPersoon
      , "UserID"    : accUserID
      , "Rol(len)"  : accRole
      ]
---------------------------------------------------------------------
PATTERN Sessions
PURPOSE PATTERN Sessions
{+Dit hoofdstuk beschrijft de termen die nodig zijn om personen (gebruikers) te laten communiceren met het systeem, en op een gepersonaliseerde manier toegang te kunnen krijgen tot de gegevens uit het systeem. Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit pattern wordt gebruikt.-}

PURPOSE CONCEPT SESSION
{+Een persoon gebruikt een gegevensruimte (en heet dan 'gebruiker') door met een browser (bijv. Chrome of Firefox) het systeem te benaderen dat de gegegensruimte beheert. Als meerdere personen een gegevensruimte delen, moet het systeem de context van elk van hen kunnen onderscheiden, bijvoorbeeld om:

* de interactie 'klein' te houden, d.w.z. alleen gegevens te laten zien die voor hem/haar relevant zijn;
* ervoor te zorgen dat een gebruiker niet ziet wat hij niet mag zien;
* te kunnen bijhouden welke persoon, of welk(e) organisatie(onderdeel) verantwoordelijk is voor een zekere transactie;
* automatisch gegevens betreffende de gebruiker of zijn context aan transacties toe te kunnen voegen

We gebruiken de term 'SESSION' of 'sessie' om de verzameling van gegevens betreffende één (actieve) gebruiker mee aan te geven. Deze term correspondeert met de gelijknamige term browsers gebruiken, nl. 'een verbinding (door de browser) met een webservice (die een URL heeft)'. Het systeem houdt één sessie bij voor elke actieve gebruiker, d.w.z. voor elke browser die het systeem benadert. Merk op dat dit in het bijzonder geldt als de gebruiker in verschillende tabbladen van dezelfde browser het systeem benadert - er is dan toch maar één sessie (en één gebruiker).
-}
CONCEPT SESSION "een verzameling van gegevens die de context beschrijven waarin één persoon het systeem gebruikt"
--CONCEPT SESSION "A session represents the context of a person that uses the information system."

sessionUsername :: SESSION * Emailadres [UNI]
MEANING "In een sessie kan het accUserID van de gebruiker bekend zijn."
sessionPW :: SESSION * Wachtwoord [UNI] -- een wachtwoord is verplicht.
MEANING "In een sessie kan het wachtwoord van de gebruiker bekend zijn."
sessionAccount :: SESSION * Account [UNI]
MEANING "In een sessie kan het account van de gebruiker bekend zijn."
sessionUser:: SESSION * Persoon [UNI]
MEANING "In een sessie kan de (responsible) actor/persoon bekend zijn."
sessionRole :: SESSION * Rol
MEANING "In een sessie kunnen rollen (autorisaties) zijn geactiveerd."
-- naar believen uit te breiden

ENDPATTERN
---------------------------------------------------------------------
INTERFACE "Sessie Context" : I[SESSION];'_SESSION'[SESSION]
BOX[ "Gebruiker" : sessionUser
   , "UserID" : sessionUsername -- afdeling of zo
   , "Actieve rollen" : sessionRole
   ]
---------------------------------------------------------------------
PROCESS "Session Management en Login"

PURPOSE RULE "Identificeren en authenticeren"
{+Deze regel garandeert dat elke persoon in de sessie een correct wachtwoord heeft ingebracht.-}
RULE "Identificeren en authenticeren": sessionUsername~;sessionPW |- accUserID~;accPassword
MESSAGE "Verkeerde inloggegevens."
VIOLATION (TXT "Gebruik een geregistreerde UserID/Emailadres-Wachtwoord combinatie.")

ROLE ExecEngine MAINTAINS "Inloggen"
RULE "Inloggen": sessionUsername;accUserID~ /\ sessionPW;accPassword~ |- sessionAccount
MEANING "De gebruiker in een sessie moet zijn geauthenticeerd."
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} InsPair;sessionUser;SESSION;", SRC I, TXT ";Persoon;", TGT accPersoon
          )

ROLE ExecEngine MAINTAINS "Uitloggen"
RULE "Uitloggen": sessionAccount |- sessionUsername;accUserID~ /\ sessionPW;accPassword~
MEANING "Als de username of het wachtwoord niet meer bekend is, wordt de gebruiker uitgelogd."
VIOLATION (TXT "{EX} DelPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;sessionUser;SESSION;", SRC I, TXT ";Persoon;", TGT accPersoon
          )

ROLE ExecEngine MAINTAINS "Activeren van rollen"
RULE "Activeren van rollen": sessionAccount;accRole |- sessionRole
MEANING "In een sessie moeten de rollen van de gebruiker zijn geactiveerd."
VIOLATION (TXT "{EX} InsPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT I)

ROLE ExecEngine MAINTAINS "Deactiveren van rollen"
RULE "Deactiveren van rollen": sessionRole |- sessionAccount;accRole
MEANING "Alleen de rollen van de gebruiker mogen zijn geactiveerd in een sessie."
VIOLATION (TXT "{EX} DelPair;sessionRole;SESSION;", SRC I, TXT ";Rol;", TGT I)

ENDPROCESS
---------------------------------------------------------------------
INTERFACE "Login" (sessionUsername, sessionPW): V[ONE*SESSION];'_SESSION'
BOX[ "e-mailadres" : sessionUsername
   , "Wachtwoord"  : sessionPW
   , "Sessie context"      : I              INTERFACE "Sessie Context"
   , "Accountgegevens"     : sessionAccount INTERFACE "Accountgegevens"
   , "Persoonsgegevens"    : sessionUser    INTERFACE "Persoonsgegevens"
   , "Medewerkersgegevens" : sessionUser    INTERFACE "Medewerkersgegevens"
   ]
---------------------------------------------------------------------
INTERFACE "Verander wachtwoord" (accPassword, sessionAccount, sessionPW):
   V[ONE*SESSION];'_SESSION';sessionAccount 
BOX[ "Account van"    : accPersoon
   , "Inloggegevens"  : I
   BOX[ "Email adres" : accUserID
      , "Wachtwoord"  : accPassword
      ]
    , "Herhaal" : sessionAccount~       -- this is a bit of a hack: to allow changing the
      BOX [ "Wachtwoord" : sessionPW    -- accPassword, the sessionPW needs to change
          ]                             -- simultaneously, or rule login will fail.   
    ]
-----------------------
POPULATION voornaam[Persoon*Voornaam]                 CONTAINS [ ("p3", "Benny")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel]       CONTAINS [ ("p3", "")]
POPULATION achternaam[Persoon*Achternaam]             CONTAINS [ ("p3", "Beer")]
POPULATION emailadres[Persoon*Emailadres]             CONTAINS [ ("p3", "benny.beer@nvwa.nl")]
POPULATION accPersoon[Account*Persoon] CONTAINS
  [ ("a1", "p1")
  ; ("a2", "p2")
  ; ("a3", "p3")
  ]
POPULATION accPassword[Account*Wachtwoord] CONTAINS
  [ ("a1", "welkom")
  ; ("a2", "welkom")
  ; ("a3", "welkom")
  ]
POPULATION accRole[Account*Rol] CONTAINS
  [ ("a1", "Medewerker")
  ; ("a1", "Leidinggevende")
  ; ("a2", "Medewerker")
  ; ("a3", "Medewerker")
  ; ("a3", "beheerder")
  ]

ENDCONTEXT