CONTEXT Session IN DUTCH LATEX
--INCLUDE "Persoonsinformatie.adl"
--META "authors" "Stef Joosten"
{-PURPOSE CONTEXT Session
{+Dit script demonstreert de sessie-functionaliteit. Door met verschillende namen in te loggen krijgt u toegang tot verschillende gegevens.

Om in te loggen gaat u naar de interface Login, drukt op edit en geeft een loginnaam/wachtwoord in.
Het Documenten-interface laat aleen de documenten zien waartoe de huidige gebruiker toegang heeft.
In edit-account interface kunnen accountdetails worden gewijzigd.

Omdat sessies gekoppeld zijn aan de browser-sessie, moet u twee verschillende browsers openen (bijv Chrome en Firefox) dan wel via twee computers inloggen.
-}

PATTERN Sessions
PURPOSE PATTERN Sessions
{+Dit thema administreert gebruikers om ze te laten inloggen op het systeem.
Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit pattern wordt gebruikt.
-}
CONCEPT SESSION "Elke sessie vertegenwoordigt één gebruiker in het systeem. Dit wordt gebruikt om activiteiten in het systeem te herleiden naar de gebruiker die ervoor verantwoordelijk is."
--CONCEPT SESSION "A session represents a user inside an information system. It is used to administer any activity in the system to the user who performs that activity."
PURPOSE CONCEPT SESSION
{+Als meerdere gebruikers een gegevensruimte delen, moet het systeem kunnen bijhouden welke gebruiker welke transacties uitvoert.
Om dat bij te houden, houdt het systeem een sessie bij voor elke actieve gebruiker. Voor elk platform (lees: browser) wordt één sessie bijgehouden.
-}
RELATION persoonsnr[Persoon*Personeelsnummer] [UNI] PRAGMA "" " heeft als persoonsnummer "
MEANING "Het persoonsnummer van een persoon wordt geregistreerd."

PURPOSE CONCEPT Persoon
{+Van medewerkers is het personeelsnummer het identificerende kenmerk.
De naam van een medewerker wordt alleen gebruikt om bruikbare output te maken.
Van medewerkers, die toegang hebben tot DTV wordt een wachtwoord en loginnaam geregistreerd conform de richtlijnen van DICTU.
-}
CONCEPT Persoon "De term persoon duidt een natuurlijke persoon aan."
--CONCEPT Persoon "A person is a named individual, who is capable of logging into the system."
CONCEPT Loginnaam "Een loginnaam is een rij karakters, die een persoon identificeert binnen het systeem en die gebruikt kan worden om via e-mail met deze persoon te communiceren."
PURPOSE CONCEPT Loginnaam
{+Omdat alle personen via e-mail bereikbaar zijn, worden loginnamen zowel gebruikt om personen te identificeren en om met hen te communiceren.
-}
PURPOSE CONCEPT PASSWORD
{+Om het moeilijk te maken voor gebruikers om de identiteit van een andere persoon te gebruiken, registreert het systeem wachtwoorden.
Door een wachtwoord geheim te houden ontstaat enige mate van zekerheid dat het systeem gebruikt word door (of met medeweten van) de persoon die het wachtwoord heeft geregistreerd.
-}
CONCEPT PASSWORD "Een wachtwoord is een rij karakters, die geheim gehouden kan worden door een gebruiker, en die gebruikt moet worden om toegang te krijgen tot het systeem."
CONCEPT Naam     "Een naam is een rij karakters, die bedoeld is om een of ander ding te identificeren."
CONCEPT Document "Een document is een eenheid van opgeslagen informatie, waarvan de inhoud geen betekenis heeft voor het systeem."

RELATION sessionLoginnaam[SESSION*Loginnaam] [UNI]
RELATION sessionPassword[SESSION*PASSWORD] [UNI] -- een wachtwoord is verplicht.

RELATION loginnaam[Persoon*Loginnaam] [UNI] -- Een persoon moet met niet meer dan één loginnaam bekend zijn in het systeem. Het toestaan van meerdere loginnamen maakt de software minder doorzichtig.
RELATION wachtwoord[Persoon*PASSWORD] [UNI]

documents :: Persoon * Document [SUR]
ENDPATTERN

PATTERN Persoonsgegevens
RELATION loginnaam[Persoon*Loginnaam] [UNI,INJ] PRAGMA "" " heeft als loginnaam "
MEANING "Het loginnaam van een persoon wordt geregistreerd."

PURPOSE RELATION persoonsnaam[Persoon*Naam]
{+Om medewerkers ook bij naam te kunnen noemen, registreert DTV ook de volledige naam van een persoon.
Deze informatie wordt door SPIN aangeleverd, en wordt verder niet door DTV gebruikt.
In beginsel zou DTV zijn werk kunnen doen zonder de naam te weten die bij een personeelsnummer hoort.
-}
RELATION persoonsnaam[Persoon*Naam] [UNI]
MEANING "Bij iedere persoon is de volledige naam bekend, zoals die in SPIN wordt gehanteerd."

ENDPATTERN

PROCESS Login
PURPOSE RELATION ingelogd
{+Om te kunnen voorkomen of toetsen of een gebruiker zijn of haar eigen gegevens wijzigt, en daarmee potentieel fraudeert,
houdt het systeem bij welke gebruiker aan de huidige sessie is gekoppeld.
-}
RELATION ingelogd[SESSION*Persoon] [INJ,AUT]

PURPOSE RULE registerSession
{+De computer registreert de gebruiker op het moment dat deze een correcte combinatie van loginnaam en wachtwoord aanbiedt.-}
RULE registerSession: sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~ |- ingelogd
VIOLATION (TXT "{EX} InsPair;ingelogd;SESSION;", SRC I, TXT ";Persoon;", TGT I)
PURPOSE RULE logoutW
{+De computer wist sessiegegevens op het moment dat de gebruiker uitlogt.
Uitloggen gebeurt door het verwijderen van de SESSIE/Persoon combinatie uit de relatie "ingelogd".
-}
RULE logoutW: ingelogd |- sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~
VIOLATION ( TXT "{EX} DelPair;ingelogd;SESSION;", SRC I, TXT ";Persoon;", TGT I )
ROLE ExecEngine MAINTAINS registerSession, logoutW

ROLE ExecEngine MAINTAINS pwdChange

PURPOSE RULE pwdChange IN ENGLISH
{+When a password is being changed, the session must continu as though logged in with the new password.-}
RULE pwdChange: wachtwoord |- ingelogd~;sessionPassword
VIOLATION (TXT "{EX} InsPair;sessionPassword;SESSION;", SRC ingelogd~, TXT ";PASSWORD;", TGT I)

PURPOSE RULE "username password combination" IN ENGLISH
{+In order to log in, a user must present a username/password combination that is linked to a Person.-}
RULE "username password combination": sessionLoginnaam~;sessionPassword |- loginnaam~;wachtwoord
MESSAGE "Ongeldige combinatie van gebruikernaam en passwoord!"
ENDPROCESS

POPULATION loginnaam CONTAINS
  [ ("p1", "stef.joosten@nvwa.nl")
  ; ("p2", "h.honderd@nvwa.nl")
  ; ("p3", "barry.stevens@gmail.com")
  ]

POPULATION persoonsnaam CONTAINS
  [ ("p1", "Stef Joosten")
  ; ("p2", "Harry in 't Honderd")
  ; ("p3", "Barry Stevens")
  ]

POPULATION wachtwoord CONTAINS
  [ ("p1", "welkom")
  ; ("p2", "welkom")
  ; ("p3", "welkom")
  ]



INTERFACE "Login" (sessionLoginnaam, sessionPassword) FOR Medewerker, Leidinggevende, FAB, KODE, "P&O" : '_SESSION'[SESSION]
BOX [ "loginnaam" : sessionLoginnaam
    , "Wachtwoord" : sessionPassword
    , "Op dit moment ingelogd" : sessionLoginnaam;loginnaam~
    ]

INTERFACE "Wijzig uw account"(persoonsnaam, wachtwoord, sessionPassword) FOR Medewerker, Leidinggevende, FAB, KODE: V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX [ "loginnaam"  : loginnaam
    , Naam :         persoonsnaam
    , "Wachtwoord" : wachtwoord
    ]

INTERFACE "Internals" FOR beheerder: I[ONE]
BOX [ "Actieve sessies" : V[ONE*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : sessionLoginnaam
          ]
    , "Gebruikers" : V[ONE*Persoon]
    ]

INTERFACE "Sessies" (ingelogd, persoonsnaam, loginnaam, wachtwoord) FOR beheerder: I[ONE]
BOX [ "Actieve sessies" : V[ONE*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : sessionLoginnaam
          ]
    ]

PURPOSE INTERFACE "Mijn gegevens"
{+De interface ``Mijn gegevens'' is bedoeld om een persoon haar eigen persoonsgegevens te laten invullen en zelf het wachtwoord te bepalen.
-}
INTERFACE "Mijn gegevens" (persoonsnaam, loginnaam, wachtwoord) FOR Medewerker, Leidinggevende, FAB: I[Persoon]
BOX [ Naam :         persoonsnaam
    , "loginnaam" :  loginnaam
    , "Wachtwoord" : wachtwoord
    ]


ENDCONTEXT