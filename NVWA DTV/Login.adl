CONTEXT Session IN DUTCH LATEX
INCLUDE "LoginDTVdoc.adl" -- use "Login_doc.adl" for comments that apply generically for many projects.

META "authors" "Stef Joosten"

PROCESS Login

RELATION loginnaam[Account*Loginnaam] [UNI,INJ] PRAGMA "" " heeft als loginnaam "
MEANING "De loginnaam van elke persoon wordt geregistreerd."

RELATION sessionLoginnaam[SESSION*Loginnaam] [UNI]
MEANING "Het systeem onthoudt de loginnaam van elke lopende sessie, zolang die sessie leeft."

RELATION sessionPassword[SESSION*PASSWORD] [UNI] -- een wachtwoord is verplicht.
MEANING "Het systeem onthoudt het wachtwoord van elke lopende sessie, zolang die sessie leeft."

RELATION wachtwoord[Account*PASSWORD] [UNI]
MEANING "Het systeem kan het wachtwoord herkennen, dat bij een gegeven account hoort."

RELATION ingelogd[SESSION*Account] [INJ,AUT]
MEANING "Zolang een gebruiker is ingelogd, onthoudt het systeem bij welk account deze sessie hoort."

RULE registerSession: sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~ |- ingelogd
MEANING "Zolang de combinatie loginnaam/wachtwoord, die in een sessie is aangeboden, overeenkomt met de gegevens van een gebruiker, is deze gebruiker ingelogd."
VIOLATION (TXT "{EX} InsPair;ingelogd;SESSION;", SRC I, TXT ";Account;", TGT I)

RULE logoutW: ingelogd |- sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~
MEANING "Zolang een gebruiker is ingelogd, moet de combinatie loginnaam/wachtwoord in een sessie overeenkomen met de gegevens van deze gebruiker."
VIOLATION ( TXT "{EX} DelPair;ingelogd;SESSION;", SRC I, TXT ";Account;", TGT I )

ROLE ExecEngine MAINTAINS registerSession, logoutW

ROLE ExecEngine MAINTAINS pwdChange, loginChange

RULE pwdChange: ingelogd;wachtwoord |- sessionPassword
MEANING "Wanneer het wachtwoord van een gebruiker wijzigt gedurende een sessie, wijzigt het sessie-wachtwoord mee."
VIOLATION (TXT "{EX} InsPair;sessionPassword;SESSION;", SRC I, TXT ";PASSWORD;", TGT I)

RULE loginChange: ingelogd;loginnaam |- sessionLoginnaam
MEANING "Wanneer de loginnaam van een gebruiker wijzigt gedurende een sessie, wijzigt de sessie-loginnaam mee."
VIOLATION (TXT "{EX} InsPair;sessionLoginnaam;SESSION;", SRC I, TXT ";Loginnaam;", TGT I)

RULE "username password combination": sessionLoginnaam~;sessionPassword |- loginnaam~;wachtwoord
MEANING "Elke loginnaam/wachtwoord combinatie dient een persoon te identificeren, van wie een loginnaam/wachtwoord combinatie in het systeem is geregistreerd."
MESSAGE "Ongeldige combinatie van gebruikernaam en password!"
ENDPROCESS


INTERFACE "Login" (sessionLoginnaam, sessionPassword) FOR Medewerker, Leidinggevende, FAB, "P&O" : '_SESSION'/\-(ingelogd;V)
BOX [ "loginnaam" : sessionLoginnaam
    , "Wachtwoord" : sessionPassword
    ]


INTERFACE "Logout" (ingelogd) FOR Medewerker, Leidinggevende, FAB, KODE, "P&O" : '_SESSION'/\ingelogd;V
BOX [ "Op dit moment ingelogd" : ingelogd BOX ["": loginnaam]
    ]


INTERFACE "Internals" FOR FAB: '_SESSION'[SESSION]
BOX [ "Actieve sessies" : V[SESSION*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : ingelogd;loginnaam
          ]
    , "Gebruikers" : V[SESSION*Account]
    ]


ENDCONTEXT