CONTEXT Session IN DUTCH LATEX
META "authors" "Stef Joosten"
PURPOSE CONTEXT Session
{+Dit script biedt login-functionaliteit. Door met verschillende namen in te loggen krijgt u toegang tot verschillende gegevens.

Om in te loggen gaat u naar de interface "Login" en geeft een loginnaam/wachtwoord in.
In de wijzig-account interface kunnen accountdetails worden gewijzigd.

Als u met meerdere sessies wilt experimenteren, bedenk dan dat elke sessie in een nieuwe browser geopend moet worden,
bijvoorbeeld een in Chrome en een andere in Firefox.
Dit komt omdat een Ampersand-sessie gekoppeld wordt aan de browser-sessie.
-}

PATTERN Persoonsgegevens
PURPOSE CONCEPT Persoon
{+Van medewerkers is het personeelsnummer het identificerende kenmerk.
De naam van een medewerker wordt alleen gebruikt om bruikbare output te maken.
Van medewerkers, die toegang hebben tot DTV wordt een wachtwoord en loginnaam geregistreerd conform de richtlijnen van DICTU.
-}
CONCEPT Persoon "De term persoon duidt een natuurlijke persoon aan."
--CONCEPT Persoon "A person is a named individual, who is capable of logging into the system."
CONCEPT Loginnaam "Een loginnaam is een rij karakters, die een persoon identificeert binnen het systeem en die gebruikt kan worden om via e-mail met deze persoon te communiceren."
PURPOSE CONCEPT Loginnaam
{+Omdat alle personen via e-mail bereikbaar zijn, worden loginnamen zowel gebruikt om personen te identificeren en om met hen te communiceren.
-}
PURPOSE CONCEPT PASSWORD
{+Om het moeilijk te maken voor gebruikers om de identiteit van een andere persoon te gebruiken, registreert het systeem wachtwoorden.
Door een wachtwoord geheim te houden ontstaat enige mate van zekerheid dat het systeem gebruikt word door (of met medeweten van) de persoon die het wachtwoord heeft geregistreerd.
-}
CONCEPT PASSWORD "Een wachtwoord is een rij karakters, die geheim gehouden kan worden door een gebruiker, en die gebruikt moet worden om toegang te krijgen tot het systeem."

RELATION loginnaam[Persoon*Loginnaam] [UNI,INJ] PRAGMA "" " heeft als loginnaam "
MEANING "Het loginnaam van een persoon wordt geregistreerd."
ENDPATTERN

PATTERN DTVPersonen
CONCEPT TEXT     "Een naam is een rij karakters, die bedoeld is om iets of iemand te identificeren."
PURPOSE RELATION persoonsnaam[Persoon*TEXT]
{+Om medewerkers ook bij naam te kunnen noemen, registreert DTV ook de volledige naam van een persoon.
Deze informatie wordt door SPIN aangeleverd, en wordt verder niet door DTV gebruikt.
In beginsel zou DTV zijn werk kunnen doen zonder de naam te weten die bij een personeelsnummer hoort.
-}
RELATION persoonsnaam[Persoon*TEXT] [UNI]
MEANING "Bij iedere persoon is de volledige naam bekend, zoals die in SPIN wordt gehanteerd."

RELATION persoonsnr[Persoon*Personeelsnummer] [UNI] PRAGMA "" " heeft als persoonsnummer "
MEANING "Het persoonsnummer van een persoon wordt geregistreerd."
ENDPATTERN

PROCESS Login
PURPOSE PROCESS Login
{+Dit thema administreert gebruikers om ze te laten inloggen op het systeem.
Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit login-proces wordt gebruikt.
-}
CONCEPT SESSION "Elke sessie vertegenwoordigt één gebruiker in het systeem."
--CONCEPT SESSION "A session represents a user inside an information system. It is used to administer any activity in the system to the user who performs that activity."
PURPOSE CONCEPT SESSION
{+Als meerdere gebruikers een gegevensruimte delen, moet het systeem kunnen bijhouden welke gebruiker welke transacties uitvoert.
Hiertoe houdt het systeem een sessie bij voor elke actieve gebruiker.
Zodoende kan het systeem activiteiten herleiden naar de gebruiker die ervoor verantwoordelijk is.
-}

RELATION sessionLoginnaam[SESSION*Loginnaam] [UNI]
RELATION sessionPassword[SESSION*PASSWORD] [UNI] -- een wachtwoord is verplicht.

RELATION loginnaam[Persoon*Loginnaam] [UNI] -- Een persoon moet met niet meer dan één loginnaam bekend zijn in het systeem. Het toestaan van meerdere loginnamen maakt de software minder doorzichtig.
RELATION wachtwoord[Persoon*PASSWORD] [UNI]
PURPOSE RELATION ingelogd
{+Om op enig moment gedurende een sessie te weten welke gebruiker aan de huidige sessie is gekoppeld,
houdt het systeem bij welke gebruiker in elke sessie is ingelogd.
-}
RELATION ingelogd[SESSION*Persoon] [INJ,AUT]

PURPOSE RULE registerSession
{+Om een gebruiker te registreren houdt het systeem de relatie "ingelogd" bij.
Op het moment dat deze een correcte combinatie van loginnaam en wachtwoord aanbiedt,
legt het systeem de koppeling tussen sessie en gebruiker.-}
RULE registerSession: sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~ |- ingelogd
MEANING "Zolang de combinatie loginnaam/wachtwoord, die in een sessie is aangeboden, overeenkomt met de gegevens van een gebruiker, is deze gebruiker ingelogd."
VIOLATION (TXT "{EX} InsPair;ingelogd;SESSION;", SRC I, TXT ";Persoon;", TGT I)
PURPOSE RULE logoutW
{+Het uitloggen van een gebruiker bestaat uit het ontkoppelen van de sessie en de gebruiker (in de relatie "ingelogd").
Op dat moment wist het systeem ook de sessiegegevens.
Uitloggen gebeurt door het verwijderen van de SESSIE/Persoon combinatie uit de relatie "ingelogd".
Dit functioneert ook wanneer de sessie zelf ophoudt te bestaan.-}
RULE logoutW: ingelogd |- sessionLoginnaam;loginnaam~ /\ sessionPassword;wachtwoord~
MEANING "Zolang een gebruiker is ingelogd, moet de combinatie loginnaam/wachtwoord in een sessie overeenkomen met de gegevens van deze gebruiker."
VIOLATION ( TXT "{EX} DelPair;ingelogd;SESSION;", SRC I, TXT ";Persoon;", TGT I )
ROLE ExecEngine MAINTAINS registerSession, logoutW

ROLE ExecEngine MAINTAINS pwdChange

PURPOSE RULE pwdChange IN ENGLISH
{+When a PASSWORD is being changed, the session must continu as though logged in with the new password.-}
PURPOSE RULE pwdChange IN DUTCH
{+Wanneer het wachtwoord van een gebruiker gedurende een sessie wijzigt, moet deze sessie doorgaan alsof er met het nieuwe wachtwoord was ingelogd.-}
RULE pwdChange: ingelogd;wachtwoord |- sessionPassword
MEANING "Wanneer het wachtwoord van een gebruiker wijzigt gedurende een sessie, wijzigt het sessie-wachtwoord mee."
VIOLATION (TXT "{EX} InsPair;sessionPassword;SESSION;", SRC I, TXT ";PASSWORD;", TGT I)

PURPOSE RULE loginChange IN ENGLISH
{+When a Loginnaam is being changed, the session must continu as though logged in with the new loginnaam.-}
PURPOSE RULE loginChange IN DUTCH
{+Wanneer de loginnaam van een gebruiker gedurende een sessie wijzigt, moet deze sessie doorgaan alsof er met de nieuwe loginnaam was ingelogd.-}
RULE loginChange: ingelogd;loginnaam |- sessionLoginnaam
MEANING "Wanneer de loginnaam van een gebruiker wijzigt gedurende een sessie, wijzigt de sessie-loginnaam mee."
VIOLATION (TXT "{EX} InsPair;sessionLoginnaam;SESSION;", SRC I, TXT ";Loginnaam;", TGT I)

PURPOSE RULE "username password combination" IN ENGLISH
{+In order to log in, a user must present a username/password combination that is linked to a Person.
Any combination that does not lead to a person is considered an error and is reported to the user.-}
PURPOSE RULE "username password combination" IN DUTCH
{+Om in te loggen dient een gebruiker een loginnaam/wachtwoord combinatie aan te bieden, die gekoppeld is aan een Persoon.
Elke combinatie, die niet naar een persoon herleidbaar is, wordt als fout gerapporteerd aan de gebruiker.-}
RULE "username password combination": sessionLoginnaam~;sessionPassword |- loginnaam~;wachtwoord
MEANING "Elke loginnaam/wachtwoord combinatie dient een persoon te identificeren, van wie een loginnaam/wachtwoord combinatie in het systeem is geregistreerd."
MESSAGE "Ongeldige combinatie van gebruikernaam en passwoord!"
ENDPROCESS

POPULATION loginnaam CONTAINS
  [ ("p1", "stef.joosten@nvwa.nl")
  ; ("p2", "h.honderd@nvwa.nl")
  ; ("p3", "barry.stevens@gmail.com")
  ]

POPULATION persoonsnaam CONTAINS
  [ ("p1", "Stef Joosten")
  ; ("p2", "Harry in 't Honderd")
  ; ("p3", "Barry Stevens")
  ]

POPULATION wachtwoord CONTAINS
  [ ("p1", "welkom")
  ; ("p2", "welkom")
  ; ("p3", "welkom")
  ]

PURPOSE INTERFACE "Login"
{+As long as nobody is logged in, the login screen is active and ready to accept new users.-}
INTERFACE "Login" (sessionLoginnaam, sessionPassword) FOR Medewerker, Leidinggevende, FAB, KODE, "P&O" : '_SESSION'/\-(ingelogd;V)
BOX [ "loginnaam" : sessionLoginnaam
    , "Wachtwoord" : sessionPassword
    ]

PURPOSE INTERFACE "Logout"
{+As long as somebody is logged in, the logout screen is ready and available for logout.
The user can log out by clicking on the dustbin, which deletes the entire session. -}
INTERFACE "Logout" (ingelogd) FOR Medewerker, Leidinggevende, FAB, KODE, "P&O" : '_SESSION'[SESSION]/\ingelogd;V
BOX [ "Op dit moment ingelogd" : ingelogd;loginnaam
    ]

INTERFACE "Wijzig uw account"(loginnaam, persoonsnaam, wachtwoord) FOR Medewerker, Leidinggevende, FAB, KODE: '_SESSION';sessionLoginnaam;loginnaam~
BOX [ "loginnaam"  : loginnaam
    , Naam :         persoonsnaam
    , "Wachtwoord" : wachtwoord
    ]

INTERFACE "Internals" FOR beheerder: '_SESSION'[SESSION]
BOX [ "Actieve sessies" : V[SESSION*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : ingelogd;loginnaam
          ]
    , "Gebruikers" : V[SESSION*Persoon]
    ]

PURPOSE INTERFACE "Mijn gegevens"
{+De interface ``Mijn gegevens'' is bedoeld om een persoon haar eigen persoonsgegevens te laten invullen en zelf het wachtwoord te bepalen.
-}
INTERFACE "Mijn gegevens" (persoonsnaam, loginnaam, wachtwoord) FOR Medewerker, Leidinggevende, FAB: I[Persoon]
BOX [ Naam :         persoonsnaam
    , "loginnaam" :  loginnaam
    , "Wachtwoord" : wachtwoord
    ]


ENDCONTEXT