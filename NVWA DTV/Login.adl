CONTEXT Session IN DUTCH LATEX
--INCLUDE "Persoonsinformatie.adl"
--META "authors" "Stef Joosten"
{-PURPOSE CONTEXT Session
{+Dit script demonstreert de sessie-functionaliteit. Door met verschillende namen in te loggen krijgt u toegang tot verschillende gegevens.

Om in te loggen gaat u naar de interface Login, drukt op edit en geeft een loginnaam/wachtwoord in.
Het Documenten-interface laat aleen de documenten zien waartoe de huidige gebruiker toegang heeft.
In edit-account interface kunnen accountdetails worden gewijzigd.

Omdat sessies gekoppeld zijn aan de browser-sessie, moet u twee verschillende browsers openen (bijv Chrome en Firefox) dan wel via twee computers inloggen.
-}

PATTERN Sessions
PURPOSE PATTERN Sessions
{+Dit pattern administreert gebruikers om ze te laten inloggen op het systeem.
Waar we spreken over ``het systeem'' bedoelen we het informatiesysteem waarbinnen dit pattern wordt gebruikt.
-}
CONCEPT SESSION "Elke sessie vertegenwoordigt één gebruiker in het systeem. Dit wordt gebruikt om tijdrubrieken in het systeem te herleiden naar de gebruiker die ervoor verantwoordelijk is."
--CONCEPT SESSION "A session represents a user inside an information system. It is used to administer any activity in the system to the user who performs that activity."
PURPOSE CONCEPT SESSION
{+Als meerdere gebruikers een gegevensruimte delen, moet het systeem kunnen bijhouden welke gebruiker welke transacties uitvoert.
Om dat bij te houden, houdt het systeem een sessie bij voor elke actieve gebruiker. Voor elk platform (lees: browser) wordt één sessie bijgehouden.
-}
PURPOSE CONCEPT Persoon
{+Van medewerkers is het personeelsnummer het identificerende kenmerk.
De naam van een medewerker wordt alleen gebruikt om bruikbare output te maken.
Van medewerkers, die toegang hebben tot DTV wordt een wachtwoord en loginnaam geregistreerd conform de richtlijnen van DICTU.
-}
CONCEPT Persoon "De term persoon duidt een natuurlijke persoon aan."
--CONCEPT Persoon "A person is a named individual, who is capable of logging into the system."
CONCEPT Loginnaam "Een loginnaam is een rij karakters, die een persoon identificeert binnen het systeem en die gebruikt kan worden om via e-mail met deze persoon te communiceren."
PURPOSE CONCEPT Loginnaam
{+Omdat alle personen via e-mail bereikbaar zijn, worden loginnamen zowel gebruikt om personen te identificeren en om met hen te communiceren.
-}
PURPOSE CONCEPT Wachtwoord
{+Om het moeilijk te maken voor gebruikers om de identiteit van een andere persoon te gebruiken, registreert het systeem wachtwoorden.
Door een wachtwoord geheim te houden ontstaat enige mate van zekerheid dat het systeem gebruikt word door (of met medeweten van) de persoon die het wachtwoord heeft geregistreerd.
-}
CONCEPT Wachtwoord "Een wachtwoord is een rij karakters, die geheim gehouden kan worden door een gebruiker, en die gebruikt moet worden om toegang te krijgen tot het systeem."
CONCEPT Naam     "Een naam is een rij karakters, die bedoeld is om een of ander ding te identificeren."
CONCEPT Document "Een document is een eenheid van opgeslagen informatie, waarvan de inhoud geen betekenis heeft voor het systeem."

RELATION sessionLoginnaam[SESSION*Loginnaam] [UNI]
RELATION sessionPassword[SESSION*Wachtwoord] [UNI] -- een wachtwoord is verplicht.

RELATION loginnaam[Persoon*Loginnaam] [UNI] -- Een persoon moet met niet meer dan één loginnaam bekend zijn in het systeem. Het toestaan van meerdere loginnamen maakt de software minder doorzichtig.
RELATION wachtwoord[Persoon*Wachtwoord] [UNI]
PURPOSE RELATION ingelogd
{+Om te kunnen voorkomen of toetsen of een gebruiker zijn of haar eigen gegevens wijzigt, en daarmee potentieel fraudeert,
houdt het systeem bij welke gebruiker aan de huidige sessie is gekoppeld.
-}
RELATION ingelogd[SESSION*Persoon] [INJ]

documents :: Persoon * Document [SUR]
ENDPATTERN

PATTERN Persoonsgegevens
RELATION loginnaam[Persoon*Loginnaam] [UNI,INJ] PRAGMA "" " heeft als loginnaam "
MEANING "Het loginnaam van een persoon wordt geregistreerd."
ENDPATTERN

PROCESS Login
PURPOSE RULE loginW
{+Deze regel garandeert dat elke persoon in de sessie een correct wachtwoord heeft ingebracht.-}
RULE loginW: sessionLoginnaam;loginnaam~ |- sessionPassword;wachtwoord~
MESSAGE "Fout wachtwoord"
VIOLATION (SRC sessionPassword, TXT " moet zijn: ", TGT wachtwoord)
RULE unknownUsername : sessionLoginnaam |- V;loginnaam
MESSAGE "Wilt u alstublieft uw loginnaam (en overige persoonsgegevens) invullen in het menu: New, submenu: \"Persoon (Mijn gegevens)\") "
VIOLATION (TXT "onbekende loginnaam: ", TGT I )
RULE unknownUserPwd : sessionPassword |- V;wachtwoord
MESSAGE "Wilt u alstublieft uw wachtwoord (en overige persoonsgegevens) registreren in het menu: New, submenu: \"Persoon (Mijn gegevens)\") "
VIOLATION (TXT "onbekend wachtwoord: ", TGT I )
ROLE Medewerker, Leidinggevende, FAB MAINTAINS unknownUsername
ENDPROCESS

POPULATION loginnaam CONTAINS
  [ ("p1", "s.joosten@nvwa.nl")
  ; ("p2", "h.honderd@nvwa.nl")
  ; ("p3", "barry.stevens@gmail.com")
  ]

POPULATION volledigenaam CONTAINS
  [ ("p1", "Stef Joosten")
  ; ("p2", "Harry in 't Honderd")
  ; ("p3", "Barry Stevens")
  ]

POPULATION wachtwoord CONTAINS
  [ ("p1", "welkom")
  ; ("p2", "welkom")
  ; ("p3", "welkom")
  ]



INTERFACE "Login" (sessionLoginnaam, sessionPassword) FOR Medewerker, Leidinggevende, FAB, KODE, "P&O" : V[ONE*SESSION];'_SESSION'
BOX [ "loginnaam" : sessionLoginnaam
    , "Wachtwoord" : sessionPassword
    , "Op dit moment ingelogd" : sessionLoginnaam;loginnaam~
    ]

INTERFACE "Wijzig uw account"(volledigenaam, wachtwoord, sessionPassword) FOR Medewerker, Leidinggevende, FAB, KODE: V[ONE*SESSION];'_SESSION';sessionLoginnaam;loginnaam~
BOX [ "loginnaam"  : loginnaam
    , Naam :           volledigenaam
    , "Wachtwoord"  : wachtwoord
    , "Sessie"   : loginnaam;sessionLoginnaam~            -- this is a bit of a hack: to allow changing the
      BOX [ "Herhaal wachtwoord" : sessionPassword          -- wachtwoord, the sessionPassword needs to change
          ]                                              -- simultaneously, or rule login will fail.   
    ]

INTERFACE "Internals" FOR beheerder: I[ONE]
BOX [ "Actieve sessies" : V[ONE*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : sessionLoginnaam
          ]
    , "Gebruikers" : V[ONE*Persoon]
    ]

INTERFACE "Sessies" (ingelogd, volledigenaam, loginnaam, wachtwoord) FOR beheerder: I[ONE]
BOX [ "Actieve sessies" : V[ONE*SESSION]
      BOX [ "Sessie id" : I[SESSION]
          , "loginnaam"  : sessionLoginnaam
          ]
    ]

PURPOSE INTERFACE "Mijn gegevens"
{+De interface ``Mijn gegevens'' is bedoeld om een persoon haar eigen persoonsgegevens te laten invullen en zelf het wachtwoord te bepalen.
-}
INTERFACE "Mijn gegevens" (volledigenaam, loginnaam, wachtwoord) FOR Medewerker, Leidinggevende, FAB: I[Persoon]
BOX [ Naam :         volledigenaam
    , "loginnaam" :  loginnaam
    , "Wachtwoord" : wachtwoord
    ]


ENDCONTEXT