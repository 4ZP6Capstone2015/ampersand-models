CONTEXT SPIN IN DUTCH
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Diensten.adl"
INCLUDE "ReferentielijstenSPIN.adl"
INCLUDE "Periodeverantwoordingen.adl"
PATTERN SPINsamenhang

POPULATION persoon[MedewerkerRecht*Persoon] CONTAINS
  [ ("mw2",  "p1")
  ; ("mw5",  "p2")
  ; ("mw11", "p3")
  ]

ENDPATTERN

PROCESS SPINprocess

PURPOSE RULE "complete periode"
{+Of een periode compleet is verantwoord, moet SPIN bepalen.
In elk geval mogen we een periodeverantwoording verwachten voor elke persoon, die in (of overlappend met) de verantwoordingsperiode medewerker is.
-}
RULE "complete periode" : persoon~;(overlap\/overlap~\/I[Verantwoordingsperiode]) |- medewerkerPer~;verantwoordingsperiode
MEANING "Alle medewerkers, die op enig moment in een verantwoordingsperiode rechten als medewerker hebben, moeten in diezelfde periode in de periodeverantwoording voorkomen."
MESSAGE "Er zijn medewerkers met rechten in een periode, waarvoor nog geen periodeverantwoording bestaat."
VIOLATION (TXT "", SRC personeelsnummer, TXT " moet meedoen in de verantwoording van ", TGT I, TXT ".")
ROLE SPIN MAINTAINS "complete periode"
ENDPROCESS

INTERFACE PeriodeverantwoordingDTV FOR SPIN : I[Periodeverantwoording]
BOX [ medewerkerPer : medewerkerPer
    , verantwoordingsperiode : verantwoordingsperiode;I[Verantwoordingsperiode]
    , dagverantwoordingen : periodeverantwoording~ -- INTERFACE DagverantwoordingDTV
      BOX [ ingediend : datum -- automatisch invullen
          , "te werken uren volgens arbeidsmodaliteit" : dagModaliteit
          , Diensten : dag[Dienst*Dagverantwoording]~
            BOX [ roostersoort :  roosterSoort
                , begin :         begin[Periode*DatumTijd]
                , eind :          eind[Periode*DatumTijd]
                ]
          , Timesheet : dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ begin :         begin[Periode*DatumTijd]
                , eind :          eind[Periode*DatumTijd]
                , tijdrubriek :   tijdrubriek
                  BOX [naam : tijdrubriekNaam ]
                , tijdcategorie : tijdrubriek
                  BOX [categorie : cat ]
                ]
          ]
    ]

POPULATION begindatum[Periode*Datum] CONTAINS
  [ ("mw2", "01-01-2014")
  ; ("mw5", "01-01-2014")
  ; ("mw11", "01-01-2014")
  ]

POPULATION recht CONTAINS
  [ ("mw2",  "salarisnummer5")
  ; ("mw5",  "salarisnummer5")
  ; ("mw11", "salarisnummer5")
  ]

ENDCONTEXT