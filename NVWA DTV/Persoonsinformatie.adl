CONTEXT DTV IN DUTCH

PATTERN Persoonsinformatie
VIEW Persoon : Persoon(persoonsnaam)
RELATION loginnaam[Persoon*Loginnaam] [UNI,INJ] PRAGMA "" " heeft als loginnaam "
MEANING "Het loginnaam van een persoon wordt geregistreerd."

RELATION initialen[Persoon*Initialen] [UNI] PRAGMA "" " heeft als initialen "
MEANING "De initialen van een persoon worden geregistreerd"

RELATION titel[Persoon*Titel] [UNI] PRAGMA "" " heeft als titel "
MEANING "De titel van een persoon wordt geregistreerd."

PURPOSE RELATION geboortedatum[Persoon*Datum]
{+ De leeftijd van een persoon kan invloed hebben op het wel of niet krijgen van bepaalde toeslagen bij ongebruikelijke werktijden (BBRA Artikel 17). Daarom moet de geboortedatum van een persoon bekend zijn. 
-}
RELATION geboortedatum[Persoon*Datum] [UNI] PRAGMA "" " heeft als geboortedatum "
MEANING "De geboortedatum van een persoon wordt geregistreerd."

RELATION telefoon[Persoon*Telefoonnummer] [UNI] PRAGMA "" " heeft als telefoonnummer "
MEANING "Het telefoonnummer van een persoon wordt geregistreerd."

CONCEPT TEXT     "Een naam is een rij karakters, die bedoeld is om iets of iemand te identificeren."
PURPOSE RELATION persoonsnaam[Persoon*TEXT]
{+Om medewerkers ook bij naam te kunnen noemen, registreert DTV ook de volledige naam van een persoon.
Deze informatie wordt door SPIN aangeleverd, en wordt verder niet door DTV gebruikt.
In beginsel zou DTV zijn werk kunnen doen zonder de naam te weten die bij een personeelsnummer hoort.
-}
RELATION persoonsnaam[Persoon*TEXT] [UNI]
MEANING "Bij iedere persoon is de volledige naam bekend, zoals die in SPIN wordt gehanteerd."

POPULATION persoonsnaam CONTAINS
  [ ("p1", "Stef Joosten")
  ; ("p2", "Harry in 't Honderd")
  ; ("p3", "Barry Stevens")
  ]

ENDPATTERN

ENDCONTEXT