CONTEXT DTV IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Berichten.adl"
INCLUDE "Autorisaties.adl"
INCLUDE "Referentielijsten.adl"
INCLUDE "Rechten.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Tijdvakken.adl"
INCLUDE "Persoonsinformatie.adl"
INCLUDE "Pcodes.adl"
INCLUDE "PopForTesting.adl"

THEMES Dagverantwoordingen, Autorisaties, Rechten, Medewerkers, Tijdvakken, Betaalperiodes, Persoonsinformatie, Roosters

PURPOSE CONTEXT DTV
{+De werktijdenregeling Nederlandse Voedsel- en Warenautoriteit (hierna: WTR)
stelt een applicatie voor het tijdschrijven beschikbaar voor alle medewerkers,
waarmee alle (werk)tijd wordt geregistreerd en verantwoord (WTR Artikel 2 lid 6).
-}

{-
INTERFACE Dagverantwoording (bon[Dienst*Dagverantwoording], bon[Tijdregistratie*Dagverantwoording], begintijd[Tijdsleuf*Tijdstip], eindtijd[Tijdsleuf*Tijdstip], dienst, werksoort) : I[Dagverantwoording]
BOX [ indiener : medewerkerW
    , Roosterregels : bon[Dienst*Dagverantwoording]~
      BOX [ ""  : I[Dienst]
          , begintijd : begintijd[Tijdsleuf*Tijdstip]
          , eindtijd : eindtijd[Tijdsleuf*Tijdstip]
          , dienst : dienst
          ]
    , Tijdregistraties : bon[Tijdregistratie*Dagverantwoording]~
      BOX [ ""  : I[Tijdregistratie]
          , begintijd : begintijd[Tijdsleuf*Tijdstip]
          , eindtijd : eindtijd[Tijdsleuf*Tijdstip]
          , werksoort : werksoort
          ]
    ]
-}

PATTERN Dagverantwoordingen
PURPOSE PATTERN Dagverantwoordingen
{+Een dagverantwoording is de verantwoording van werk-/pauze- en rusttijden.
FaTijDec hanteert hiervoor het begrip "werkbon".
-}
CLASSIFY Tijdregistratie ISA Tijdsleuf
RELATION werksoort[Tijdregistratie*Werksoort]

RELATION medewerkerW[Dagverantwoording*Persoon] [UNI] PRAGMA "" " is door " " ingevuld"
MEANING "Elke dagverantwoording wordt geacht te zijn ingevuld door de persoon die erop vermeld staat."
PURPOSE RELATION bon[Tijdregistratie*Dagverantwoording]
{+Eén dagverantwoording kan meerdere tijdregistraties bevatten,
zodat ze tezamen kunnen worden goedgekeurd door de leidinggevende.
-}
RELATION bon[Tijdregistratie*Dagverantwoording] [UNI] PRAGMA "" " is op " " ingevuld"
MEANING "Een dagverantwoording vermeldt een aantal tijdregistraties."
ENDPATTERN


PROCESS Roosters
PURPOSE PROCESS Roosters
{+Als er sprake is van wisselende werktijden in verband met vroege-, avond- en nachtdiensten,
werken op zaterdag en zondag
dan wel als door de aard van het voorhanden werk de individuele medewerker niet of nauwelijks invloed heeft op de begin- en eindtijd waarop dat werk moet worden verricht,
wordt volgens een voor die diensten vastgesteld rooster gewerkt (WTR Artikel 3 lid 1).
Iedere medewerker heeft een standaard werkrooster dat is vastgelegd in zijn/haar werktijden profiel.
Dit werktijdenprofiel wordt ``arbeidsmodaliteit'' (ook ``modaliteit'') genoemd.
Duurzame tijdverantwoording heeft informatie nodig over het rooster van elke medewerker, om te kunnen bepalen op welke roostersoort een tijdvakresultaat kan worden geboekt.
Deze informatie wordt uit Rostar CAS gehaald, maar alleen zoveel als nodig om het juiste tijdvakresultaat te kunnen bepalen.
-}

CLASSIFY Dienst ISA Tijdsleuf
RELATION dienst[Dienst*Dienstsoort]

RELATION bon[Dienst*Dagverantwoording]

PURPOSE CONCEPT Dienst REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, pg. 14."
{+ Het werkrooster bepaalt welke regels worden toegepast tijdens de verwerking van tijdregistraties.
Het rooster is opgebouwd uit diensten (roosterregels) die per tijdvak aangeven wat de geplande activiteit voor die werknemer is.
-}
CONCEPT Dienst "Een dienst is een aaneengesloten periode, inclusief de voorgeschreven pauze, waarin arbeid wrodt verricht." "WTR Artikel 1 lid 7"
CONCEPT Dienst "een aaneengesloten periode waarin arbeid wordt verricht en die gelegen is tussen twee opeenvolgende onafgebroken rusttijden van ten minste 8 uren." "Arbeidstijdenwet Artikel 1:7 lid 1 sub c"
CONCEPT Nachtdienst "een dienst waarin meer dan een uur arbeid wordt verricht tussen 00.00 uur en 06.00 uur." "Arbeidstijdenwet Artikel 1:7 lid 1 sub d"
CONCEPT Pauze "een periode van ten minste 15 achtereenvolgende minuten, waarmee de arbeid tijdens de dienst wordt onderbroken en de werknemer geen enkele verplichting heeft ten aanzien van de bedongen arbeid." "Arbeidstijdenwet Artikel 1:7 lid 1 sub e"
CONCEPT Consignatie "een periode tussen twee opeenvolgende diensten of tijdens een pauze, waarin de werknemer uitsluitend verplicht is bereikbaar te zijn om in geval van onvoorziene omstandigheden op oproep zo spoedig mogelijk de bedongen arbeid te verrichten." "Arbeidstijdenwet Artikel 1:7 lid 1 sub g"
CONCEPT Arbeidstijd "Arbeidstijd is de tijd dat de werknemer onder gezag van de werkgever arbeid verricht." "Arbeidstijdenwet Artikel 1:7 lid 1 sub k"
CONCEPT Rusttijd "Rusttijd is de tijd die geen arbeidstijd is." "Arbeidstijdenwet Artikel 1:7 lid 1 sub l"

RELATION roosterSoortR[Dienst*RoosterSoort] [UNI,TOT] PRAGMA "" " valt onder roostersoort "
MEANING "Binnen een dienst is een bepaalde roostersoort van toepassing."
PURPOSE RELATION roosterSoortR[Dienst*RoosterSoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, p14."
{+De roostersoort bepaalt welke regels worden toegepast op tijdregistraties.
-}
RELATION dagR[Dienst*Dagverantwoording] [UNI,TOT] PRAGMA "" " is onderdeel van dagverantwoording " "" -- 
MEANING "Elke dienst is onderdeel van een dagverantwoording."
PURPOSE RELATION dagR[Dienst*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een dienst heeft betrekking op werkzaamheden die op een bepaalde dag moeten worden uitgevoerd en is daarmee onderdeel van een dagverantwoording.
-}

-- RELATION standaardrooster[Dienst*Persoon]
-- MEANING "Roosterregels, die behoren tot het standaardrooster van een persoon, worden als zodanig geregistreerd."
-- PURPOSE RELATION standaardrooster[Dienst*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
-- {+Omdat afwijkingen op het standaardrooster zijn toegestaan, wordt verschil gemaakt tussen een dienst uit het standaardrooster en een dienst uit het afwijkende rooster.
-- Het standaardrooster kan worden opgehaald uit Rostar CAS. Afwijkingen daarvan kunnen door de leidinggevende worden ingevoerd.
-- -}
-- RELATION afwijkendrooster[Dienst*Persoon]
-- MEANING "Roosterregels, die afwijken van het standaardrooster van een persoon, worden als zodanig geregistreerd."
-- RELATION datum[Dienst*Datum]
-- MEANING "De datum, waarop een dienst van toepassing is, is in het systeem bekend."
-- PURPOSE RELATION datum[Dienst*Datum]
-- {+Om een dienst aan een dagverantwoording te kunnen verbinden, houdt het systeem bij op welke datum deze dienst geldt.
-- -}
--RULE standaardrooster~;standaardrooster |- I
--RULE afwijkendrooster~;afwijkendrooster |- I
--RULE I |- (standaardrooster\/afwijkendrooster);(standaardrooster\/afwijkendrooster)~   --- TIJDELIJK FERLON
--MEANING "Elke dienst geldt voor precies één persoon. "

--RULE dienst : dagR~;begintijd |- dagS~;begintijd
--MEANING "Voor elke dag en begintijd van een dienst moet er een dag en begintijd van een dagverantwoording zijn."
--VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")

--ROLE Medewerker MAINTAINS dienst

RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Persoon]
MEANING "Elke persoon heeft een arbeidsmodaliteit."
PURPOSE RELATION arbeidsmodaliteitPersoon[Arbeidsmodaliteit*Persoon] REF "Werktijdenregeling NVWA"
{+Een medewerker wordt in staat gesteld om arbeid te verrichten volgens een jaarlijks vast te stellen arbeidsmodaliteit.
-}

RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum]
MEANING "Een arbeidsmodaliteit heeft een ingangsdatum."
PURPOSE RELATION arbeidsmodaliteitIngangsdatum[Arbeidsmodaliteit*Datum] REF "Werktijdenregeling NVWA"
{+De arbeidsmodaliteit van een medewerker wordt in beginsel eens per jaar vastgesteld, en heeft dus een ingangsdatum.
-}

RELATION arbeidsmod[Arbeidsmodaliteit*Arbeidsmod]
MEANING "Een arbeidsmodaliteit zegt iets over het aantal uren die een medewerker op dagen van een week geacht wordt te werken"
PURPOSE RELATION arbeidsmod[Arbeidsmodaliteit*Arbeidsmod] 
{+In de arbeidsmodaliteit is voor een medewerker vastgelegd hoeveel uren hij op een bepaalde dag in een bepaalde week geacht wordt werkzaamheden te verrichten.
-}

RELATION arbeidsmodWeek[Arbeidsmod*Week]
MEANING "Een arbeidsmodaliteit heeft betrekking op een week."
PURPOSE RELATION arbeidsmodWeek[Arbeidsmod*Week]
{+Bij de arbeidsmodaliteit van een werknemer kan voor 2 weken het aantal uren dat per dag van die week moet worden gewerkt worden vastgelegd.
-}

RELATION arbeidsmodDag[Arbeidsmod*Dag]
MEANING "Een dag van de week is onderdeel van de arbeidsmodaliteit."
PURPOSE RELATION arbeidsmodDag[Arbeidsmod*Dag]
{+Bij de arbeidsmodaliteit wordt per dag van de week (ma,di,wo,do,vr,za,zo) aangegeven hoeveel uren een medewerker geacht wordt werkzaamheden te verrichten.
-}

RELATION arbeidsmodUren[Arbeidsmod*Uren]
MEANING "Een aantal te werken uren is onderdeel van de arbeidsmodaliteit"
PURPOSE RELATION arbeidsmodUren[Arbeidsmod*Uren]
{+Bij de arbeidsmodaliteit wordt het aantal uren dat een medewerker geacht wordt op een bepaalde dag van de week arbeid te verrichten, vastgelegd.
-}

ENDPROCESS

RULE I[DagverantwoordingStatus] = 'Akkoord' \/ 'Retour'
VIEW Tijdregistratie : Tijdregistratie (TXT "activiteit \"", activiteit, TXT " op tijdstip ", begintijd[Tijdsleuf*Tijdstip], TXT "\"")
VIEW Dienst : Dienst (TXT "Dienst \"", roosterSoortR, TXT " op ", dagR;datum, TXT "\"")
VIEW Betaalperiode : Betaalperiode (begindatumP, TXT " - ", einddatumP)
VIEW Periodeverantwoording : Periodeverantwoording (TXT "periode ",periodeverantwoordingPeriode, TXT " van ", periodeverantwoordingPersoon)

INTERFACE "Bereken ongebruikelijke tijden" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~;leidinggevende~
BOX [ "Medewerker : " : I[Persoon]
    , "" : periodeverantwoordingPersoon~   --Periodeverantwoording
      BOX[ "Betaalperiode : " : periodeverantwoordingPeriode  -- Betaalperiode
         , "Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
           BOX [ "betreft datum" :    datum
               , tijdverantwoording : dagS~
                 BOX [ beginTijd :    begintijd
                     , eindTijd :     eindtijd
                     , duur :         tijdsduur
                     , activiteit :   activiteit
                     ]
               ]
         ]
    ]

INTERFACE "Bereken Art17 per betaalperiode" 
        (periodeverantwoordingOngebruikelijkeUren
         ,periodeverantwoordingWezenlijkeOnderbreking
         ,periodeverantwoordingSalarisnummer10Salarisschaal7
         ,periodeverantwoordingVasteToelageArt17lid4
         ,periodeverantwoordingVasteToelageArt17lid7
         ,periodeverantwoordingToelageVoorafgaande36Kalendermaanden) FOR Leidinggevende : I[Periodeverantwoording]
    BOX[ "Medewerker" : periodeverantwoordingPersoon
       , "Betaalperiode : " : periodeverantwoordingPeriode  
       , "Geboortedatum : " : periodeverantwoordingPersoon;geboortedatum
       , "Wezenlijke onderbreking (2 maanden) : " : periodeverantwoordingWezenlijkeOnderbreking
       , "Toelage voorafgaande 36 maanden : " : periodeverantwoordingToelageVoorafgaande36Kalendermaanden
       , "Salaris per uur van salarisnummer 10 van salarisschaal 7 : " : periodeverantwoordingSalarisnummer10Salarisschaal7
       , "Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
         BOX [ "betreft datum" :    datum
             , tijdverantwoording : dagS~
               BOX [ beginTijd :    begintijd
                   , eindTijd :     eindtijd
                   , duur :         tijdsduur
                   , activiteit :   activiteit
                   ]
             ]
       , "Extra uren door ongebruikelijke tijden : " : periodeverantwoordingOngebruikelijkeUren
       , "Vaste toelage als gevolg van Artikel 17 lid 4 : " : periodeverantwoordingVasteToelageArt17lid4
       , "Vaste toelage als gevolg van Artikel 17 lid 7 : " : periodeverantwoordingVasteToelageArt17lid7
       ] 

--INTERFACE "Nieuwe periodeverantwoording" FOR Leidinggevende : I[Periodeverantwoording]
--BOX ["Voor medewerker : " : periodeverantwoordingPersoon
--    ,"Voor betaalperiode : " : periodeverantwoordingPeriode
--    ]


INTERFACE "Geaccordeerde tijdregistraties"
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties van " : I[Persoon]
    , "" : medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    begintijd[Tijdsleuf*Tijdstip]
                , eindTijd :     eindtijd[Tijdsleuf*Tijdstip]
                ]
          , toelichting : toelichting
          ]
    ]

INTERFACE "Retour"
           ( dagS
           , begintijd[Tijdsleuf*Tijdstip]
           , eindtijd[Tijdsleuf*Tijdstip]
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties van " : I[Persoon]
    , "" : medewerkerD~/\V;'Retour';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , status :       dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    begintijd[Tijdsleuf*Tijdstip]
                , eindTijd :     eindtijd[Tijdsleuf*Tijdstip]
                ]
          , toelichting : toelichting
          , "Commentaar" :opmerkingValidatie
          ]
    ]

{-
INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dagS
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanBetaalperiode
         , periodeverantwoordingDagverantwoording
           , roosterSoortR
           , dagR
           , begintijd[Tijdsleuf*Tijdstip]
           , eindtijd[Tijdsleuf*Tijdstip]
           ) FOR Medewerker : I[Dagverantwoording]
BOX [ "datum invoer" :    ontvangen -- automatisch invullen
    , "Medewerker" :   medewerkerD  -- automatisch invullen
    , "betreft datum" :    datum    -- door gebruiker laten invullen
    , "betreft betaalperiode" :  dagverantwoordingVanBetaalperiode -- automatisch laten invullen
    , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~ -- automatisch laten invullen
    , tijdverantwoording : dagS~
      BOX [ beginTijd :    begintijd -- twee van de drie in laten vullen, de derde automatisch.
          , eindTijd :     eindtijd
          , duur :         tijdsduur
          , activiteit :   activiteit      -- door gebruiker laten invullen
          , project :      project         -- door gebruiker laten invullen
          , kostenplaats : kostenplaats    -- door gebruiker laten invullen
          ]
    , roosterverantwoording : dagR~
      BOX [ roosterSoort : roosterSoortR
          , beginTijd :    begintijd[Tijdsleuf*Tijdstip]
          , eindTijd :     eindtijd[Tijdsleuf*Tijdstip]
          ]
    , Opmerkingen : toelichting
    ]
-}

INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dagS
           , begintijd[Tijdsleuf*Tijdstip]
           , eindtijd[Tijdsleuf*Tijdstip]
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanBetaalperiode
           , periodeverantwoordingDagverantwoording
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~ --I[Dagverantwoording]
BOX ["Dagverantwoording : " : medewerkerD~  -- Dagverantwoording
    BOX ["Betreft datum" : datum[Dagverantwoording*Datum] 
        , "datum invoer" :    ontvangen -- automatisch invullen
        , "betreft betaalperiode" :  dagverantwoordingVanBetaalperiode -- automatisch laten invullen
        , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~ -- automatisch laten invullen
        , tijdverantwoording : dagS~
          BOX [ beginTijd :    begintijd[Tijdsleuf*Tijdstip] -- twee van de drie in laten vullen, de derde automatisch.
              , eindTijd :     eindtijd[Tijdsleuf*Tijdstip]
              , duur :         tijdsduur
              , activiteit :   activiteit      -- door gebruiker laten invullen
              , project :      project         -- door gebruiker laten invullen
              , kostenplaats : kostenplaats    -- door gebruiker laten invullen
              ]
--            , rooster : datum[Dagverantwoording*Datum];datum[Dienst*Datum]~/\medewerkerD;standaardrooster~
       , rooster : dagR~
       BOX [ roosterSoort : roosterSoortR
             , beginTijd :    begintijd[Tijdsleuf*Tijdstip]
             , eindTijd :     eindtijd[Tijdsleuf*Tijdstip]
           ]
       , Opmerkingen : toelichting
       ]
    ]


{-  OUD
          
INTERFACE "Werkrooster inregelen"
          (  datum[Dienst*Datum]
           , roosterSoortR
           , dagverantwoordingVanBetaalperiode
           , periodeverantwoordingDagverantwoording, begintijd
           , eindtijd
           , standaardrooster
          )FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
	   , datum[Dagverantwoording*Datum]
	  )
	  FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
            , "" : standaardrooster~     -- Dienst
            BOX [ "Dienst : " : I[Dienst]
                      BOX [ "Datum" : datum[Dienst*Datum]
                        , roostersoort : roosterSoortR
                        , begintijd : begintijd
                        , eindtijd : eindtijd
                      ]
          BOX [ "Betreft datum : " : datum[Dagverantwoording*Datum]
                ]
          ]
    ]
-}

INTERFACE "Werkrooster inregelen"
	  (  roosterSoortR
           , begintijd[Tijdsleuf*Tijdstip]
           , eindtijd[Tijdsleuf*Tijdstip]
	   , dagR
           , datum[Dagverantwoording*Datum]
	   , medewerkerD
          )
          FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
     , "" : medewerkerD~     -- Dagverantwoording
     BOX ["Datum" : datum[Dagverantwoording*Datum]
	  ,"Rooster" : dagR~	-- Dienst
	  BOX [ roostersoort : roosterSoortR
	  	, begintijd : begintijd[Tijdsleuf*Tijdstip]
          	, eindtijd : eindtijd[Tijdsleuf*Tijdstip] 
              ]
         ]
    ]

INTERFACE "Werkrooster bekijken"
          FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
     , "" : medewerkerD~;dagR~  --Dienst
     BOX ["Datum" : dagR;datum[Dagverantwoording*Datum]
           , roostersoort : roosterSoortR
           , begintijd : begintijd[Tijdsleuf*Tijdstip]
           , eindtijd : eindtijd[Tijdsleuf*Tijdstip]
	 ]
    ]

INTERFACE "Arbeidsmodaliteit inregelen"
	  (  arbeidsmodaliteitPersoon
	   , arbeidsmodaliteitIngangsdatum
	   , arbeidsmod
	   , arbeidsmodWeek
	   , arbeidsmodDag
	   , arbeidsmodUren
	  )
	  FOR Leidinggevende : I[Arbeidsmodaliteit]
      BOX ["Medewerker : " : arbeidsmodaliteitPersoon
	  ,"Ingangsdatum : " : arbeidsmodaliteitIngangsdatum
	  , "Modaliteit" : arbeidsmod  --Arbeidsmod
	  BOX [ "Week : " : arbeidsmodWeek
		,"Dag : " : arbeidsmodDag
		,"Uren : " : arbeidsmodUren
	      ]
    ]

INTERFACE "Arbeidsmodaliteit bekijken"
	  FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Modaliteit van : " : I[Persoon]
     , "" : arbeidsmodaliteitPersoon~;arbeidsmod  --Arbeidsmod
     BOX ["Week" : arbeidsmodWeek
 	  , dag : arbeidsmodDag
	  , uren : arbeidsmodUren
         ]
    ]

-- ENFORCE [TOT] kostenplaats[Tijdregistratie*Kostenplaats] BY Gebruiker

-- Vraag: Welke roostersoorten bestaan er?
-- Antwoord: Tenminste "Regulier", "Dienst", "Geen dienst", "Overwerk io", "Beschikbaar"
-- Antwoord: Volgens de IA bestaat er een referentielijst van roostersoorten.
-- Vraag: Welke uurgroepen bestaan er?
-- Antwoord: Tenminste "Arbeidsuren", "Meeruren", "TOD 0%", "TOD 20%", "TOD 40%", "VRU 45%", "Overuren 25%", "Overuren 50%", "Beschikbaarheid 5%"
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze"
-- Vraag: Welke activiteiten bestaan er?
-- Antwoord: Tenminste "Toezicht", "Inspectie", "Pauze", "Advies", "Compensatie verlof", "Overleg", "Support", "Reistijd naar inspectielokatie"
-- Antwoord: In de IA wordt een activiteit "tijdRubriek" genoemd. Daarvan bestaat een referentielijst.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze", "Compensatie"
-- Vraag: Welke urenpools bestaan er?
-- Antwoord: Tenminste "Vakantiedagen", "Compensatie"

CONCEPT Betaalperiode "Een betaalperiode eriode waarover het salaris wordt berekend en uitbetaald"
PURPOSE CONCEPT Betaalperiode REF "BBRA Artikel 3 lid 1"
{+Het salaris, de toelagen, de periodieke toeslag en de vergoedingen voor extra diensten worden maandelijks betaald.
De salarisverwerking vindt dan ook plaats per maand.
In DTV gebruiken we hiervoor het woord ``betaalperiode''.
-}

CONCEPT TijdvakResultaat "Een TijdvakResultaat is het resultaat van opsplitsing van de tijdregistraties naar specifieke uurtijdvakken en roostersoorten." "IA"
PURPOSE CONCEPT TijdvakResultaat REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Tijdregistraties worden in een tijdvakresultaat opgesplitst naar specifieke uurtijdvakken en roostersoorten.
De tijd geregistreert bij een tijdvakresultaat wordt vervolgens als volgt toegekend aan uurgroepen:
(opzoeken in BBRA/ARAR/WTR)
-}

CONCEPT Uurgroep "Een Uurgroep is een hoeveelheid uren van een bepaalde categorie binnen een specifieke periode. Bijvoorbeeld de overwerk uren op een dag."
PURPOSE CONCEPT Uurgroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De juiste inrichting van uurgroepen zorgt er voor dat de juiste berekeningen gemaakt kunnen worden. Voorafgaand aan de uitvoering van betaalperiode worden er per uurGroep totalen gemaakt voor de hele betaalperiode.
-} 

CONCEPT PayrollGroep "Een PayrollGroep is een groep uren binnen een bepaalde Betaalperiode die onder een specifieke code aan de salarisverwerking worden doorgegeven."
PURPOSE CONCEPT PayrollGroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Aan het einde van een betaalperiode worden de uren per payrollGroep aangeleverd van P-Direct voor verwerking in de salarisadministratie.
-}

CONCEPT Periodeverantwoording "Een periodeverantwoording is een periodiek overzicht van de gewerkte uren van één medewerker in één betaalperiode."
PURPOSE CONCEPT Periodeverantwoording 
{+Voor elke medewerker worden de uren per betaalperiode verwerkt.
De urenverantwoording van één medewerker voor één betaalperiode wordt een Periodeverantwoording genoemd.
Het woord ``timesheet'' wordt hiervoor als synoniem gebruikt. Dit woord is afkomstig uit de informatie-analyse.
-}

CLASSIFY Dagverantwoording ISA Bericht
CONCEPT Dagverantwoording "Een dagverantwoording is een overzicht van alle gewerkte uren van één dag voor één medewerker. (De PSA spreekt over een dagfilm)." "IA"
PURPOSE CONCEPT Dagverantwoording 
{+Wanneer een medewerker tijd registreert, resulteert dat in een aantal tijdregistraties, die samen in één bericht realtime het systeem binnenkomen.
Om deze tijdregistraties bijeen te houden, gebruiken we daarvoor het concept Dagverantwoording.
-}

CONCEPT Werktijdenpakket "Een werktijdenpakket is een vooraf gedefinieerd pakket van werktijdRules die een geheel vormen en van toepassing zijn op een bepaalde groep medewerkers." "IA"
PURPOSE CONCEPT Werktijdenpakket
{+ Om te bepalen welke werktijdrules wegens gemaakte afspraken of wetgeving voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding), 
moet worden nagegaan welk werktijdenpakket op dat moment geldig is voor die medewerker.
-}

CONCEPT MedewerkerWerktijdenpakket "Een MedewerkerWerktijdenpakket geeft voor een medewerker aan in welke betaalperiode een werktijdenpakket geldig is."
PURPOSE CONCEPT MedewerkerWerktijdenpakket
{+Voor elke medewerker is voor elke betaalperiode één werktijdenpakket van toepassing. Een Medewerkerwerktijdenpakket geeft voor één medewerker aan in welke betaalperiode (met een begin en einddatum) een bepaald werktijdenpakket  
van toepassing is.
-}



-- Interface Beheren Tijdverantwoording (reeds bestaand) moet worden uitgebreid.

-- Interface Beheren Roostertypen moet worden toegevoegd

-- Regel: Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd.

-- Regel: Alle uren moeten verantwoord zijn.
-- Regel: Een autorisatie voor een medewerker kan alleen gegeven worden door degene die op het moment van autoriseren de leidinggevende is van die persoon.
-- Regel: Een periodeverantwoording kan alleen goedgekeurd worden als alle combinaties van tijdRubriek, project en kostenplaats ook geautoriseerd zijn.
-- Regel "JaarRule" "Elk jaar begint met nul vakantiedagen" 
-- Regel "JaarRule" "Het aantal overuren waar jaar (n+1) mee begint is het aantal overuren ontstaan in december van jaar n en niet opgenomen is in die maand."


ENDCONTEXT