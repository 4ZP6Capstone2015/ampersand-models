CONTEXT Spin
INCLUDE "Login.adl"
INCLUDE "Referentielijsten.adl"
INCLUDE "Rechten.adl"
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Tijdvakken.adl"
INCLUDE "Persoonsinformatie.adl"
INCLUDE "Pcodes.adl"

INTERFACE Werkbon (bon[Roosterregel*Werkbon], bon[TimesheetRegel*Werkbon], begintijd, eindtijd, dienst, werksoort) : I[Werkbon]
BOX [ indiener : medewerkerW
    , Roosterregels : bon[Roosterregel*Werkbon]~
      BOX [ ""  : I[Roosterregel]
          , begintijd : begintijd
          , eindtijd : eindtijd
          , dienst : dienst
          ]
    , Timesheetregels : bon[TimesheetRegel*Werkbon]~
      BOX [ ""  : I[TimesheetRegel]
          , begintijd : begintijd 
          , eindtijd : eindtijd
          , werksoort : werksoort
          ]
    ]

PATTERN Werkbonnen
CLASSIFY TimesheetRegel ISA Tijdsleuf
RELATION werksoort[TimesheetRegel*Werksoort]

RELATION medewerkerW[Werkbon*Persoon] [UNI,TOT] PRAGMA "" "is door" "ingevuld"
MEANING "Elke werkbon wordt geacht te zijn ingevuld door de persoon die erop vermeld staat."
PURPOSE RELATION bon[TimesheetRegel*Werkbon]
{+Eén werkbon kan meerdere timesheetregels bevatten,
zodat ze tezamen kunnen worden goedgekeurd door de leidinggevende.
-}
RELATION bon[TimesheetRegel*Werkbon] [UNI] PRAGMA "" "is op" "ingevuld"
MEANING "Een werkbon vermeldt een aantal timesheetregels."

ENDPATTERN

PATTERN Medewerkers
--PURPOSE PATTERN "Artikel 17"

PURPOSE CONCEPT Medewerker REF "WTR Artikel 1 lid 2"
{+De Werktijdenregeling NVWA (hierna: WTR) spreekt van "Medewerker", en het ERD van SPIN bevat een entiteit SPN_Medewerker.
Daarom gebruikt de tijdverantwoording dit begrip ook, en wel conform de WTR.
-}
CONCEPT Medewerker "Een medewerker is een ambtenaar in de zin van artikel 1 van het ARAR die werkzaam is bij of voor de NVWA."
CLASSIFY Medewerker ISA Persoon


PURPOSE RELATION functie[Medewerker*Functie] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Op basis van de functie van een persoon wordt bepaald welke processtappen mogen worden uitgevoerd. Daarom moet de functie van een persoon bekend zijn.
-}
RELATION functie[Medewerker*Functie] [UNI] PRAGMA "" "heeft als functie" ""
MEANING "De functie van een persoon wordt geregistreerd."
PURPOSE RELATION leidinggevende[Medewerker*Medewerker] REF "WTR Artikel 1 lid 3."
{+Een leidinggevende mag voor een medewerker bepaalde processtappen uitvoeren zoals het valideren/afkeuren van dagverantwoordingen, het autoriseren of terugtrekken van een autorisatie van een medewerker
om op een bepaalde combinatie te mogen tijdschrijven. Daarom moet de leidinggevende van een persoon bekend zijn (bron: Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0).
-}
RELATION leidinggevende[Medewerker*Medewerker] [UNI] PRAGMA "" "heeft als leidinggevende" ""
MEANING "Een leidinggevende is de functionaris aan wie de medewerker volgens de hiërarchische organisatiestructuur verantwoording verschuldigd is."
ENDPATTERN


PROCESS Roosters
PURPOSE PROCESS Roosters
{+Duurzame tijdverantwoording heeft informatie nodig over het rooster van elke medewerker, om te kunnen bepalen op welke roostersoort een tijdvakresultaat kan worden geboekt.
Deze informatie wordt uit Rostar CAS gehaald, maar alleen zoveel als nodig om het juiste tijdvakresultaat te kunnen bepalen.
-}

CLASSIFY Roosterregel ISA Tijdsleuf
RELATION dienst[Roosterregel*Dienstsoort]

RELATION bon[Roosterregel*Werkbon]

CONCEPT RoosterRegel "Een RoosterRegel op een dagverantwoording geeft aan dat een medewerker in het daarin genoemde tijdvak beschikbaar is voor werk."
PURPOSE CONCEPT RoosterRegel REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, pg. 14."
{+Iedere medewerker heeft een standaard werkrooster dat is vastgelegd in zijn/haar werktijden profiel. Het werkrooster bepaalt welke regels worden toegepast tijdens de verwerking van
de dagverantwooding (timesheet). Het rooster is opgebouwd uit roosterregels die per tijdvak aangeven wat de geplande activiteit voor die werknemer is.
-}
RELATION beginTijdR[RoosterRegel*Tijdstip] [UNI,TOT] PRAGMA "" "begint op tijdstip" ""
MEANING "Elke roosterregel heeft een begintijd"
PURPOSE RELATION beginTijdR[RoosterRegel*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Omdat elke roosterregel een aaneengesloten tijdvak beslaat, wordt het begin- en eindtijdstip van de roosterregel opgeslagen.
-}
RELATION eindTijdR[RoosterRegel*Tijdstip] [UNI,TOT] PRAGMA "" "eindigt op tijdstip" ""
MEANING "Elke roosterregel heeft een eindtijd."

RELATION roosterSoortR[RoosterRegel*RoosterSoort] [UNI,TOT] PRAGMA "" "valt onder roostersoort" ""
MEANING "Binnen een roosterregel is een bepaalde roostersoort van toepassing."
PURPOSE RELATION roosterSoortR[RoosterRegel*RoosterSoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, p14."
{+De roostersoort bepaalt welke regels worden toegepast tijdens de verwerking van de dagverantwooding (timesheet).
-}
RELATION dagR[RoosterRegel*Dagverantwoording] [UNI,TOT] PRAGMA "" "is onderdeel van dagverantwoording" "" -- 
MEANING "Elke roosterregel is onderdeel van een dagverantwoording."
PURPOSE RELATION dagR[RoosterRegel*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een roosterregel heeft betrekking op werkzaamheden die op een bepaalde dag moeten worden uitgevoerd en is daarmee onderdeel van een dagverantwoording.
-}
RELATION standaardrooster[RoosterRegel*Persoon]
MEANING "Roosterregels, die behoren tot het standaardrooster van een persoon, worden als zodanig geregistreerd."
PURPOSE RELATION standaardrooster[RoosterRegel*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Omdat afwijkingen op het standaardrooster zijn toegestaan, wordt verschil gemaakt tussen een roosterregel uit het standaardrooster en een roosterregel uit het afwijkende rooster.
Het standaardrooster kan worden opgehaald uit Rostar CAS. Afwijkingen daarvan kunnen door de leidinggevende worden ingevoerd.
-}
RELATION afwijkendrooster[RoosterRegel*Persoon]
MEANING "Roosterregels, die afwijken van het standaardrooster van een persoon, worden als zodanig geregistreerd."
RELATION datum[RoosterRegel*Datum]
MEANING "De datum, waarop een roosterregel van toepassing is, is in het systeem bekend."
PURPOSE RELATION datum[RoosterRegel*Datum]
{+Om een roosterregel aan een dagverantwoording te kunnen verbinden, houdt het systeem bij op welke datum deze roosterregel geldt.
-}
RULE standaardrooster~;standaardrooster |- I
RULE afwijkendrooster~;afwijkendrooster |- I
RULE I |- (standaardrooster\/afwijkendrooster);(standaardrooster\/afwijkendrooster)~
MEANING "Elke roosterregel geldt voor precies één persoon. "

RULE roosterregel : dagR~;beginTijdR |- dagS~;begintijd
MEANING "Voor elke dag en begintijd van een roosterregel moet er een dag en begintijd van een dagverantwoording zijn."
--VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")

ROLE Medewerker MAINTAINS roosterregel
ENDPROCESS

POPULATION standaardrooster[RoosterRegel*Persoon] CONTAINS
   [ ("r1","p2")
   ; ("r2","p2")
   ; ("r3","p2")
   ; ("rr1","p2")
   ; ("rr2","p2")
   ; ("rr3","p2")
   ]

POPULATION afwijkendrooster[RoosterRegel*Persoon] CONTAINS
   [ ("rr4","p2")
   ; ("rr5","p2")
   ; ("rr6","p2")
   ]

POPULATION beginTijdR[RoosterRegel*Tijdstip] CONTAINS
   [ ("rr1","8:00")
   ; ("rr2","12:00")
   ; ("rr3","13:00")
   ; ("rr4","17:00")
   ; ("rr5","19:00")
   ; ("rr6","0:00")
   ]

POPULATION dagR[RoosterRegel*Dagverantwoording] CONTAINS
   [ ("rr1","DV3")
   ; ("rr2","DV3")
   ; ("rr3","DV3")
   ; ("rr4","DV3")
   ; ("rr5","DV3")
   ; ("rr6","DV3")
   ]

POPULATION eindTijdR[RoosterRegel*Tijdstip] CONTAINS
   [ ("rr1","12:00")
   ; ("rr2","13:00")
   ; ("rr3","17:00")
   ; ("rr4","19:00")
   ; ("rr5","0:00")
   ; ("rr6","8:00")
   ]

POPULATION roosterSoortR[RoosterRegel*RoosterSoort] CONTAINS
   [ ("rr1","Regulier")
   ; ("rr2","Beschikbaar")
   ; ("rr3","Regulier")
   ; ("rr4","Regulier")
   ; ("rr5","Beschikbaar")
   ; ("rr6","Beschikbaar")
   ]

POPULATION standaardrooster[RoosterRegel*Persoon] CONTAINS
  [ ("rr1", "p2")
  ; ("rr2", "p2")
  ; ("rr3", "p2")
  ]

POPULATION afwijkendrooster[RoosterRegel*Persoon] CONTAINS
  [ ("rr4", "p2")
  ; ("rr5", "p2")
  ; ("rr6", "p2")
  ]

--POPULATION payrollPeriode[Periode*Weeknummer]
--  [ ("p1", "p201401")
--  ; ("p2", "p201402")
--  ; ("p3", "p201403")
--  ; ("p4", "p201404")
--  ; ("p5", "p201405")
--  ; ("p6", "p201406")
--  ; ("p7", "p201407")
--  ; ("p8", "p201408")
--  ; ("p9", "p201409")
--  ; ("p10", "p201410")
--  ; ("p11", "p201411")
--  ; ("p12", "p201412")
--  ; ("p13", "p201413")
--  ]

RULE I[DagverantwoordingStatus] = 'Akkoord' \/ 'Retour'
VIEW TimesheetRegel : TimesheetRegel (TXT "activiteit \"", activiteit, TXT " op tijdstip ", begintijd, TXT "\"")
VIEW RoosterRegel : RoosterRegel (TXT "Roosterregel \"", roosterSoortR, TXT " op ", dagR;datum, TXT "\"")
VIEW PayrollTijdvak : PayrollTijdvak (begindatumP, TXT " - ", einddatumP)
VIEW Periodeverantwoording : Periodeverantwoording (TXT "periode ",periodeverantwoordingPeriode, TXT " van ", periodeverantwoordingPersoon)

RULE autorisatieregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~/\(activiteit;cat)~\tijdCategorie~);medewerkerA
--RULE autorisatregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~/\(tijdCategorie/(activiteit;cat))~);medewerkerA
--RULE autorisatieregel : dagS;medewerkerD |- (project;projectA~/\kostenplaats;kostenPlaats~);medewerkerA
MEANING "Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd."
VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")

INTERFACE "Verkregen Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          ]
    ]

-----   BEGIN OUD ------

--			INTERFACE "Bereken Art17 per Payrollperiode" (ongebruikelijkUrenPayrollPeriode) FOR Leidinggevende : I[Periode]
--			BOX ["Payrollperiode" : I[Periode]                                                                            
--			     ,"" : I[Periode]                                                                                         
--			      BOX ["" : dagverantwoordingVanPayrollPeriode~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoordin
--			     BOX [ "betreft datum" :    datum                                                                         
--			    	          , tijdverantwoording : dagS~                                                                
--				    BOX [ beginTijd :    begintijd                                                                   
--					  , eindTijd :     eindtijd                                                                  
--					  , duur :         tijdsduur                                                                  
--					  , activiteit :   activiteit                                                                 
--			    	        ]                                                                                             
--			               ]                                                                                              
--			     ]                                                                                                        
--			     ,"Ongebruikelijke uren (Uurgroep en uren)" : uurgroepInPeriode  --Uurgroep                               
--			     BOX ["Uurgroep : " : uurgroepInPeriode~;uurgroepInPeriode                                                
--			          , "Aantal uren : " : urenInUurgroep~                                                                
--			         ]                                                                                                    
--			     , "Totaal Ongebruikelijke uren periode : " : ongebruikelijkUrenPayrollPeriode                            
--			    ]                                                                                                         


--			INTERFACE "Bereken Ongebruikelijke tijden" 
--				(uurgroepInPeriode
--			--	 ,payrollPeriodePersoon
--			--	 ,payrollPeriode
--				 ,urenInUurgroep
--				 ,refLijstUurgroepen
--				 ,wezenlijkeOnderbrekingPersoon
--				 ,wezenlijkeOnderbreking
--				 ,toelageVoorafgaande36Kalendermaanden
--			         ,ongebruikelijkUrenPayrollPeriode)FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
--			BOX ["Autorisaties verleend door " : I[Persoon]
--			     , "" : verleendDoor~/\V;'Verleend';eventType~   -- AutorisatieEventTijdschrijven
--			--     , "" : leidinggevende~  --  Persoon
--			     BOX [ "" :      medewerkerA          -- Persoon
--			           	BOX ["Tijdregistratieberichten van " : I[Persoon]
--					     , "Geboortedatum : " :  geboortedatum
--					     , "Wezenlijke onderbreking : " : wezenlijkeOnderbrekingPersoon~  --WezenlijkeOnderbreking
--						BOX ["Sprake van wezenlijke onderbreking : " : wezenlijkeOnderbreking
--					    	    ]
--					     , "Toelage voorafgaande 36 maanden : " : toelageVoorafgaande36Kalendermaanden
--					     ,"" : I[Persoon]	
--				             BOX ["" : medewerkerD~;dagverantwoordingVanPayrollPeriode -- Periode
--				                  BOX ["Payrollperiode" : I[Periode] 
--						       ,"" : I[Periode]
--				  	                BOX ["" : dagverantwoordingVanPayrollPeriode~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoording
--			--	  	                BOX ["" : dagverantwoordingVanPayrollPeriode~/\medewerkerD;medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoording
--				      			     BOX [ "betreft datum" :    datum
--				          	      	          , tijdverantwoording : dagS~
--				            			    BOX [ beginTijd :    begintijd
--				                			  , eindTijd :     eindtijd
--				                			  , duur :         tijdsduur
--				                			  , activiteit :   activiteit
--				                	    	        ] 
--					                         ]
--							     ]
--						       ,"Ongebruikelijke uren (Uurgroep en uren)" : uurgroepInPeriode  --Uurgroep
--						       BOX ["Uurgroep : " : uurgroepInPeriode~;uurgroepInPeriode
--						            , "Aantal uren : " : urenInUurgroep~
--						           ]
--						       , "Totaal Ongebruikelijke uren periode : " : ongebruikelijkUrenPayrollPeriode
--			   			      ]
--			                         ]
--				      	    ]
--				 ]
--			    ]

----- EINDE OUD ------

INTERFACE "Bereken ongebruikelijke tijden" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~;leidinggevende~
BOX ["Medewerker : " : I[Persoon]
     ,"" : periodeverantwoordingPersoon~   --Periodeverantwoording
     BOX["Periode : " : periodeverantwoordingPeriode  -- PayrollTijdvak
	 ,"Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
	       BOX ["betreft datum" :    datum
       	            , tijdverantwoording : dagS~
		    BOX [ beginTijd :    begintijd
       			  , eindTijd :     eindtijd
       			  , duur :         tijdsduur
       			  , activiteit :   activiteit
       	    	        ] 
                   ]
	   
	]
    ]

INTERFACE "Bereken Art17 per payroll periode" 
	  (periodeverantwoordingOngebruikelijkeUren
	   ,periodeverantwoordingWezenlijkeOnderbreking
	   ,periodeverantwoordingSalarisnummer10Salarisschaal7
	   ,periodeverantwoordingVasteToelageArt17lid4
	   ,periodeverantwoordingVasteToelageArt17lid7
	   ,periodeverantwoordingToelageVoorafgaande36Kalendermaanden) FOR Leidinggevende : I[Periodeverantwoording]
     BOX["Medewerker" : periodeverantwoordingPersoon
	 ,"Periode : " : periodeverantwoordingPeriode  
	 ,"Geboortedatum : " : periodeverantwoordingPersoon;geboortedatum
	 ,"Wezenlijke onderbreking (2 maanden) : " : periodeverantwoordingWezenlijkeOnderbreking
	 ,"Toelage voorafgaande 36 maanden : " : periodeverantwoordingToelageVoorafgaande36Kalendermaanden
	 ,"Salaris per uur van salarisnummer 10 van salarisschaal 7 : " : periodeverantwoordingSalarisnummer10Salarisschaal7
	 ,"Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
	       BOX ["betreft datum" :    datum
       	            , tijdverantwoording : dagS~
		    BOX [ beginTijd :    begintijd
       			  , eindTijd :     eindtijd
       			  , duur :         tijdsduur
       			  , activiteit :   activiteit
       	    	        ] 
                   ]
	  , "Extra uren door ongebruikelijke tijden : " : periodeverantwoordingOngebruikelijkeUren
	  , "Vaste toelage als gevolg van Artikel 17 lid 4 : " : periodeverantwoordingVasteToelageArt17lid4
	  , "Vaste toelage als gevolg van Artikel 17 lid 7 : " : periodeverantwoordingVasteToelageArt17lid7
	]

--INTERFACE "Nieuwe periodeverantwoording" FOR Leidinggevende : I[Periodeverantwoording]
--BOX ["Voor medewerker : " : periodeverantwoordingPersoon
--     ,"Voor periode : " : periodeverantwoordingPeriode
--    ]


INTERFACE "Geaccordeerde tijdregistraties"
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    beginTijdR
                , eindTijd :     eindTijdR
                ]
          , toelichting : toelichting
          ]
    ]

INTERFACE "Retour"
           ( dagS
           , begintijd
           , eindtijd
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerkerD~/\V;'Retour';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , status :       dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    beginTijdR
                , eindTijd :     eindTijdR
                ]
          , toelichting : toelichting
          , "Commentaar" :opmerkingValidatie
          ]
    ]


INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dagS
           , begintijd
           , eindtijd
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanPayrollPeriode
	   , periodeverantwoordingDagverantwoording
           , roosterSoortR
           , dagR
           , beginTijdR
           , eindTijdR
           ) FOR Medewerker : I[Dagverantwoording]
BOX [ "datum vandaag" :    ontvangen
    , "ingediend door" :   medewerkerD
    , "betreft datum" :    datum
    , "betreft payrollperiode" :  dagverantwoordingVanPayrollPeriode
    , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~
    , tijdverantwoording : dagS~
      BOX [ beginTijd :    begintijd
          , eindTijd :     eindtijd
          , duur :         tijdsduur
          , activiteit :   activiteit
          , project :      project
          , kostenplaats : kostenplaats
          ]
    , roosterverantwoording : dagR~
      BOX [ roosterSoort : roosterSoortR
          , beginTijd :    beginTijdR
          , eindTijd :     eindTijdR
          ]
    , toelichting : toelichting
    ]


INTERFACE "Te valideren"
           ( dagverantwoordingStatus
           , activiteit
           , project
           , kostenplaats
           , opmerkingValidatie
           ) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties voor " : I[Persoon]
    , "" : leidinggevende~;medewerkerD~/\-((V[Medewerker*DagverantwoordingStatus]#'Akkoord');dagverantwoordingStatus~)   -- Dagverantwoording
      BOX [ ontvangen :          ontvangen
          , medewerker :         medewerkerD
          , "betreft datum" :    datum
          , akkoord :            dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort :    roosterSoortR
                , beginTijd :       beginTijdR
                , eindTijd :        eindTijdR
                ]
          , "Commentaar bij validatie" : opmerkingValidatie
          ]
    ]



-- ENFORCE [TOT] kostenplaats[TimesheetRegel*Kostenplaats] BY Gebruiker

-- Vraag: Welke roostersoorten bestaan er?
-- Antwoord: Tenminste "Regulier", "Dienst", "Geen dienst", "Overwerk io", "Beschikbaar"
-- Antwoord: Volgens de IA bestaat er een referentielijst van roostersoorten.
-- Vraag: Welke uurgroepen bestaan er?
-- Antwoord: Tenminste "Arbeidsuren", "Meeruren", "TOD 0%", "TOD 20%", "TOD 40%", "VRU 45%", "Overuren 25%", "Overuren 50%", "Beschikbaarheid 5%"
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze"
-- Vraag: Welke activiteiten bestaan er?
-- Antwoord: Tenminste "Toezicht", "Inspectie", "Pauze", "Advies", "Compensatie verlof", "Overleg", "Support", "Reistijd naar inspectielokatie"
-- Antwoord: In de IA wordt een activiteit "tijdRubriek" genoemd. Daarvan bestaat een referentielijst.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze", "Compensatie"
-- Vraag: Welke urenpools bestaan er?
-- Antwoord: Tenminste "Vakantiedagen", "Compensatie"

-- CONCEPT Payrollperiode "Periode van 4 weken waarover het salaris wordt berekend en uitbetaald"
-- PURPOSE CONCEPT Payrollperiode REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
-- {+De salarisverwerking vindt plaats per tijdvak van 4 weken. Dit tijdvak wordt een payrollperiode (payrollTijdvak) genoemd.
-- -}

CONCEPT PayrollTijdvak "Periode van 4 weken waarover het salaris wordt berekend en uitbetaald"
PURPOSE CONCEPT PayrollTijdvak REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De salarisverwerking vindt plaats per tijdvak van 4 weken. Dit tijdvak wordt een payrollperiode (payrollTijdvak) genoemd.
-}

CONCEPT TijdvakResultaat "Een TijdvakResultaat is het resultaat van opsplitsing van de dagverantwoordingregels naar specifieke uurtijdvakken en roostersoorten." "IA"
PURPOSE CONCEPT TijdvakResultaat REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Dagverantwoordingsregels worden in een TijdvakResultaat opgesplitst naar specifieke uurtijdvakken en roostersoorten. De tijd geregistreert bij een TijdvakResultaten wordt vervolgens volgens bepaalde regels toegekend aan uurgroepen.
-}

CONCEPT Uurgroep "Een Uurgroep is een hoeveelheid uren van een bepaalde categorie binnen een specifieke periode. Bijvoorbeeld de overwerk uren op een dag."
PURPOSE CONCEPT Uurgroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De juiste inrichting van uurgroepen zorgt er voor dat de juiste berekeningen gemaakt kunnen worden. Voorafgaand aan de uitvoering van payroll periode worden er per uurGroep totalen gemaakt voor de hele payrollPeriode.
-} 

CONCEPT PayrollGroep "Een PayrollGroep is een groep uren binnen een bepaald payrollTijdvak die onder een specifieke code aan de salarisverwerking worden doorgegeven."
PURPOSE CONCEPT PayrollGroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Aan het einde van een payroll periode worden de uren per payrollGroep aangeleverd van P-Direct voor verwerking in de salarisadministratie.
-}

CONCEPT Timesheet "Een timesheet is een periodiek overzicht van de gewerkte uren van één medewerker."

CONCEPT Periodeverantwoording "Een periodeverantwoording is een periodiek overzicht van de gewerkte uren van één medewerker in één payrollperiode."
PURPOSE CONCEPT Periodeverantwoording 
{+Voor elke medewerker worden de uren per payrolltijdvak van 4 weken verwerkt. De urenverantwoording van één medewerker voor één payrolltijdvak wordt een Periodeverantwoording genoemd
-}

CONCEPT Dagverantwoording "Een dagverantwoording is een overzicht van alle gewerkte uren van één dag voor één medewerker. (De PSA spreekt over een dagfilm)." "IA"
PURPOSE CONCEPT Dagverantwoording 
{+Wanneer een medewerker tijd registreert, resulteert dat in een aantal SPN_TIJDVERANTWOORDINGen, die samen in één bericht realtime het systeem binnenkomen.
Om deze SPN_TIJDVERANTWOORDINGen bijeen te houden, gebruiken we daarvoor het concept Dagverantwoording.
-}

CONCEPT UurTijdvak "Een UurTijdvak is een tijdvak binnen een specifieke dag van de week met een begin- en eindtijd. Bijvoorbeeld maandag van 8:00 tot 10:00."
PURPOSE CONCEPT UurTijdvak
{+Om een werkdag onder te verdelen in kleinere tijdseenheden waarin werkzaamheden worden verricht of ingeroosterd, zijn er UurTijdvakken gedefinieerd.
-}
--** Wat wordt precies verstaan onder een uurtijdvak (op een bepaalde datum of op een bepaalde dag)*

CONCEPT Dagsoort "We onderscheiden de dagsoorten: ma-vr, za-zo, feestdag."
PURPOSE CONCEPT Dagsoort
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toeslagpercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}

CONCEPT Werktijdenpakket "Een werktijdenpakket is een vooraf gedefinieerd pakket van werktijdRules die een geheel vormen en van toepassing zijn op een bepaalde groep medewerkers." "IA"
PURPOSE CONCEPT Werktijdenpakket
{+ Om te bepalen welke werktijdrules wegens gemaakte afspraken of wetgeving voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding), 
moet worden nagegaan welk werktijdenpakket op dat moment geldig is voor die medewerker.
-}

CONCEPT MedewerkerWerktijdenpakket "Een MedewerkerWerktijdenpakket geeft voor een medewerker aan in welke periode een werktijdenpakket geldig is."
PURPOSE CONCEPT MedewerkerWerktijdenpakket
{+Voor elke medewerker is voor elke periode één werktijdenpakket van toepassing. Een Medewerkerwerktijdenpakket geeft voor één medewerker aan in welke periode (met een begin en einddatum) een bepaald werktijdenpakket  
van toepassing is.
-}

CONCEPT AutorisatieEventTijdschrijven "Een autorisatieTijdschrijven is de toestemming aan een of meer medewerkers om in een gegeven periode tijd te verantwoorden in de dagverantwoording op een bepaalde combinatie van tijdRubriek, kostenplaats en eventueel project."
RELATION occurA[AutorisatieEventTijdschrijven*DateTime] [UNI,TOT]
RELATION eventType[AutorisatieEventTijdschrijven*EventType] [UNI,TOT]
RELATION medewerkerA[AutorisatieEventTijdschrijven*Persoon]
RELATION geldigVan[AutorisatieEventTijdschrijven*Datum] [UNI,TOT]
RELATION geldigTot[AutorisatieEventTijdschrijven*Datum] [UNI,TOT]
RELATION kostenPlaats[AutorisatieEventTijdschrijven*Kostenplaats] [UNI] --  Kostenplaatsen komen uit een referentielijst
RELATION tijdCategorie[AutorisatieEventTijdschrijven*TijdRubriekCategorie]
RELATION projectA[AutorisatieEventTijdschrijven*Project] [UNI]
RELATION verleendDoor[AutorisatieEventTijdschrijven*Persoon] [UNI,TOT]

RULE I[EventType] = 'Verleend' \/ 'Ingetrokken'

INTERFACE "Verleende autorisaties" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties verleend door " : I[Persoon]
    , "" : verleendDoor~/\V;'Verleend';eventType~   -- AutorisatieEventTijdschrijven
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          ]
    ]

INTERFACE "Nieuwe autorisatie"
           ( verleendDoor
           , eventType
           , medewerkerA
           , geldigVan
           , geldigTot
           , tijdCategorie
           , kostenPlaats
           , projectA
           , occurA
           ) FOR Leidinggevende : I[AutorisatieEventTijdschrijven]
      BOX [ "geautoriseerd door" :       verleendDoor
          , "Nieuw/Intrekken/Wijzigen" : eventType
          , "verleend aan" :             medewerkerA
          , "geldig van" :               geldigVan
          , "geldig tot" :               geldigTot
          , "tijdcategorie" :            tijdCategorie
          , kostenplaats :               kostenPlaats
          , project :                    projectA
          , "tijdstip van verlenen" :    occurA
          ]



-- Interface Beheren Tijdverantwoording (reeds bestaand) moet worden uitgebreid.

-- Interface Beheren Roostertypen moet worden toegevoegd

-- Regel: Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd.

-- Regel: Alle uren moeten verantwoord zijn.
-- Regel: Een autorisatie voor een medewerker kan alleen gegeven worden door degene die op het moment van autoriseren de leidinggevende is van die persoon.
-- Regel: Een timesheet kan alleen goedgekeurd worden als alle combinaties van tijdRubriek, project en kostenplaats ook geautoriseerd zijn.
-- Regel "JaarRule" "Elk jaar begint met nul vakantiedagen" 
-- Regel "JaarRule" "Het aantal overuren waar jaar (n+1) mee begint is het aantal overuren ontstaan in december van jaar n en niet opgenomen is in die maand."
POPULATION begindatum[Tijdsleuf*Datum] CONTAINS
  [ ("s1", "21-08-2014")
  ; ("s2", "21-08-2014")
  ; ("s3", "21-08-2014")
  ; ("s4", "21-08-2014")
  ; ("s5", "21-08-2014")
  ; ("s6", "21-08-2014")
  ; ("s7", "21-08-2014")
  ; ("s8", "21-08-2014")
  ]

POPULATION begintijd[Tijdsleuf*Tijdstip] CONTAINS
  [ ("s1", "8:00")
  ; ("s2", "10:00")
  ; ("s3", "12:00")
  ; ("s4", "14:00")
  ; ("s5", "16:00")
  ; ("s6", "18:00")
  ; ("s7", "20:00")
  ; ("s8", "22:00")
  ]

POPULATION einddatum[Tijdsleuf*Datum] CONTAINS
  [ ("s1", "21-08-2014")
  ; ("s2", "21-08-2014")
  ; ("s3", "21-08-2014")
  ; ("s4", "21-08-2014")
  ; ("s5", "21-08-2014")
  ; ("s6", "21-08-2014")
  ; ("s7", "21-08-2014")
  ; ("s8", "21-08-2014")
  ]

POPULATION eindtijd[Tijdsleuf*Tijdstip] CONTAINS
  [ ("s1", "10:00")
  ; ("s2", "12:00")
  ; ("s3", "14:00")
  ; ("s4", "16:00")
  ; ("s5", "18:00")
  ; ("s6", "20:00")
  ; ("s7", "22:00")
  ; ("s8", "24:00")
  ]

POPULATION project[TimesheetRegel*Project] CONTAINS
  [ ("s1", "DTV")
  ; ("s2", "Project A")
  ; ("s3", "Project X")
  ]

POPULATION kostenplaats[TimesheetRegel*Kostenplaats] CONTAINS
  [ ("s1", "5")
  ; ("s2", "7")
  ; ("s3", "4")
  ]

POPULATION activiteit[TimesheetRegel*Activiteit]  CONTAINS
  [ ("s1", "Toezicht")
  ; ("s2", "Inspectie")
  ; ("s3", "Pauze")
  ; ("s4", "Advies")
  ; ("s5", "Compensatie verlof")
  ; ("s6", "Overleg")
  ; ("s7", "Support")
  ; ("s8", "Reistijd naar inspectielokatie")
  ]

POPULATION datum[Dagverantwoording*Datum] CONTAINS
  [ ("DV1", "01-01-2014")
  ; ("DV2", "03-02-2014")
  ; ("DV3", "12-03-2014")
  ]

POPULATION dagS[TimesheetRegel*Dagverantwoording] CONTAINS
  [ ("s1","DV1")
  ; ("s2","DV2")
  ; ("s3","DV3")
  ; ("s4","DV1")
  ; ("s5","DV3")
  ; ("s6","DV2")
  ; ("s7","DV3")
  ; ("s8","DV2")
  ]

POPULATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus] CONTAINS
  [ ("DV1","Akkoord")
  ; ("DV2","Retour")
  ]

--POPULATION uurgroepInPeriode[Periode*Uurgroep] CONTAINS
--  [ ("p201401","Arbeidsuren")
--  ; ("p201402","TOD 20%")
--  ; ("p201403","TOD 40%")
--  ]

POPULATION urenInUurgroep[Uren*Uurgroep] CONTAINS
  [ ("3","Arbeidsuren")
  ; ("4","TOD 20%")
  ; ("2","TOD 40%")
  ]

POPULATION dagR[RoosterRegel*Dagverantwoording]  CONTAINS
  [ ("r1","DV1")
  ; ("r2","DV2")
  ; ("r3","DV3")
  ]

POPULATION roosterSoortR[RoosterRegel*RoosterSoort]  CONTAINS
  [ ("r1","Regulier")
  ; ("r2","Dienst")
  ; ("r3","Beschikbaar")
  ]

POPULATION beginTijdR[RoosterRegel*Tijdstip] CONTAINS
  [ ("r1","8:00")
  ; ("r2","10:00")
  ; ("r3","12:00")
  ]

POPULATION eindTijdR[RoosterRegel*Tijdstip] CONTAINS
  [ ("r1","10:00")
  ; ("r2","12:00")
  ; ("r3","14:00")
  ]

POPULATION periodeverantwoordingPersoon[Periodeverantwoording*Persoon] CONTAINS
  [ ("pv1","p2")
  ; ("pv2","p3")
  ]

POPULATION periodeverantwoordingPeriode[Periodeverantwoording*PayrollTijdvak] CONTAINS
  [ ("pv1","p201401")
  ; ("pv2","p201401")
  ]



POPULATION voornaam[Persoon*Voornaam]           CONTAINS [ ("p1", "Stef")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel] CONTAINS [ ("p1", " ")]
POPULATION achternaam[Persoon*Achternaam]       CONTAINS [ ("p1", "Joosten")]
POPULATION initialen[Persoon*Initialen]         CONTAINS [ ("p1", "S.M.M.")]
POPULATION titel[Persoon*Titel]                 CONTAINS [ ("p1", "prof.dr.ir.")]
POPULATION geboortedatum[Persoon*Datum]         CONTAINS [ ("p1", "12 april 1959")]
POPULATION telefoon[Persoon*Telefoonnummer]     CONTAINS [ ("p1", "0651348895")]
POPULATION emailadres[Persoon*Emailadres]       CONTAINS [ ("p1", "stef.joosten@ou.nl")]
POPULATION functie[Medewerker*Functie]             CONTAINS [ ("p1", "directeur")]
POPULATION wachtwoord[Persoon*Wachtwoord]       CONTAINS [ ("p1", "welkom")]
POPULATION voornaam[Persoon*Voornaam]           CONTAINS [ ("p2", "Harrie")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel] CONTAINS [ ("p2", "in 't ")]
POPULATION achternaam[Persoon*Achternaam]       CONTAINS [ ("p2", "Honderd")]
POPULATION initialen[Persoon*Initialen]         CONTAINS [ ("p2", "H.J.")]
POPULATION geboortedatum[Persoon*Datum]         CONTAINS [ ("p2", "23 oktober 1972")]
POPULATION emailadres[Persoon*Emailadres]       CONTAINS [ ("p2", "harrie.honderd@nvwa.nl")]
POPULATION functie[Medewerker*Functie]             CONTAINS [ ("p2", "inspecteur")]
POPULATION wachtwoord[Persoon*Wachtwoord]       CONTAINS [ ("p2", "welkom")]
POPULATION leidinggevende[Medewerker*Medewerker]      CONTAINS [ ("p2", "p1")]
POPULATION verleendDoor [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e2", "p1")]
POPULATION eventType    [AutorisatieEventTijdschrijven*EventType]            CONTAINS [ ("e2", "Verleend")]
POPULATION medewerkerA  [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e2", "p2")]
POPULATION geldigVan    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e2", "1 januari 2014")]
POPULATION geldigTot    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e2", "31 december 2014")]
POPULATION tijdCategorie[AutorisatieEventTijdschrijven*TijdRubriekCategorie] CONTAINS [ ("e2", "Werktijd"); ("e2", "Pauze")]
POPULATION kostenPlaats [AutorisatieEventTijdschrijven*Kostenplaats]         CONTAINS [ ("e2", "5")]
POPULATION projectA     [AutorisatieEventTijdschrijven*Project]              CONTAINS [ ("e2", "DTV")]
POPULATION occurA       [AutorisatieEventTijdschrijven*DateTime]             CONTAINS [ ("e2", "18 juli 2014")]


POPULATION voornaam[Persoon*Voornaam]           CONTAINS [ ("p3", "Benny")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel] CONTAINS [ ("p3", "")]
POPULATION achternaam[Persoon*Achternaam]       CONTAINS [ ("p3", "Beer")]
POPULATION initialen[Persoon*Initialen]         CONTAINS [ ("p3", "B.S.")]
POPULATION geboortedatum[Persoon*Datum]         CONTAINS [ ("p3", "8 juni 1960")]
POPULATION emailadres[Persoon*Emailadres]       CONTAINS [ ("p3", "benny.beer@nvwa.nl")]
POPULATION functie[Medewerker*Functie]             CONTAINS [ ("p3", "inspecteur")]
POPULATION wachtwoord[Persoon*Wachtwoord]       CONTAINS [ ("p3", "welkom")]
POPULATION leidinggevende[Medewerker*Medewerker]      CONTAINS [ ("p3", "p1")]
POPULATION verleendDoor [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e3", "p3")]
POPULATION eventType    [AutorisatieEventTijdschrijven*EventType]            CONTAINS [ ("e3", "Verleend")]
POPULATION medewerkerA  [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e3", "p3")]
POPULATION geldigVan    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e3", "1 januari 2014")]
POPULATION geldigTot    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e3", "31 december 2014")]
POPULATION tijdCategorie[AutorisatieEventTijdschrijven*TijdRubriekCategorie] CONTAINS [ ("e3", "Werktijd"); ("e3", "Pauze")]
POPULATION kostenPlaats [AutorisatieEventTijdschrijven*Kostenplaats]         CONTAINS [ ("e3", "7")]
POPULATION projectA     [AutorisatieEventTijdschrijven*Project]              CONTAINS [ ("e3", "DTV")]
POPULATION occurA       [AutorisatieEventTijdschrijven*DateTime]             CONTAINS [ ("e3", "18 juli 2014")]







ENDCONTEXT