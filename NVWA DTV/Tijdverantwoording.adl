CONTEXT Spin
INCLUDE "Login.adl"
INCLUDE "Tijdregistratie.adl"

PATTERN Tijdverantwoording
--PURPOSE PATTERN "Artikel 17"

VIEW Dagverantwoording : Dagverantwoording(TXT "DV-", datum)
VIEW Uurtijdvak : Uurtijdvak(van,TXT "-", tot)
VIEW Persoon : Persoon(voornaam, TXT " ", tussenvoegsel, achternaam)
RELATION voornaam[Persoon*Voornaam] [UNI] PRAGMA "" "heeft als voornaam" ""
MEANING "De voornaam van een persoon wordt geregistreerd."

RELATION tussenvoegsel[Persoon*Tussenvoegsel] [UNI] PRAGMA "" "heeft als tussenvoegsel" ""
MEANING "De tussenvoegsel van een persoon wordt geregistreerd"

RELATION achternaam[Persoon*Achternaam] [UNI] PRAGMA "" "heeft als achternaam" ""
MEANING "EDe achternaam van een persoon wordt geregistreerd."

RELATION initialen[Persoon*Initialen] [UNI] PRAGMA "" "heeft als initialen" ""
MEANING "EDe initialen van een persoon worden geregistreerd"

RELATION titel[Persoon*Titel] [UNI] PRAGMA "" "heeft als titel" ""
MEANING "De titel van een persoon wordt geregistreerd."

RELATION geboortedatum[Persoon*Datum] [UNI] PRAGMA "" "heeft als geboortedatum" ""
MEANING "De geboortedatum van een persoon wordt geregistreerd."
PURPOSE RELATION geboortedatum[Persoon*Datum] REF ""
{+ De leeftijd van een persoon kan invloed hebben op het wel of niet krijgen van bepaalde toeslagen bij ongebruikelijke werktijden (BBRA Artikel 17). Daarom moet de geboortedatum van een persoon bekend zijn. 
-}
RELATION telefoon[Persoon*Telefoonnummer] [UNI] PRAGMA "" "iheeft als telefoonnummer" ""
MEANING "Het telefoonnummer van een persoon wordt geregistreerd."

RELATION emailadres[Persoon*Emailadres] [UNI,INJ] PRAGMA "" "heeft als emailadres" ""
MEANING "Het emailadres van een persoon wordt geregistreerd."

RELATION functie[Persoon*Functie] [UNI] PRAGMA "" "heeft als functie" ""
MEANING "De functie van een persoon wordt geregistreerd."
PURPOSE RELATION functie[Persoon*Functie] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Op basis van de functie van een persoon wordt bepaald welke processtappen mogen worden uitgevoerd. Daarom moet de functie van een persoon bekend zijn.
-}
RELATION leidinggevende[Persoon*Persoon] [UNI] PRAGMA "" "heeft als leidinggevende" ""
MEANING "De leidinggevende van een persoon wordt geregistreerd."
PURPOSE RELATION leidinggevende[Persoon*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een leidinggevende mag voor een medewerker bepaalde processtappen uitvoeren zoals het valideren/afkeuren van dagverantwoordingen, het autoriseren of terugtrekken van een autorisatie van een medewerker
om op een bepaalde combinatie te mogen tijdschrijven. Daarom moet de leidinggevende van een persoon bekend zijn.
-}


RELATION dagSoort[Uurtijdvak*Dagsoort] PRAGMA "" "valt op dagsoort" ""
MEANING "Een uurtijdvak valt op een dagsoort."
PURPOSE RELATION dagSoort[Uurtijdvak*Dagsoort] REF "BBRA Artikel 17 lid 2"
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toeslagpercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}
RELATION van[Uurtijdvak*Tijdstip] PRAGMA "" "begint op tijdstip" ""
MEANING "De begintijd van een uurtijdvak wordt geregistreerd."
PURPOSE RELATION van[Uurtijdvak*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Dagen van de week zijn onderverdeeld in uurtijdvakken. Elk tijdvak begint op een bepaald tijdstip, bijvoorbeeld maandag van 08:000 tot 10:00. 
-}
RELATION tot[Uurtijdvak*Tijdstip] PRAGMA "" "eindigt op tijdstip" ""
MEANING "De eindtijd van een uurtijdvak wordt geregistreerd."
PURPOSE RELATION tot[Uurtijdvak*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Dagen van de week zijn onderverdeeld in uurtijdvakken. Elk tijdvak eindigt op een bepaald tijdstip, bijvoorbeeld maandag van 08:000 tot 10:00. 
-}
RELATION begindatumP[PayrollTijdvak*Datum] PRAGMA "periode" "begint op" ""
MEANING "De begindatum van een payroltijdvak wordt geregistreerd."
PURPOSE RELATION begindatumP[PayrollTijdvak*Datum] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Salarissen en toeslagen worden berekend en verwerkt over een payrollperiode van 4 weken. Om te weten binnen welke periode werkzaamheden zijn uitgevoerd, moet worden nagegaan
wanneer een payrollperiode is gestart. 
-}
RELATION einddatumP[PayrollTijdvak*Datum] PRAGMA "periode" "eindigt op" ""
MEANING "De einddatum van een payrolltijdvak wordt geregistreerd."
PURPOSE RELATION einddatumP[PayrollTijdvak*Datum] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om te weten binnen welke payrollperiode werkzaamheden zijn uitgevoerd, moet worden nagegaan wanneer een payrollperiode eindigt. 
-}
RELATION begindatumM[Maandtijdvak*Datum] PRAGMA "" "begint op" ""
MEANING "De begindatum van een maandtijdvak wordt geregistreerd."
PURPOSE RELATION begindatumM[Maandtijdvak*Datum] REF ""
{+
-}
RELATION einddatumM[Maandtijdvak*Datum] PRAGMA "" "eindigt op" ""
MEANING "De einddatum van een maandtijdvak wordt geregistreerd."
PURPOSE RELATION einddatumM[Maandtijdvak*Datum] REF ""
{+
-}
RELATION date[DatumTijd*Datum] PRAGMA "" "heeft als datum" ""
MEANING "Een DatumTijd heeft een datum"
PURPOSE RELATION date[DatumTijd*Datum] REF ""
{+
-}
RELATION time[DatumTijd*Tijdstip] PRAGMA "" "heeft als tijdstip" ""
MEANING "Een DatumTijd heeft een tijdstip"
PURPOSE RELATION time[DatumTijd*Tijdstip] REF ""
{+
-}



-- Welke transacties tussen Tijdverantwoording en Toezichtuitvoering?
-- Toezichtuitvoering is de leverancier is van gegevens met betrekking tot productieuren.


-- Welke transacties tussen Tijdverantwoording en Personeelsadministratie?
-- Personeelsadministratie is de leverancier van gegevens met betrekking tot medewerkers en organisatie inrichting.


-- Welke transacties tussen Tijdverantwoording en Salarisadministratie?
-- Salarisadministratie is afnemer van gegevens voor het correct toepassen van toeslagen


-- Welke transacties tussen Tijdverantwoording en Operationele sturing/planning?
-- Operationele sturing/planning is een mogelijke leverancier van gegevens met betrekking tot roosters.


-- interface naar FaTijDec
-- binnen FaTijDec wordt voor een deel van de medewerkers het proces van tijdregistratie en tijdverwerking ondersteund. Het is o.a. ook de gegevensleverancier voor P-Direkt.


-- interface naar SPIN Productie (Verificatie, Controle, Interventie)
-- SPIN Productie is een applicatie waarmee een deel van de toezichtuitvoering wordt ondersteund en belangrijkste leverancier van gegevens met betrekking tot productie-uren.


-- interface vanuit SPIN Tijdschrijven
-- SPIN Tijdschrijven is een module waarmee een groot deel van de organisatie op dit moment tijd registreert.


-- interface vanuit P-Direkt
-- P-Direkt is de leverancier van gegevens omtrent medewerkers en organisatie inrichting. Tevens is het de afnemer van gegevens met betrekking tot uren en toeslagpercentages.


-- interface vanuit Rostar CAS
-- Rostar CAS is een applicatie waarmee planning wordt ondersteund en mogelijk leverancier van gegevens met betrekking tot roosters. 


-- interface naar Oracle eBS
-- Oracle eBS is een applicatie waarmee de financiële administratie wordt ondersteund en afnemer van gegevens als 'reversed billing' voor partitioners wordt geïntroduceerd.


-- interface naar Dashboard Centraal (OBIEE)
-- Dashboard Centraal is een management rapportage omgeving die onder andere inzicht geeft en geplande en gerealiseerde uren. Een wijziging het gegevensmodel voor tijdregistratie en tijdverwerking heeft impact op de rapportages.

CONCEPT SPN_TIJDVERANTWOORDING "Een SPN_TIJDVERANTWOORDING is een gebeurtenis, waarin een aaneengesloten hoeveelheid tijd van een medewerker wordt geregistreerd ten behoeve van verantwoording en facturatie."
-- Het concept SPN_TIJDVERANTWOORDING komt overeen met dagverantwoordingRegel in de IA.
RELATION beginTijdS[SPN_TIJDVERANTWOORDING*Tijdstip] [UNI,TOT] PRAGMA "" "heeft als begintijdstip" ""
MEANING "De begintijd van elke SPIN tijdverantwoording wordt geregistreerd."
PURPOSE RELATION beginTijdS[SPN_TIJDVERANTWOORDING*Tijdstip]  REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om de duur van werkzaamheden te verantwoorden moet de begintijd van die werkzaamheden worden geregistreerd.
-}
RELATION eindTijdS[SPN_TIJDVERANTWOORDING*Tijdstip] [UNI] PRAGMA "" "heeft als eindtijdstip" ""
MEANING "De eindtijd van een SPIN tijdverantwoording wordt geregistreerd."
PURPOSE RELATION eindTijdS[SPN_TIJDVERANTWOORDING*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om de duur van werkzaamheden te verantwoorden moet de eindtijd van die werkzaamheden worden geregistreerd.
-}
RELATION tijdsduur[SPN_TIJDVERANTWOORDING*Tijdsduur] [UNI] PRAGMA "" "heeft een tijdsduur van" ""
MEANING "De tijdsduur van een SPIN tijdverantwoording wordt geregistreerd."
PURPOSE RELATION tijdsduur[SPN_TIJDVERANTWOORDING*Tijdsduur] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om werkzaamheden te verantwoorden moet de tijdsduur van de werkzaaamheden worden geregistreerd.
-}
RELATION activiteit[SPN_TIJDVERANTWOORDING*Activiteit] [UNI,TOT] PRAGMA "" "heeft als activiteit" "" -- Het concept Activiteit komt overeen met tijdRubriek in de IA. Daarvan bestaat een referentielijst
MEANING "De activiteit van elke tijdverantwoording wordt geregistreerd"
PURPOSE RELATION activiteit[SPN_TIJDVERANTWOORDING*Activiteit] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Medewerkers kunnen alleen uren registreren op activiteiten waar ze voor zijn geautoriseerd. Daarom moet de activiteit binnen een SPN_TIJDVERANTWOORDING bekend zijn.
-}
RELATION cat[Activiteit*TijdRubriekCategorie] [UNI] PRAGMA "" "valt binnen de categorie" ""
MEANING "Een activiteit valt binnen een categorie."
PURPOSE RELATION cat[Activiteit*TijdRubriekCategorie] REF "Business Case - Project Duurzaam Tijdverantwoorden versie 0.9"
{+Op basis van Tijdvak, Categorie en Rooster wordt bepaalt aan welke Uurgroep de tijd wordt toegekend
-}

RELATION project[SPN_TIJDVERANTWOORDING*Project] [UNI] PRAGMA "" "heeft betrekking op het project" ""
MEANING "Een tijdverantwoording heeft betrekking op een project"
PURPOSE RELATION project[SPN_TIJDVERANTWOORDING*Project] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een medewerker wordt geautoriseerd om uren te mogen
schrijven op een bepaalde combinatie van: - Activiteit,- Project,- Kostenplaats
-}
RELATION kostenplaats[SPN_TIJDVERANTWOORDING*Kostenplaats] [UNI] PRAGMA "" "heeft als kostenplaats" ""
MEANING "Een tijdverantwoording heeft betrekking op een kostenplaats."
PURPOSE RELATION kostenplaats[SPN_TIJDVERANTWOORDING*Kostenplaats] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een medewerker wordt geautoriseerd om uren te mogen schrijven op een bepaalde combinatie van: - Activiteit,- Project,- Kostenplaats
-}


RELATION dagS[SPN_TIJDVERANTWOORDING*Dagverantwoording] [UNI,TOT] PRAGMA "" "is onderdeel van dagverantwoording" ""
MEANING "Een tijdverantwoording maakt deel uit van een dagverantwoording."
PURPOSE RELATION dagS[SPN_TIJDVERANTWOORDING*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een SPN_TIJDVERANTWOORDING bevat een aaneengesloten hoeveelheid tijd op een bepaalde dag.
-}



RELATION bron[TijdvakResultaat*SPN_TIJDVERANTWOORDING] [UNI,TOT]
MEANING "Elk TijdvakResultaat heeft een SPN_TIJDVERANTWOORDING."
PURPOSE RELATION bron[TijdvakResultaat*SPN_TIJDVERANTWOORDING] REF ""
{+Een tijdvakresultaat is een opsplitsing van de dagverantwoordingRegels naar specifieke uurTijdvakken en roosterSoorten. Bij elk TijdvakResultaat hoort dus een SPN_TIJDVERANTWOORDING.
-}
RELATION tijdvak[TijdvakResultaat*UurTijdvak] [UNI,TOT] PRAGMA "" "heeft als uurtijdvak" ""
MEANING "Elk TijdvakResultaat heeft betrekking op een uurtijdvak"
PURPOSE RELATION tijdvak[TijdvakResultaat*UurTijdvak] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een tijdvakresultaat is een opsplitsing van de dagverantwoordingRegels naar specifieke
uurTijdvakken en roosterSoorten.
-}
RELATION categorie[TijdvakResultaat*TijdRubriekCategorie] [UNI,TOT] PRAGMA "" "heeft als categorie" ""
MEANING "Elk TijdvakResultaat heeft betrekking op een categorie."
PURPOSE RELATION categorie[TijdvakResultaat*TijdRubriekCategorie] REF ""
{+
-}
RELATION roosterSoortT[TijdvakResultaat*RoosterSoort] [UNI,TOT] PRAGMA "" "is onderdeel van roostersoort" ""
MEANING "Elk TijdvakResultaat heeft betrekking op een roostersoort."
PURPOSE RELATION roosterSoortT[TijdvakResultaat*RoosterSoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een tijdvakresultaat is een opsplitsing van de dagverantwoordingRegels naar specifieke
uurTijdvakken en roosterSoorten. 
-}
RELATION uurgroep[TijdvakResultaat*Uurgroep] [UNI,TOT] PRAGMA "" "heeft als uurgroep" ""
MEANING "Een TijdvakResultaat heeft betrekking op een uurgroep"
PURPOSE RELATION uurgroep[TijdvakResultaat*Uurgroep]  REF ""
{+Op basis van Tijdvak, Categorie en Rooster wordt bepaalt aan welke Uurgroep de tijd wordt toegekend
-}
RELATION beginTijdR[RoosterRegel*Tijdstip] [UNI,TOT] PRAGMA "" "begint op tijdstip" ""
MEANING "Elke roosterregel heeft een begintijd"
PURPOSE RELATION beginTijdR[RoosterRegel*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om een roosterregel te definieren moet de begintijd van de werkzaamheden die volgens het rooster moeten worden uitgevoerd bekend zijn.
-}
RELATION eindTijdR[RoosterRegel*Tijdstip] [UNI,TOT] PRAGMA "" "eindigt op tijdstip" ""
MEANING "Elke roosterregel heeft een eindtijd."
PURPOSE RELATION eindTijdR[RoosterRegel*Tijdstip] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Om een roosterregel te definieren moet de eindtijd van de werkzaamheden die volgens het rooster moeten worden uitgevoerd bekend zijn.
-}
RELATION roosterSoortR[RoosterRegel*RoosterSoort] [UNI,TOT] PRAGMA "" "valt onder roostersoort" ""
MEANING "Binnen een roosterregel is een bepaalde roostersoort van toepassing."
PURPOSE RELATION roosterSoortR[RoosterRegel*RoosterSoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De roostersoort bepaalt welke regels worden toegepast tijdens de verwerking van de dagverantwooding (timesheet).
-}
RELATION dagR[RoosterRegel*Dagverantwoording] [UNI,TOT] PRAGMA "" "id onderdeel van dagverantwoording" ""
MEANING "Elke roosterregel is onderdeel van een dagverantwoording."
PURPOSE RELATION dagR[RoosterRegel*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een roosterregel heeft betrekking op werkzaamheden die op een bepaalde dag moeten worden uitgevoerd en is daarmee onderdeel van een dagverantwoording.
-}
RELATION medewerker[Dagverantwoording*Persoon] [UNI,TOT] PRAGMA "" "is een dagverantwoording van" ""
MEANING "Elke dagverantwoording hoort bij 1 persoon."
PURPOSE RELATION medewerker[Dagverantwoording*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Er wordt per medewerker een dagverantwoording aangemaakt om de tijd te verantwoorden.
-}
RELATION datum[Dagverantwoording*Datum] [UNI,TOT] PRAGMA "" "is de verantwoording van dag" ""
MEANING "Elke dagverantwoording gaat over een dag"
PURPOSE RELATION datum[Dagverantwoording*Datum]  REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Iedere medewerker moet per dag de (modaliteit) tijd verantwoorden.
-}
RELATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus] [UNI] PRAGMA "" "heeft de status" ""
MEANING "De status van een dagverantwoording wordt geregistreerd"
PURPOSE RELATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus] REF ""
{+Op basis van de status van een dagverantwoording wordt deze of aangeboden ter verwerking (goedgekeurd), of ter correctie opnieuw aangeboden aan de werknemer (retour). Daarom moet de status van een dagverantwoording bekend zijn.
-}
RELATION toelichting[Dagverantwoording*Tekst] [UNI] PRAGMA "" "heeft bij registratie als toelichting meegekregen" ""
MEANING "Een medewerker kan een toelichting toevoegen bij het registreren van een dagverantwoording."
PURPOSE RELATION toelichting[Dagverantwoording*Tekst] REF ""
{+Een medewerker kan aan een dagverantwoording een toelichting toevoegen voordat deze wordt aangeboden.
-}
RELATION opmerkingValidatie[Dagverantwoording*Tekst] [UNI] PRAGMA "" "heeft bij validatie als opmerking meegekregen" ""
MEANING "Een leidinggevende kan bij het valideren van  een dagverantwoording een opmerking toevoegen."
PURPOSE RELATION opmerkingValidatie[Dagverantwoording*Tekst] REF ""
{+Een leidinggevende kan een opmerking toevoegen tijdens het valideren van een dagverantwoording.
-}

RELATION payrollPeriode[Periode*Weeknummer]

RELATION uurgroepInPeriode[Periode*Uurgroep] PRAGMA "periode " "heeft als onderdeel uurgroep" ""
MEANING "Elke uurgroep valt binnen een bepaalde payroll periode."
PURPOSE RELATION uurgroepInPeriode[Periode*Uurgroep] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Voorafgaand aan de uitvoering van payroll periode worden er per uurGroep totalen gemaakt
-}
RELATION urenInUurgroep[Uren*Uurgroep] PRAGMA "" "uren zijn onderdeel van uurgroep" ""
MEANING "Een uurgroep bestaat uit een aantal uren."
PURPOSE RELATION urenInUurgroep[Uren*Uurgroep] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De tijd die binnen het rooster of binnen een dagverantwoording is geregistreerd kan worden toegekend aan
een uurGroep
-}

RELATION payrollPeriodePersoon[Persoon*Periode] PRAGMA "" "is de betreffende medewerker voor periode" ""
MEANING "Een payrollperiode geldt voor een persoon."
PURPOSE RELATION payrollPeriodePersoon[Persoon*Periode] REF ""
{+De geregistreerde uren van een medewerker worden per payrollperiode verwerkt. Het totaal van een payrollperiode bevat dus de te verwerken uren van een bepaalde persoon.
-}

RELATION wezenlijkeOnderbreking[WezenlijkeOnderbreking*JaOfNee] [UNI,TOT]
MEANING "Er is wel of geen sprake van een wezenlijke onderbreking."
PURPOSE RELATION wezenlijkeOnderbreking[WezenlijkeOnderbreking*JaOfNee] REF "BBRA Artikel 17 lid 4"
{+n BBRA Artikel 17 lid 4 is bij het bepalen van ongebruikelijke uren is het minstens 5 jaar zonder wezenlijke onderbreking ontvangen van een toelage een relevante factor. 
-}

RELATION wezenlijkeOnderbrekingPersoon[WezenlijkeOnderbreking*Persoon]
MEANING "Bij een medewerker kan er sprake zijn van een wezenlijke onderbreking."
PURPOSE RELATION wezenlijkeOnderbrekingPersoon[WezenlijkeOnderbreking*Persoon] REF "BBRA Artikel 17 lid 4"
{+Een persoon kan binnen de afgelopen 5 jaar wel of zonder wezenlijke onderbrekging een toelage hebben ontvangen. Dit is in BBRA Artikel 17 lid 4 een relavante factor bij het bepalen van de ongebruikelijke uren voor die persoon.
** Uitzoeken hoe wordt bepaald of er sprake is van een wezenlijke onderbreking*
-}

RELATION toelageVoorafgaande36Kalendermaanden[Persoon*Toelage]
MEANING "Een persoon kan de 36 maanden voorafgaand aan de maand waarin hij 55 jaar is geworden een gemiddeld bedrag aan toelagen hebben ontvangen."
PURPOSE RELATION toelageVoorafgaande36Kalendermaanden[Persoon*Toelage] REF "BBRA Artikel 17 lid 4"
{+Bij het berekenen van de toelage voor ongebruikelijke tijden is het bedrag dat de ambtenaar over de zesendertig kalendermaanden voorafgaande aan de maand 
waarin hij de leeftijd van 55 jaar bereikt gemiddeld per maand aan toelage heeft ontvangen een relevante factor zijn. 
-}

 RELATION dagverantwoordingVanPayrollPeriode[Dagverantwoording*Periode] PRAGMA "" "valt binnen periode" ""
 MEANING "Een dagverantwoording valt binnen een payrollperiode."
 PURPOSE RELATION dagverantwoordingVanPayrollPeriode[Dagverantwoording*Periode] REF "BBRA Artikel 17"
 {+Om de ongebruikelijke uren binnen een bepaalde payrollperiode te berekenen, moeten alle dagverantwoordingen die binnen die periode vallen worden geraadpleegd.
 -}

RELATION ongebruikelijkUrenPayrollPeriode[Periode*Uren] PRAGMA "" "heeft als gevolg van ongebruikelijke werktijden als totaal aantal extra uren" ""
MEANING "Een payrollperiode heeft een totaal aan extra uren als gevolg van ongebruikelijke werktijden."
PURPOSE RELATION ongebruikelijkUrenPayrollPeriode[Periode*Uren] REF ""
{+Het totaal aantal extra uren dat moet worden uitbetaald als gevolg van ongebruikelijke werktijden moet worden doorgegeven aan de organisatie die zorgt voor de salarisuitkering.
-}

ENDPATTERN

POPULATION payrollPeriode[Periode*Weeknummer] CONTAINS
  [ ("p1", "p201401")
  ; ("p2", "p201402")
  ; ("p3", "p201403")
  ; ("p4", "p201404")
  ; ("p5", "p201405")
  ; ("p6", "p201406")
  ; ("p7", "p201407")
  ; ("p8", "p201408")
  ; ("p9", "p201409")
  ; ("p10", "p201410")
  ; ("p11", "p201411")
  ; ("p12", "p201412")
  ; ("p13", "p201413")
  ]

RULE I[DagverantwoordingStatus] = 'Akkoord' \/ 'Retour'
VIEW SPN_TIJDVERANTWOORDING : SPN_TIJDVERANTWOORDING (TXT "activiteit \"", activiteit, TXT " op tijdstip ", beginTijdS, TXT "\"")
VIEW RoosterRegel : RoosterRegel (TXT "Roosterregel \"", roosterSoortR, TXT " op ", dagR;datum, TXT "\"")

RULE autorisatieregel : dagS;medewerker |- (project;projectA~/\kostenplaats;kostenPlaats~/\(activiteit;cat)~\tijdCategorie~);medewerkerA
--RULE autorisatregel : dagS;medewerker |- (project;projectA~/\kostenplaats;kostenPlaats~/\(tijdCategorie/(activiteit;cat))~);medewerkerA
--RULE autorisatieregel : dagS;medewerker |- (project;projectA~/\kostenplaats;kostenPlaats~);medewerkerA
MEANING "Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd."
VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")

RULE roosterregel : dagR~;beginTijdR |- dagS~;beginTijdS
--MEANING "Voor elke dag en begintijd van een roosterregel moet er een dag en begintijd van een dagverantwoording zijn."
--VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")



INTERFACE "Verkregen Autorisaties" 
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties van " : I[Persoon]
     , "" : medewerkerA~
      BOX [ "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          ]
    ]

INTERFACE "Bereken Ongebruikelijke tijden" 
	(uurgroepInPeriode
--	 ,payrollPeriodePersoon
--	 ,payrollPeriode
	 ,urenInUurgroep
	 ,refLijstUurgroepen
	 ,wezenlijkeOnderbrekingPersoon
	 ,wezenlijkeOnderbreking
	 ,toelageVoorafgaande36Kalendermaanden
         ,ongebruikelijkUrenPayrollPeriode)FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties verleend door " : I[Persoon]
     , "" : verleendDoor~/\V;'Verleend';eventType~   -- AutorisatieEventTijdschrijven
      BOX [ "" :      medewerkerA          -- Persoon
           	BOX ["Tijdregistratieberichten van " : I[Persoon]
		     , "Geboortedatum : " :  geboortedatum
		     , "Wezenlijke onderbreking : " : wezenlijkeOnderbrekingPersoon~  --WezenlijkeOnderbreking
			BOX ["Sprake van wezenlijke onderbreking : " : wezenlijkeOnderbreking
		    	    ]
		     , "Toelage voorafgaande 36 maanden : " : toelageVoorafgaande36Kalendermaanden
		     ,"" : I[Persoon]	
	             BOX ["" : medewerker~;dagverantwoordingVanPayrollPeriode -- Periode
	                  BOX ["Payrollperiode" : I[Periode] 
			       ,"" : I[Periode]
	  	                BOX ["" : dagverantwoordingVanPayrollPeriode~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoording
	      			     BOX [ "betreft datum" :    datum
	          	      	          , tijdverantwoording : dagS~
	            			    BOX [ beginTijd :    beginTijdS
	                			  , eindTijd :     eindTijdS
	                			  , duur :         tijdsduur
	                			  , activiteit :   activiteit
	                	    	        ] 
		                         ]
				     ]
			       ,"Ongebruikelijke uren (Uurgroep en uren)" : uurgroepInPeriode  --Uurgroep
			       BOX ["Uurgroep : " : refLijstUurgroepen~
			            , "Aantal uren : " : urenInUurgroep~
			           ]
			       , "Totaal Ongebruikelijke uren periode : " : ongebruikelijkUrenPayrollPeriode
   			      ]
                         ]
	      	    ]
	 ]
    ]

INTERFACE "Geaccordeerde tijdregistraties"
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerker~/\V;'Akkoord';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    beginTijdS
                , eindTijd :     eindTijdS
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    beginTijdR
                , eindTijd :     eindTijdR
                ]
          , toelichting : toelichting
          ]
    ]

INTERFACE "Retour"
           ( dagS
           , beginTijdS
           , eindTijdS
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerker~/\V;'Retour';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , status :       dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    beginTijdS
                , eindTijd :     eindTijdS
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    beginTijdR
                , eindTijd :     eindTijdR
                ]
          , toelichting : toelichting
          , "Commentaar" :opmerkingValidatie
          ]
    ]


INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerker
           , datum
           , dagS
           , beginTijdS
           , eindTijdS
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanPayrollPeriode
           , roosterSoortR
           , dagR
           , beginTijdR
           , eindTijdR
           ) FOR Medewerker : I[Dagverantwoording]
BOX [ "datum vandaag" :    ontvangen
    , "ingediend door" :   medewerker
    , "betreft datum" :    datum
    , "betreft payrollperiode" :  dagverantwoordingVanPayrollPeriode
    , tijdverantwoording : dagS~
      BOX [ beginTijd :    beginTijdS
          , eindTijd :     eindTijdS
          , duur :         tijdsduur
          , activiteit :   activiteit
          , project :      project
          , kostenplaats : kostenplaats
          ]
    , roosterverantwoording : dagR~
      BOX [ roosterSoort : roosterSoortR
          , beginTijd :    beginTijdR
          , eindTijd :     eindTijdR
          ]
    , toelichting : toelichting
    ]

INTERFACE "Te valideren"
           ( dagverantwoordingStatus
           , activiteit
           , project
           , kostenplaats
           , opmerkingValidatie
           ) FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties voor " : I[Persoon]
    , "" : leidinggevende~;medewerker~/\-((V#'Akkoord');dagverantwoordingStatus~)   -- Dagverantwoording
      BOX [ ontvangen :          ontvangen
          , medewerker :         medewerker
          , "betreft datum" :    datum
          , akkoord :            dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    beginTijdS
                , eindTijd :     eindTijdS
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort :    roosterSoortR
                , beginTijd :       beginTijdR
                , eindTijd :        eindTijdR
                ]
          , "Commentaar bij validatie" : opmerkingValidatie
          ]
    ]



-- ENFORCE [TOT] kostenplaats[SPN_TIJDVERANTWOORDING*Kostenplaats] BY Gebruiker

-- Vraag: Welke roostersoorten bestaan er?
-- Antwoord: Tenminste "Regulier", "Dienst", "Geen dienst", "Overwerk io", "Beschikbaar"
-- Antwoord: Volgens de IA bestaat er een referentielijst van roostersoorten.
-- Vraag: Welke uurgroepen bestaan er?
-- Antwoord: Tenminste "Arbeidsuren", "Meeruren", "TOD 0%", "TOD 20%", "TOD 40%", "VRU 45%", "Overuren 25%", "Overuren 50%", "Beschikbaarheid 5%"
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze"
-- Vraag: Welke activiteiten bestaan er?
-- Antwoord: Tenminste "Toezicht", "Inspectie", "Pauze", "Advies", "Compensatie verlof", "Overleg", "Support", "Reistijd naar inspectielokatie"
-- Antwoord: In de IA wordt een activiteit "tijdRubriek" genoemd. Daarvan bestaat een referentielijst.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze", "Compensatie"
-- Vraag: Welke urenpools bestaan er?
-- Antwoord: Tenminste "Vakantiedagen", "Compensatie"

CONCEPT Payrollperiode "Periode van 4 weken waarover het salaris wordt berekend en uitbetaald"

CONCEPT Roosterregel "Een roosterregel op een dagverantwoording geeft aan dat een medewerker in het daarin genoemde tijdvak beschikbaar is voor werk."

CONCEPT TijdvakResultaat "Een tijdvakresultaat is het resultaat van opsplitsing van de dagverantwoordingregels naar specifieke uurtijdvakken en roostersoorten." "IA"

CONCEPT Timesheet "Een timesheet is een periodiek overzicht van de gewerkte uren van één medewerker."

CONCEPT Dagverantwoording "Een dagverantwoording is een overzicht van alle gewerkte uren van één dag voor één medewerker. (De PSA spreekt over een dagfilm)." "IA"
PURPOSE CONCEPT Dagverantwoording
{+Wanneer een medewerker tijd registreert, resulteert dat in een aantal SPN_TIJDVERANTWOORDINGen, die samen in één bericht realtime het systeem binnenkomen.
Om deze SPN_TIJDVERANTWOORDINGen bijeen te houden, gebruiken we daarvoor het concept Dagverantwoording.
-}


CONCEPT AutorisatieEventTijdschrijven "Een autorisatieTijdschrijven is de toestemming aan een of meer medewerkers om in een gegeven periode tijd te verantwoorden in de dagverantwoording op een bepaalde combinatie van tijdRubriek, kostenplaats en eventueel project."
RELATION occurA[AutorisatieEventTijdschrijven*DateTime] [UNI,TOT]
RELATION eventType[AutorisatieEventTijdschrijven*EventType] [UNI,TOT]
RELATION medewerkerA[AutorisatieEventTijdschrijven*Persoon]
RELATION geldigVan[AutorisatieEventTijdschrijven*Datum] [UNI,TOT]
RELATION geldigTot[AutorisatieEventTijdschrijven*Datum] [UNI,TOT]
RELATION kostenPlaats[AutorisatieEventTijdschrijven*Kostenplaats] [UNI] --  Kostenplaatsen komen uit een referentielijst
RELATION tijdCategorie[AutorisatieEventTijdschrijven*TijdRubriekCategorie]
RELATION projectA[AutorisatieEventTijdschrijven*Project] [UNI]
RELATION verleendDoor[AutorisatieEventTijdschrijven*Persoon] [UNI,TOT]

RULE I[EventType] = 'Verleend' \/ 'Ingetrokken'

INTERFACE "Verleende autorisaties" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Autorisaties verleend door " : I[Persoon]
    , "" : verleendDoor~/\V;'Verleend';eventType~   -- AutorisatieEventTijdschrijven
      BOX [ medewerker :      medewerkerA
          , "geldig van" :    geldigVan
          , "geldig tot" :    geldigTot
          , kostenplaats :    kostenPlaats
          , project :         projectA
          , tijdCategorieën : tijdCategorie
          ]
    ]

INTERFACE "Nieuwe autorisatie"
           ( verleendDoor
           , eventType
           , medewerkerA
           , geldigVan
           , geldigTot
           , tijdCategorie
           , kostenPlaats
           , projectA
           , occurA
           ) FOR Leidinggevende : I[AutorisatieEventTijdschrijven]
      BOX [ "geautoriseerd door" :       verleendDoor
          , "Nieuw/Intrekken/Wijzigen" : eventType
          , "verleend aan" :             medewerkerA
          , "geldig van" :               geldigVan
          , "geldig tot" :               geldigTot
          , "tijdcategorie" :            tijdCategorie
          , kostenplaats :               kostenPlaats
          , project :                    projectA
          , "tijdstip van verlenen" :    occurA
          ]



-- Interface Beheren Tijdverantwoording (reeds bestaand) moet worden uitgebreid.

-- Interface Beheren Roostertypen moet worden toegevoegd

-- Regel: Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd.

-- Regel: Alle uren moeten verantwoord zijn.
-- Regel: Een autorisatie voor een medewerker kan alleen gegeven worden door degene die op het moment van autoriseren de leidinggevende is van die persoon.
-- Regel: Een timesheet kan alleen goedgekeurd worden als alle combinaties van tijdRubriek, project en kostenplaats ook geautoriseerd zijn.
-- Regel "JaarRule" "Elk jaar begint met nul vakantiedagen" 
-- Regel "JaarRule" "Het aantal overuren waar jaar (n+1) mee begint is het aantal overuren ontstaan in december van jaar n en niet opgenomen is in die maand."
POPULATION dagSoort[Uurtijdvak*Dagsoort] CONTAINS
  [ ("w1", "ma-vr")
  ; ("w2", "ma-vr")
  ; ("w3", "ma-vr")
  ; ("w4", "ma-vr")
  ; ("w5", "ma-vr")
  ; ("z1", "za-zo")
  ; ("z2", "za-zo")
  ; ("z3", "za-zo")
  ; ("f1", "feestdag")
  ; ("f2", "feestdag")
  ; ("f3", "feestdag")
  ]

POPULATION van[Uurtijdvak*Tijdstip] CONTAINS
  [ ("w1", "0:00")
  ; ("w2", "6:00")
  ; ("w3", "8:00")
  ; ("w4", "18:00")
  ; ("w5", "22:00")
  ; ("z1", "0:00")
  ; ("z2", "6:00")
  ; ("z3", "22:00")
  ; ("f1", "0:00")
  ; ("f2", "6:00")
  ; ("f3", "22:00")
  ]

POPULATION tot[Uurtijdvak*Tijdstip] CONTAINS
  [ ("w1", "6:00")
  ; ("w2", "8:00")
  ; ("w3", "18:00")
  ; ("w4", "22:00")
  ; ("w5", "0:00")
  ; ("z1", "6:00")
  ; ("z2", "22:00")
  ; ("z3", "0:00")
  ; ("f1", "6:00")
  ; ("f2", "22:00")
  ; ("f3", "0:00")
  ]

POPULATION begindatumP[PayrollTijdvak*Datum] CONTAINS
  [ ("p201401", "01-01-2014")
  ; ("p201402", "29-01-2014")
  ; ("p201403", "26-02-2014")
  ; ("p201404", "26-03-2014")
  ]

POPULATION einddatumP[PayrollTijdvak*Datum] CONTAINS
  [ ("p201401", "28-01-2014")
  ; ("p201402", "25-02-2014")
  ; ("p201403", "25-03-2014")
  ; ("p201404", "22-04-2014")
  ]

POPULATION beginTijdS[SPN_TIJDVERANTWOORDING*Tijdstip] CONTAINS
  [ ("s1", "08:00")
  ; ("s2", "10:00")
  ; ("s3", "12:00")
  ; ("s4", "14:00")
  ]

POPULATION eindTijdS[SPN_TIJDVERANTWOORDING*Tijdstip] CONTAINS
  [ ("s1", "10:00")
  ; ("s2", "12:00")
  ; ("s3", "14:00")
  ; ("s4", "16:00")
  ]

POPULATION project[SPN_TIJDVERANTWOORDING*Project] CONTAINS
  [ ("s1", "DTV")
  ; ("s2", "Project A")
  ; ("s3", "Project X")
  ]

POPULATION kostenplaats[SPN_TIJDVERANTWOORDING*Kostenplaats] CONTAINS
  [ ("s1", "5")
  ; ("s2", "7")
  ; ("s3", "4")
  ]

POPULATION activiteit[SPN_TIJDVERANTWOORDING*Activiteit]  CONTAINS
  [ ("s1", "Toezicht")
  ; ("s2", "Inspectie")
  ; ("s3", "Pauze")
  ]

POPULATION datum[Dagverantwoording*Datum] CONTAINS
  [ ("DV1", "01-01-2014")
  ; ("DV2", "03-02-2014")
  ; ("DV3", "12-03-2014")
  ]

POPULATION dagS[SPN_TIJDVERANTWOORDING*Dagverantwoording] CONTAINS
  [ ("s1","DV1")
  ; ("s2","DV2")
  ; ("s3","DV3")
  ]

POPULATION dagverantwoordingStatus[Dagverantwoording*DagverantwoordingStatus] CONTAINS
  [ ("DV1","Akkoord")
  ; ("DV2","Retour")
  ]

POPULATION uurgroepInPeriode[Periode*Uurgroep] CONTAINS
  [ ("p201401","Arbeidsuren")
  ; ("p201402","OD 20%")
  ; ("p201403","OD 40%")
  ]

POPULATION urenInUurgroep[Uren*Uurgroep] CONTAINS
  [ ("3","Arbeidsuren")
  ; ("4","OD 20%")
  ; ("2","OD 40%")
  ]

POPULATION roosterSoortR[RoosterRegel*RoosterSoort]  CONTAINS
  [ ("r1","Regulier")
  ; ("r2","Dienst")
  ; ("r3","Beschikbaar")
  ]

POPULATION beginTijdR[RoosterRegel*Tijdstip] CONTAINS
  [ ("r1","8:00")
  ; ("r2","10:00")
  ; ("r3","12:00")
  ]

POPULATION eindTijdR[RoosterRegel*Tijdstip] CONTAINS
  [ ("r1","10:00")
  ; ("r2","12:00")
  ; ("r3","14:00")
  ]

POPULATION voornaam[Persoon*Voornaam]           CONTAINS [ ("p1", "Stef")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel] CONTAINS [ ("p1", " ")]
POPULATION achternaam[Persoon*Achternaam]       CONTAINS [ ("p1", "Joosten")]
POPULATION initialen[Persoon*Initialen]         CONTAINS [ ("p1", "S.M.M.")]
POPULATION titel[Persoon*Titel]                 CONTAINS [ ("p1", "prof.dr.ir.")]
POPULATION geboortedatum[Persoon*Datum]         CONTAINS [ ("p1", "12 april 1959")]
POPULATION telefoon[Persoon*Telefoonnummer]     CONTAINS [ ("p1", "0651348895")]
POPULATION emailadres[Persoon*Emailadres]       CONTAINS [ ("p1", "stef.joosten@ou.nl")]
POPULATION functie[Persoon*Functie]             CONTAINS [ ("p1", "directeur")]
POPULATION wachtwoord[Persoon*Wachtwoord]       CONTAINS [ ("p1", "welkom")]
POPULATION voornaam[Persoon*Voornaam]           CONTAINS [ ("p2", "Harrie")]
POPULATION tussenvoegsel[Persoon*Tussenvoegsel] CONTAINS [ ("p2", "in 't ")]
POPULATION achternaam[Persoon*Achternaam]       CONTAINS [ ("p2", "Honderd")]
POPULATION initialen[Persoon*Initialen]         CONTAINS [ ("p2", "H.J.")]
POPULATION geboortedatum[Persoon*Datum]         CONTAINS [ ("p2", "23 oktober 1972")]
POPULATION emailadres[Persoon*Emailadres]       CONTAINS [ ("p2", "harrie.honderd@nvwa.nl")]
POPULATION functie[Persoon*Functie]             CONTAINS [ ("p2", "inspecteur")]
POPULATION wachtwoord[Persoon*Wachtwoord]       CONTAINS [ ("p2", "welkom")]
POPULATION leidinggevende[Persoon*Persoon]      CONTAINS [ ("p2", "p1")]
POPULATION verleendDoor [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e2", "p1")]
POPULATION eventType    [AutorisatieEventTijdschrijven*EventType]            CONTAINS [ ("e2", "Verleend")]
POPULATION medewerkerA  [AutorisatieEventTijdschrijven*Persoon]              CONTAINS [ ("e2", "p2")]
POPULATION geldigVan    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e2", "1 januari 2014")]
POPULATION geldigTot    [AutorisatieEventTijdschrijven*Datum]                CONTAINS [ ("e2", "31 december 2014")]
POPULATION tijdCategorie[AutorisatieEventTijdschrijven*TijdRubriekCategorie] CONTAINS [ ("e2", "Werktijd"); ("e2", "Pauze")]
POPULATION kostenPlaats [AutorisatieEventTijdschrijven*Kostenplaats]         CONTAINS [ ("e2", "5")]
POPULATION projectA     [AutorisatieEventTijdschrijven*Project]              CONTAINS [ ("e2", "DTV")]
POPULATION occurA       [AutorisatieEventTijdschrijven*DateTime]             CONTAINS [ ("e2", "18 juli 2014")]
ENDCONTEXT