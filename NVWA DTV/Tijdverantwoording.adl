CONTEXT DTV IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Berichten.adl"
INCLUDE "Autorisaties.adl"
INCLUDE "Referentielijsten.adl"
INCLUDE "Rechten.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Tijdvakken.adl"
INCLUDE "Persoonsinformatie.adl"
INCLUDE "Pcodes.adl"
INCLUDE "PopForTesting.adl"

PURPOSE CONTEXT DTV
{+De werktijdenregeling Nederlandse Voedsel- en Warenautoriteit (hierna: WTR)
stelt een applicatie voor het tijdschrijven beschikbaar voor alle medewerkers,
waarmee alle (werk)tijd wordt geregistreerd en verantwoord (WTR Artikel 2 lid 6).
-}

INTERFACE Werkbon (bon[Dienst*Werkbon], bon[TimesheetRegel*Werkbon], begintijd, eindtijd, dienst, werksoort) : I[Werkbon]
BOX [ indiener : medewerkerW
    , Roosterregels : bon[Dienst*Werkbon]~
      BOX [ ""  : I[Dienst]
          , begintijd : begintijd
          , eindtijd : eindtijd
          , dienst : dienst
          ]
    , Timesheetregels : bon[TimesheetRegel*Werkbon]~
      BOX [ ""  : I[TimesheetRegel]
          , begintijd : begintijd 
          , eindtijd : eindtijd
          , werksoort : werksoort
          ]
    ]

PATTERN Werkbonnen
PURPOSE PATTERN Werkbonnen
{+Het begrip "werkbon" is afkomstig van FaTijDec.
Een werkbon is de verantwoording van werk-/pauze- en rusttijden.
In het DTV-project noemen we dit "dagverantwoording".
-}
CLASSIFY TimesheetRegel ISA Tijdsleuf
RELATION werksoort[TimesheetRegel*Werksoort]

RELATION medewerkerW[Werkbon*Persoon] [UNI,TOT] PRAGMA "" " is door " " ingevuld"
MEANING "Elke werkbon wordt geacht te zijn ingevuld door de persoon die erop vermeld staat."
PURPOSE RELATION bon[TimesheetRegel*Werkbon]
{+Eén werkbon kan meerdere timesheetregels bevatten,
zodat ze tezamen kunnen worden goedgekeurd door de leidinggevende.
-}
RELATION bon[TimesheetRegel*Werkbon] [UNI] PRAGMA "" " is op " " ingevuld"
MEANING "Een werkbon vermeldt een aantal timesheetregels."
ENDPATTERN


PROCESS Roosters
PURPOSE PROCESS Roosters
{+Als er sprake is van wisselende werktijden in verband met vroege-, avond- en nachtdiensten,
werken op zaterdag en zondag
dan wel als door de aard van het voorhanden werk de individuele medewerker niet of nauwelijks invloed heeft op de begin- en eindtijd waarop dat werk moet worden verricht,
wordt volgens een voor die diensten vastgesteld rooster gewerkt (WTR Artikel 3 lid 1).
Duurzame tijdverantwoording heeft informatie nodig over het rooster van elke medewerker, om te kunnen bepalen op welke roostersoort een tijdvakresultaat kan worden geboekt.
Deze informatie wordt uit Rostar CAS gehaald, maar alleen zoveel als nodig om het juiste tijdvakresultaat te kunnen bepalen.
-}

CLASSIFY Dienst ISA Tijdsleuf
RELATION dienst[Dienst*Dienstsoort]

RELATION bon[Dienst*Werkbon]

PURPOSE CONCEPT Dienst REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, pg. 14."
{+Iedere medewerker heeft een standaard werkrooster dat is vastgelegd in zijn/haar werktijden profiel. Het werkrooster bepaalt welke regels worden toegepast tijdens de verwerking van
de dagverantwooding (timesheet). Het rooster is opgebouwd uit diensten (roosterregels) die per tijdvak aangeven wat de geplande activiteit voor die werknemer is.
-}
CONCEPT Dienst "Een dienst is een aaneengesloten periode, inclusief de voorgeschreven pauze, waarin arbeid wrodt verricht." "WTR Artikel 1 lid 7"
CONCEPT Dienst "een aaneengesloten periode waarin arbeid wordt verricht en die gelegen is tussen twee opeenvolgende onafgebroken rusttijden van ten minste 8 uren." "Arbeidstijdenwet Artikel 1:7 lid 1 sub c"
CONCEPT Nachtdienst "een dienst waarin meer dan een uur arbeid wordt verricht tussen 00.00 uur en 06.00 uur." "Arbeidstijdenwet Artikel 1:7 lid 1 sub d"
CONCEPT Pauze "een periode van ten minste 15 achtereenvolgende minuten, waarmee de arbeid tijdens de dienst wordt onderbroken en de werknemer geen enkele verplichting heeft ten aanzien van de bedongen arbeid." "Arbeidstijdenwet Artikel 1:7 lid 1 sub e"
CONCEPT Consignatie "een periode tussen twee opeenvolgende diensten of tijdens een pauze, waarin de werknemer uitsluitend verplicht is bereikbaar te zijn om in geval van onvoorziene omstandigheden op oproep zo spoedig mogelijk de bedongen arbeid te verrichten." "Arbeidstijdenwet Artikel 1:7 lid 1 sub g"
CONCEPT Arbeidstijd "Arbeidstijd is de tijd dat de werknemer onder gezag van de werkgever arbeid verricht." "Arbeidstijdenwet Artikel 1:7 lid 1 sub k"
CONCEPT Rusttijd "Rusttijd is de tijd die geen arbeidstijd is." "Arbeidstijdenwet Artikel 1:7 lid 1 sub l"

RELATION roosterSoortR[Dienst*RoosterSoort] [UNI,TOT] PRAGMA "" " valt onder roostersoort "
MEANING "Binnen een dienst is een bepaalde roostersoort van toepassing."
PURPOSE RELATION roosterSoortR[Dienst*RoosterSoort] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0, p14."
{+De roostersoort bepaalt welke regels worden toegepast tijdens de verwerking van de dagverantwooding (timesheet).
-}
RELATION dagR[Dienst*Dagverantwoording]  PRAGMA "" " is onderdeel van dagverantwoording " "" -- [UNI,TOT]
MEANING "Elke dienst is onderdeel van een dagverantwoording."
PURPOSE RELATION dagR[Dienst*Dagverantwoording] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Een dienst heeft betrekking op werkzaamheden die op een bepaalde dag moeten worden uitgevoerd en is daarmee onderdeel van een dagverantwoording.
-}
RELATION standaardrooster[Dienst*Persoon]
MEANING "Roosterregels, die behoren tot het standaardrooster van een persoon, worden als zodanig geregistreerd."
PURPOSE RELATION standaardrooster[Dienst*Persoon] REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Omdat afwijkingen op het standaardrooster zijn toegestaan, wordt verschil gemaakt tussen een dienst uit het standaardrooster en een dienst uit het afwijkende rooster.
Het standaardrooster kan worden opgehaald uit Rostar CAS. Afwijkingen daarvan kunnen door de leidinggevende worden ingevoerd.
-}
RELATION afwijkendrooster[Dienst*Persoon]
MEANING "Roosterregels, die afwijken van het standaardrooster van een persoon, worden als zodanig geregistreerd."
RELATION datum[Dienst*Datum]
MEANING "De datum, waarop een dienst van toepassing is, is in het systeem bekend."
PURPOSE RELATION datum[Dienst*Datum]
{+Om een dienst aan een dagverantwoording te kunnen verbinden, houdt het systeem bij op welke datum deze dienst geldt.
-}
RULE standaardrooster~;standaardrooster |- I
RULE afwijkendrooster~;afwijkendrooster |- I
--RULE I |- (standaardrooster\/afwijkendrooster);(standaardrooster\/afwijkendrooster)~   --- TIJDELIJK FERLON
MEANING "Elke dienst geldt voor precies één persoon. "

RULE dienst : dagR~;begintijd |- dagS~;begintijd
MEANING "Voor elke dag en begintijd van een dienst moet er een dag en begintijd van een dagverantwoording zijn."
--VIOLATION (TXT "Voor ", SRC I, TXT " bestaat geen autorisatie.")

ROLE Medewerker MAINTAINS dienst
ENDPROCESS

RULE I[DagverantwoordingStatus] = 'Akkoord' \/ 'Retour'
VIEW TimesheetRegel : TimesheetRegel (TXT "activiteit \"", activiteit, TXT " op tijdstip ", begintijd, TXT "\"")
VIEW Dienst : Dienst (TXT "Dienst \"", roosterSoortR, TXT " op ", dagR;datum, TXT "\"")
VIEW PayrollTijdvak : PayrollTijdvak (begindatumP, TXT " - ", einddatumP)
VIEW Periodeverantwoording : Periodeverantwoording (TXT "periode ",periodeverantwoordingPeriode, TXT " van ", periodeverantwoordingPersoon)


-----   BEGIN OUD ------

--			INTERFACE "Bereken Art17 per PayrollTijdvak" (ongebruikelijkUrenPayrollPeriode) FOR Leidinggevende : I[Periode]
--			BOX ["PayrollTijdvak" : I[Periode]                                                                            
--			     ,"" : I[Periode]                                                                                         
--			      BOX ["" : dagverantwoordingVanPayrollPeriode~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoordin
--			     BOX [ "betreft datum" :    datum                                                                         
--			    	          , tijdverantwoording : dagS~                                                                
--				    BOX [ beginTijd :    begintijd                                                                   
--					  , eindTijd :     eindtijd                                                                  
--					  , duur :         tijdsduur                                                                  
--					  , activiteit :   activiteit                                                                 
--			    	        ]                                                                                             
--			               ]                                                                                              
--			     ]                                                                                                        
--			     ,"Ongebruikelijke uren (Uurgroep en uren)" : uurgroepInPeriode  --Uurgroep                               
--			     BOX ["Uurgroep : " : uurgroepInPeriode~;uurgroepInPeriode                                                
--			          , "Aantal uren : " : urenInUurgroep~                                                                
--			         ]                                                                                                    
--			     , "Totaal Ongebruikelijke uren periode : " : ongebruikelijkUrenPayrollPeriode                            
--			    ]                                                                                                         


--			INTERFACE "Bereken Ongebruikelijke tijden" 
--				(uurgroepInPeriode
--			--	 ,payrollPeriodePersoon
--			--	 ,payrolltijdvak
--				 ,urenInUurgroep
--				 ,refLijstUurgroepen
--				 ,wezenlijkeOnderbrekingPersoon
--				 ,wezenlijkeOnderbreking
--				 ,toelageVoorafgaande36Kalendermaanden
--			         ,ongebruikelijkUrenPayrollPeriode)FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
--			BOX ["Autorisaties verleend door " : I[Persoon]
--			     , "" : verleendDoor~/\V;'Verleend';eventType~   -- AutorisatieEventTijdschrijven
--			--     , "" : leidinggevende~  --  Persoon
--			     BOX [ "" :      medewerkerA          -- Persoon
--			           	BOX ["Tijdregistratieberichten van " : I[Persoon]
--					     , "Geboortedatum : " :  geboortedatum
--					     , "Wezenlijke onderbreking : " : wezenlijkeOnderbrekingPersoon~  --WezenlijkeOnderbreking
--						BOX ["Sprake van wezenlijke onderbreking : " : wezenlijkeOnderbreking
--					    	    ]
--					     , "Toelage voorafgaande 36 maanden : " : toelageVoorafgaande36Kalendermaanden
--					     ,"" : I[Persoon]	
--				             BOX ["" : medewerkerD~;dagverantwoordingVanPayrollPeriode -- Periode
--				                  BOX ["PayrollTijdvak" : I[Periode] 
--						       ,"" : I[Periode]
--				  	                BOX ["" : dagverantwoordingVanPayrollPeriode~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoording
--			--	  	                BOX ["" : dagverantwoordingVanPayrollPeriode~/\medewerkerD;medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~  -- Dagverantwoording
--				      			     BOX [ "betreft datum" :    datum
--				          	      	          , tijdverantwoording : dagS~
--				            			    BOX [ beginTijd :    begintijd
--				                			  , eindTijd :     eindtijd
--				                			  , duur :         tijdsduur
--				                			  , activiteit :   activiteit
--				                	    	        ] 
--					                         ]
--							     ]
--						       ,"Ongebruikelijke uren (Uurgroep en uren)" : uurgroepInPeriode  --Uurgroep
--						       BOX ["Uurgroep : " : uurgroepInPeriode~;uurgroepInPeriode
--						            , "Aantal uren : " : urenInUurgroep~
--						           ]
--						       , "Totaal Ongebruikelijke uren periode : " : ongebruikelijkUrenPayrollPeriode
--			   			      ]
--			                         ]
--				      	    ]
--				 ]
--			    ]

----- EINDE OUD ------

INTERFACE "Bereken ongebruikelijke tijden" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~;leidinggevende~
BOX ["Medewerker : " : I[Persoon]
     ,"" : periodeverantwoordingPersoon~   --Periodeverantwoording
     BOX["Periode : " : periodeverantwoordingPeriode  -- PayrollTijdvak
	 ,"Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
	       BOX ["betreft datum" :    datum
       	            , tijdverantwoording : dagS~
		    BOX [ beginTijd :    begintijd
       			  , eindTijd :     eindtijd
       			  , duur :         tijdsduur
       			  , activiteit :   activiteit
       	    	        ] 
                   ]
	   
	]
    ]

INTERFACE "Bereken Art17 per payroll periode" 
	  (periodeverantwoordingOngebruikelijkeUren
	   ,periodeverantwoordingWezenlijkeOnderbreking
	   ,periodeverantwoordingSalarisnummer10Salarisschaal7
	   ,periodeverantwoordingVasteToelageArt17lid4
	   ,periodeverantwoordingVasteToelageArt17lid7
	   ,periodeverantwoordingToelageVoorafgaande36Kalendermaanden) FOR Leidinggevende : I[Periodeverantwoording]
     BOX["Medewerker" : periodeverantwoordingPersoon
	 ,"Periode : " : periodeverantwoordingPeriode  
	 ,"Geboortedatum : " : periodeverantwoordingPersoon;geboortedatum
	 ,"Wezenlijke onderbreking (2 maanden) : " : periodeverantwoordingWezenlijkeOnderbreking
	 ,"Toelage voorafgaande 36 maanden : " : periodeverantwoordingToelageVoorafgaande36Kalendermaanden
	 ,"Salaris per uur van salarisnummer 10 van salarisschaal 7 : " : periodeverantwoordingSalarisnummer10Salarisschaal7
	 ,"Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
	       BOX ["betreft datum" :    datum
       	            , tijdverantwoording : dagS~
		    BOX [ beginTijd :    begintijd
       			  , eindTijd :     eindtijd
       			  , duur :         tijdsduur
       			  , activiteit :   activiteit
       	    	        ] 
                   ]
	  , "Extra uren door ongebruikelijke tijden : " : periodeverantwoordingOngebruikelijkeUren
	  , "Vaste toelage als gevolg van Artikel 17 lid 4 : " : periodeverantwoordingVasteToelageArt17lid4
	  , "Vaste toelage als gevolg van Artikel 17 lid 7 : " : periodeverantwoordingVasteToelageArt17lid7
	]

--INTERFACE "Nieuwe periodeverantwoording" FOR Leidinggevende : I[Periodeverantwoording]
--BOX ["Voor medewerker : " : periodeverantwoordingPersoon
--     ,"Voor periode : " : periodeverantwoordingPeriode
--    ]


INTERFACE "Geaccordeerde tijdregistraties"
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    begintijd
                , eindTijd :     eindtijd
                ]
          , toelichting : toelichting
          ]
    ]

INTERFACE "Retour"
           ( dagS
           , begintijd
           , eindtijd
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistratieberichten van " : I[Persoon]
    , "" : medewerkerD~/\V;'Retour';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , status :       dagverantwoordingStatus
          , tijdverantwoording : dagS~
            BOX [ beginTijd :    begintijd
                , eindTijd :     eindtijd
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dagR~
            BOX [ roosterSoort : roosterSoortR
                , beginTijd :    begintijd
                , eindTijd :     eindtijd
                ]
          , toelichting : toelichting
          , "Commentaar" :opmerkingValidatie
          ]
    ]


INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dagS
           , begintijd
           , eindtijd
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanPayrollPeriode
	   , periodeverantwoordingDagverantwoording
           , roosterSoortR
           , dagR
           , begintijd
           , eindtijd
           ) FOR Medewerker : I[Dagverantwoording]
BOX [ "datum invoer" :    ontvangen -- automatisch invullen
    , "Medewerker" :   medewerkerD  -- automatisch invullen
    , "betreft datum" :    datum    -- door gebruiker laten invullen
    , "betreft payrolltijdvak" :  dagverantwoordingVanPayrollPeriode -- automatisch laten invullen
    , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~ -- automatisch laten invullen
    , tijdverantwoording : dagS~
      BOX [ beginTijd :    begintijd -- twee van de drie in laten vullen, de derde automatisch.
          , eindTijd :     eindtijd
          , duur :         tijdsduur
          , activiteit :   activiteit      -- door gebruiker laten invullen
          , project :      project         -- door gebruiker laten invullen
          , kostenplaats : kostenplaats    -- door gebruiker laten invullen
          ]
    , roosterverantwoording : dagR~
      BOX [ roosterSoort : roosterSoortR
          , beginTijd :    begintijd
          , eindTijd :     eindtijd
          ]
    , Opmerkingen : toelichting
    ]


INTERFACE "Dagverantwoording voor DEMO"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dagS
           , begintijd
           , eindtijd
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanPayrollPeriode
	   , periodeverantwoordingDagverantwoording
           , roosterSoortR
           , dagR
           , begintijd
           , eindtijd
           ) FOR Medewerker :  V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "DV's" : medewerkerD~
     BOX [ "datum invoer" :    ontvangen -- automatisch invullen
    	  , "Medewerker" :   medewerkerD  -- automatisch invullen
    	  , "betreft datum" :    datum    -- door gebruiker laten invullen
    	  , "betreft payrolltijdvak" :  dagverantwoordingVanPayrollPeriode -- automatisch laten invullen
    	  , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~ -- automatisch laten invullen
    	  , tijdverantwoording : dagS~
      	  BOX [ beginTijd :    begintijd -- twee van de drie in laten vullen, de derde automatisch.
          	, eindTijd :     eindtijd
          	, duur :         tijdsduur
          	, activiteit :   activiteit      -- door gebruiker laten invullen
          	, project :      project         -- door gebruiker laten invullen
          	, kostenplaats : kostenplaats    -- door gebruiker laten invullen
              ]
    	  , roosterverantwoording : dagR~
      	  BOX [ roosterSoort : roosterSoortR
          	, beginTijd :    begintijd
          	, eindTijd :     eindtijd
              ]
    	  , Opmerkingen : toelichting
    	 ]
    ]

{-
INTERFACE "Werkrooster inregelen"
	  (  datum[Dienst*Datum]
	   , roosterSoortR
	   , begintijd
	   , eindtijd
	   , standaardrooster
	  )FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Medewerkers : " : leidinggevende~
      BOX [ "Rooster van : " : I[Persoon]
	    , "" : standaardrooster~     -- Dienst
	    BOX [ "Dienst : " : I[Dienst]
	    	  BOX [ "Datum" : datum[Dienst*Datum]
		        , roostersoort : roosterSoortR
		        , begintijd : begintijd
	                , eindtijd : eindtijd
		      ]
		]	
	  ]
    ]
-}

INTERFACE "Werkrooster inregelen"
	  (  datum[Dienst*Datum]
	   , roosterSoortR
	   , begintijd
	   , eindtijd
	   , standaardrooster
	  )
	  FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
     , "" : standaardrooster~     -- Dienst
     BOX ["Datum" : datum[Dienst*Datum]
 	  , roostersoort : roosterSoortR
	  , begintijd : begintijd
          , eindtijd : eindtijd
	 ]
    ]



INTERFACE "Werkrooster bekijken"
	  FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
     , "" : standaardrooster~     -- Dienst
     BOX ["Datum" : datum[Dienst*Datum]
 	  , roostersoort : roosterSoortR
	  , begintijd : begintijd
          , eindtijd : eindtijd
	 ]
    ]

-- ENFORCE [TOT] kostenplaats[TimesheetRegel*Kostenplaats] BY Gebruiker

-- Vraag: Welke roostersoorten bestaan er?
-- Antwoord: Tenminste "Regulier", "Dienst", "Geen dienst", "Overwerk io", "Beschikbaar"
-- Antwoord: Volgens de IA bestaat er een referentielijst van roostersoorten.
-- Vraag: Welke uurgroepen bestaan er?
-- Antwoord: Tenminste "Arbeidsuren", "Meeruren", "TOD 0%", "TOD 20%", "TOD 40%", "VRU 45%", "Overuren 25%", "Overuren 50%", "Beschikbaarheid 5%"
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze"
-- Vraag: Welke activiteiten bestaan er?
-- Antwoord: Tenminste "Toezicht", "Inspectie", "Pauze", "Advies", "Compensatie verlof", "Overleg", "Support", "Reistijd naar inspectielokatie"
-- Antwoord: In de IA wordt een activiteit "tijdRubriek" genoemd. Daarvan bestaat een referentielijst.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze", "Compensatie"
-- Vraag: Welke urenpools bestaan er?
-- Antwoord: Tenminste "Vakantiedagen", "Compensatie"

CONCEPT PayrollTijdvak "Periode van 4 weken waarover het salaris wordt berekend en uitbetaald"
PURPOSE CONCEPT PayrollTijdvak REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De salarisverwerking vindt plaats per tijdvak van 4 weken. Dit tijdvak wordt een payrollTijdvak (payrolltijdvak) genoemd.
-}

CONCEPT TijdvakResultaat "Een TijdvakResultaat is het resultaat van opsplitsing van de dagverantwoordingregels naar specifieke uurtijdvakken en roostersoorten." "IA"
PURPOSE CONCEPT TijdvakResultaat REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Dagverantwoordingsregels worden in een TijdvakResultaat opgesplitst naar specifieke uurtijdvakken en roostersoorten. De tijd geregistreert bij een TijdvakResultaten wordt vervolgens volgens bepaalde regels toegekend aan uurgroepen.
-}

CONCEPT Uurgroep "Een Uurgroep is een hoeveelheid uren van een bepaalde categorie binnen een specifieke periode. Bijvoorbeeld de overwerk uren op een dag."
PURPOSE CONCEPT Uurgroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+De juiste inrichting van uurgroepen zorgt er voor dat de juiste berekeningen gemaakt kunnen worden. Voorafgaand aan de uitvoering van payroll periode worden er per uurGroep totalen gemaakt voor het hele payrolltijdvak.
-} 

CONCEPT PayrollGroep "Een PayrollGroep is een groep uren binnen een bepaald payrollTijdvak die onder een specifieke code aan de salarisverwerking worden doorgegeven."
PURPOSE CONCEPT PayrollGroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Aan het einde van een payroll periode worden de uren per payrollGroep aangeleverd van P-Direct voor verwerking in de salarisadministratie.
-}

CONCEPT Timesheet "Een timesheet is een periodiek overzicht van de gewerkte uren van één medewerker."

CONCEPT Periodeverantwoording "Een periodeverantwoording is een periodiek overzicht van de gewerkte uren van één medewerker in één payrolltijdvak."
PURPOSE CONCEPT Periodeverantwoording 
{+Voor elke medewerker worden de uren per payrolltijdvak van 4 weken verwerkt. De urenverantwoording van één medewerker voor één payrolltijdvak wordt een Periodeverantwoording genoemd
-}

CLASSIFY Dagverantwoording ISA Bericht
CONCEPT Dagverantwoording "Een dagverantwoording is een overzicht van alle gewerkte uren van één dag voor één medewerker. (De PSA spreekt over een dagfilm)." "IA"
PURPOSE CONCEPT Dagverantwoording 
{+Wanneer een medewerker tijd registreert, resulteert dat in een aantal SPN_TIJDVERANTWOORDINGen, die samen in één bericht realtime het systeem binnenkomen.
Om deze SPN_TIJDVERANTWOORDINGen bijeen te houden, gebruiken we daarvoor het concept Dagverantwoording.
-}

CONCEPT UurTijdvak "Een UurTijdvak is een tijdvak binnen een specifieke dag van de week met een begin- en eindtijd. Bijvoorbeeld maandag van 8:00 tot 10:00."
PURPOSE CONCEPT UurTijdvak
{+Om een werkdag onder te verdelen in kleinere tijdseenheden waarin werkzaamheden worden verricht of ingeroosterd, zijn er UurTijdvakken gedefinieerd.
-}
--** Wat wordt precies verstaan onder een uurtijdvak (op een bepaalde datum of op een bepaalde dag)*

CONCEPT Dagsoort "We onderscheiden de dagsoorten: ma-vr, za-zo, feestdag."
PURPOSE CONCEPT Dagsoort
{+Voor verschillende dagsoorten (ma-vr, za, zo, feestdagen) gelden andere toeslagpercentages bij het berekenen van ongebruikelijk tijden in BBRA Artikel 17 lid 2.
-}

CONCEPT Werktijdenpakket "Een werktijdenpakket is een vooraf gedefinieerd pakket van werktijdRules die een geheel vormen en van toepassing zijn op een bepaalde groep medewerkers." "IA"
PURPOSE CONCEPT Werktijdenpakket
{+ Om te bepalen welke werktijdrules wegens gemaakte afspraken of wetgeving voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding), 
moet worden nagegaan welk werktijdenpakket op dat moment geldig is voor die medewerker.
-}

CONCEPT MedewerkerWerktijdenpakket "Een MedewerkerWerktijdenpakket geeft voor een medewerker aan in welke periode een werktijdenpakket geldig is."
PURPOSE CONCEPT MedewerkerWerktijdenpakket
{+Voor elke medewerker is voor elke periode één werktijdenpakket van toepassing. Een Medewerkerwerktijdenpakket geeft voor één medewerker aan in welke periode (met een begin en einddatum) een bepaald werktijdenpakket  
van toepassing is.
-}



-- Interface Beheren Tijdverantwoording (reeds bestaand) moet worden uitgebreid.

-- Interface Beheren Roostertypen moet worden toegevoegd

-- Regel: Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd.

-- Regel: Alle uren moeten verantwoord zijn.
-- Regel: Een autorisatie voor een medewerker kan alleen gegeven worden door degene die op het moment van autoriseren de leidinggevende is van die persoon.
-- Regel: Een timesheet kan alleen goedgekeurd worden als alle combinaties van tijdRubriek, project en kostenplaats ook geautoriseerd zijn.
-- Regel "JaarRule" "Elk jaar begint met nul vakantiedagen" 
-- Regel "JaarRule" "Het aantal overuren waar jaar (n+1) mee begint is het aantal overuren ontstaan in december van jaar n en niet opgenomen is in die maand."


ENDCONTEXT