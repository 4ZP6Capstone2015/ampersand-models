CONTEXT DTV IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Berichten.adl"
INCLUDE "Autorisaties.adl"
INCLUDE "Roosters.adl"
INCLUDE "Referentielijsten.adl"
INCLUDE "Rechten.adl"
INCLUDE "Medewerkers.adl"
INCLUDE "Gebeurtenissen.adl"
INCLUDE "Tijdvakken.adl"
INCLUDE "Persoonsinformatie.adl"
INCLUDE "Pcodes.adl"
INCLUDE "PopForTesting.adl"

THEMES Dagverantwoordingen, Autorisaties, Rechten, Medewerkers, Tijdvakken, Betaalperiodes, Persoonsinformatie, Roosters

PURPOSE CONTEXT DTV
{+De werktijdenregeling Nederlandse Voedsel- en Warenautoriteit (hierna: WTR)
stelt een applicatie voor het tijdschrijven beschikbaar voor alle medewerkers,
waarmee alle (werk)tijd wordt geregistreerd en verantwoord (WTR Artikel 2 lid 6).
-}

{-
INTERFACE Dagverantwoording (dag[Dienst*Dagverantwoording], dag[Tijdregistratie*Dagverantwoording], begin[Periode*DatumTijd], eind[Periode*DatumTijd], dienst, werksoort) : I[Dagverantwoording]
BOX [ indiener : medewerkerD
    , Roosterregels : dag[Dienst*Dagverantwoording]~
      BOX [ "" :     I[Dienst]
          , begin :  begin[Periode*DatumTijd]
          , eind :   eind[Periode*DatumTijd]
          , dienst : dienst
          ]
    , Tijdregistraties : dag[Tijdregistratie*Dagverantwoording]~
      BOX [ ""  :       I[Tijdregistratie]
          , begin :     begin[Periode*DatumTijd]
          , eind :      eind[Periode*DatumTijd]
          , werksoort : werksoort
          ]
    ]
-}

PATTERN Dagverantwoordingen
PURPOSE PATTERN Dagverantwoordingen
{+Een dagverantwoording is de verantwoording van werk-/pauze- en rusttijden.
FaTijDec hanteert hiervoor het begrip "werkbon".
-}
-- RELATION werksoort[Tijdregistratie*Werksoort] [UNI,TOT]

ENDPATTERN


RULE I[DagverantwoordingStatus] = 'Akkoord' \/ 'Retour'
VIEW Tijdregistratie : Tijdregistratie (TXT "activiteit \"", activiteit, TXT " op tijdstip ", begin[Periode*DatumTijd], TXT "\"")
VIEW Dienst : Dienst (TXT "Dienst \"", roosterSoort, TXT " op ", dag;datum, TXT "\"")
VIEW Betaalperiode : Betaalperiode (TXT "de dagperiode met betaaldatum ", betaaldatum)
VIEW Periodeverantwoording : Periodeverantwoording (TXT "dagperiode ",periodeverantwoordingPeriode, TXT " van ", periodeverantwoordingDagverantwoording;medewerkerD)

{-
INTERFACE "Bereken onregelmatige tijden" FOR Leidinggevende : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~;leidinggevende~
BOX [ "Medewerker : " : I[Persoon]
    , "" : periodeverantwoordingPersoon~   --Periodeverantwoording
      BOX[ "Betaalperiode : " : periodeverantwoordingPeriode  -- Betaalperiode
         , "Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
           BOX [ "betreft datum" :    datum
               , tijdverantwoording : dag~
                 BOX [ begin :        begin
                     , eind :         eind
                     , duur :         tijdsduur
                     , activiteit :   activiteit
                     ]
               ]
         ]
    ]


INTERFACE "Bereken Art17 per betaalperiode" 
        (periodeverantwoordingOngebruikelijkeUren
         ,periodeverantwoordingWezenlijkeOnderbreking
         ,periodeverantwoordingSalarisnummer10Salarisschaal7
         ,periodeverantwoordingVasteToelageArt17lid4
         ,periodeverantwoordingVasteToelageArt17lid7
         ,periodeverantwoordingToelageVoorafgaande36Kalendermaanden) FOR Leidinggevende : I[Periodeverantwoording]
    BOX[ "Medewerker" : periodeverantwoordingPersoon
       , "Betaalperiode : " : periodeverantwoordingPeriode  
       , "Geboortedatum : " : periodeverantwoordingPersoon;geboortedatum
       , "Wezenlijke onderbreking (2 maanden) : " : periodeverantwoordingWezenlijkeOnderbreking
       , "Toelage voorafgaande 36 maanden : " : periodeverantwoordingToelageVoorafgaande36Kalendermaanden
       , "Salaris per uur van salarisnummer 10 van salarisschaal 7 : " : periodeverantwoordingSalarisnummer10Salarisschaal7
       , "Dagverantwoordingen : " : periodeverantwoordingDagverantwoording
         BOX [ "betreft datum" :    datum
             , tijdverantwoording : dag~
               BOX [ begin :        begin
                   , eind :         eind
                   , duur :         tijdsduur
                   , activiteit :   activiteit
                   ]
             ]
       , "Extra uren door ongebruikelijke tijden : " : periodeverantwoordingOngebruikelijkeUren
       , "Vaste toelage als gevolg van Artikel 17 lid 4 : " : periodeverantwoordingVasteToelageArt17lid4
       , "Vaste toelage als gevolg van Artikel 17 lid 7 : " : periodeverantwoordingVasteToelageArt17lid7
       ] 
-}

--INTERFACE "Nieuwe periodeverantwoording" FOR Leidinggevende : I[Periodeverantwoording]
--BOX ["Voor medewerker : " : periodeverantwoordingPersoon
--    ,"Voor betaalperiode : " : periodeverantwoordingPeriode
--    ]


INTERFACE "Geaccordeerde tijdregistraties"
FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties van " : I[Persoon]
    , "" : medewerkerD~/\V;'Akkoord';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , tijdverantwoording : dag~
            BOX [ begin :        begin
                , eind :         eind
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dag~
            BOX [ roosterSoort : roosterSoort
                , begin :        begin[Periode*DatumTijd]
                , eind :         eind[Periode*DatumTijd]
                ]
          , toelichting : toelichting
          ]
    ]

INTERFACE "Retour"
           ( dag[Tijdregistratie*Dagverantwoording]
           , dag[Dienst*Dagverantwoording]
           , begin[Periode*DatumTijd]
           , eind[Periode*DatumTijd]
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX ["Tijdregistraties van " : I[Persoon]
    , "" : medewerkerD~/\V;'Retour';dagverantwoordingStatus~   -- Dagverantwoording
      BOX [ ingediend :          ontvangen
          , "betreft datum" :    datum
          , status :       dagverantwoordingStatus
          , tijdverantwoording : dag[Tijdregistratie*Dagverantwoording]~
            BOX [ begin :        begin
                , eind :         eind
                , duur :         tijdsduur
                , activiteit :   activiteit
                , project :      project
                , kostenplaats : kostenplaats
                ]
          , roosterverantwoording : dag[Dienst*Dagverantwoording]~
            BOX [ roosterSoort : roosterSoort
                , begin :        begin[Periode*DatumTijd]
                , eind :         eind[Periode*DatumTijd]
                ]
          , toelichting : toelichting
          , "Commentaar" :opmerkingValidatie
          ]
    ]

INTERFACE "Nieuwe Dagverantwoording"
           ( ontvangen
           , medewerkerD
           , datum[Dagverantwoording*Datum]
           , dag[Tijdregistratie*Dagverantwoording]
           , dag[Dienst*Dagverantwoording]
           , tijdsduur
           , activiteit
           , project
           , kostenplaats
           , toelichting
           , dagverantwoordingVanBetaalperiode
           , periodeverantwoordingDagverantwoording
           , roosterSoort[Dienst*Roostersoort]
           , begin[Periode*DatumTijd]
           , eind[Periode*DatumTijd]
           ) FOR Medewerker : I[Dagverantwoording]
BOX [ "datum invoer" :    ontvangen -- automatisch invullen
    , "Medewerker" :   medewerkerD  -- automatisch invullen
    , "betreft datum" :    datum    -- door gebruiker laten invullen
    , "betreft betaalperiode" :  dagverantwoordingVanBetaalperiode -- automatisch laten invullen
    , "betreft periodeverantwoording" : periodeverantwoordingDagverantwoording~ -- automatisch laten invullen
    , tijdverantwoording : dag~
      BOX [ begin :        begin -- twee van de drie in laten vullen, de derde automatisch.
          , eind :         eind
          , duur :         tijdsduur
          , activiteit :   activiteit      -- door gebruiker laten invullen
          , project :      project         -- door gebruiker laten invullen
          , kostenplaats : kostenplaats    -- door gebruiker laten invullen
          ]
    , roosterverantwoording : dag~
      BOX [ roosterSoort : roosterSoort
          , begin :        begin[Periode*DatumTijd]
          , eind :         eind[Periode*DatumTijd]
          ]
    , Opmerkingen : toelichting
    ]

INTERFACE "Mijn Dagverantwoordingen"
           ( medewerkerD
           ) FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~ --I[Dagverantwoording]
BOX ["Dagverantwoording : " : medewerkerD~  -- Dagverantwoording
    ]



INTERFACE "Werkrooster inregelen"
        (  roosterSoort[Dienst*Roostersoort]
        , begin[Periode*DatumTijd]
        , eind[Periode*DatumTijd]
        , dag[Dienst*Dagverantwoording]
        , datum[Dagverantwoording*Datum]
        , medewerkerD
        )
        FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
    , "" : medewerkerD~     -- Dagverantwoording
      BOX [ Datum : datum[Dagverantwoording*Datum]
          , Rooster : dag~      -- Dienst
            BOX [ roostersoort : roosterSoort
                , begin :        begin[Periode*DatumTijd]
                , eind :         eind[Periode*DatumTijd]
                ]
          ]
    ]

{-
INTERFACE "Werkrooster bekijken"
          FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
    , "" : medewerkerD~;dag~  --Dienst
      BOX ["Datum" :       dag;datum[Dagverantwoording*Datum]
          , roostersoort : roosterSoort
          , begin :        begin[Periode*DatumTijd]
          , eind :         eind[Periode*DatumTijd]
          ]
    ]
-}

INTERFACE "Werkrooster bekijken"
          FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Rooster van : " : I[Persoon]
    , "" : medewerkerD~     -- Dagverantwoording
      BOX [ Datum : datum[Dagverantwoording*Datum]
          , Rooster : dag~      -- Dienst
            BOX [ roostersoort : roosterSoort
                , begin :        begin[Periode*DatumTijd]
                , eind :         eind[Periode*DatumTijd]
                ]
          ]
    ]

INTERFACE "Arbeidsmodaliteit inregelen"
         ( arbeidsmodaliteitPersoon
         , arbeidsmodaliteitIngangsdatum
         , arbeidsmod
         , arbeidsmodWeek
         , arbeidsmodDag
         , arbeidsmodUren
         )
        FOR Leidinggevende : I[Arbeidsmodaliteit]
      BOX [ "Medewerker : " :   arbeidsmodaliteitPersoon
          , "Ingangsdatum : " : arbeidsmodaliteitIngangsdatum
          , "Modaliteit" :      arbeidsmod  --Arbeidsmod
            BOX [ "Week : " : arbeidsmodWeek
                ,"Dag : " :   arbeidsmodDag
                ,"Uren : " :  arbeidsmodUren
                ]
          ]

INTERFACE "Arbeidsmodaliteit bekijken"
        FOR Medewerker : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
BOX [ "Modaliteit van : " : I[Persoon]
    , "" : arbeidsmodaliteitPersoon~;arbeidsmod  --Arbeidsmod
      BOX ["Week" : arbeidsmodWeek
          , dag : arbeidsmodDag
          , uren : arbeidsmodUren
          ]
    ]

-- ENFORCE [TOT] kostenplaats[Tijdregistratie*Kostenplaats] BY Gebruiker

INTERFACE "Rule engine" 
         (periodeverantwoordingOngebruikelijkeUren
         ,periodeverantwoordingWezenlijkeOnderbreking
         ,periodeverantwoordingToelageVoorafgaande36Kalendermaanden
         ,resultaat
         ,resultaatPDirektCode
         ,resultaatWaarde) FOR RuleEngine : V[ONE*SESSION];'_SESSION';sessionUsername;emailadres~
   BOX [ "Medewerker" : I[Persoon]
           BOX [
{-         BOX [ geboortedatum : geboortedatum
             , Modaliteit : arbeidsmodaliteitPersoon~;arbeidsmod  --Arbeidsmod
              BOX [ "Week" : arbeidsmodWeek
                   , dag : arbeidsmodDag
                   , uren : arbeidsmodUren
                   ]
               , "Werktijdenpakketten : " : werktijdenpakketPersoon~           -- MedewerkerWerktijdenpakket
                 BOX [ "Werktijdenpakket" : medewerkerWerktijdenpakket
                       , "geldig van" : pakketGeldigVan
                       , "geldig tot" : pakketGeldigTot
                     ]
             , "Rechten : " : rechthebbende~;recht  
             , "" : periodeverantwoordingPersoon~
-}             "Functieschaal : " : functieschaal
             , "Werktijdenpakketten : " : werktijdenpakketPersoon~  
             , "" : medewerkerD~;periodeverantwoordingDagverantwoording~
               BOX [ periode : periodeverantwoordingPeriode
--                   , "Wezenlijke onderbreking (2 maanden) : " : periodeverantwoordingWezenlijkeOnderbreking
--                   , "Toelage voorafgaande 36 maanden : " : periodeverantwoordingToelageVoorafgaande36Kalendermaanden
                   , "Dagverantwoordingen : " : periodeverantwoordingDagverantwoording  --Dagverantwoording
                     BOX [ "betreft datum" :    datum
                         , tijdverantwoording : dag~
                           BOX [ begin :        begin[Periode*DatumTijd]
                               , eind :         eind[Periode*DatumTijd]
                               , duur :         tijdsduur
                               , activiteit :   activiteit
                               ]
                         , Rooster : dag~      -- Dienst
                           BOX [ roostersoort : roosterSoort
                               , begin :        begin[Periode*DatumTijd]
                               , eind :         eind[Periode*DatumTijd]
                               ]
                         , "Resultaat verwerking : " : resultaat
                           BOX [ "P-direct code : " : resultaatPDirektCode
                                 ,"Aantal uren : " : resultaatWaarde
                               ]
                         ]
                   ]
              ]
       ]




-- Vraag: Welke roostersoorten bestaan er?
-- Antwoord: Tenminste "Regulier", "Dienst", "Geen dienst", "Overwerk io", "Beschikbaar"
-- Antwoord: Volgens de IA bestaat er een referentielijst van roostersoorten.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze"
-- Vraag: Welke activiteiten bestaan er?
-- Antwoord: Tenminste "Toezicht", "Inspectie", "Pauze", "Advies", "Compensatie verlof", "Overleg", "Support", "Reistijd naar inspectielokatie"
-- Antwoord: In de IA wordt een activiteit "tijdRubriek" genoemd. Daarvan bestaat een referentielijst.
-- Vraag: Welke categorieën bestaan er?
-- Antwoord: Tenminste "Werktijd", "Pauze", "Compensatie"
-- Vraag: Welke urenpools bestaan er?
-- Antwoord: Tenminste "Vakantiedagen", "Compensatie"

CONCEPT Betaalperiode "Een betaalperiode eriode waarover het salaris wordt berekend en uitbetaald"
PURPOSE CONCEPT Betaalperiode REF "BBRA Artikel 3 lid 1"
{+Het salaris, de toelagen, de periodieke toeslag en de vergoedingen voor extra diensten worden maandelijks betaald.
De salarisverwerking vindt dan ook plaats per maand.
In DTV gebruiken we hiervoor het woord ``betaalperiode''.
-}

CONCEPT TijdvakResultaat "Een TijdvakResultaat is het resultaat van opsplitsing van de tijdregistraties naar specifieke uurtijdvakken en roostersoorten." "IA"
PURPOSE CONCEPT TijdvakResultaat REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Tijdregistraties worden in een tijdvakresultaat opgesplitst naar specifieke uurtijdvakken en roostersoorten.
De tijd geregistreerd bij een tijdvakresultaat wordt vervolgens als volgt toegekend aan uurgroepen:
(opzoeken in BBRA/ARAR/WTR)
-}

CONCEPT PayrollGroep "Een PayrollGroep is een groep uren binnen een bepaalde Betaalperiode die onder een specifieke code aan de salarisverwerking worden doorgegeven."
PURPOSE CONCEPT PayrollGroep REF "Informatieanalyse 'Duurzaam Tijdverantwoorden' versie 5.0"
{+Aan het einde van een betaalperiode worden de uren per payrollGroep aangeleverd van P-Direct voor verwerking in de salarisadministratie.
-}

CONCEPT Periodeverantwoording "Een periodeverantwoording is een periodiek overzicht van de gewerkte uren van één medewerker in één betaalperiode."
PURPOSE CONCEPT Periodeverantwoording 
{+Voor elke medewerker worden de uren per betaalperiode verwerkt.
De urenverantwoording van één medewerker voor één betaalperiode wordt een Periodeverantwoording genoemd.
Het woord ``timesheet'' wordt hiervoor als synoniem gebruikt. Dit woord is afkomstig uit de informatie-analyse.
-}

CLASSIFY Dagverantwoording ISA Bericht
CONCEPT Dagverantwoording "Een dagverantwoording is een overzicht van alle gewerkte uren van één dag voor één medewerker. (De PSA spreekt over een dagfilm)." "IA"
PURPOSE CONCEPT Dagverantwoording 
{+Wanneer een medewerker tijd registreert, resulteert dat in een aantal tijdregistraties, die samen in één bericht realtime het systeem binnenkomen.
Dat gaat bijvoorbeeld om werk-/pauze- en rusttijden.
Om deze tijdregistraties bijeen te houden, gebruiken we daarvoor het concept Dagverantwoording.
FaTijDec hanteert hiervoor het begrip "werkbon".
-}

CONCEPT Werktijdenpakket "Een werktijdenpakket is een vooraf gedefinieerd pakket van werktijdRules die een geheel vormen en van toepassing zijn op een bepaalde groep medewerkers." "IA"
PURPOSE CONCEPT Werktijdenpakket
{+ Om te bepalen welke werktijdrules wegens gemaakte afspraken of wetgeving voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding), 
moet worden nagegaan welk werktijdenpakket op dat moment geldig is voor die medewerker.
-}

CONCEPT MedewerkerWerktijdenpakket "Een MedewerkerWerktijdenpakket geeft voor een medewerker aan in welke betaalperiode een werktijdenpakket geldig is."
PURPOSE CONCEPT MedewerkerWerktijdenpakket
{+Voor elke medewerker is voor elke betaalperiode één werktijdenpakket van toepassing. Een Medewerkerwerktijdenpakket geeft voor één medewerker aan in welke betaalperiode (met een begin en einddatum) een bepaald werktijdenpakket  
van toepassing is.
-}



-- Interface Beheren Tijdverantwoording (reeds bestaand) moet worden uitgebreid.

-- Interface Beheren Roostertypen moet worden toegevoegd

-- Regel: Een medewerker kan alleen uren registreren op een combinatie van tijdRubriek, project en kostenplaats waarvoor hij/zij geautoriseerd is of een autorisatie heeft aangevraagd.

-- Regel: Alle uren moeten verantwoord zijn.
-- Regel: Een autorisatie voor een medewerker kan alleen gegeven worden door degene die op het moment van autoriseren de leidinggevende is van die persoon.
-- Regel: Een periodeverantwoording kan alleen goedgekeurd worden als alle combinaties van tijdRubriek, project en kostenplaats ook geautoriseerd zijn.
-- Regel "JaarRule" "Elk jaar begint met nul vakantiedagen" 
-- Regel "JaarRule" "Het aantal overuren waar jaar (n+1) mee begint is het aantal overuren ontstaan in december van jaar n en niet opgenomen is in die maand."


ENDCONTEXT