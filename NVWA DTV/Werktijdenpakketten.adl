CONTEXT DTV IN DUTCH LATEX

PATTERN MedewerkerRechten
PURPOSE PATTERN Werktijdenpakketten REF "Tijdschrijven Fatijdec sept 2013"
{+In Fatijdec wordt bij de medewerker-gegevens aangegeven of iemand al dan niet recht heeft op vaste toelagen. Dit moet in DTV ook kunnen.
Een verzameling rechten heet in DTV een ``medewerkerRecht''.
Werktijdenpakketten worden door de rol HR bewerkt.
Medewerkers en hun leidinggevenden kunnen werktijdenpakketten dus niet veranderen.

Op elk willekeurig moment in de tijd geldt voor één medewerker één medewerkerRecht.
Daarmee zijn alle rechten van die medewerker op dat moment eenduidig in het systeem bepaald.
Omdat het systeem ook moet kunnen rekenen met rechten die in het verleden golden, wordt aan elk medewerkerRecht een geldigheidsperiode gekoppeld.

Het systeem dient zorg te dragen dat voor iedere medewerker er voor elk moment in de tijd precies één werktijdepakket geldt.
Als de rechten van een medewerker op tijdstip $t$ moeten worden opgevraagd, zal het systeem dus het medewerkerRecht opzoeken waarbinnen dat tijdstip valt.
Uit dat medewerkerRecht worden vervolgens alle rechten gehaald.
-}

CONCEPT MedewerkerRecht "Een medewerkerRecht is een vooraf pakket van rechten die van toepassing zijn op een specifieke persoon."
PURPOSE CONCEPT MedewerkerRecht
{+ Om te bepalen welke werktijdrules wegens gemaakte afspraken of wetgeving voor een bepaalde medewerker van toepassing zijn (b.v. variabele of vaste onkostenvergoeding), 
moet worden nagegaan welk medewerkerRecht op dat moment geldig is voor die medewerker.
Een medewerkerRecht geeft voor een medewerker aan welk medewerkerRecht in een bepaalde verantwoordingsperiode van toepassing is.
Om de duur van een recht te verantwoorden worden begin- en einddatum van dat recht worden geregistreerd.
Wanneer er geen einddatum geregistreerd is, dan geldt het recht voor onbepaalde tijd.
-}

CLASSIFY MedewerkerRecht ISA Periode

PURPOSE RULE "Ingangsdatum verplicht in medewerkerRecht"
{+ Opdat duidelijk is wanneer welke rechten gelden, is een ingangsdatum voor elk medewerkerRecht verplicht.
Wanneer ook een einddatum is gespecificeerd, gelden de rechten uit dit medewerkerRecht tot de einddatum.
-}

RULE "Ingangsdatum verplicht in medewerkerRecht" : I[MedewerkerRecht] |- begindatum;begindatum~
MEANING "Elk medewerkerRecht dient voorzien te zijn van de datum vanaf welke de geldigheidsperiode ingaat."
MESSAGE "Elk medewerkerRecht dient voorzien te zijn van een ingangsdatum."
VIOLATION (TXT "Het medewerkerRecht voor ", SRC medewerkerRecht~;personeelsnummer, TXT " van ", SRC begindatum, TXT " tot ", SRC einddatum, TXT " moet nog voorzien worden van een ingangsdatum.")

PURPOSE RELATION salarisschaal[MedewerkerRecht*Salarisschaal]  REF "BBRA Artikel 2 sub c"
{+Een salarisschaal is een als zodanig in de bijlage B van het Bezoldigingsbesluit Burgerlijke Rijksambtenaren 1984 (BBRA) vermelde reeks van genummerde salarissen.
Omdat bepaalde regels van toepassing zijn afhankelijk van iemand's salarisschaal, wordt dit gegeven in de DTV geregistreerd.
-}
RELATION salarisschaal[MedewerkerRecht*Salarisschaal] [UNI] PRAGMA "Medewerker " " zit in salarisschaal " " "   -- 
MEANING "Van medewerkers, die worden bezoldigd onder het BBRA 1984, registreert het systeem de actuele salarisschaal in een medewerkerRecht."

PURPOSE RULE "Salarisschaal verplicht in medewerkerRecht"
{+ Elke medewerker, die onder de BBRA bezoldigd wordt, is ingedeeld in een salarisschaal die hoort bij zijn of haar functie.
Dus heeft elke medewerker een salarisschaal.
Om die reden wordt de salarisschaal als verplicht veld aan het medewerkerRecht toegevoegd.
De salarisschaal van een medewerker op enig moment $t$, kan dan ook altijd gevonden worden
door het medewerkerRecht te nemen van diezelfde medewerker waarbij $t$ binnen de periode valt.
-}
RULE "Salarisschaal verplicht in medewerkerRecht" : I[MedewerkerRecht] |- salarisschaal;salarisschaal~
MEANING "Elk medewerkerRecht dient voorzien te zijn van de salarisschaal die geldt voor de betreffende medewerker in de geldigheidsperiode van dit medewerkerRecht."
MESSAGE "Elk medewerkerRecht dient voorzien te zijn van een salarisschaal."
VIOLATION (TXT "Het medewerkerRecht voor ", SRC medewerkerRecht~;personeelsnummer, TXT " van ", SRC begindatum, TXT " tot ", SRC einddatum, TXT " moet nog voorzien worden van een salarisschaal.")

PURPOSE RELATION salarisnummer[MedewerkerRecht*Salarisnummer] REF "BBRA Artikel 2 sub d"
{+Een salarisnummer is een aanduiding, bestaande uit een getal, dat in een salarisschaal voor een salaris is vermeld.
Samen met de salarisschaal bepaalt het salarisnummer het precieze bruto salaris.
Om het salaris te kunnen bepalen, moeten salarisschaal en salarisnummer dus beide bekend zijn.
-}
RELATION salarisnummer[MedewerkerRecht*Salarisnummer] [UNI] PRAGMA "" " heeft salarisnummer " " "  
MEANING "Van medewerkers, die worden bezoldigd onder het BBRA 1984, registreert het systeem de actuele salarisnummer in een medewerkerRecht.."

PURPOSE RULE "Salarisnummer verplicht in medewerkerRecht"
{+Om dezelfde reden als de salarisschaal, is dus ook het salarisnummer een verplicht veld in een medewerkerRecht.
-}
RULE "Salarisnummer verplicht in medewerkerRecht" : I[MedewerkerRecht] |- salarisnummer;salarisnummer~
MEANING "Elk medewerkerRecht dient voorzien te zijn van de salarisnummer die geldt voor de betreffende medewerker in de geldigheidsperiode van dit medewerkerRecht."
MESSAGE "Elk medewerkerRecht dient voorzien te zijn van een salarisnummer."
VIOLATION (TXT "Het medewerkerRecht voor ", SRC medewerkerRecht~;personeelsnummer, TXT " van ", SRC begindatum, TXT " tot ", SRC einddatum, TXT " moet nog voorzien worden van een salarisnummer.")

PURPOSE RELATION toelagerecht[MedewerkerRecht*ToelageRecht] REF "BBRA Artikel 13-20"
{+De rechten van een medewerker op vaste toelagen moeten in DTV worden geadministreerd.
Deze rechten worden ondergebracht in een zogenaamd medewerkerRecht.
-}
RELATION toelagerecht[MedewerkerRecht*ToelageRecht] PRAGMA "" " geeft recht op "
MEANING "De toelage(s) waar een medewerker recht op heeft worden in DTV gekoppeld aan het medewerkerRecht van de medewerker."

PURPOSE RELATION grond[ToelageRecht*Wetsartikel]
{+De rechtsgrond van elke toelage dient in het systeem bekend te zijn,
zodat een auditor de rechtmatigheid kan traceren vanuit een juridische werkelijkheid.
-}
RELATION grond[ToelageRecht*Wetsartikel] [UNI] PRAGMA "" " heeft als rechtsgrond "
MEANING "Van elk toelage wordt geregistreerd welk wetsartikel als rechtsgrond dient."

RELATION categorie[MedewerkerRecht*Categorie] [UNI,TOT]
MEANING "Een medewerkerRecht bevat een categorie (1,2 of 3) waar een medewerker toe behoort."
PURPOSE RELATION categorie[MedewerkerRecht*Categorie] REF "WTR 2012"
{+In een medewerkerRecht is aangegeven tot welke categorie een medewerker hoort.
Op basis van deze categorie wordt o.a. bepaald of voor deze medewerker een werkperiode van 1 dag of van 28 dagen geldt, alsook voor welke toeslagen
de medewerker in aamkerking kan komen.
-}

{-
RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage TOD."
PURPOSE RELATION vasteToelageTOD[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 17 lid 4 en 7 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het werken op onregelmatige tijden.
Dit houdt in dat geen variabele toelage op basis van de werktijden aan deze medewerker wordt toegekend.
Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen recht heeft op een vaste toelage voor Bereik en beschikbaarheidsdiensten."
PURPOSE RELATION vasteToelageBB[MedewerkerRecht*MedewerkerRecht]
{+Volgens BBRA artikel 18a lid 4 kan een medewerker in bepaalde situaties in aanmerking komen voor een vaste toelage voor het zich regelmatig of vrij regelmatig bereikbaar en beschikbaar moet houden. Dit houdt in dat hiervoor geen variabele toelage aan deze 
medewerker wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerker geregistreerd.
-}

{-
RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht] [PROP]
MEANING "In een medewerkerRecht is aangegeven of een medewerker wel of geen aanspraak maakt op de afbouwregeling."
PURPOSE RELATION afbouwregeling[MedewerkerRecht*MedewerkerRecht]
{+In het "Onderhandelaarsakkoord arbeidsvoorwaarden nVWA 28062011" hoofdtstuk B is opgenomen dat een bepaalde groep medewerkers op jaarbasis een vaste inconveniëntentoelage krijgt, die bestaat uit: de toelage voor onregelmatige dienst, overwerk,
bereikbaarheids/beschikbaarheidsdienst. Dit houdt in dat hiervoor geen variabele toelage aan deze medewerkers wordt toegekend. Dit wordt daarom in het medewerkerRecht van de medewerkers geregistreerd.
-}

RELATION medewerkerRecht[Persoon*MedewerkerRecht] [INJ,SUR,TOT]
MEANING "De rechten op vaste toelagen worden per per persoon geregistreerd."
PURPOSE RELATION medewerkerRecht[Persoon*MedewerkerRecht]
{+Om vast te leggen welke rechten in een bepaalde verantwoordingsperiode van toepassing zijn voor een bepaalde medewerker, wordt voor elke medewerker een medewerkerRecht geregistreerd.
Omdat wijzigingen in de rechten op vaste toelages worden bewaard, ontstaan meerdere werktijdenpakketten voor dezelfde persoon.
Werktijdenpakketten van één persoon mogen niet in de tijd overlappen, en tezamen moeten zij het volledige dienstverband (voor zover in de administratie aanwezig) overdekken.
-}

ENDPATTERN

INTERFACE "Rechten per Medewerker"
          ( medewerkerRecht
          , personeelsnummer
          , salarisschaal
          , salarisnummer
          , begindatum
          , einddatum
          , categorie[MedewerkerRecht*Categorie]
          , toelagerecht
          ) FOR HR: I[MedewerkerRecht]
BOX [ "voor medewerker " : medewerkerRecht~
      BOX [ "personeelsnummer " : personeelsnummer ]
    , salarisschaal : salarisschaal
    , salarisnummer : salarisnummer
    , "geldig vanaf : " : begindatum
    , "geldig tot : " : einddatum
    , "categorie : " : categorie
    , "vaste toelagen : " : toelagerecht
    ]

INTERFACE "Beheer rechten van medewerkers"
          FOR HR:V[ONE*Persoon] 
BOX [ "medewerker : " : personeelsnummer
    , "" : I[Persoon]
      BOX [ "" : medewerkerRecht
            BOX [ "geldig vanaf : " : begindatum
                , "Categorie : " : categorie
                , "Vaste toelagen : " : toelagerecht
                ]
          ]
    ]

POPULATION begindatum[Periode*Datum] CONTAINS
  [ ("mw1", "01-01-2014")
  ; ("mw2", "01-01-2014")
  ; ("mw3", "01-01-2014")
  ]
POPULATION salarisschaal[MedewerkerRecht*Salarisschaal] CONTAINS
  [ ("mw1", "9")
  ; ("mw2", "5")
  ; ("mw3", "13")
  ]
POPULATION salarisnummer[MedewerkerRecht*Salarisnummer] CONTAINS
  [ ("mw1", "9")
  ; ("mw2", "2")
  ; ("mw3", "10")
  ]
POPULATION categorie[MedewerkerRecht*Categorie] CONTAINS
  [ ("mw1", "1")
  ; ("mw2", "2")
  ; ("mw3", "3")
  ]
POPULATION medewerkerRecht[Persoon*MedewerkerRecht] CONTAINS
  [ ("p1", "mw1")
  ; ("p2", "mw2")
  ; ("p3", "mw3")
  ]

ENDCONTEXT