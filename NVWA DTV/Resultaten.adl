CONTEXT DTV IN DUTCH LATEX

PATTERN Resultaten
PURPOSE PATTERN Resultaten
{+SJ: Hierin heb ik alles rond het concept ``ToelageResultaat'' geparkeerd.
-}

CONCEPT ToelageResultaat "Onder toelageresultaat verstaan we een antwoord van de kennismotor."
PURPOSE CONCEPT ToelageResultaat
{+Elk toelagerecord in een P-Direkt bestand komt overeen met één toelageresultaat van de kennismotor.
-}

{- Ferlon, deze relatie vind ik niet een goed idee, omdat we resultaten over een verantwoordingsperiode niet anders willen behandelen dan resultaten over een dag.
   Een dag is immers ook een werkperiode (art 23, BBRA) ...
RELATION dagverantwoording[ToelageResultaat*Dagverantwoording] [UNI]
MEANING "Het resultaat van een berekening kan een dagverantwoording betreffen. Dat wordt in het systeem geregistreerd."
PURPOSE RELATION dagverantwoording[ToelageResultaat*Dagverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren, moet de DTV-applicatie bijhouden bij welke dagverantwoording een toelageresultaat hoort.
-}

PURPOSE RELATION resultaatSoort
{+Om aan de DTV-applicatie te vertellen wat er met een toelageresultaat moet gebeuren,
voorziet de kennismotor elk resultaat van een resultaatSoort.
\begin{itemize}
\item   {\em P-Direktcode}: Dit resultaat moet naar P-Direkt worden verzonden.
\item   {\em urenpool}: Dit resultaat is een mutatie op een urentegoed.
\item   {\em Opslaan}: Dit resultaat wordt niet naar P-Direkt verzonden, maar wordt in DTV opgeslagen.
\item   {\em Fout}: Dit resultaat meldt een fout in de periodeverantwoording. Deze periodeverantwoording moet handmatig in P-Direkt worden verwerkt.
\end{itemize}
-}
RELATION resultaatSoort[ToelageResultaat*Resultaatsoort] [UNI,TOT]
MEANING "Elk toelageresultaat van een verwerkte periodeverantwoording heeft een P-Direktcode of urenpool (waar een aantal uren aan wordt toegekend) als onderdeel."
PURPOSE RELATION resultaatSoort[ToelageResultaat*Resultaatsoort]
{+Als een dagverantwoording is verwerkt, resulteert dit in het toekennen van een bepaald aantal uren aan een bepaalde P-Direktcode.
-}

RULE resultaatSoort : I[Resultaatsoort] = 'P-Direktcode' \/ 'urenpool' \/ 'Fout' \/ 'Opslaan'
MEANING "Elke toelageresultaat dient van de soort \"P-Direktcode\", \"urenpool\", \"Fout\" of \"Opslaan\" te zijn."
MESSAGE "De volgende resultaatsoort is niet \"P-Direktcode\", \"urenpool\", \"Fout\" of \"Opslaan\":"
VIOLATION (SRC I, TXT " is onbekend.")

PURPOSE RELATION code
{+Om aan P-Direkt te laten weten in welke uitbetalingsrubriek een aantal uren valt,
en welk uitbetalingspercentage daarvoor geldt,
gebruikt P-Direkt een eigen codering.
Deze codes worden door de kennismotor opgeleverd, zodat de DTV-applicatie deze percentages en rubrieken niet zelf hoeft bij te houden.
De kennismotor levert een P-Direktcode bij elk toelageresultaat dat voor P-Direkt bestemd is.
-}
RELATION code[ToelageResultaat*Code] [UNI]
MEANING "In een toelageresultaat kan een P-Direktcode worden geregistreerd."

RELATION resultaatWaarde[ToelageResultaat*Tijdsduur] [UNI]
MEANING "Het toelageresultaat van een verwerkte dagverantwoording heeft een aantal uren (die aan een bepaalde P-Direktcode of urenpool worden toegekend) als onderdeel."
PURPOSE RELATION resultaatWaarde[ToelageResultaat*Tijdsduur]
{+
-}

PURPOSE RELATION opmerking[ToelageResultaat*Melding]
{+Wanneer de kennismotor in een periodeverantwoording een fout ontdekt, waardoor hij niet aan P-Direkt kan worden aangeboden,
registreert de DTV-applicatie de foutmelding in het toelageresultaat.
-}
RELATION opmerking[ToelageResultaat*Melding] [UNI]
MEANING "In een toelageresultaat kan een foutmelding van de kennismotor worden geregistreerd."

RELATION periodeResultaat[ToelageResultaat*Periodeverantwoording] [UNI,TOT]
MEANING "Elk toelageresultaat hoort bij één periodeverantwoording."
PURPOSE RELATION periodeResultaat[ToelageResultaat*Periodeverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een toelageresultaat hoort.
Daarom bewaart de DTV-applicatie alle toelageresultaten van iedere periodeverantwoording.
Omdat elk toelageresultaat maar één keer uitbetaald mag worden, behoort ieder ToelageResultaat bij precies één Periodeverantwoording.
-}

ENDPATTERN

PURPOSE INTERFACE "Berekenen resultaten"
{+Dit bericht gaat van de kennismotor naar de DTV-applicatie, en bevat een aantal toelageresultaten die bij één periodeverantwoording horen.
Omdat de kennismotor zelf toestandsloos werkt, bevat dit bericht alleen toelageresultaten over gegevens, die in de vraag aanwezig zijn.

De kennismotor dient te zorgen dat de resultaatSoort voor elk resultaat is ingevuld
en dat elk opgeleverd toelageresultaat is te herleiden tot de periodeverantwoording waar dat resultaat van is afgeleid.
Ook zorgt de kennismotor dat:
\begin{itemize}
\item	elk toelageresultaat van de soort `Fout' is voorzien van een passende foutmelding in het veld ``opmerking'';
\item	elk toelageresultaat van de soort `P-Direktcode' is voorzien van een geldige P-Direktcode in het veld ``code''
	en van het juiste aantal uren in het veld ``resultaatWaarde'';
\item	elk toelageresultaat van de soort `urenpool' is voorzien van een geldige urenpool in het veld ``code''
	en van het juiste aantal toe te voegen uren in het veld ``resultaatWaarde'';
\end{itemize}
Deze eigenschappen hoeven in productie niet door de DTV-applicatie te worden getoetst,
als voldoende zekerheid bestaat dat de kennismotor deze eigenschappen garandeert.
Alle toelageresultaten worden in de DTV-database opgeslagen en bewaard.
-}
INTERFACE "Berekenen resultaten" CLASS Message
    ( code
    , resultaatSoort
    , resultaatWaarde
    , periodeResultaat
    ) FOR KODE : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : medewerker
    , resultaten : periodeResultaat~
      BOX [ code :      code
          , soort :     resultaatSoort
          , waarde :    resultaatWaarde
          , opmerking : opmerking
          ]
    ]

PURPOSE INTERFACE PeriodeverantwoordingKODE
{+Dit bericht gaat van de DTV-applicatie naar de kennismotor, en bevat één periodeverantwoording.
De bedoeling is dat de kennismotor hierop antwoordt met een bericht ``berekenen resultaten''.

Dit bericht bevat:
\begin{itemize}
\item	de verantwoordingsperiode;
\item	het personeelsnummer van de medewerker;
\item	alle rechten van de medewerker, waarvan de verantwoordingsperiode en de geldigheidsperiode overlap vertonen;
	Hiervan worden begin- en einddatum en het recht in het bericht opgenomen.
\item	Alle actuele urentegoeden van de betreffende medewerker.
	Van elk tegoed wordt de urenpool, het tegoed (aantal uren) en de datum vanaf welke dat tegoed geldig is in het bericht opgenomen.
\item	Alle dagverantwoordingen, die in de periodeverantwoording voorkomen.
\end{itemize}
-}
INTERFACE PeriodeverantwoordingKODE CLASS Message
    FOR DTV : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : medewerker
    , MedewerkerRechten :  medewerker;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ begindatum : begindatum
          , einddatum :  einddatum
          , rechten :    recht
          ]
    , Urentegoeden : medewerker;medewerker
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~
      BOX [ datum :                     datum
          , "uren volgens arbeidsmodaliteit" : arbeidsModaliteit
          , verroosterd :               dag[Dienst*Dagverantwoording]~ 
            BOX [ roostersoort : roosterSoort
                , begintijd :    begintijd[Dienst*DATUMTIJD]
                , eindtijd :     eindtijd[Dienst*DATUMTIJD]
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ begintijd :   begintijd[Tijdvak*DATUMTIJD]
                , eindtijd :    eindtijd[Tijdvak*DATUMTIJD]
                , tijdrubriek : tijdrubriek
                ]
          ]
    ]

PURPOSE INTERFACE "Berekenen Periodeverantwoording"
{+Deze interface is een combinatie van de berichten "Berekenen resultaten" en PeriodeverantwoordingKODE,
bedoeld om de kennismotor te simuleren met behulp van een gebruiker.
Deze interface wordt dus niet in de productieversie van de DTV-applicatie gerealiseerd.
-}
INTERFACE "Berekenen Periodeverantwoording" CLASS IF
    ( code
    , resultaatSoort
    , resultaatWaarde
    , periodeResultaat
    ) FOR KODE : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : medewerker
    , MedewerkerRechten : medewerker;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ personeelsnummer : persoon
          , rechten : recht
          ]
    , Urentegoeden : medewerker;medewerker
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~  -- Dagverantwoording
      BOX [ datum :                     datum
          , "uren volgens arbeidsmodaliteit" : arbeidsModaliteit
          , verroosterd :               dag[Dienst*Dagverantwoording]~
            BOX [ "" :     I[Dienst]
                , begin :  begintijd[Dienst*DATUMTIJD]
                , eind :   eindtijd[Dienst*DATUMTIJD]
                , dienst : roosterSoort
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ ""  :         I[Tijdverantwoording]
                , begin :       begintijd[Tijdvak*DATUMTIJD]
                , eind :        eindtijd[Tijdvak*DATUMTIJD]
                , tijdrubriek : tijdrubriek
                ]
          ]
    , resultaten : periodeResultaat~
      BOX [ code :      code
          , soort :     resultaatSoort
          , waarde :    resultaatWaarde
          , opmerking : opmerking
          ]
    ]

POPULATION resultaatSoort[ToelageResultaat*Resultaatsoort] CONTAINS
  [ ("toel1", "Fout")
  ; ("toel2", "Fout")
  ; ("toel3", "urenpool")
  ; ("res2", "P-Direktcode")
  ; ("res3", "P-Direktcode")
  ; ("res5", "P-Direktcode")
  ; ("res6", "P-Direktcode")
  ; ("res7", "P-Direktcode")
  ; ("res8", "P-Direktcode")
  ; ("res9", "P-Direktcode")
  ; ("res10", "P-Direktcode")
  ; ("res11", "P-Direktcode")
  ]

POPULATION resultaatWaarde[ToelageResultaat*Tijdsduur] CONTAINS
  [ ("res2", "5.25")
  ; ("res3", "1")
  ; ("res5", "1")
  ; ("res6", "2")
  ; ("res7", "0.5")
  ; ("res8", "2")
  ; ("res9", "1")
  ; ("res10", "2,5")
  ; ("res11", "0.5")
  ]

POPULATION periodeResultaat[ToelageResultaat*Periodeverantwoording] CONTAINS
  [ ("toel1", "pv13")
  ; ("toel2", "pv14")
  ; ("toel3", "pv15")
  ; ("res2", "pv1201411")
  ; ("res3", "pv2201411")
  ; ("res5", "pv3201411")
  ; ("res6", "pv1201412")
  ; ("res7", "pv2201412")
  ; ("res8", "pv3201412")
  ; ("res9", "pv1201413")
  ; ("res10", "pv2201413")
  ; ("res11", "pv3201413")
  ]

POPULATION code[ToelageResultaat*Code] CONTAINS
  [ ("toel1", "8401")
  ; ("toel2", "8411")
  ; ("toel3", "8421")
  ; ("res2", "8401")
  ; ("res3", "8411")
  ; ("res5", "8361")
  ; ("res6", "8341")
  ; ("res7", "8401")
  ; ("res8", "8341")
  ; ("res9", "8331")
  ; ("res10", "8341")
  ; ("res11", "8361")
  ]

POPULATION resultaatWaarde[ToelageResultaat*Tijdsduur] CONTAINS
  [ ("toel1", "14")
  ; ("toel2", "0,5")
  ; ("toel3", "2")
  ]

ENDCONTEXT
