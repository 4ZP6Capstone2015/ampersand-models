CONTEXT DTV IN DUTCH LATEX

PATTERN Resultaten
PURPOSE PATTERN Resultaten
{+SJ: Hierin heb ik alles rond het concept ``ToelageResultaat'' geparkeerd.
-}

CONCEPT ToelageResultaat "Onder resultaat verstaan we een antwoord van de kennismotor."
PURPOSE CONCEPT ToelageResultaat
{+Elk toelagerecord in een P-Direkt bestand komt overeen met één resultaat van de kennismotor.
-}

{- Ferlon, deze relatie vind ik niet een goed idee, omdat we resultaten over een periode niet anders willen behandelen dan resultaten over een dag.
   Een dag is immers ook een periode...
RELATION dagverantwoording[ToelageResultaat*Dagverantwoording] [UNI]
MEANING "Het resultaat van een berekening kan een dagverantwoording betreffen. Dat wordt in het systeem geregistreerd."
PURPOSE RELATION dagverantwoording[ToelageResultaat*Dagverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren, moet de DTV-applicatie bijhouden bij welke dagverantwoording een resultaat hoort.
-}

PURPOSE RELATION code
{+Om aan P-Direkt te laten weten in welke uitbetalingsrubriek een aantal uren valt,
en welk uitbetalingspercentage daarvoor geldt,
gebruikt P-Direkt een eigen codering.
Deze codes worden door de kennismotor opgeleverd, zodat de DTV-applicatie deze percentages en rubrieken niet zelf hoeft bij te houden.
De applicatie registreert alleen de P-Direktcode bij elk toelageresultaat.
-}
RELATION code[ToelageResultaat*PDirektCode] [UNI,TOT]
MEANING "In elk resultaat wordt een P-Direktcode geregistreerd."

RELATION resultaatSoort[ToelageResultaat*Resultaatsoort] [UNI]
MEANING "Het resultaat van een verwerkte periode heeft een P-Direkt code of urenpool (waar een aantal uren aan wordt toegekend) als onderdeel."
PURPOSE RELATION resultaatSoort[ToelageResultaat*Resultaatsoort]
{+Als een dagverantwoording is verwerkt, resulteert dit in het toekennen van een bepaald aantal uren aan een bepaalde P-Direkt code.
-}

RULE resultaatSoort : I[Resultaatsoort] = 'P-Direkt code'\/'urenpool'
MEANING "Elke resultaat dient van de soort \"P-Direkt code\" of \"urenpool\" te zijn."
MESSAGE "Het volgende resultaatsoort is niet \"P-Direkt code\" of \"urenpool\":"
VIOLATION (SRC I, TXT " is onbekend.")

RELATION resultaatWaarde[ToelageResultaat*AantalUren] [UNI]
MEANING "Het resultaat van een verwerkte dagverantwoording heeft een aantal uren (die aan een bepaalde P-Direkt code of urenpool worden toegekend) als onderdeel."
PURPOSE RELATION resultaatWaarde[ToelageResultaat*AantalUren]
{+
-}

RELATION periodeResultaat[ToelageResultaat*Periodeverantwoording] [UNI]
MEANING "Het resultaat van een berekening kan een periode betreffen. Dat wordt in het systeem geregistreerd."
PURPOSE RELATION periodeResultaat[ToelageResultaat*Periodeverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een resultaat hoort.
Omdat elk toelageresultaat maar één keer uitbetaald mag worden, behoort ieder ToelageResultaat bij precies één Periodeverantwoording.
-}

ENDPATTERN

INTERFACE "Overzicht resultaten" : V[ONE*ToelageResultaat]
BOX [ "" : I
      BOX [ code : code
          , soort : resultaatSoort
          , waarde : resultaatWaarde
          , resultaat : periodeResultaat
          , verzonden : inBestand
          ]
    ]

INTERFACE "Berekenen resultaten"
    ( code
    , resultaatSoort
    , resultaatWaarde
    , periodeResultaat
    , inBestand
    ) FOR KODE : I[ONE]
BOX [ periodes : V[ONE*Periodeverantwoording]
      BOX [ periode : verantwoordingsperiode
          , medewerker : medewerkerPer
          ]
    ]

INTERFACE "Berekenen Periodeverantwoording"
    ( code
    , resultaatSoort
    , resultaatWaarde
    , periodeResultaat
    ) FOR KODE : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : medewerkerPer
    , MedewerkerRecht : medewerkerPer;persoon~/\verantwoordingsperiode;binnen~
      BOX [ personeelsnummer : persoon;personeelsnummer
          , rechten : recht
          ]
    , Urentegoeden : medewerkerPer;medewerker
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~ INTERFACE DagverantwoordingKODE
    , resultaten : periodeResultaat~
      BOX [ code :      code
          , soort :     resultaatSoort
          , waarde :    resultaatWaarde
          ]
    ]

INTERFACE DagverantwoordingKODE
      FOR KODE : I[Dagverantwoording]
BOX [ datum :                     datum
    , "uren volgens modaliteit" : dagModaliteit
    , verroosterd :               dag[Dienst*Dagverantwoording]~
      BOX [ "" :     I[Dienst]
          , begin :  begin[Periode*DatumTijd]
          , eind :   eind[Periode*DatumTijd]
          , dienst : roosterSoort
          ]
    , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
      BOX [ ""  :       I[Tijdverantwoording]
          , begin :     begin[Periode*DatumTijd]
          , eind :      eind[Periode*DatumTijd]
          , tijdrubriek : tijdrubriek
          ]
    ]

POPULATION resultaatSoort[ToelageResultaat*Resultaatsoort] CONTAINS
  [ ("res2", "P-Direkt code")
  ; ("res3", "P-Direkt code")
  ; ("res5", "P-Direkt code")
  ; ("res6", "P-Direkt code")
  ; ("res7", "P-Direkt code")
  ; ("res8", "P-Direkt code")
  ; ("res9", "P-Direkt code")
  ; ("res10", "P-Direkt code")
  ; ("res11", "P-Direkt code")
  ]

POPULATION resultaatWaarde[ToelageResultaat*AantalUren] CONTAINS
  [ ("res2", "5.25")
  ; ("res3", "1")
  ; ("res5", "1")
  ; ("res6", "2")
  ; ("res7", "0.5")
  ; ("res8", "2")
  ; ("res9", "1")
  ; ("res10", "2,5")
  ; ("res11", "0.5")
  ]

POPULATION periodeResultaat[ToelageResultaat*Periodeverantwoording] CONTAINS
  [ ("toel1", "pv13")
  ; ("toel2", "pv14")
  ; ("toel3", "pv15")
  ]

POPULATION code[ToelageResultaat*PDirektCode] CONTAINS
  [ ("toel1", "8401")
  ; ("toel2", "8411")
  ; ("toel3", "8421")
  ; ("res2", "8401")
  ; ("res3", "8411")
  ; ("res5", "8361")
  ; ("res6", "8341")
  ; ("res7", "8401")
  ; ("res8", "8341")
  ; ("res9", "8331")
  ; ("res10", "8341")
  ; ("res11", "8361")
  ]

POPULATION resultaatWaarde[ToelageResultaat*AantalUren] CONTAINS
  [ ("toel1", "14")
  ; ("toel2", "0,5")
  ; ("toel3", "2")
  ]

ENDCONTEXT
