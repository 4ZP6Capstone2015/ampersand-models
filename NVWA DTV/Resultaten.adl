CONTEXT DTV IN DUTCH LATEX
INCLUDE "Periodeverantwoordingen.adl"
INCLUDE "Urenpools.adl"
INCLUDE "MedewerkerRechten.adl"

PATTERN Resultaten
PURPOSE PATTERN Resultaten
{+SJ: Dit pattern beschrijft hoe de output van de kennismotor aan de input (periodeverantwoording) wordt gekoppeld.
-}
CONCEPT Perioderesultaat "Een perioderesultaat is het resultaat van het verwerken van een periodeverantwoording door de kennismotor."

RELATION perioderesultaat[ToelageResultaat*Perioderesultaat] [UNI,TOT]
RELATION res[Perioderesultaat*Periodeverantwoording] [UNI,TOT,INJ]
MEANING "Elk perioderesultaat hoort bij één periodeverantwoording."
RELATION res[Dagresultaat*Dagverantwoording] [UNI,TOT,INJ]
RELATION periode[Dagresultaat*Perioderesultaat] [UNI,TOT]
RELATION res[Tijdresultaat*Tijdverantwoording] [UNI,TOT,INJ]
RELATION dag[Tijdresultaat*Dagresultaat] [UNI,TOT]
RULE res;dag = dag;res
RULE periode;res = res;periodeverantwoording
ENDPATTERN

PATTERN Toelageresultaten
PURPOSE PATTERN Resultaten
{+SJ: Hierin heb ik alles rond het concept ``ToelageResultaat'' geparkeerd.
-}

CONCEPT ToelageResultaat "Een toelageresultaat is het antwoord van de kennismotor op de vraag hoeveel uren in een gegeven periodeverantwoording op één code als toelage wordt geboekt dan wel hoeveel uren op één urenpool wordt gemuteerd."
PURPOSE CONCEPT ToelageResultaat
{+Elk toelagerecord in een P-Direkt bestand komt overeen met één toelageresultaat van de kennismotor.
-}

{- Ferlon, deze relatie vind ik niet een goed idee, omdat we resultaten over een verantwoordingsperiode niet anders willen behandelen dan resultaten over een dag.
   Een dag is immers ook een werkperiode (art 23, BBRA) ...
RELATION dagverantwoording[ToelageResultaat*Dagverantwoording] [UNI]
MEANING "Het resultaat van een berekening kan een dagverantwoording betreffen. Dat wordt in het systeem geregistreerd."
PURPOSE RELATION dagverantwoording[ToelageResultaat*Dagverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren, moet de DTV-applicatie bijhouden bij welke dagverantwoording een toelageresultaat hoort.
-}

PURPOSE RELATION resultaatSoort LATEX
{+Om aan de DTV-applicatie te vertellen wat er met een toelageresultaat moet gebeuren,
voorziet de kennismotor elk resultaat van een resultaatSoort.
\begin{itemize}
\item   {\em p-direkt}: Dit resultaat moet naar P-Direkt worden verzonden.
\item   {\em uurpool}: Dit resultaat is een mutatie op een urentegoed.
\item   {\em opslaan}: Dit resultaat wordt niet naar P-Direkt verzonden, maar wordt in DTV opgeslagen.
\end{itemize}
Als een dagverantwoording is verwerkt, resulteert dit in het toekennen van een bepaald aantal uren aan een bepaalde P-Direktcode.
-}
RELATION resultaatSoort[ToelageResultaat*Resultaatsoort] [UNI,TOT]
MEANING "Elk toelageresultaat van een verwerkte periodeverantwoording heeft een P-Direktcode of urenpool (waar een aantal uren aan wordt toegekend) als onderdeel."

CONCEPT Resultaatsoort "De aanduiding van het type resultaat dat door de kennismotor wordt aangeleverd. Dit is één van 'p-direkt', 'uurpool', 'opslaan'."
RULE resultaatSoort : I[Resultaatsoort] = 'p-direkt' \/ 'uurpool' \/ 'opslaan'
MEANING "Elke toelageresultaat dient van de soort \"p-direkt\", \"uurpool\" of \"opslaan\" te zijn."
MESSAGE "De volgende resultaatsoort is niet \"p-direkt\", \"uurpool\" of \"opslaan\":"
VIOLATION (SRC I, TXT " is onbekend.")
PURPOSE RULE resultaatSoort
{+Omdat een resultaatsoort aangeeft wat de DTV-applicatie doet met een toelageresultaat, moet duidelijk zijn welke resultaatsoorten er zijn. Wanneer de DTV-applicatie in de toekomst meer functionaliteit biedt, dient het aantal resultaatsoorten navenant uitgebreid te worden.
-}
CONCEPT Code "De code, die P-Direkt voorschrijft om in het P-Direktbestand aan te geven om welke toelagesoort en –hoogte het gaat."
PURPOSE RELATION code
{+Om aan P-Direkt te laten weten in welke uitbetalingsrubriek een aantal uren valt,
en welk uitbetalingspercentage daarvoor geldt,
gebruikt P-Direkt een eigen codering.
Deze codes worden door de kennismotor opgeleverd, zodat de DTV-applicatie deze percentages en rubrieken niet zelf hoeft bij te houden.
De kennismotor levert een P-Direktcode bij elk toelageresultaat dat voor P-Direkt bestemd is.
-}
RELATION code[ToelageResultaat*Code] [UNI]
MEANING "In een toelageresultaat kan een P-Direktcode worden geregistreerd."

RELATION resultaatWaarde[ToelageResultaat*Tijdsduur] [UNI]
MEANING "Het toelageresultaat van een verwerkte dagverantwoording heeft een aantal uren (die aan een bepaalde P-Direktcode of urenpool worden toegekend) als onderdeel."
PURPOSE RELATION resultaatWaarde[ToelageResultaat*Tijdsduur]
{+
-}

PURPOSE RELATION opmerking[ToelageResultaat*Melding]
{+Wanneer de kennismotor in een periodeverantwoording een fout ontdekt, waardoor hij niet aan P-Direkt kan worden aangeboden,
registreert de DTV-applicatie de foutmelding in het toelageresultaat.
-}
RELATION opmerking[ToelageResultaat*Melding] [UNI]
MEANING "In een toelageresultaat kan een foutmelding van de kennismotor worden geregistreerd."
CONCEPT Melding "Een (fout-) melding van de kennismotor."

PURPOSE RELATION perioderesultaat[ToelageResultaat*Perioderesultaat]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een toelageresultaat hoort.
Daarom bewaart de DTV-applicatie alle toelageresultaten van ieder perioderesultaat.
Omdat elk toelageresultaat maar één keer uitbetaald mag worden, behoort ieder ToelageResultaat bij precies één Perioderesultaat en behoort ieder Perioderesultaat bij precies één verantwoording.
-}
PURPOSE RELATION res[Perioderesultaat*Periodeverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een perioderesultaat hoort.
Daarom bewaart de DTV-applicatie alle perioderesultaten van iedere periodeverantwoording.
Omdat elk toelageresultaat maar één keer uitbetaald mag worden, behoort ieder ToelageResultaat bij precies één Perioderesultaat en behoort ieder Perioderesultaat bij precies één verantwoording.
-}
ENDPATTERN

PURPOSE INTERFACE "Berekenen resultaten"
{+Dit bericht gaat van de kennismotor naar de DTV-applicatie, en bevat een aantal toelageresultaten die bij één periodeverantwoording horen.
Omdat de kennismotor zelf toestandsloos werkt, bevat dit bericht alleen toelageresultaten over gegevens, die in de vraag aanwezig zijn.

De kennismotor dient te zorgen dat de resultaatSoort voor elk resultaat is ingevuld
en dat elk opgeleverd toelageresultaat is te herleiden tot de periodeverantwoording waar dat resultaat van is afgeleid.
Ook zorgt de kennismotor dat:
\begin{itemize}
\item	elk toelageresultaat van de soort `Fout' is voorzien van een passende foutmelding in het veld ``opmerking'';
\item	elk toelageresultaat van de soort `P-Direktcode' is voorzien van een geldige P-Direktcode in het veld ``code''
	en van het juiste aantal uren in het veld ``resultaatWaarde'';
\item	elk toelageresultaat van de soort `urenpool' is voorzien van een geldige urenpool in het veld ``code''
	en van het juiste aantal toe te voegen uren in het veld ``resultaatWaarde'';
\end{itemize}
Deze eigenschappen hoeven in productie niet door de DTV-applicatie te worden getoetst,
als voldoende zekerheid bestaat dat de kennismotor deze eigenschappen garandeert.
Alle toelageresultaten worden in de DTV-database opgeslagen en bewaard.
-}
INTERFACE "Berekenen resultaten" CLASS Message
    ( code
    , resultaatSoort
    , resultaatWaarde
    , perioderesultaat, res[Perioderesultaat*Periodeverantwoording]
    ) FOR KODE : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : afkomstigVan
    , resultaten : res~;perioderesultaat~
      BOX [ code :      code
          , soort :     resultaatSoort
          , waarde :    resultaatWaarde
          , opmerking : opmerking
          ]
    ]

PURPOSE INTERFACE PeriodeverantwoordingKODE
{+Dit bericht gaat van de DTV-applicatie naar de kennismotor, en bevat één periodeverantwoording.
De bedoeling is dat de kennismotor hierop antwoordt met een bericht ``berekenen resultaten''.

Dit bericht bevat:
\begin{itemize}
\item	de verantwoordingsperiode;
\item	het personeelsnummer van de medewerker (= de afzender van het bericht);
\item	alle rechten van de medewerker, waarvan de verantwoordingsperiode en de geldigheidsperiode overlap vertonen;
	Hiervan worden begin- en einddatum en het recht in het bericht opgenomen.
\item	Alle actuele urentegoeden van de betreffende medewerker.
	Van elk tegoed wordt de urenpool, het tegoed (aantal uren) en de datum vanaf welke dat tegoed geldig is in het bericht opgenomen.
\item	Alle dagverantwoordingen, die in de periodeverantwoording voorkomen.
\end{itemize}
-}
INTERFACE PeriodeverantwoordingKODE CLASS Message
    FOR DTV : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : afkomstigVan
    , MedewerkerRechten :  afkomstigVan;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ begindatum : begindatum
          , einddatum :  einddatum
          , rechten :    recht
          ]
    , Urentegoeden : afkomstigVan;medewerker
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~
      BOX [ datum :                     datum
          , verroosterd :               dag[Dienst*Dagverantwoording]~ 
            BOX [ roostersoort : roosterSoort
                , begintijd :    begin
                , eindtijd :     eind
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ begintijd :   begin
                , eindtijd :    eind
                , tijdrubriek : tijdrubriek
                ]
          ]
    ]

PURPOSE INTERFACE "Berekenen Periodeverantwoording"
{+Deze interface is een combinatie van de berichten "Berekenen resultaten" en PeriodeverantwoordingKODE,
bedoeld om de kennismotor te simuleren met behulp van een gebruiker.
Deze interface wordt dus niet in de productieversie van de DTV-applicatie gerealiseerd.
-}
INTERFACE "Berekenen Periodeverantwoording" CLASS IF
    ( code
    , resultaatSoort
    , resultaatWaarde
    , perioderesultaat, res[Perioderesultaat*Periodeverantwoording]
    ) FOR KODE : I[Periodeverantwoording]
BOX [ periode : verantwoordingsperiode
    , medewerker : afkomstigVan
    , MedewerkerRechten : afkomstigVan;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ personeelsnummer : persoon
          , rechten : recht
          ]
    , Urentegoeden : afkomstigVan;medewerker[Personeelsnummer*Urentegoed]
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~  -- Dagverantwoording
      BOX [ datum :                     datum
          , verroosterd :               dag[Dienst*Dagverantwoording]~
            BOX [ "" :     I[Dienst]
                , begin :  begin
                , eind :   eind
                , dienst : roosterSoort
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ ""  :         I[Tijdverantwoording]
                , begin :       begin
                , eind :        eind
                , tijdrubriek : tijdrubriek
                ]
          ]
    , resultaten : res~;perioderesultaat~
      BOX [ code :      code
          , soort :     resultaatSoort
          , waarde :    resultaatWaarde
          , opmerking : opmerking
          ]
    ]

ENDCONTEXT
