CONTEXT DTV IN DUTCH LATEX
INCLUDE "Periodeverantwoordingen.adl"
INCLUDE "Urenpools.adl"
INCLUDE "MedewerkerRechten.adl"

PATTERN Resultaten
PURPOSE PATTERN Resultaten
{+SJ: Dit pattern beschrijft hoe de output van de kennismotor aan de input (periodeverantwoording) wordt gekoppeld.
-}
PURPOSE CONCEPT Perioderesultaat
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een perioderesultaat hoort.
Daarom bewaart de DTV-applicatie alle perioderesultaten van iedere periodeverantwoording.
Omdat elk resultaat maar één keer uitbetaald mag worden, behoort ieder Resultaat bij precies één Perioderesultaat en behoort ieder Perioderesultaat bij precies één verantwoording.
-}
CONCEPT Perioderesultaat "Een perioderesultaat is het resultaat van het verwerken van een periodeverantwoording door de kennismotor."
CONCEPT TeCorrResultaat "Een perioderesultaat, waarvan de kennismotor heeft vastgesteld dat het niet verwerkt mag worden in P-Direkt."
CONCEPT OKresultaat "Een perioderesultaat, waarvan de kennismotor heeft vastgesteld dat het wel verwerkt mag worden in P-Direkt."
CLASSIFY OKresultaat ISA Perioderesultaat
CLASSIFY TeCorrResultaat ISA Perioderesultaat
CLASSIFY Perioderesultaat ISA Periodeverantwoording
CLASSIFY Dagresultaat ISA Dagverantwoording
CLASSIFY Tijdresultaat ISA Tijdverantwoording
RELATION goedresultaat[Resultaat*OKresultaat] [UNI,TOT]
MEANING "Elk perioderesultaat hoort bij één periodeverantwoording."
RELATION onderbouwing[Perioderesultaat*Onderbouwing] [UNI,TOT]
MEANING "Elk perioderesultaat wordt door de kennismotor onderbouwd met een verantwoording van alle berekeningen in Excel."

PURPOSE PATTERN Resultaten
{+SJ: Hierin heb ik alles rond het concept ``Resultaat'' geparkeerd.
-}

CONCEPT Resultaat "Een resultaat is het antwoord van de kennismotor op de vraag hoeveel uren in een gegeven periodeverantwoording op één code als toelage wordt geboekt dan wel hoeveel uren op één urenpool wordt gemuteerd."
PURPOSE CONCEPT Resultaat
{+Elk toelagerecord in een P-Direkt bestand komt overeen met één resultaat van de kennismotor.
-}

{- Ferlon, deze relatie vind ik niet een goed idee, omdat we resultaten over een verantwoordingsperiode niet anders willen behandelen dan resultaten over een dag.
   Een dag is immers ook een werkperiode (art 23, BBRA) ...
RELATION dagverantwoording[Resultaat*Dagverantwoording] [UNI]
MEANING "Het resultaat van een berekening kan een dagverantwoording betreffen. Dat wordt in het systeem geregistreerd."
PURPOSE RELATION dagverantwoording[Resultaat*Dagverantwoording]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren, moet de DTV-applicatie bijhouden bij welke dagverantwoording een resultaat hoort.
-}

PURPOSE RELATION resultaatSoort LATEX
{+Om aan de DTV-applicatie te vertellen wat er met een resultaat moet gebeuren,
voorziet de kennismotor elk resultaat van een resultaatSoort.
\begin{itemize}
\item   {\em p-direkt}: Dit resultaat moet naar P-Direkt worden verzonden.
\item   {\em uurpool}: Dit resultaat is een mutatie op een urentegoed.
\item   {\em opslaan}: Dit resultaat wordt niet naar P-Direkt verzonden, maar wordt in DTV opgeslagen.
\end{itemize}
Als een dagverantwoording is verwerkt, resulteert dit in het toekennen van een bepaald aantal uren aan een bepaalde P-Direktcode.
-}
RELATION resultaatSoort[Resultaat*Resultaatsoort] [UNI,TOT]
MEANING "Elk resultaat van een verwerkte periodeverantwoording heeft een P-Direktcode of urenpool (waar een aantal uren aan wordt toegekend) als onderdeel."

CONCEPT Resultaatsoort "De aanduiding van het type resultaat dat door de kennismotor wordt aangeleverd. Dit is één van 'p-direkt', 'uurpool', 'opslaan'."
RULE resultaatSoort : I[Resultaatsoort] = 'p-direkt' \/ 'uurpool' \/ 'opslaan'
MEANING "Elke resultaat dient van de soort \"p-direkt\", \"uurpool\" of \"opslaan\" te zijn."
MESSAGE "De volgende resultaatsoort is niet \"p-direkt\", \"uurpool\" of \"opslaan\":"
VIOLATION (SRC I, TXT " is onbekend.")
PURPOSE RULE resultaatSoort
{+Omdat een resultaatsoort aangeeft wat de DTV-applicatie doet met een resultaat, moet duidelijk zijn welke resultaatsoorten er zijn. Wanneer de DTV-applicatie in de toekomst meer functionaliteit biedt, dient het aantal resultaatsoorten navenant uitgebreid te worden.
-}
CONCEPT Code "De code, die P-Direkt voorschrijft om in het P-Direktbestand aan te geven om welke toelagesoort en –hoogte het gaat."
PURPOSE RELATION code
{+Om aan P-Direkt te laten weten in welke uitbetalingsrubriek een aantal uren valt,
en welk uitbetalingspercentage daarvoor geldt,
gebruikt P-Direkt een eigen codering.
Deze codes worden door de kennismotor opgeleverd, zodat de DTV-applicatie deze percentages en rubrieken niet zelf hoeft bij te houden.
De kennismotor levert een P-Direktcode bij elk resultaat dat voor P-Direkt bestemd is.
-}
RELATION code[Resultaat*Code] [UNI]
MEANING "In een resultaat kan een P-Direktcode worden geregistreerd."

RELATION resultaatWaarde[Resultaat*Tijdsduur] [UNI]
MEANING "Het resultaat van een verwerkte dagverantwoording heeft een aantal uren (die aan een bepaalde P-Direktcode of urenpool worden toegekend) als onderdeel."
PURPOSE RELATION resultaatWaarde[Resultaat*Tijdsduur]
{+
-}


PURPOSE RELATION feedback[Terugkoppeling*Perioderesultaat]
{+Wanneer de kennismotor in een periodeverantwoording een fout ontdekt of andere opmerkingen over het invullen heeft,
registreert de DTV-applicatie dit als een terugkoppeling.
Dit is een onderdeel van een perioderesultaat.
-}
RELATION feedback[Terugkoppeling*Perioderesultaat] [UNI,TOT]
RELATION feedbackCode[Terugkoppeling*TEKST] [UNI,TOT]
RELATION feedbackStatus[Terugkoppeling*TEKST] [UNI,TOT]
RELATION opmerking[Terugkoppeling*TEKST] [UNI,TOT]
MEANING "In een terugkoppeling staat een tekst waarin de kennismotor melding doet van een bevinding."
CONCEPT Melding "Een (fout-) melding van de kennismotor."

PURPOSE RELATION goedresultaat[Resultaat*OKresultaat]
{+Om resultaten van berekeningen door de kennismotor te kunnen administreren,
moet de DTV-applicatie bijhouden bij welke periodeverantwoording een resultaat hoort.
Daarom bewaart de DTV-applicatie alle resultaten van ieder perioderesultaat dat verwerkt is en verzonden naar P-Direkt.
Omdat elk resultaat maar één keer uitbetaald mag worden, behoort ieder Resultaat bij precies één OKresultaat en behoort ieder Perioderesultaat bij precies één Periodeverantwoording.
-}
ENDPATTERN

PURPOSE INTERFACE "Berekenen Periodeverantwoording"
{+Deze interface is een combinatie van de berichten "Berekenen resultaten" en PeriodeverantwoordingKODE,
bedoeld om de kennismotor te simuleren met behulp van een gebruiker.
Deze interface wordt dus niet in de productieversie van de DTV-applicatie gerealiseerd.
-}
INTERFACE "Berekenen Periodeverantwoording" CLASS IF
    ( code
    , resultaatSoort
    , resultaatWaarde
    , goedresultaat
    ) FOR KODE : I[Perioderesultaat]
BOX [ periode : verantwoordingsperiode
    , medewerker : personeelsnummer
    , MedewerkerRechten : personeelsnummer;persoon~/\periodeverantwoording~;datum;geldigOp~
      BOX [ personeelsnummer : persoon
          , rechten : recht
          ]
    , Urentegoeden : personeelsnummer;medewerker[Personeelsnummer*Urentegoed]
      BOX [ urenpool : urenpool
          , "saldo (uren)" : tegoed
          , vanaf : vanaf
          ]
    , dagverantwoordingen : periodeverantwoording~  -- Dagverantwoording
      BOX [ datum :                     datum
          , verroosterd :               dag[Dienst*Dagverantwoording]~
            BOX [ "" :     I[Dienst]
                , begin :  begin
                , eind :   eind
                , dienst : roosterSoort
                ]
          , verantwoord :               dag[Tijdverantwoording*Dagverantwoording]~
            BOX [ ""  :         I[Tijdverantwoording]
                , begin :       begin
                , eind :        eind
                , tijdrubriek : tijdrubriek
                ]
          ]
    , actie : I[Perioderesultaat]
      TABS [ goed : I[Perioderesultaat];I[OKresultaat];I[Perioderesultaat]
             BOX<SCOLS>
               [ resultaat : goedresultaat~
                 BOX [ resultaatsoort : resultaatSoort
                     , code :           code
                     , uren :           resultaatWaarde
                     ]
               , feedback : feedback~
                 BOX [ code :         feedbackCode
                     , status :       feedbackStatus
                     , omschrijving : opmerking
                     ]
               ]
           , fout : I[Perioderesultaat];I[TeCorrResultaat];I[Perioderesultaat]
             BOX<SCOLS>
               [ fout : feedback~
                 BOX [ code :         feedbackCode
                     , status :       feedbackStatus
                     , omschrijving : opmerking
                     ]
               ]
           ]
    , onderbouwing : onderbouwing
    ]

ENDCONTEXT
