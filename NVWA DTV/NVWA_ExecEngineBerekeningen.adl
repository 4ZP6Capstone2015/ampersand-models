CONTEXT NVWA_ExecEngineBerekeningen IN DUTCH
{---------------------------------------------------------------------
   Dates in the m/d/y or d-m-y formats are disambiguated by looking
   at the separator between the various components: if the separator
   is a slash (/), then the American m/d/y is assumed; whereas if
   the separator is a dash (-) or a dot (.), then the European 
   d-m-y format is assumed.
---------------------------------------------------------------------}
PROCESS "Datum- en tijdsberekeningen"

--$ Ik heb de tekst uit Stef's mailtje letterlijk overgenomen Mijn eigen toevoegingen staan als stappen in het vinden van een oplossing herkenbaar. De tekst '-- Klaar.' geeft aan waar mijn spul ophoudt.
RELATION geqDatum[Datum*Datum]
-- geqDatum  : de groter-dan-of-gelijk operatie op data (>=)
-- Stap 1: Zorgen dat 'geqDatum' bestaat voor de betreffende data
-- De Botte Oplossing: we zorgen ervoor dat dit voor alle paren (Datum1,Datum2) is gecheckt:
-- Omdat dit een goeie positie is om alle andere Datum-relaties te populeren, nemen we die meteen mee
ROLE ExecEngine MAINTAINS "Bereken geqDatum"
-- Met deze regel pakken we alle paren (D1,D2) waarbij D1 of D2 niet eerder was geprocest.
RULE "Bereken geqDatum": V[Datum] |- geqDatum \/ leqDatum
VIOLATION (TXT "{EX} dateGEQ;geqDatum;Datum;", SRC I, TXT ";", TGT I)
-- Klaar.

RELATION geqTijdstip[Tijdstip*Tijdstip]
-- geqTijdstip  : de groter-dan-of-gelijk operatie op tijdstippen (>=)
-- Stap 1: Zorgen dat 'geqTijdstip' bestaat voor de betreffende tijdstippen
ROLE ExecEngine MAINTAINS "Bereken geqTijdstip"
-- Met deze regel pakken we alle paren (T1,T2) waarbij T1 of T2 niet eerder was geprocest.
RULE "Bereken geqTijdstip": V[Tijdstip] |- geqTijdstip \/ leqTijdstip
VIOLATION (TXT "{EX} timeofdayGEQ;geqTijdstip;Tijdstip;", SRC I, TXT ";", TGT I)
-- Klaar.


RELATION geqDatumtijd[DatumTijd*DatumTijd]
-- geqDatumtijd  : de groter-dan-of-gelijk operatie op DatumTijden (>=)
-- Stap 1: Zorgen dat 'geqDatumtijd' bestaat voor de betreffende Datumtijden
ROLE ExecEngine MAINTAINS "Bereken geqDatumtijd"
-- Met deze regel pakken we alle paren (T1,T2) waarbij T1 of T2 niet eerder was geprocest.
RULE "Bereken geqDatumtijd": V[DatumTijd] |- geqDatumtijd \/ leqDatumtijd
VIOLATION (TXT "{EX} timeofdayGEQ;geqDatumtijd;DatumTijd;", SRC I, TXT ";", TGT I)
-- Klaar.

RELATION geqBegin[Periode*Periode]
-- geqBegin  : de groter-dan-of-gelijk operatie op het aanvangstijdstip van periodes (>=)
-- RULE geqBegin = ((V[Periode*Periode]- begintijd;begintijd~ )  /\ begindatum;geqDatum;begindatum~ ) \/
--                 ((V[Periode*Periode]-begindatum;begindatum~)  /\ begintijd;geqTijdstip;begintijd~) \/
--                 begin;geqDatumtijd;begin~ 
-- Stap 1: Deze invariant in stand houden doe je door:
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insgeqBegin
RULE insgeqBegin: 
                    ((V[Periode*Periode]- begintijd;begintijd~ )  /\ begindatum;geqDatum;begindatum~ ) \/
                    ((V[Periode*Periode]-begindatum;begindatum~)  /\ begintijd;geqTijdstip;begintijd~) \/
                    begin;geqDatumtijd;begin~ 
                  |- geqBegin
VIOLATION (TXT "{EX} InsPair;geqBegin;Periode;", SRC I[Periode], TXT ";Periode;", TGT I[Periode])
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS delgeqBegin
RULE delgeqBegin: 
                  geqBegin |- 
                    ((V[Periode*Periode]- begintijd;begintijd~ )  /\ begindatum;geqDatum;begindatum~ ) \/
                    ((V[Periode*Periode]-begindatum;begindatum~)  /\ begintijd;geqTijdstip;begintijd~) \/
                    begin;geqDatumtijd;begin~ 
VIOLATION (TXT "{EX} DelPair;geqBegin;Periode;", SRC I[Periode], TXT ";Periode;", TGT I[Periode])
-- Klaar.

RELATION geqEind[Periode*Periode]
-- geqEind  : de groter-dan-of-gelijk operatie op het aanvangstijdstip van periodes (>=)
-- RULE geqEind = ((V[Periode*Periode]- eindtijd;eindtijd~ )  /\ einddatum;geqDatum;einddatum~ ) \/
--                ((V[Periode*Periode]-einddatum;einddatum~)  /\ eindtijd;geqTijdstip;eindtijd~) \/
--                eind;geqDatumtijd;eind~ 
-- Stap 1: Deze invariant in stand houden doe je door:
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insgeqEind
RULE insgeqEind: 
                  ((V[Periode*Periode]- eindtijd;eindtijd~ )  /\ einddatum;geqDatum;einddatum~ ) \/
                  ((V[Periode*Periode]-einddatum;einddatum~)  /\ eindtijd;geqTijdstip;eindtijd~) \/
                  eind;geqDatumtijd;eind~ 
                |- geqEind
VIOLATION (TXT "{EX} InsPair;geqEind;Periode;", SRC I[Periode], TXT ";Periode;", TGT I[Periode])
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS delgeqEind
RULE delgeqEind: 
                geqEind |- 
                  ((V[Periode*Periode]- eindtijd;eindtijd~ )  /\ einddatum;geqDatum;einddatum~ ) \/
                  ((V[Periode*Periode]-einddatum;einddatum~)  /\ eindtijd;geqTijdstip;eindtijd~) \/
                  eind;geqDatumtijd;eind~ 
VIOLATION (TXT "{EX} DelPair;geqEind;Periode;", SRC I[Periode], TXT ";Periode;", TGT I[Periode])
-- Klaar.


RELATION begindatum[Periode*Datum] [UNI]
RELATION einddatum[Periode*Datum] [UNI]

RELATION binnenDatum[Periode*Periode]
RULE binnenDatum    = begindatum;geqDatum;begindatum~ /\ einddatum;geqDatum~;einddatum~
-- Stap 2: Deze invariant in stand houden doe je door:
-- Stap 2a: bijpopuleren
ROLE ExecEngine MAINTAINS insbinnenDatum
RULE insbinnenDatum: begindatum;geqDatum;begindatum~ /\ einddatum;geqDatum~;einddatum~ |- binnenDatum
VIOLATION (TXT "{EX} InsPair;binnenDatum;Datum;", SRC I, TXT ";Datum;", TGT I)
-- Stap 2a: afpopuleren
ROLE ExecEngine MAINTAINS delbinnenDatum
RULE delbinnenDatum: binnenDatum |- begindatum;geqDatum;begindatum~ /\ einddatum;geqDatum~;einddatum~
VIOLATION (TXT "{EX} DelPair;binnenDatum;Datum;", SRC I, TXT ";Datum;", TGT I)

RELATION begintijd[Periode*Tijdstip] [UNI]
RELATION eindtijd[Periode*Tijdstip] [UNI]

RELATION binnenTijdstip[Periode*Periode]
RULE binnenTijdstip = begintijd;geqTijdstip;begintijd~ /\ eindtijd;geqTijdstip~;eindtijd~
-- Stap 2: Deze invariant in stand houden doe je door:
-- Stap 2a: bijpopuleren
ROLE ExecEngine MAINTAINS insbinnenTijdstip
RULE insbinnenTijdstip: begintijd;geqTijdstip;begintijd~ /\ eindtijd;geqTijdstip~;eindtijd~ |- binnenTijdstip
VIOLATION (TXT "{EX} InsPair;binnenTijdstip;Tijdstip;", SRC I, TXT ";Tijdstip;", TGT I)
-- Stap 2a: afpopuleren
ROLE ExecEngine MAINTAINS delbinnenTijdstip
RULE delbinnenTijdstip: binnenTijdstip |- begintijd;geqTijdstip;begintijd~ /\ eindtijd;geqTijdstip~;eindtijd~
VIOLATION (TXT "{EX} DelPair;binnenTijdstip;Tijdstip;", SRC I, TXT ";Tijdstip;", TGT I)

RELATION begin[Periode*DatumTijd] [UNI]
RELATION eind[Periode*DatumTijd] [UNI]

RELATION binnenDatumtijd[Periode*Periode]
-- Stap 2: Deze invariant in stand houden doe je door:
-- Stap 2a: bijpopuleren
ROLE ExecEngine MAINTAINS insbinnenDatumtijd
RULE insbinnenDatumtijd: begintijd;geqDatumtijd;begintijd~ /\ eindtijd;geqDatumtijd~;eindtijd~ |- binnenDatumtijd
VIOLATION (TXT "{EX} InsPair;binnenDatumtijd;DatumTijd;", SRC I, TXT ";DatumTijd;", TGT I)
-- Stap 2a: afpopuleren
ROLE ExecEngine MAINTAINS delbinnenDatumtijd
RULE delbinnenDatumtijd: binnenDatumtijd |- begintijd;geqDatumtijd;begintijd~ /\ eindtijd;geqDatumtijd~;eindtijd~
VIOLATION (TXT "{EX} DelPair;binnenDatumtijd;DatumTijd;", SRC I, TXT ";DatumTijd;", TGT I)

RELATION binnen[Periode*Periode]
RULE binnen         = (begin;geqDatumtijd;begin~ /\ eind;geqDatumtijd~;eind~) \/
                      (-(begindatum;V;begindatum~) /\ -(einddatum;V;einddatum~) /\ binnenTijdstip) \/
                      ( -(begintijd;V;begintijd~)  /\  -(eindtijd;V;eindtijd~)  /\ binnenDatum   )
ROLE ExecEngine MAINTAINS insbinnen
RULE insbinnen:       (begin;geqDatumtijd;begin~ /\ eind;geqDatumtijd~;eind~) \/
                      (-(begindatum;V;begindatum~) /\ -(einddatum;V;einddatum~) /\ binnenTijdstip) \/
                      ( -(begintijd;V;begintijd~)  /\  -(eindtijd;V;eindtijd~)  /\ binnenDatum   )
                  |- binnen
VIOLATION (TXT "{EX} InsPair;binnen;Periode;", SRC I, TXT ";Periode;", TGT I)
ROLE ExecEngine MAINTAINS delbinnen
RULE delbinnen:   binnen |-
                      (begin;geqDatumtijd;begin~ /\ eind;geqDatumtijd~;eind~) \/
                      (-(begindatum;V;begindatum~) /\ -(einddatum;V;einddatum~) /\ binnenTijdstip) \/
                      ( -(begintijd;V;begintijd~)  /\  -(eindtijd;V;eindtijd~)  /\ binnenDatum   )
VIOLATION (TXT "{EX} DelPair;binnen;Periode;", SRC I, TXT ";Periode;", TGT I)


CLASSIFY DatumTijd IS Datum/\Tijdstip
--$Clarify: Tijdstip is een tijd die zich elke 24 uur herhaalt, bijv. 12:53u
--$Clarify: Datum is een periode van (meestal) 24 uur, bijv. 5 december 2014
--$Clarify: DatumTijd is een combinatie van een Datum en een Tijdstip, bijv. 5 december 2014 12:53u.
ROLE ExecEngine MAINTAINS insgeqPeriode
RULE iRuleTijdstip: I[DatumTijd] |- I[Tijdstip]
VIOLATION ( TXT "{EX} DelAtomDatumTijd;", SRC I[DatumTijd]
          , TXT "{EX} InsAtomTijdstip;", SRC I[Tijdstip]
          )
RULE iRuleDatum:    I[DatumTijd] |- I[Datum]
VIOLATION ( TXT "{EX} DelAtomDatumTijd;", SRC I[DatumTijd]
          , TXT "{EX} InsAtomDatum;",     SRC I[Datum]
          )


RULE overlap = eind;geqDatumtijd;begin~ /\ begin;geqDatumtijd~;eind~
-- Stap 1: Deze invariant in stand houden doe je door:
-- Eerst de relatie te gaan definieren:
RELATION overlap[Periode*Periode] [AUT]
-- Stap 1a: bijpopuleren
ROLE ExecEngine MAINTAINS insoverlap
RULE insoverlap: 
                    eind;geqDatumtijd;begin~ /\ begin;geqDatumtijd~;eind~
                  |- overlap
VIOLATION (TXT "{EX} InsPair;overlap;Periode;", SRC I, TXT ";Periode;", TGT I)
-- Stap 1b: afpopuleren
ROLE ExecEngine MAINTAINS deloverlap
RULE deloverlap: 
                    eind;geqDatumtijd;begin~ /\ begin;geqDatumtijd~;eind~
                  |- overlap
VIOLATION (TXT "{EX} DelPair;overlap;Periode;", SRC I, TXT ";Periode;", TGT I)
-- Klaar.

ENDPROCESS

ENDCONTEXT
