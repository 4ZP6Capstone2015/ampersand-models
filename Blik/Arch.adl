CONTEXT Architectuur IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Login.xlsx"
INCLUDE "ProductCodes.xlsx"
INCLUDE "Grondslagen.xlsx"
INCLUDE "Medewerkers.xlsx"

PATTERN Zaken
RELATION zaaknummer[Zaak*Zaaknummer] [TOT,INJ]  -- Een zaak kan meerdere zaaknummers hebben. Hiermee kan het samenvoegen van zaken plaatsvinden.
REPRESENT Zaaknummer TYPE INTEGER
RELATION zaaktype[Zaak*Zaaktype] [UNI]
RELATION domein[Zaak*Domein] [UNI,TOT]
RELATION behandelaar[Zaak*Persoon] [UNI,TOT]
RELATION start[Zaak*Datum] [UNI,TOT]
RELATION project[Zaak*Project] [UNI] -- De taken van de NVWA staan in Art XVII lid 2 Besluit mandaat, volmacht en machtiging van het Ministerie van Economische Zaken 2015
RELATION dossier[Zaak*Document]
RELATION afgedaan[Zaak*Datum] [UNI]
ENDPATTERN

PATTERN Personen
VIEW Persoon : Persoon (volledigenaam)
RELATION naam[Rol*Tekst] [UNI]
RELATION grond[Rol*RechtsGrond]
RELATION persoon[Dienstverband*Persoon] [UNI]
REPRESENT Dienstverband TYPE ALPHANUMERIC
RELATION team[Dienstverband*Eenheid] [UNI]
RELATION functie[Dienstverband*Functie] [UNI]
RELATION contract[Dienstverband*Contractsoort] [UNI]
RELATION wtrCategorie[Dienstverband*WTRcat] [UNI]
RELATION wtr4[Dienstverband*WTRcat] [UNI]
RELATION volledigenaam[Persoon*Tekst] [UNI]
RELATION naam[Eenheid*Tekst] [UNI]
RELATION sub[Eenheid*Eenheid] [UNI]
ENDPATTERN

PATTERN Organisatie
VIEW Eenheid : Eenheid (afkorting)
RELATION naam[Eenheid*Tekst] [UNI]
RELATION grond[Eenheid*RechtsGrond]
RELATION afkorting[Eenheid*Tekst] [UNI]
ENDPATTERN

PATTERN Zaaktypen
RELATION grond[Zaaktype*RechtsGrond]
RELATION naam[Zaaktype*Tekst] [UNI,TOT]
VIEW Zaaktype : Zaaktype (naam)
RELATION definitie[Zaaktype*Tekst] [UNI]
RELATION pf[Zaaktype*Procesflow]
RELATION grond[Taak*RechtsGrond] -- De taken van de NVWA staan in Art XVII lid 2 Besluit mandaat, volmacht en machtiging van het Ministerie van Economische Zaken 2015
RELATION naam[Taak*Tekst] [UNI,TOT]
ENDPATTERN

PATTERN Overtredingen
RELATION omschrijving [Overtreding*Tekst] [UNI]       -- als bedoeld in Art. 5:9 sub a. Awb
RELATION voorschrift [Overtreding*RechtsGrond]        -- als bedoeld in Art. 5:9 sub a. Awb
RELATION plaats [Overtreding*Plaatsaanduiding] [UNI]  -- als bedoeld in Art. 5:9 sub b. Awb
RELATION tijdstip [Overtreding*Tijdsaanduiding] [UNI] -- als bedoeld in Art. 5:9 sub b. Awb
RELATION geconstateerd [Overtreding*Persoon]          -- als bedoeld in Art. 5:9 Awb
RELATION overtreder [Overtreding*Persoon] [UNI]       -- als bedoeld in Art. 5:1 lid 2 Awb
RELATION uitgereikt [Overtreding*Datum] [UNI]         -- als bedoeld in Art. 5:48 lid 3 Awb
ENDPATTERN

PATTERN Klachten
CLASSIFY Klacht ISA Zaak
RELATION indiener [Klacht*Persoon]             -- als bedoeld in Art. 9:4 lid 2 sub a. AwB
RELATION dagtekening [Klacht*Datum] [UNI]      -- als bedoeld in Art. 9:4 lid 2 sub b. AwB
RELATION omschrijving [Klacht*Tekst] [UNI]     -- als bedoeld in Art. 9:4 lid 2 sub c. AwB
RELATION klaagschrift [Klacht*Document] [UNI]  -- als bedoeld in Art. 5:9 sub a. Awb
RELATION stuk [Klacht*Document]                -- als bedoeld in Art. 5:9 sub b. Awb
RELATION beklaagde [Klacht*Persoon]            -- 
RELATION klager [Klacht*Persoon]               -- 
RELATION hoorzitting [Klacht*Zitting]          -- 
ENDPATTERN

PATTERN Inspecties
CLASSIFY Inspectie ISA Zaak
RELATION object [Inspectie*Object]
RELATION overtreding[Inspectie*Overtreding]
ENDPATTERN

PATTERN Requirements
RELATION omschrijving [Requirement*Tekst] [UNI]
RELATION rol [Requirement*Rol]
RELATION wens [Requirement*Tekst] [UNI]
RELATION doel [Requirement*Tekst] [UNI]
RELATION auteur [Requirement*Tekst]
RELATION processtap [Requirement*Processtap] [UNI]
RELATION opmerking [Requirement*Tekst] [UNI]
RELATION procesflow [Requirement*Procesflow] [UNI]
RELATION url[RechtsGrond*URL]
ENDPATTERN

PATTERN Wetsartikelen
VIEW RechtsGrond : RechtsGrond(artikel, TXT " ", regeling;naam)
RELATION url[RechtsGrond*URL]
RELATION regeling[RechtsGrond*Regeling] [UNI,TOT]
RELATION artikel[RechtsGrond*Artikel] [UNI]
RELATION bwb[Regeling*BasisWettenBestand] [UNI]
RELATION naam[Regeling*Tekst] [UNI,TOT]
RELATION afkorting[Regeling*Tekst] [UNI]
REPRESENT Tekst TYPE BIGALPHANUMERIC
RELATION artikelen[Regeling*Aantal] [UNI]
REPRESENT Aantal TYPE INTEGER
RELATION url[Regeling*URL]
RELATION regelgeving[Domein*Regeling]
VIEW Domein : Domein (domeinnaam)
ENDPATTERN

PATTERN Procesflows
RELATION naam[Procesflow*Tekst] [UNI,TOT]
RELATION niveau[Procesflow*Tekst] [UNI,TOT]
RELATION uren[Procesflow*Aantal] [UNI]
RELATION fte[Procesflow*Aantal] [UNI]
RELATION voorziening[Procesflow*Voorziening]
RELATION taak[Procesflow*Taak]
ENDPATTERN

PATTERN Handelingen
RELATION naam[Handeling*Tekst] [UNI,TOT]
RELATION werkwoord[Handeling*Werkwoord]
RELATION voorwerp[Handeling*Voorwerp]
--RELATION regeling[Handeling*Regeling]
RELATION grondslag[Handeling*RechtsGrond]
RELATION periode[Handeling*Periode] [UNI]
RELATION opmerking[Handeling*Tekst] [UNI]
RELATION producttype[Handeling*Producttype]
RELATION waardering[Handeling*Waardering]
ENDPATTERN

PATTERN Applicaties
RELATION onderdeel[Component*Voorziening]
RELATION ondersteunt[Component*RechtsGrond]
RELATION componentnaam[Component*Tekst] [UNI,TOT]
RELATION regelingen[Component*Regeling]
RULE Regelingenscope : ondersteunt;regeling |- regelingen
VIOLATION (TXT "Scope fout! De component ", SRC componentnaam, TXT " kan ", SRC ondersteunt, TXT " niet ondersteunen omdat \"", TGT naam, TXT "\" niet in de regelingen van deze component is opgenomen.")
ENDPATTERN

PATTERN Producten
RELATION grondslag[WerkpakketCode*RechtsGrond] [UNI]
RELATION werkpakketproces[WerkpakketCode*Procesflow] [UNI]
RELATION werkpakket[ProductDienstCode*WerkpakketCode] [UNI,TOT]
RELATION productcode[ProductDienstCode*ProductCode] [UNI,TOT]
RELATION divisie[ProductDienstCode*Divisie] [UNI,TOT]
RELATION domein[ProductDienstCode*Domein] [UNI,TOT]
RELATION opdrachtgever[ProductDienstCode*Opdrachtgever] [UNI,TOT]
RELATION vanaf[ProductDienstCode*Datum] [UNI,TOT]
REPRESENT Datum TYPE DATE
RELATION tot[ProductDienstCode*Datum] [UNI,TOT]
RELATION gepland[ProductDienstCode*Uren] [UNI]
REPRESENT Uren TYPE INTEGER
RELATION doelstelling[ProductDienstCode*Tekst] [UNI,TOT]

RELATION domeinnaam[Domein*Tekst] [UNI,TOT]
RELATION productnaam[ProductCode*Tekst] [UNI,TOT]
RELATION werkpakketnaam[WerkpakketCode*Tekst] [UNI,TOT]
VIEW WerkpakketCode : WerkpakketCode(I, TXT ": ", werkpakketnaam)
ENDPATTERN

VIEW Component : Component(componentnaam)

INTERFACE Component (componentnaam, ondersteunt, regelingen) FOR Architect : I[Component] BOX
    [ naam : componentnaam
    , ondersteunt : ondersteunt
    , "regeling(en)" : regelingen
    ]

INTERFACE Voorziening (onderdeel, ondersteunt, regelingen) FOR Architect : I[Voorziening] BOX
            [ Componenten : onderdeel~
            , "regeling(en)" : onderdeel~;regelingen
            , "te onderhouden" : onderdeel~ BOX<SCOLS>
                [ regels : ondersteunt
                ]
            ]

INTERFACE VoorzieningUser (onderdeel) FOR User : I[Voorziening] BOX
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , grondslagen : onderdeel~;ondersteunt
            ]

VIEW Regeling : Regeling(naam)

INTERFACE Regeling (naam[Regeling*Tekst], regeling[RechtsGrond*Regeling], regelgeving, url[Regeling*URL]) FOR User : I[Regeling] ROWS
    [ Regeling : naam
    , afkorting : afkorting
    , domein : regelgeving~
    , URL : url
    ]

INTERFACE "Regeling details" (naam[Regeling*Tekst], regeling[RechtsGrond*Regeling], regelgeving, artikel[RechtsGrond*Artikel], url[Regeling*URL]) FOR Jurist : I[Regeling] ROWS
    [ Regeling : naam
    , Artikel  : regeling[RechtsGrond*Regeling]~ COLS [ artikel : artikel]
    , URL      : url
    , domein : regelgeving~
    , "voorziening(en)" : regelingen~;onderdeel
    , "component(en)" : regelingen~;ondersteunt
    ]

INTERFACE Handelingen FOR User : '_SESSION';ingelogd;V[Account*Handeling] BOX<SCOLS>
            [ naam        : naam[Handeling*Tekst]
            , werkwoord   : werkwoord[Handeling*Werkwoord]
            , voorwerp    : voorwerp[Handeling*Voorwerp]
            , periode     : periode[Handeling*Periode]
            , opmerking   : opmerking[Handeling*Tekst]
            , producttype : producttype[Handeling*Producttype]
            , waardering  : waardering[Handeling*Waardering]
            ]

INTERFACE Regelingen FOR User : '_SESSION';ingelogd;V[Account*Regeling] BOX<SCOLS>
    [ Regeling : naam
    , handelingen : regeling~;grondslag~ BOX<SCOLS>
            [ naam        : naam[Handeling*Tekst]
            , werkwoord   : werkwoord[Handeling*Werkwoord]
            , voorwerp    : voorwerp[Handeling*Voorwerp]
            , periode     : periode[Handeling*Periode]
            , opmerking   : opmerking[Handeling*Tekst]
            , producttype : producttype[Handeling*Producttype]
            , waardering  : waardering[Handeling*Waardering]
            ]
    , afkorting : afkorting
    , domein : regelgeving~
    , URL : url
    ]

INTERFACE Zaken FOR User : '_SESSION';ingelogd;V[Account*Zaak] BOX<SCOLS>
            [ zaaknummer : zaaknummer
            , zaaktype : zaaktype
            , domein : domein
            , behandelaar : behandelaar
            , startdatum : start
            , project : project
            ]

INTERFACE Projecten FOR User : '_SESSION';ingelogd;V[Account*Project] BOX<SCOLS>
            [ project : I
            , zaaktypen : project~;zaaktype
            , domein : project~;domein
            , behandelaars : project~;behandelaar
            ]

INTERFACE Zaak (zaaknummer, zaaktype, domein[Zaak*Domein], behandelaar, start, afgedaan, project) FOR User: I[Zaak] BOX<SCOLS>
            [ zaaknummer : zaaknummer
            , zaaktype : zaaktype
            , domein : domein
            , behandelaar : behandelaar
            , startdatum : start
            , project : project
            , afgedaan : afgedaan
            ]

INTERFACE Project (zaaknummer, zaaktype, domein[Zaak*Domein], behandelaar, start, project) FOR User: I[Project] BOX<SCOLS>
      [ project : project~ BOX<SCOLS>
            [ zaaknummer : zaaknummer
            , zaaktype : zaaktype
            , domein : domein
            , behandelaar : behandelaar
            , startdatum : start
            , afgedaan : afgedaan
            ]
      ]

INTERFACE Rollen (naam[Rol*Tekst], omschrijving[Requirement*Tekst], wens, doel, opmerking[Requirement*Tekst]) FOR Architect : '_SESSION';ingelogd;V[Account*Rol] ROWS
      [ rol : naam
      , rechtsgrond : grond
      , eisen : rol~ BOX<SCOLS>
            [ activiteit : omschrijving
            , "ik wil"     : wens
            , opdat         : doel
            , opmerking    : opmerking
            ]
      ]

INTERFACE Zaaktypes (naam[Zaaktype*Tekst], definitie) FOR Architect : '_SESSION';ingelogd BOX<SCOLS>
      [ gedefinieerd : V[Account*Zaaktype];(I/\definitie;definitie~) BOX<SCOLS>
            [ Zaaktype : naam
            , definitie : definitie
            , grond : grond
            ]
      , ongedefinieerd : V[Account*Zaaktype];(I-definitie;definitie~) BOX<SCOLS>
            [ Zaaktype : naam
            , definitie : definitie
            , grond : grond
            ]
      ]

INTERFACE Procesflow (naam[Procesflow*Tekst],niveau,voorziening) FOR Architect : I[Procesflow] BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , niveau : niveau
            , stappen : procesflow~;processtap
            , "Rechtsgronden" : werkpakketproces~;grondslag
            , "uren 2013" : uren
            , voorzieningen : voorziening
            ]

INTERFACE Procesflows FOR Architect : '_SESSION';ingelogd;V[Account*Procesflow] BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , niveau : niveau
            , stappen : procesflow~;processtap
            , "uren 2013" : uren
            , voorzieningen : voorziening
            ]

INTERFACE Bronnen FOR Jurist : '_SESSION';ingelogd;V[Account*Regeling] BOX<SCOLS>
            [ Regeling : naam
            , domein : regelgeving~
            , Artikel : regeling~;I[RechtsGrond];artikel
            , URL : url
            ]

INTERFACE Wetsartikelen FOR Jurist : '_SESSION';ingelogd;V[Account*Domein] BOX<SCOLS>
      [ domeinnaam : domeinnaam
      , "" : regelgeving BOX<SCOLS>
            [ Regeling : naam
            , Artikel : regeling~;I[RechtsGrond];artikel
            , URL : url
            ]
      ]

INTERFACE Requirements FOR Architect : '_SESSION';ingelogd;V[Account*Requirement] BOX<SCOLS>
            [ requirement  : I
            , omschrijving : omschrijving
            , rol          : rol
            , wens         : wens
            , doel         : doel
            , processtap   : processtap
            , opmerking    : opmerking
            , procesflow   : procesflow
            ]

INTERFACE Componenten FOR Architect : '_SESSION';ingelogd;V[Account*Voorziening] BOX<SCOLS>
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , grondslagen : onderdeel~;ondersteunt
            ]

INTERFACE Medewerkers FOR User : '_SESSION';ingelogd;V[Account*Dienstverband] BOX<SCOLS>
            [ naam : persoon;volledigenaam
            , functie : functie
            , team : team
            , divisie : team;sub;naam
            ]

ENDCONTEXT