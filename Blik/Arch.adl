CONTEXT Architectuur IN DUTCH
INCLUDE "Login.adl"
INCLUDE "Login.xlsx"
--INCLUDE "ProductCodes.xlsx"
--INCLUDE "Wetsartikelen.xlsx"

PATTERN Wetsartikelen
CLASSIFY Wetsartikel ISA RechtsGrond
VIEW Wetsartikel : Wetsartikel(artikel, TXT " ", regeling;naam)
RELATION url[Wetsartikel*URL]
RELATION regeling[Wetsartikel*Regeling] [UNI,TOT]
RELATION artikel[Wetsartikel*Artikel] [UNI,TOT]

RELATION naam[Regeling*Tekst] [UNI,TOT]
REPRESENT Tekst TYPE ALPHANUMERIC
RELATION artikelen[Regeling*Aantal] [UNI]
REPRESENT Aantal TYPE INTEGER
RELATION url[Regeling*URL]
ENDPATTERN

PATTERN Procesflows
RELATION naam[Procesflow*Tekst] [UNI,TOT]
RELATION niveau[Procesflow*Tekst] [UNI,TOT]
RELATION uren[Procesflow*Aantal] [UNI]
RELATION fte[Procesflow*Aantal] [UNI]
RELATION voorziening[Procesflow*Voorziening]
ENDPATTERN

PATTERN Applicaties
RELATION onderdeel[Component*Voorziening]
RELATION ondersteunt[Component*Wetsartikel]
RELATION regelingen[Component*Regeling]
RELATION componentnaam[Component*Tekst] [UNI,TOT]
RULE Regelingenscope : ondersteunt;regeling |- regelingen
VIOLATION (TXT "Scope fout! De component ", SRC componentnaam, TXT " kan ", SRC ondersteunt;artikel, TXT " niet ondersteunen omdat \"", TGT naam, TXT "\" niet in de regelingen van deze component is opgenomen.")
ENDPATTERN

PATTERN Zaaksoorten
RELATION grond[Zaaksoort*RechtsGrond]
RELATION pf[Zaaksoort*Procesflow]
ENDPATTERN

PATTERN Producten
RELATION grondslag[WerkpakketCode*Wetsartikel] [UNI]
RELATION werkpakketproces[WerkpakketCode*Procesflow] [UNI]
RELATION werkpakket[ProductDienstCode*WerkpakketCode] [UNI,TOT]
RELATION productcode[ProductDienstCode*ProductCode] [UNI,TOT]
RELATION divisie[ProductDienstCode*Divisie] [UNI,TOT]
RELATION domein[ProductDienstCode*Domein] [UNI,TOT]
RELATION opdrachtgever[ProductDienstCode*Opdrachtgever] [UNI,TOT]
RELATION vanaf[ProductDienstCode*Datum] [UNI,TOT]
REPRESENT Datum TYPE DATE
RELATION tot[ProductDienstCode*Datum] [UNI,TOT]
RELATION gepland[ProductDienstCode*Uren] [UNI]
REPRESENT Uren TYPE INTEGER
RELATION doelstelling[ProductDienstCode*Tekst] [UNI,TOT]

RELATION domeinnaam[Domein*Tekst] [UNI,TOT]
RELATION productnaam[ProductCode*Tekst] [UNI,TOT]
RELATION werkpakketnaam[WerkpakketCode*Tekst] [UNI,TOT]
VIEW WerkpakketCode : WerkpakketCode(I, TXT ": ", werkpakketnaam)
ENDPATTERN

INTERFACE Verbanden (naam[Regeling*Tekst], url[Regeling*URL], grondslag, voorziening, werkpakketproces, onderdeel, ondersteunt)
FOR Architect : '_SESSION'/\ingelogd;ingelogd~ TABS
     [ "Procesflows" : V[SESSION*Procesflow]  BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , Werkpakket : werkpakketproces~
            , "Rechtsgronden" : werkpakketproces~;grondslag
            , "gebruikte applicaties" : voorziening
            ]
     , "Wetsartikelen" : V[SESSION*Regeling] BOX<Rows>
            [ Regeling : naam
            , Artikel : regeling~;artikel
            , URL : url
            ]
     , "Werkpakketten" : V[SESSION*WerkpakketCode] BOX<SCOLS>
            [ "Werkpakket (code: naam)" : I
            , Procesflow : werkpakketproces
            , "Rechtsgrond" : grondslag
            ]
     , "Componenten" : V[SESSION*Voorziening] BOX<SCOLS>
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , "te onderhouden" : onderdeel~ BOX<SCOLS>
                [ "regeling(en)" : regelingen
                , regels : ondersteunt
                ]
            ]
     ]

VIEW Component : Component(componentnaam)

INTERFACE Component (componentnaam, ondersteunt, regelingen) FOR Architect : I[Component] BOX
    [ naam : componentnaam
    , ondersteunt : ondersteunt
    , "regeling(en)" : regelingen
    ]

INTERFACE Voorziening (onderdeel, ondersteunt, regelingen) FOR Architect : I[Voorziening] BOX
            [ Componenten : onderdeel~
            , "regeling(en)" : onderdeel~;regelingen
            , "te onderhouden" : onderdeel~ BOX<SCOLS>
                [ regels : ondersteunt
                ]
            ]

VIEW Regeling : Regeling(naam)

INTERFACE Regeling (naam[Regeling*Tekst], regeling, artikel, url[Regeling*URL]) FOR Architect : I[Regeling] ROWS
    [ Regeling : naam
    , Artikel : regeling~ COLS [ artikel : artikel]
    , URL : url
    ]

INTERFACE "Regeling details" : I[Regeling] ROWS
    [ Regeling : naam
    , Artikel  : regeling~ COLS [ artikel : artikel]
    , URL      : url
    , "voorziening(en)" : regelingen~;onderdeel
    , "component(en)" : regelingen~;ondersteunt
    ]

INTERFACE Overzicht : '_SESSION'/\ingelogd;ingelogd~ TABS
     [ "Procesflows" : V[SESSION*Procesflow] BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , niveau : niveau
            , "uren 2013" : uren
            , "gebruikte applicaties" : voorziening
            ]
     , "Wetsartikelen" : V[SESSION*Regeling] BOX<Rows>
            [ Regeling : naam
            , Artikel : regeling~;artikel
            , URL : url
            ]
     , "Werkpakketten" : V[SESSION*WerkpakketCode] BOX<SCOLS>
            [ "Werkpakket code" : I
            , Werkpakket : werkpakketnaam
            , Domein : werkpakket~;domein;domeinnaam
            , Divisie : werkpakket~;divisie
            , Opdrachtgever : werkpakket~;opdrachtgever
            ]
     , "Componenten" : V[SESSION*Voorziening] BOX<SCOLS>
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , grondslagen : onderdeel~;ondersteunt
            ]
     ]
ENDCONTEXT